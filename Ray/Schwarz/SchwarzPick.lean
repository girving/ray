import Mathlib.Analysis.Complex.Schwarz
import Ray.Schwarz.Mobius

/-!
## Schwarz-Pick theorem

The Schwarz-Pick theorem provides the tightest bounds on finite differences and derivatives of
an anlytic function on the unit disk:

  https://en.wikipedia.org/wiki/Schwarz_lemma#Schwarz%E2%80%93Pick_theorem
-/

open Filter (Tendsto)
open Metric (ball isOpen_ball)
open Set
open scoped ComplexConjugate ContDiff Topology
noncomputable section

variable {w z : â„‚} {f : â„‚ â†’ â„‚}

/-- Finite difference version of Schwarz-Pick for the unit disk -/
lemma Complex.dist_le_mul_mobius_of_mapsTo_unit_ball (fa : ContDiffOn â„‚ Ï‰ f (ball 0 1))
    (fi : MapsTo f (ball 0 1) (ball 0 1)) (z1 : â€–zâ€– < 1) (w1 : â€–wâ€– < 1) :
    â€–f z - f wâ€– â‰¤ â€–1 - conj (f z) * f wâ€– * â€–mobius z wâ€– := by
  have fz1 : â€–f zâ€– < 1 := by simpa using fi (x := z) (by simpa)
  have fw1 : â€–f wâ€– < 1 := by simpa using fi (x := w) (by simpa)
  set g := mobius (f z) âˆ˜ f âˆ˜ mobius z
  have gm' := fi.comp (mapsTo_mobius z1)
  have gm : MapsTo g (ball 0 1) (ball 0 1) := (mapsTo_mobius fz1).comp gm'
  have ga : ContDiffOn â„‚ Ï‰ g (ball 0 1) :=
    (contDiffOn_mobius fz1).comp (fa.comp (contDiffOn_mobius z1) (mapsTo_mobius z1)) gm'
  have g0 : g 0 = 0 := by simp only [g, Function.comp_apply, mobius_zero, mobius_self]
  set u := mobius z w
  have u1 : â€–uâ€– < 1 := norm_mobius_lt_one z1 w1
  simpa only [g, Function.comp_apply, mobius_def (f z), u, mobius_mobius z1 w1, norm_div,
    div_le_iffâ‚€ (norm_mobius_denom_pos fz1 fw1), mul_comm â€–mobius _ _â€–] using
    Complex.norm_le_norm_of_mapsTo_ball_self (ga.differentiableOn le_top) gm g0 u1

/-- Derivative version of Schwarz-Pick for the unit disk -/
lemma Complex.norm_deriv_le_div_of_mapsTo_unit_ball (fa : ContDiffOn â„‚ Ï‰ f (ball 0 1))
    (fi : MapsTo f (ball 0 1) (ball 0 1)) (z1 : â€–zâ€– < 1) :
    â€–deriv f zâ€– â‰¤ (1 - â€–f zâ€– ^ 2) / (1 - â€–zâ€– ^ 2) := by
  have zm : z âˆˆ ball 0 1 := by simpa using z1
  have fz1 : â€–f zâ€– < 1 := by simpa using fi (x := z) (by simpa)
  have df := (fa.differentiableOn le_top).differentiableAt (x := z) (isOpen_ball.mem_nhds zm)
  have s : âˆ€á¶  w in ð“[â‰ ] z, â€–slope f z wâ€– - â€–1 - conj (f z) * f wâ€– / â€–1 - conj z * wâ€– â‰¤ 0 := by
    simp only [eventually_nhdsWithin_iff, mem_compl_iff, mem_singleton_iff]
    filter_upwards [isOpen_ball.eventually_mem zm] with w w1 wz
    simp only [Metric.mem_ball, dist_zero_right] at w1
    have s := Complex.dist_le_mul_mobius_of_mapsTo_unit_ball fa fi z1 w1
    simp only [mobius, Complex.norm_div, â† mul_div_assoc, mul_div_right_comm] at s
    rw [â† div_le_iffâ‚€ (norm_pos_iff.mpr (by grind))] at s
    simpa [slope, â† div_eq_inv_mul, norm_sub_rev (f w), norm_sub_rev w]
  have dc : ContinuousAt (fun w â†¦ â€–1 - conj (f z) * f wâ€– / â€–1 - conj z * wâ€–) z :=
    ContinuousAt.div (by fun_prop) (by fun_prop) (norm_mobius_denom_pos z1 z1).ne'
  have t1 := (continuous_norm.tendsto _).comp df.hasDerivAt.tendsto_slope
  have t2 := dc.tendsto
  have e : âˆ€ x : â„, (1 - x : â„‚) = (1 - x : â„) := by simp
  have n : âˆ€ {z : â„‚}, â€–zâ€– < 1 â†’ |1 - â€–zâ€– ^ 2| = (1 - â€–zâ€– ^ 2) := by
    intro z z1
    rw [abs_of_nonneg]
    bound
  simp only [Function.comp_def, conj_mul', â† Complex.ofReal_pow, e, Complex.norm_real,
    Real.norm_eq_abs, n z1, n fz1] at t1 t2
  rw [â† sub_nonpos]
  exact le_of_tendsto (t1.sub (t2.mono_left nhdsWithin_le_nhds)) s
