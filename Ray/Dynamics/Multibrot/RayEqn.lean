import Ray.Dynamics.Multibrot.Area
import Ray.Dynamics.Multibrot.BottcherInv
import Ray.Dynamics.Multibrot.RayBound

/-!
## Functional equations for `ray` and `pray`
-/

open Asymptotics
open MeasureTheory (volume)
open Metric (ball closedBall isOpen_ball mem_ball_self mem_ball mem_closedBall mem_closedBall_self)
open RiemannSphere
open OneDimension
open Set
open scoped OnePoint Real RiemannSphere Topology
noncomputable section

variable {c x z : ‚ÑÇ} {n : ‚Ñï}

-- We fix `d ‚â• 2`
variable {d : ‚Ñï} [Fact (2 ‚â§ d)]

/-- `ray d` with the zero value cut out -/
def ray' (d : ‚Ñï) [Fact (2 ‚â§ d)] (z : ‚ÑÇ) : ‚ÑÇ :=
  (ray d z).toComplex

@[simp] lemma coe_ray' (z0 : z ‚â† 0) (z1 : z ‚àà ball (0 : ‚ÑÇ) 1) : (ray' d z : ùïä) = ray d z := by
  rw [ray', coe_toComplex]
  simp only [ne_eq, ray_eq_inf z1, z0, not_false_eq_true]

lemma ray'_mem_ext (z0 : z ‚â† 0) (z1 : z ‚àà ball (0 : ‚ÑÇ) 1) :
    (ray' d z, z ^ d ^ n) ‚àà (superF d).ext := by
  set s := superF d
  set c := ray' d z
  have ce : c = ray d z := by simp only [c, coe_ray' z0 z1]
  have e : z = bottcher d c := by simp only [ce, bottcher_ray z1]
  apply s.pow_ext
  simp only [e, bottcher, fill_coe, bottcher']
  refine s.bottcher_ext (multibrotPost ?_)
  simp only [‚Üê multibrotExt_coe, ce]
  exact (bottcherHomeomorph d).map_target z1

/-- `ray'` in terms of `pray` -/
lemma ray'_eq_pray (m : z ‚àà ball (0 : ‚ÑÇ) 1) : ray' d z = pray d z / z := by
  by_cases z0 : z = 0
  ¬∑ simp only [ray', z0, ray_zero, toComplex_inf, pray_zero, div_zero]
  ¬∑ rw [ray', ray_eq_pray m, inv_coe, toComplex_coe, inv_div]
    simp only [ne_eq, div_eq_zero_iff, z0, pray_ne_zero m, or_self, not_false_eq_true]

/-- Conjugacy identity for the parameter external map `ray`. This is the key functional equation
relating the dynamical-space ray map `s.ray` to the parameter-space ray map `ray d`. -/
lemma ray_conjugacy (z0 : z ‚â† 0) (z1 : z ‚àà ball (0 : ‚ÑÇ) 1) :
    ray d z = (superF d).ray (ray' d z) z := by
  set s := superF d
  set c := ray' d z
  have ce : c = ray d z := by simp only [c, coe_ray' z0 z1]
  have e : z = bottcher d c := by simp only [ce, bottcher_ray z1]
  nth_rw 2 [e]
  rw [bottcher, fill_coe, bottcher', s.ray_bottcher, ce]
  apply multibrotPost
  simp only [‚Üê multibrotExt_coe, ce]
  exact (bottcherHomeomorph d).map_target z1

/-- The cascaded version of `pray d`, using higher powers for the second argument to `s.ray`. We'll
start with power s.t. `cascade d n z ‚âà 1` and descend to `cascade d 0 z = pray d z`. -/
def cascade (d : ‚Ñï) [Fact (2 ‚â§ d)] (n : ‚Ñï) (z : ‚ÑÇ) : ‚ÑÇ :=
  if z = 0 then 1 else z ^ d ^ n * ((superF d).ray (ray' d z) (z ^ d ^ n)).toComplex

@[simp] lemma cascade_z0 : cascade d n 0 = 1 := by
  simp only [cascade, ‚ÜìreduceIte]

/-- At the bottom of the cascade, we get `pray d` -/
lemma cascade_zero (m : z ‚àà ball (0 : ‚ÑÇ) 1) : cascade d 0 z = pray d z := by
  by_cases z0 : z = 0
  ¬∑ simp only [z0, cascade_z0, pray_zero]
  ¬∑ simp only [cascade, z0, ‚ÜìreduceIte, pow_zero, pow_one, ‚Üê ray_conjugacy z0 m, ray_eq_pray m]
    rw [inv_coe, toComplex_coe]
    ¬∑ field_simp [z0]
    ¬∑ simp only [ne_eq, div_eq_zero_iff, z0, m, pray_ne_zero, or_self, not_false_eq_true]

/-- One step down the cascade -/
lemma cascade_succ (m : z ‚àà ball (0 : ‚ÑÇ) 1) :
    cascade d (n + 1) z = cascade d n z ^ d + z ^ (d ^ (n + 1) - 1) * pray d z := by
  set s := superF d
  by_cases z0 : z = 0
  ¬∑ simp [z0, cascade_z0, Nat.sub_eq_zero_iff_le, s.d1]
  ¬∑ simp only [cascade, z0, if_false, pow_succ, pow_mul]
    have em : (ray' d z, z ^ d ^ n) ‚àà s.ext := ray'_mem_ext z0 m
    rw [‚Üê s.ray_eqn em]
    unfold f
    rw [toComplex_lift', f', mul_add, mul_pow, add_right_inj, ray'_eq_pray m,
      div_eq_inv_mul, ‚Üê mul_assoc, pow_sub‚ÇÄ _ z0 (by bound), pow_one, pow_mul]
    rw [ne_eq, s.ray_eq_a_iff em]
    simp only [pow_eq_zero_iff', z0, ne_eq, not_and, not_not, false_and, not_false_eq_true]

/-- The whole `cascade` is analytic -/
lemma cascade_analytic (m : z ‚àà ball (0 : ‚ÑÇ) 1) : ContDiffAt ‚ÑÇ ‚ä§ (cascade d n) z := by
  induction' n with n h
  ¬∑ refine (pray_analytic (d := d) m).congr_of_eventuallyEq ?_
    filter_upwards [isOpen_ball.eventually_mem m] with w m
    simp only [cascade_zero m]
  ¬∑ have e : cascade d (n + 1) =·∂†[ùìù z]
        fun z ‚Ü¶ cascade d n z ^ d + z ^ (d ^ (n + 1) - 1) * pray d z := by
      filter_upwards [isOpen_ball.eventually_mem m] with w m
      simp only [cascade_succ m]
    refine ContDiffAt.congr_of_eventuallyEq ?_ e
    exact (h.pow _).add ((contDiffAt_id.pow _).mul (pray_analytic m))

/-- `cascade ‚âà 1` for large `n` -/
lemma cascade_approx : (fun z ‚Ü¶ cascade d n z - 1) =O[ùìù 0] (fun z : ‚ÑÇ ‚Ü¶ z ^ d ^ n) := by
  by_cases n0 : n = 0
  ¬∑ simpa only [n0, pow_zero, pow_one, cascade_z0, sub_zero] using
      ((cascade_analytic (d := d) (n := 0) (z := 0) (by simp)).differentiableAt le_top).isBigO_sub
  set s := superF d
  have cz := Asymptotics.isLittleO_iff.mp (hasDerivAt_iff_isLittleO_nhds_zero.mp
    ((inv_ray_analytic (d := d) (z := 0) (by simp)).differentiableAt le_top).hasDerivAt)
    (c := 2‚Åª¬π) (by norm_num)
  simp only [inv_ray_zero, sub_zero, zero_add, deriv_inv_ray_zero, smul_eq_mul, mul_one] at cz
  simp only [cascade]
  refine Asymptotics.isBigO_iff.mpr ‚ü®64, ?_‚ü©
  filter_upwards [cz, eventually_norm_sub_lt 0 (Œµ := 32) (by norm_num),
    eventually_norm_sub_lt 0 (Œµ := 80‚Åª¬π) (by bound)] with z cz lt_c z_lt
  by_cases z0 : z = 0
  ¬∑ simp [z0]
  set c := ray' d z
  have hc : (ray d z).toComplex = c := rfl
  simp only [z0, ‚ÜìreduceIte, sub_zero, inv_ray, hc, toComplex_inv] at cz lt_c z_lt ‚ä¢
  have cb := hc ‚ñ∏ ray_le (d := d) (by linarith)
  have lt_zi : 80 < ‚Äñz‚Äñ‚Åª¬π := by rwa [lt_inv_comm‚ÇÄ (by norm_num) (norm_pos_iff.mpr z0)]
  have lt_c : 16 < ‚Äñc‚Äñ := by
    calc ‚Äñc‚Äñ
      _ = ‚Äñz‚Åª¬π + (c - z‚Åª¬π)‚Äñ := by ring_nf
      _ ‚â• ‚Äñz‚Åª¬π‚Äñ - ‚Äñc - z‚Åª¬π‚Äñ := by bound
      _ > 80 - 64 := by rw [norm_inv]; bound
      _ = 16 := by norm_num
  have le_ci : ‚Äñz‚Äñ ‚â§ 2 * ‚Äñc‚Äñ‚Åª¬π := by
    calc 2 * ‚Äñc‚Äñ‚Åª¬π
      _ = 2 * ‚Äñz + (c‚Åª¬π - z)‚Äñ := by rw [‚Üê norm_inv c]; ring_nf
      _ ‚â• 2 * (‚Äñz‚Äñ - ‚Äñc‚Åª¬π - z‚Äñ) := by bound
      _ ‚â• 2 * (‚Äñz‚Äñ - 2‚Åª¬π * ‚Äñz‚Äñ) := by bound
      _ = ‚Äñz‚Äñ := by ring
  have small : ‚Äñz ^ d ^ n‚Äñ < ‚Äñc‚Äñ‚Åª¬π / 4 := by
    have le_p : 2 ‚â§ d ^ n := by
      calc d ^ n
        _ ‚â• 2 ^ n := by bound
        _ ‚â• 2 := Nat.le_self_pow n0 2
    calc ‚Äñz ^ d ^ n‚Äñ
      _ = ‚Äñz‚Äñ * ‚Äñz‚Äñ * ‚Äñz‚Äñ ^ (d ^ n - 2) := by
          rw [‚Üê pow_two, ‚Üê pow_add, Nat.add_sub_cancel' le_p, norm_pow]
      _ ‚â§ 2 * ‚Äñc‚Äñ‚Åª¬π * 80‚Åª¬π * 1 := by bound
      _ = 40‚Åª¬π * ‚Äñc‚Äñ‚Åª¬π := by ring_nf
      _ < 4‚Åª¬π * ‚Äñc‚Äñ‚Åª¬π := by bound
      _ = ‚Äñc‚Äñ‚Åª¬π / 4 := by ring_nf
  generalize hw : z ^ d ^ n = w at small
  have w0 : w ‚â† 0 := by simp [‚Üê hw, n0, z0]
  calc ‚Äñw * (s.ray c w).toComplex - 1‚Äñ
    _ = ‚Äñ(s.ray c w).toComplex - w‚Åª¬π‚Äñ * ‚Äñw‚Äñ := by
        rw [‚Üê norm_mul, sub_mul, inv_mul_cancel‚ÇÄ w0, mul_comm w]
    _ ‚â§ 64 * ‚Äñw‚Äñ := by bound [sray_le (d := d) lt_c (x := w) (by linarith)]
