import Ray.Dynamics.Multibrot.Bottcher

/-!
## B√∂ttcher coordinates form an isomorphism between the exterior Multibrot set and the unit disk

We show that

1. `(c,c)` is postcritical for each `c` not in the Multibrot set.  To see this, note that `0` and
   `‚àû` are the only critical points of `f z = z^d + c`, and `c` is postcritical since it is the
   image of `0` (and thus has smaller potential).
2. Therefore, the diagonal B√∂ttcher map `bottcher d c = s.bottcher c c` is analytic throughout
   the exterior of the Multibrot set.
3. `bottcher d` is nontrivial throughout the exterior of the Multibrot set, as otherwise triviality
   spreads throughout `ùïä`.
4. `bottcher d` bijects from the exterior of the Multibrot set to `ball 0 1`.
5. There is an explicit, analytic homeomorphism `bottcherHomeomorph d` from the exterior of the
   Multibrot set to `ball 0 1`.

Connectivity of the Multibrot set and its complement are easy consequences of (5); see
`Multibrot/Connected.lean` and `Mandelbrot.lean`.
-/

open Complex
open Filter (Tendsto atTop)
open Function (uncurry)
open Metric (ball closedBall isOpen_ball mem_ball_self mem_ball mem_closedBall mem_closedBall_self)
open Real (exp log)
open RiemannSphere
open OneDimension
open Set
open scoped OnePoint RiemannSphere Topology
noncomputable section

variable {c : ‚ÑÇ}

-- We fix `d ‚â• 2`
variable {d : ‚Ñï} [Fact (2 ‚â§ d)]

/-!
## Injectivity of B√∂ttcher coordinates
-/

/-- `bottcher d` is injective.

    We use induction on potential down 0, expressed using closed sets of pairs.  Intuitively,
    1. Near 0, `bottcher d` is injective since it is noncritical.
    2. The set of potentials with an injectivity counterexample is open.
    3. A limit of counterexamples is either already a counterexample, or shows that `bottcher d`
       is critical at the limit.
    4. But every value is repeated near critical points of analytic functions, so in particular
       smaller values are repeated, which gives us a smaller potential counterexample. -/
theorem bottcher_inj : InjOn (bottcher d) (multibrotExt d) := by
  -- We operate by induction on potential down to 0, expressed using closed sets of pairs.
  -- Preliminaries first:
  by_contra bad
  simp only [InjOn, not_forall, ‚Üê ne_eq] at bad
  rcases bad with ‚ü®x, xm, y, ym, bxy, xy‚ü©
  generalize hb : potential d x = b
  have b1 : b < 1 := by rwa [‚Üê hb, potential_lt_one]
  set u := {c | potential d c ‚â§ b}
  set t0 := u √óÀ¢ u
  set t1 := {q : ùïä √ó ùïä | bottcher d q.1 = bottcher d q.2 ‚àß q ‚àà t0}
  set t2 := {q : ùïä √ó ùïä | q.1 ‚â† q.2 ‚àß q ‚àà t1}
  have t2ne : t2.Nonempty := by
    refine ‚ü®‚ü®x, y‚ü©, xy, bxy, ?_, ?_‚ü©
    ¬∑ simp only [mem_setOf, ‚Üê hb, le_refl, u]
    ¬∑ simp only [mem_setOf, ‚Üê hb, ‚Üê norm_bottcher, bxy, le_refl, u]
  clear x xm y ym bxy xy hb
  have ue : u ‚äÜ multibrotExt d := by intro c m; rw [‚Üê potential_lt_one]; exact lt_of_le_of_lt m b1
  have t01 : t1 ‚äÜ t0 := inter_subset_right
  have t12 : t2 ‚äÜ t1 := inter_subset_right
  have uc : IsClosed u := isClosed_le potential_continuous continuous_const
  have t0c : IsClosed t0 := uc.prod uc
  have t1c : IsClosed t1 := by
    rw [isClosed_iff_frequently]; intro ‚ü®x, y‚ü© f
    have m0 : (x, y) ‚àà t0 :=
      Filter.Frequently.mem_of_closed (f.mp (.of_forall fun _ m ‚Ü¶ t01 m)) t0c
    refine ‚ü®tendsto_nhds_unique_of_frequently_eq ?_ ?_
      (f.mp (.of_forall fun _ m ‚Ü¶ m.1)), m0‚ü©
    ¬∑ exact (bottcherMAnalytic d _ (ue m0.1)).continuousAt.comp continuousAt_fst
    ¬∑ exact (bottcherMAnalytic d _ (ue m0.2)).continuousAt.comp continuousAt_snd
  have t12' : closure t2 ‚äÜ t1 := by rw [‚Üê t1c.closure_eq]; exact closure_mono t12
  have t2c' : IsCompact (closure t2) := isClosed_closure.isCompact
  have t2ne' : (closure t2).Nonempty := t2ne.closure
  -- Find the smallest potential which (almost) violates injectivity,
  -- and a pair (x,y) which realizes it
  have pc : Continuous fun q : ùïä √ó ùïä ‚Ü¶ potential d q.1 := potential_continuous.comp continuous_fst
  rcases t2c'.exists_isMinOn t2ne' pc.continuousOn with ‚ü®‚ü®x, y‚ü©, m2, min‚ü©
  simp only [isMinOn_iff] at min
  generalize xp : potential d x = p; rw [xp] at min
  have m1 := t12' m2
  have pb : p ‚â§ b := by rw [‚Üê xp]; exact m1.2.1
  have xm : x ‚àà multibrotExt d := ue m1.2.1
  have ym : y ‚àà multibrotExt d := ue m1.2.2
  have yp : potential d y = p := by rw [‚Üê norm_bottcher, ‚Üê m1.1, norm_bottcher, xp]
  have p0i : p = 0 ‚Üí x = ‚àû ‚àß y = ‚àû := by intro p0; rw [p0, potential_eq_zero] at xp yp; use xp, yp
  -- Split into three cases to find a contradiction
  by_cases xy : x ‚â† y
  ¬∑ -- Case 1: If x ‚â† y, we can move a bit downwards in potential
    have p0 : p ‚â† 0 := by
      contrapose xy; simp only [not_not] at xy ‚ä¢; rcases p0i xy with ‚ü®xi, yi‚ü©; rw [xi, yi]
    have f : ‚àÉ·∂† q : ‚ÑÇ √ó ‚ÑÇ in Filter.map
        (fun q : ùïä √ó ùïä ‚Ü¶ (bottcher d q.1, bottcher d q.2)) (ùìù (x, y)),
        q.1 = q.2 ‚àß ‚Äñq.1‚Äñ < p := by
      rw [nhds_prod_eq, ‚Üê Filter.prod_map_map_eq, ‚Üê (bottcherNontrivial xm).nhds_eq_map_nhds, ‚Üê
        (bottcherNontrivial ym).nhds_eq_map_nhds, m1.1, ‚Üê nhds_prod_eq]
      apply (continuous_id.prodMk continuous_id).continuousAt.frequently
      simp only [true_and, ‚Üê yp, ‚Üê norm_bottcher]; apply frequently_smaller
      rw [‚Üê norm_ne_zero_iff, norm_bottcher, yp]; exact p0
    simp only [Filter.frequently_map] at f
    rcases(f.and_eventually (Ne.eventually_ne xy)).exists with ‚ü®‚ü®v, w‚ü©, ‚ü®bvw, pv‚ü©, vw‚ü©
    simp only [norm_bottcher] at vw bvw pv ‚ä¢
    have pw : potential d w < p := by rwa [‚Üê norm_bottcher, ‚Üê bvw, norm_bottcher]
    have m : (v, w) ‚àà t2 := ‚ü®vw, bvw, le_trans pv.le pb, le_trans pw.le pb‚ü©
    contrapose pv; clear pv; simp only [not_lt]; exact min ‚ü®v, w‚ü© (subset_closure m)
  -- x = y, so we're at a singular point
  simp only [not_not] at xy
  rw [‚Üê xy] at m1 m2 p0i; clear xy ym yp y
  have db : mfderiv I I (bottcher d) x = 0 := by
    contrapose m2; simp only [mem_closure_iff_frequently, Filter.not_frequently]
    refine ((bottcherMAnalytic d _ xm).local_inj m2).mp (.of_forall ?_)
    intro ‚ü®x, y‚ü© inj ‚ü®xy, e, _‚ü©; simp only at xy e inj; exact xy (inj e)
  by_cases p0 : p ‚â† 0
  ¬∑ -- Case 2: At a singular point we're not locally injective,
    -- so we can find a smaller potential value
    rcases not_local_inj_of_mfderiv_zero (bottcherMAnalytic d _ xm) db with ‚ü®r, ra, rx, e‚ü©
    simp only [eventually_nhdsWithin_iff, mem_compl_singleton_iff] at e
    rw [‚Üê xp, ‚Üê norm_bottcher, norm_ne_zero_iff] at p0
    have h := frequently_smaller p0
    rw [(bottcherNontrivial xm).nhds_eq_map_nhds, Filter.frequently_map] at h
    have m : ‚àÉ·∂† z in ùìù x, potential d z < p ‚àß (z, r z) ‚àà t2 := by
      refine h.mp (e.mp (.of_forall fun z e lt ‚Ü¶ ?_))
      have zx : z ‚â† x := by
        contrapose lt; simp only [not_not, not_lt] at lt ‚ä¢; simp only [lt, le_refl]
      rw [norm_bottcher, norm_bottcher, xp] at lt
      rcases e zx with ‚ü®rz, e‚ü©
      refine ‚ü®lt, rz.symm, e.symm, le_trans lt.le pb, ?_‚ü©
      rw [‚Üê norm_bottcher, ‚Üê e, norm_bottcher] at lt; exact le_trans lt.le pb
    rcases m.exists with ‚ü®y, yp, m‚ü©
    linarith [min _ (subset_closure m)]
  ¬∑ -- Case 1: x = ‚àû, which we know is nonsingular
    simp only [not_not] at p0; rw [(p0i p0).1] at db
    exact bottcher_mfderiv_inf_ne_zero db

@[simp] lemma bottcher_coe_ne_zero {c : ‚ÑÇ} (m : ‚Üëc ‚àà multibrotExt d) : bottcher d c ‚â† 0 := by
  rw [‚Üê bottcher_inf (d := d)]
  exact bottcher_inj.ne m (by simp) (by simp)

/-!
## The external ray map, and `bottcherHomeomorph`
-/

lemma ray_exists (d : ‚Ñï) [Fact (2 ‚â§ d)] :
    ‚àÉ g, ContMDiffOnNhd I I g (bottcher d '' multibrotExt d) ‚àß
      ‚àÄ z : ùïä, z ‚àà multibrotExt d ‚Üí g (bottcher d z) = z :=
  global_complex_inverse_fun_open' (bottcherMAnalytic d).contMDiffOn bottcher_inj
    isOpen_multibrotExt

/-- The inverse to `bottcher d`, defining external rays throughout the exterior -/
def ray (d : ‚Ñï) [Fact (2 ‚â§ d)] : ‚ÑÇ ‚Üí ùïä :=
  Classical.choose (ray_exists d)

/-- `ray d` is analytic on `ball 0 1` -/
theorem rayMAnalytic (d : ‚Ñï) [Fact (2 ‚â§ d)] : ContMDiffOnNhd I I (ray d) (ball 0 1) := by
  rw [‚Üê bottcher_surj d]; exact (Classical.choose_spec (ray_exists d)).1

/-- `ray d` is the left inverse to `bottcher d` -/
theorem ray_bottcher {c : ùïä} (m : c ‚àà multibrotExt d) : ray d (bottcher d c) = c :=
  (Classical.choose_spec (ray_exists d)).2 _ m

/-- `ray d` is the right inverse to `bottcher d` -/
theorem bottcher_ray {z : ‚ÑÇ} (m : z ‚àà ball (0 : ‚ÑÇ) 1) : bottcher d (ray d z) = z := by
  rw [‚Üê bottcher_surj d] at m; rcases m with ‚ü®c, m, cz‚ü©
  nth_rw 1 [‚Üê cz]; rw [ray_bottcher m]; exact cz

/-- `ray d` surjects from `ball 0 1` to the exterior of the Multibrot set -/
theorem ray_surj (d : ‚Ñï) [Fact (2 ‚â§ d)] : ray d '' ball 0 1 = multibrotExt d := by
  rw [‚Üê bottcher_surj d]; apply Set.ext; intro c; simp only [‚Üê image_comp, mem_image]; constructor
  ¬∑ intro ‚ü®e, m, ec‚ü©; simp only [Function.comp, ray_bottcher m] at ec; rwa [‚Üê ec]
  ¬∑ intro m; use c, m, ray_bottcher m

/-- `bottcher d` as an (analytic) homeomorphism from `multibrotExt d` to `ball 0 1` -/
def bottcherHomeomorph (d : ‚Ñï) [Fact (2 ‚â§ d)] : PartialHomeomorph ùïä ‚ÑÇ where
  toFun := bottcher d
  invFun := ray d
  source := multibrotExt d
  target := ball 0 1
  map_source' := by intro c m; simp only [‚Üê bottcher_surj d]; exact mem_image_of_mem _ m
  map_target' := by intro z m; simp only [‚Üê ray_surj d]; exact mem_image_of_mem _ m
  left_inv' c m := ray_bottcher m
  right_inv' z m := bottcher_ray m
  open_source := isOpen_multibrotExt
  open_target := isOpen_ball
  continuousOn_toFun := (bottcherMAnalytic d).continuousOn
  continuousOn_invFun := (rayMAnalytic d).continuousOn

lemma ray_inj : InjOn (ray d) (ball (0 : ‚ÑÇ) 1) :=
  (bottcherHomeomorph d).symm.injOn

@[simp] lemma ray_zero : ray d 0 = ‚àû := by
  simpa only [bottcher_inf] using ray_bottcher (d := d) (c := ‚àû) (by simp)

@[simp] lemma ray_ne_zero {z : ‚ÑÇ} (m : z ‚àà ball (0 : ‚ÑÇ) 1) : ray d z ‚â† 0 := by
  have h := (bottcherHomeomorph d).map_target m
  contrapose h
  simp only [ne_eq, not_not] at h
  simp [bottcherHomeomorph, h]

@[simp] lemma ray_eq_inf {z : ‚ÑÇ} (m : z ‚àà ball (0 : ‚ÑÇ) 1) : ray d z = ‚àû ‚Üî z = 0 := by
  rw [‚Üê ray_zero (d := d)]
  exact ray_inj.eq_iff m (by simp)

@[simp] lemma norm_bottcher_lt_one {z : ùïä} (m : z ‚àà multibrotExt d) : ‚Äñbottcher d z‚Äñ < 1 := by
  simpa [bottcherHomeomorph] using (bottcherHomeomorph d).map_source m

/-!
### `inv_ray`: Ray as an analytic function
-/

/-- `ray` as an analytic `‚ÑÇ ‚Üí ‚ÑÇ` function -/
def inv_ray (d : ‚Ñï) [Fact (2 ‚â§ d)] : ‚ÑÇ ‚Üí ‚ÑÇ :=
  fun z ‚Ü¶ (ray d z)‚Åª¬π.toComplex

@[simp] lemma inv_ray_zero : inv_ray d 0 = 0 := by
  simp only [inv_ray, ray_zero, RiemannSphere.inv_inf, toComplex_zero]

@[simp] lemma inv_ray_ne_zero {z : ‚ÑÇ} (z0 : z ‚â† 0) (m : z ‚àà ball (0 : ‚ÑÇ) 1) : inv_ray d z ‚â† 0 := by
  simp only [inv_ray, ne_eq, toComplex_eq_zero, RiemannSphere.inv_eq_zero, inv_eq_inf,
    ray_ne_zero m, or_false]
  rw [‚Üê ray_zero (d := d)]
  exact ray_inj.ne m (by simp) z0

/-- `ray_inv d` is analytic on `ball 0 1` -/
lemma inv_ray_analytic {z : ‚ÑÇ} (m : z ‚àà ball (0 : ‚ÑÇ) 1) : ContDiffAt ‚ÑÇ ‚ä§ (inv_ray d) z := by
  refine ContDiffAt.contDiffWithinAt (ContMDiffAt.contDiffAt ?_)
  refine ((mAnalyticAt_toComplex' ?_).comp _ (mAnalytic_inv _)).comp _ (rayMAnalytic d z m)
  simp [m]

lemma bottcher_inv_inv_ray {z : ‚ÑÇ} (m : z ‚àà ball (0 : ‚ÑÇ) 1) :
    bottcher_inv d (inv_ray d z) = z := by
  simp only [bottcher_inv, inv_ray]
  rw [RiemannSphere.coe_toComplex (by simp [ray_ne_zero m]), inv_inv, bottcher_ray m]

/-- `ray` is monic at `0 ‚Ü¶ ‚àû` -/
lemma ray_hasDerivAt_one : HasDerivAt (inv_ray d) 1 0 := by
  have m : 0 ‚àà ball (0 : ‚ÑÇ) 1 := by simp
  have dr : DifferentiableAt ‚ÑÇ (inv_ray d) 0 := (inv_ray_analytic m).differentiableAt le_top
  have dc : HasDerivAt (fun z ‚Ü¶ bottcher_inv d (inv_ray d z)) 1 0 := by
    apply (hasDerivAt_id _).congr_of_eventuallyEq
    filter_upwards [isOpen_ball.eventually_mem m] with z m
    simp only [bottcher_inv_inv_ray m, id]
  have c := deriv_comp_of_eq (h‚ÇÇ := bottcher_inv d) (h := inv_ray d) 0
    bottcher_hasDerivAt_one.differentiableAt dr (by simp)
  simp only [Function.comp_def, dc.deriv, bottcher_hasDerivAt_one.deriv, one_mul, inv_ray_zero] at c
  exact c ‚ñ∏ dr.hasDerivAt
