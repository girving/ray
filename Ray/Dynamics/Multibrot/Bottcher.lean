import Ray.Dynamics.Multibrot.Postcritical

/-!
## Effective bounds on the Multibrot `bottcher` function

We derive effective bounds and estimates for the BÃ¶ttcher coordinates of the Multibrot sets.  These
are used in `Isomorphism.lean` and `Connected.lean` to prove our main theoretical results.

We mainly need that our diagonal BÃ¶ttcher `bottcher d c` is analytic with derivative 1 at `âˆž`,
by showing that the analytically continued map is given by the infinite product for large `c`.
This does not follow immediately from our dynamical work, which covers only finite `c : â„‚`.  I'm
uneasy that I've missed some basic conceptual arguments that would get to the analyticity result
more directly, though the effective calculations we did along the way are also useful for numerics.

Our main results are:

1. If `16 < abs c â‰¤ abs z`, `s.bottcher = bottcherNear`, and thus the infinite produce holds.
2. If `16 < abs c â‰¤ abs z`, `abs (s.bottcher c z - zâ»Â¹) â‰¤ 16 * (abs z)â»Â¹^2`
3. `bottcher d` is monic at `âˆž` (has derivative 1 there)
-/

open Complex
open Filter (Tendsto)
open Metric (closedBall mem_closedBall mem_closedBall_self)
open Real (exp log)
open RiemannSphere
open OneDimension
open Set
open scoped OnePoint RiemannSphere Topology
noncomputable section

variable {c : â„‚}

-- We fix `d â‰¥ 2`
variable {d : â„•} [Fact (2 â‰¤ d)]

/-- `s.bottcher = bottcherNear` for large `c,z`.
    This means that `s.bottcher` is given by the infinite product formula from `BottcherNear.lean`
    for large `c,z`. -/
theorem bottcher_eq_bottcherNear_z {c z : â„‚} (c16 : 16 < â€–câ€–) (cz : â€–câ€– â‰¤ â€–zâ€–) :
    (superF d).bottcher c z = bottcherNear (fl (f d) âˆž c) d zâ»Â¹ := by
  have c0 : 0 < â€–câ€– := lt_trans (by norm_num) c16
  have z0 : 0 < â€–zâ€– := lt_of_lt_of_le c0 cz
  set s := superF d
  set t := closedBall (0 : â„‚) (â€–câ€–)â»Â¹
  suffices e : EqOn (fun z : â„‚ â†¦ s.bottcher c (z : ð•Š)â»Â¹) (bottcherNear (fl (f d) âˆž c) d) t by
    have z0' : z â‰  0 := norm_ne_zero_iff.mp z0.ne'
    convert @e zâ»Â¹ _; rw [inv_coe (inv_ne_zero z0'), inv_inv]
    simp only [mem_closedBall, Complex.dist_eq, sub_zero, norm_inv, inv_le_invâ‚€ z0 c0, cz, t]
  have a0 : ContMDiffOnNhd I I (fun z : â„‚ â†¦ s.bottcher c (z : ð•Š)â»Â¹) t := by
    intro z m
    refine (s.bottcher_mAnalyticOn _ ?_).along_snd.comp _ (mAnalytic_inv.comp mAnalytic_coe _)
    simp only [mem_closedBall, Complex.dist_eq, sub_zero, t] at m
    by_cases z0 : z = 0; simp only [z0, coe_zero, inv_zero']; exact s.post_a c
    rw [inv_coe z0]; refine postcritical_large (by linarith) ?_
    rwa [norm_inv, le_inv_commâ‚€ c0]; exact norm_pos_iff.mpr z0
  have a1 : ContMDiffOnNhd I I (bottcherNear (fl (f d) âˆž c) d) t := by
    intro z m; apply AnalyticAt.mAnalyticAt
    apply bottcherNear_analytic_z (superNearF d c)
    simp only [mem_setOf, mem_closedBall, Complex.dist_eq, sub_zero, t] at m âŠ¢
    refine lt_of_le_of_lt m ?_
    refine inv_strictAntiâ‚€ (lt_of_lt_of_le (by norm_num) (le_max_left _ _)) ?_
    exact max_lt c16 (half_lt_self (lt_trans (by norm_num) c16))
  refine (a0.eq_of_locally_eq a1 (convex_closedBall _ _).isPreconnected ?_).self_of_nhdsSet
  use 0, mem_closedBall_self (by bound)
  have e : âˆ€á¶  z in ð“ 0, bottcherNear (fl (f d) âˆž c) d z = s.bottcherNear c (z : ð•Š)â»Â¹ := by
    simp only [Super.bottcherNear, extChartAt_inf_apply, inv_inv, toComplex_coe,
      RiemannSphere.inv_inf, toComplex_zero, sub_zero, Super.fl, Filter.eventually_true]
  refine Filter.EventuallyEq.trans ?_ (Filter.EventuallyEq.symm e)
  have i : Tendsto (fun z : â„‚ â†¦ (z : ð•Š)â»Â¹) (ð“ 0) (ð“ âˆž) := by
    have h : ContinuousAt (fun z : â„‚ â†¦ (z : ð•Š)â»Â¹) 0 :=
      (RiemannSphere.continuous_inv.comp continuous_coe).continuousAt
    simp only [ContinuousAt, coe_zero, inv_zero'] at h; exact h
  exact i.eventually (s.bottcher_eq_bottcherNear c)

/-- `bottcher' = bottcherNear` for large `c` -/
theorem bottcher_eq_bottcherNear {c : â„‚} (c16 : 16 < â€–câ€–) :
    bottcher' d c = bottcherNear (fl (f d) âˆž c) d câ»Â¹ :=
  bottcher_eq_bottcherNear_z c16 (le_refl _)

/-- `zâ»Â¹` is in the `superNearC` region for large `c,z` -/
theorem inv_mem_t {c z : â„‚} (c16 : 16 < â€–câ€–) (cz : â€–câ€– â‰¤ â€–zâ€–) :
    zâ»Â¹ âˆˆ {z : â„‚ | â€–zâ€– < (max 16 (â€–câ€– / 2))â»Â¹} := by
  simp only [mem_setOf, norm_inv]
  refine inv_strictAntiâ‚€ (lt_of_lt_of_le (by norm_num) (le_max_left _ _)) ?_
  exact lt_of_lt_of_le (max_lt c16 (half_lt_self (lt_trans (by norm_num) c16))) cz

/-- Terms in the `bottcherNear` product are close to 1 -/
theorem term_approx (d : â„•) [Fact (2 â‰¤ d)] {c z : â„‚} (c16 : 16 < â€–câ€–) (cz : â€–câ€– â‰¤ â€–zâ€–)
    (n : â„•) : â€–term (fl (f d) âˆž c) d n zâ»Â¹ - 1â€– â‰¤ 2 * (1 / 2 : â„) ^ n * â€–zâ€–â»Â¹ := by
  set s := superF d
  have z0 : â€–zâ€– â‰  0 := (lt_of_lt_of_le (lt_trans (by norm_num) c16) cz).ne'
  have i8 : â€–zâ€–â»Â¹ â‰¤ 1 / 8 := by
    rw [one_div]; apply inv_antiâ‚€; norm_num
    exact le_trans (by norm_num) (le_trans c16.le cz)
  have i1 : â€–zâ€–â»Â¹ â‰¤ 1 := le_trans i8 (by norm_num)
  simp only [term]
  have wc := iterates_converge (superNearF d c) n (inv_mem_t c16 cz)
  generalize hw : (fl (f d) âˆž c)^[n] zâ»Â¹ = w; rw [hw] at wc
  replace wc : â€–wâ€– â‰¤ â€–zâ€–â»Â¹ := by
    rw [norm_inv] at wc
    exact le_trans wc (mul_le_of_le_one_left (inv_nonneg.mpr (norm_nonneg _)) (by bound))
  have cw : â€–c * w ^ dâ€– â‰¤ â€–zâ€–â»Â¹ := by
    simp only [norm_mul, norm_pow]
    calc â€–câ€– * â€–wâ€– ^ d
      _ â‰¤ â€–zâ€– * â€–zâ€–â»Â¹ ^ d := by bound
      _ â‰¤ â€–zâ€– * â€–zâ€–â»Â¹ ^ 2 := by bound
      _ = â€–zâ€–â»Â¹ := by rw [pow_two]; field_simp [z0]
  have cw2 : â€–c * w ^ dâ€– â‰¤ 1 / 2 := le_trans cw (le_trans i8 (by norm_num))
  simp only [gl_f, gl]; rw [Complex.inv_cpow, â† Complex.cpow_neg]; swap
  Â· refine (lt_of_le_of_lt (le_abs_self _) (lt_of_le_of_lt ?_ (half_lt_self Real.pi_pos))).ne
    rw [Complex.abs_arg_le_pi_div_two_iff, Complex.add_re, Complex.one_re]
    calc 1 + (c * w ^ d).re
      _ â‰¥ 1 + -|(c * w ^ d).re| := by bound
      _ = 1 - |(c * w ^ d).re| := by ring
      _ â‰¥ 1 - â€–c * w ^ dâ€– := by bound
      _ â‰¥ 1 - 1 / 2 := by linarith
      _ â‰¥ 0 := by norm_num
  Â· have dn : â€–-(1 / ((d ^ (n + 1) : â„•) : â„‚))â€– â‰¤ (1 / 2 : â„) ^ (n + 1) := by
      simp only [Nat.cast_pow, one_div, norm_neg, norm_inv, norm_pow, Complex.norm_natCast, inv_pow]
      bound
    have d1 : â€–-(1 / ((d ^ (n + 1) : â„•) : â„‚))â€– â‰¤ 1 := le_trans dn (by bound)
    refine le_trans (pow_small ?_ d1) ?_
    Â· rw [add_sub_cancel_left]; exact cw2
    Â· rw [add_sub_cancel_left]
      calc 4 * â€–c * w ^ dâ€– * â€–-(1 / ((d ^ (n + 1) : â„•) : â„‚))â€–
        _ â‰¤ 4 * â€–zâ€–â»Â¹ * (1/2 : â„) ^ (n + 1) := by bound
        _ â‰¤ 2 * (1/2 : â„) ^ n * â€–zâ€–â»Â¹ := by
          simp only [pow_succ, â†mul_assoc, mul_comm _ (1/2:â„)]
          ring_nf
          rfl

/-- `s.bottcher c z = zâ»Â¹ + O(zâ»Â¹^2)` -/
theorem bottcher_approx_z (d : â„•) [Fact (2 â‰¤ d)] {c z : â„‚} (c16 : 16 < â€–câ€–) (cz : â€–câ€– â‰¤ â€–zâ€–) :
    â€–(superF d).bottcher c z - zâ»Â¹â€– â‰¤ (16:â„) * â€–zâ€–â»Â¹ ^ 2 := by
  set s := superF d
  have i8 : â€–zâ€–â»Â¹ â‰¤ 1 / 8 := by
    rw [one_div]; apply inv_antiâ‚€; norm_num
    exact le_trans (by norm_num) (le_trans c16.le cz)
  simp only [bottcher_eq_bottcherNear_z c16 cz, bottcherNear, norm_mul, â† mul_sub_one, pow_two,
    â† mul_assoc, norm_inv, mul_comm â€–zâ€–â»Â¹]
  refine mul_le_mul_of_nonneg_right ?_ (inv_nonneg.mpr (norm_nonneg _))
  rcases term_prod_exists (superNearF d c) _ (inv_mem_t c16 cz) with âŸ¨p, hâŸ©
  rw [h.tprod_eq]; simp only [HasProd] at h
  apply le_of_tendsto' (Filter.Tendsto.comp continuous_norm.continuousAt (h.sub_const 1))
  clear h; intro A; simp only [Function.comp_def]
  rw [(by norm_num : (16 : â„) = 4 * 4), mul_assoc]
  refine dist_prod_one_le_abs_sum ?_ (by linarith)
  refine le_trans (Finset.sum_le_sum fun n _ â†¦ term_approx d (by linarith) cz n) ?_
  simp only [mul_comm _ _â»Â¹, â† mul_assoc, â† Finset.mul_sum]
  calc â€–zâ€–â»Â¹ * 2 * A.sum (fun n â†¦ (1/2:â„)^n)
    _ â‰¤ â€–zâ€–â»Â¹ * 2 * (1 - 1 / 2)â»Â¹ := by gcongr; apply partial_geometric_bound; repeat norm_num
    _ = â€–zâ€–â»Â¹ * 4 := by ring

/-- `bottcher' d c = câ»Â¹ + O(câ»Â¹^2)` -/
theorem bottcher_approx (d : â„•) [Fact (2 â‰¤ d)] {c : â„‚} (c16 : 16 < â€–câ€–) :
    â€–bottcher' d c - câ»Â¹â€– â‰¤ 16 * â€–câ€–â»Â¹ ^ 2 :=
  bottcher_approx_z d c16 (le_refl _)

/-- bottcher is monic at `âˆž` (has derivative 1) -/
theorem bottcher_hasDerivAt_one : HasDerivAt (fun z : â„‚ â†¦ bottcher d (â†‘z)â»Â¹) 1 0 := by
  rw [HasDerivAt, HasDerivAtFilter, bottcher, hasFDerivAtFilter_iff_isLittleO, coe_zero, inv_zero',
    fill_inf]
  simp only [sub_zero, ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply,
    smul_eq_mul, mul_one]
  rw [Asymptotics.isLittleO_iff]
  intro k k0; rw [Metric.eventually_nhds_iff]
  refine âŸ¨min 16â»Â¹ (k / 16), by bound, ?_âŸ©; intro z le
  simp only [dist_eq_norm, sub_zero, lt_min_iff] at le
  by_cases z0 : z = 0
  Â· simp only [z0, coe_zero, inv_zero', fill_inf, sub_zero, norm_zero,
      MulZeroClass.mul_zero, le_refl]
  simp only [inv_coe z0, fill_coe]
  have b : â€–bottcher' d zâ»Â¹ - zâ»Â¹â»Â¹â€– â‰¤ (16:â„) * â€–zâ»Â¹â€–â»Â¹ ^ 2 := bottcher_approx d ?_
  Â· simp only [inv_inv] at b; apply le_trans b
    simp only [norm_inv, inv_inv, pow_two, â† mul_assoc]
    refine mul_le_mul_of_nonneg_right ?_ (norm_nonneg _)
    calc 16 * â€–zâ€–
      _ â‰¤ 16 * (k / 16) := by linarith [le.2]
      _ = k := by ring
  Â· rw [norm_inv, lt_inv_commâ‚€ (by norm_num) (norm_pos_iff.mpr z0)]; exact le.1

/-- bottcher is nonsingular at `âˆž` -/
theorem bottcher_mfderiv_inf_ne_zero : mfderiv I I (bottcher d) âˆž â‰  0 := by
  simp only [mfderiv, (bottcherMAnalytic d _ multibrotExt_inf).mdifferentiableAt le_top, if_pos,
    writtenInExtChartAt, bottcher_inf, extChartAt_inf, extChartAt_eq_refl, Function.comp_def,
    PartialEquiv.refl_coe, id, PartialEquiv.trans_apply, Equiv.toPartialEquiv_apply, invEquiv_apply,
    RiemannSphere.inv_inf, coePartialEquiv_symm_apply, toComplex_zero, PartialEquiv.coe_trans_symm,
    PartialEquiv.symm_symm, coePartialEquiv_apply, Equiv.toPartialEquiv_symm_apply, invEquiv_symm,
    ModelWithCorners.Boundaryless.range_eq_univ, fderivWithin_univ]
  rw [bottcher_hasDerivAt_one.hasFDerivAt.fderiv]
  rw [Ne, ContinuousLinearMap.ext_iff, not_forall]; use 1
  simp only [ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply,
    Algebra.id.smul_eq_mul, mul_one]
  convert one_ne_zero; exact NeZero.one
