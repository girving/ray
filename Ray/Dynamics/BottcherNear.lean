module
public import Mathlib.Analysis.Analytic.Basic
public import Mathlib.Analysis.Complex.Basic
public import Mathlib.Analysis.SpecialFunctions.Pow.Complex
public import Ray.Dynamics.Defs
import Mathlib.Analysis.Complex.Exponential
import Mathlib.Analysis.Complex.RemovableSingularity
import Mathlib.Analysis.SpecialFunctions.Complex.Analytic
import Mathlib.Analysis.SpecialFunctions.Complex.Log
import Mathlib.Tactic.Cases
import Mathlib.Topology.Basic
import Ray.Analytic.Analytic
import Ray.Analytic.Products
import Ray.Hartogs.Hartogs
import Ray.Misc.Bound
import Ray.Misc.Bounds
import Ray.Misc.Pow
import Ray.Misc.Topology

/-!
## B√∂ttcher map near a superattracting fixpoint

We define superattracting fixed points at `0` of a parameterized analytic map `f : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ`
(namely a fixed point of order `d ‚â• 2`).  Near the fixpoint, B√∂ttcher coordinates `bottcherNear`
conjugate `f c` to `z ^ d`:

  `bottcherNear c (f c z) = bottcherNear c z ^ d`

This file defines `bottcherNear` locally near `0` using the explicit infinite product formula.
Later in `Potential.lean` we lift to 1D complex manifolds, and `Grow.lean`, `Ray.lean`, and
`Bottcher.lean` analytically continue the map to all postcritical points.

One wart: we require not only that `f c` has a zero of order `d ‚â• 2`, but also that `f c` is
"monic": that the leading coefficient of the Taylor series is `1`.  This slightly simplifies the
formulas, but is probably better to remove.
-/

open Classical
open Complex (exp log cpow)
open Filter (Tendsto atTop)
open Function (curry uncurry)
open Metric (ball closedBall isOpen_ball ball_mem_nhds mem_ball_self nonempty_ball)
open Nat (iterate)
open Set
open scoped NNReal Topology
noncomputable section

section Bottcher

-- All information for a monic superattracting fixed point at the origin
variable {f : ‚ÑÇ ‚Üí ‚ÑÇ}
variable {d : ‚Ñï}
variable {z : ‚ÑÇ}
variable {t : Set ‚ÑÇ}
variable {a b : ‚Ñù}

-- Facts about d
public lemma SuperAt.d0 (s : SuperAt f d) : d ‚â† 0 := by have h := s.d2; omega
public lemma SuperAt.dp (s : SuperAt f d) : 0 < d := lt_of_lt_of_le two_pos s.d2
public lemma SuperAt.drp (s : SuperAt f d) : 0 < (d : ‚Ñù) := Nat.cast_pos.mpr s.dp
public lemma SuperAt.drz (s : SuperAt f d) : (d : ‚Ñù) ‚â† 0 := s.drp.ne'
public lemma SuperAt.dz (s : SuperAt f d) : (d : ‚ÑÇ) ‚â† 0 := Nat.cast_ne_zero.mpr s.dp.ne'
public lemma SuperAt.dr2 (s : SuperAt f d) : 2 ‚â§ (d : ‚Ñù) :=
  le_trans (by norm_num) (Nat.cast_le.mpr s.d2)

-- Teach `bound` that `0 < d` and `2 ‚â§ d`
attribute [bound_forward] SuperAt.d2 SuperAt.dp SuperAt.dr2 SuperNear.toSuperAt SuperNear.b0
  SuperNear.b1

-- More basic inequalities
@[bound_forward] lemma SuperNear.a0 (s : SuperNear f d t a b) : 0 ‚â§ a := by
  simpa only [norm_zero] using s.t2 s.t0

/-- `c` is nonnegative -/
@[bound] public lemma SuperNear.c0 (s : SuperNear f d t a b) : 0 ‚â§ s.c := by
  simp only [SuperNear.c]
  bound
@[bound_forward, bound] public lemma SuperNear.c1 (s : SuperNear f d t a b) : s.c < 1 := s.c1'
@[bound] public lemma SuperNear.c_le_one (s : SuperNear f d t a b) : s.c ‚â§ 1 := by bound

/-- g 0 = 1 -/
theorem g0 {f : ‚ÑÇ ‚Üí ‚ÑÇ} {d : ‚Ñï} : g f d 0 = 1 := by simp only [g, if_true]

/-- Asymptotic bound on `f` based on the order `d` zero -/
theorem SuperAt.approx (s : SuperAt f d) : (fun z ‚Ü¶ f z - z ^ d) =o[ùìù 0] fun z ‚Ü¶ z ^ d := by
  have a := s.fa0.leading_approx
  simp only [s.fd, s.fc, sub_zero, smul_eq_mul, mul_one] at a
  exact a

/-- `f 0 = 0` -/
public theorem SuperAt.f0 (s : SuperAt f d) : f 0 = 0 :=
  haveI p : orderAt f 0 > 0 := by simp [s.fd, s.dp]
  s.fa0.zero_of_order_pos p

/-- `f = z^d g` -/
theorem SuperAt.fg (s : SuperAt f d) (z : ‚ÑÇ) : f z = z ^ d * g f d z := by
  by_cases z0 : z = 0
  ¬∑ simp only [z0, zero_pow s.d0, s.f0, MulZeroClass.zero_mul]
  ¬∑ simp only [g, z0, if_false]; field_simp [z0]

/-- `g` is analytic where `f` is -/
theorem SuperAt.ga_of_fa (s : SuperAt f d) {c : ‚ÑÇ} (fa : AnalyticAt ‚ÑÇ f c) :
    AnalyticAt ‚ÑÇ (g f d) c := by
  rcases fa.exists_ball_analyticOnNhd with ‚ü®r, rp, fa‚ü©
  have o : IsOpen (ball c r) := isOpen_ball
  generalize ht : ball c r = t
  rw [ht] at fa o
  suffices h : AnalyticOnNhd ‚ÑÇ (g f d) t by rw [‚Üê ht] at h; exact h _ (mem_ball_self rp)
  have ga : DifferentiableOn ‚ÑÇ (g f d) (t \ {0}) := by
    have e : ‚àÄ z : ‚ÑÇ, z ‚àà t \ {0} ‚Üí g f d z = f z / z ^ d := by
      intro z zs; simp only [Set.mem_diff, Set.mem_singleton_iff] at zs
      simp only [g, zs.2, if_false]
    rw [differentiableOn_congr e]
    apply DifferentiableOn.div (fa.mono diff_subset).differentiableOn
    exact (Differentiable.pow differentiable_id _).differentiableOn
    intro z zs; exact pow_ne_zero _ (Set.mem_diff_singleton.mp zs).2
  rw [Complex.analyticOnNhd_iff_differentiableOn o]
  by_cases t0 : (0 : ‚ÑÇ) ‚àâ t; ¬∑ rw [Set.diff_singleton_eq_self t0] at ga; exact ga
  simp only [Set.not_notMem] at t0
  have gc : ContinuousAt (g f d) 0 := by
    rw [Metric.continuousAt_iff]; intro e ep
    rcases Metric.eventually_nhds_iff.mp
        (Asymptotics.isBigOWith_iff.mp (s.approx.forall_isBigOWith (by linarith : e / 2 > 0))) with
      ‚ü®t, tp, h‚ü©
    use t, tp; intro z zs; specialize h zs
    simp only [g, Complex.dist_eq]
    by_cases z0 : z = 0; ¬∑ simp only [z0, sub_self, norm_zero]; exact ep
    simp only [z0, if_false, if_true]
    calc ‚Äñf z / z ^ d - 1‚Äñ
      _ = ‚Äñf z * (z ^ d)‚Åª¬π - 1‚Äñ := by rw [div_eq_mul_inv]
      _ = ‚Äñ(f z - z ^ d) * (z ^ d)‚Åª¬π‚Äñ := by
        rw [mul_sub_right_distrib, mul_inv_cancel‚ÇÄ (pow_ne_zero d z0)]
      _ = ‚Äñf z - z ^ d‚Äñ * ‚Äñz ^ d‚Äñ‚Åª¬π := by rw [norm_mul, norm_inv]
      _ ‚â§ e / 2 * ‚Äñz ^ d‚Äñ * ‚Äñz ^ d‚Äñ‚Åª¬π := by bound
      _ = e / 2 * (‚Äñz ^ d‚Äñ * ‚Äñz ^ d‚Äñ‚Åª¬π) := by ring
      _ ‚â§ e / 2 * 1 := by bound
      _ = e / 2 := by ring
      _ < e := half_lt_self ep
  exact (Complex.differentiableOn_compl_singleton_and_continuousAt_iff (o.mem_nhds t0)).mp ‚ü®ga, gc‚ü©

/-- `g` is analytic -/
theorem SuperNear.ga (s : SuperNear f d t a b) : AnalyticOnNhd ‚ÑÇ (g f d) t := fun z m ‚Ü¶
  s.ga_of_fa (s.fa z m)

/-- `SuperAt ‚Üí SuperNear`, manual radius version: if we know a ball where `f` is analytic and
    the resulting `g` is small, then `SuperAt` becomes `SuperNear` -/
theorem SuperAt.super_on_ball (s : SuperAt f d) {r : ‚Ñù} (rp : 0 < r) (r2 : r ‚â§ a) (a0 : 0 < a)
    (a1 : a < 1) (b0 : 0 ‚â§ b) (b1 : b < 1) (c1 : a * (1 + b) < 1)
    (fa : AnalyticOnNhd ‚ÑÇ f (ball 0 r)) (gs : ‚àÄ {z : ‚ÑÇ}, ‚Äñz‚Äñ < r ‚Üí ‚Äñg f d z - 1‚Äñ < b) :
    SuperNear f d (ball 0 r) a b :=
  haveI gs : ‚àÄ {z : ‚ÑÇ}, z ‚â† 0 ‚Üí z ‚àà ball (0 : ‚ÑÇ) r ‚Üí ‚Äñf z / z ^ d - 1‚Äñ ‚â§ b := by
    intro z z0 zs
    simp only [mem_ball_zero_iff] at zs
    specialize gs zs
    simp only [g, z0, if_false] at gs
    exact gs.le
  { d2 := s.d2
    fa0 := s.fa0
    fd := s.fd
    fc := s.fc
    o := isOpen_ball
    t0 := mem_ball_self rp
    gs' := fun z0 ‚Ü¶ gs z0
    a1 := a1
    b0 := b0
    b1 := b1
    c1' := c1
    fa := fa
    t2 := by
      intro z zs
      simp only [mem_ball_zero_iff] at zs
      exact le_trans zs.le r2
    ft := by
      intro z zs; simp only [mem_ball_zero_iff] at zs gs ‚ä¢
      by_cases z0 : z = 0; ¬∑ simp only [z0, s.f0, rp, norm_zero]
      by_cases f0 : f z = 0
      ¬∑ simp only [f0, norm_zero, rp]
      ¬∑ calc ‚Äñf z‚Äñ
          _ = ‚Äñf z / z ^ d‚Äñ * ‚Äñz‚Äñ ^ d := by
            rw [‚Üê norm_pow, ‚Üê norm_mul, div_mul_cancel‚ÇÄ _ (pow_ne_zero d z0)]
          _ < ‚Äñf z / z ^ d‚Äñ * r ^ d := by
            apply mul_lt_mul_of_pos_left (by bound)
            simp only [Complex.norm_div, norm_pow]
            positivity
          _ = ‚Äñf z / z ^ d - 1 + 1‚Äñ * r ^ d := by
            simp only [Complex.norm_div, norm_pow, sub_add_cancel]
          _ ‚â§ (‚Äñf z / z ^ d - 1‚Äñ + ‚Äñ(1 : ‚ÑÇ)‚Äñ) * r ^ d := by bound
          _ ‚â§ (b + ‚Äñ(1 : ‚ÑÇ)‚Äñ) * r ^ d := by bound [gs z0 zs]
          _ ‚â§ (1 + b) * r ^ (d - 1) * r := by
            simp only [mul_assoc, ‚Üê pow_succ, Nat.sub_add_cancel (le_trans one_le_two s.d2), norm_one,
              add_comm b, le_refl]
          _ ‚â§ (1 + b) * a ^ (d - 1) * r := by bound
          _ ‚â§ (1 + b) * a ^ (2 - 1) * r := by bound
          _ = a * (1 + b) * r := by ring
          _ ‚â§ r := by bound }

/-- `SuperAt ‚Üí SuperNear`, automatic radius version: given `SuperAt`, we can find a ball where the
    smallness conditions needed for `SuperNear` hold. -/
public theorem SuperAt.superNear (s : SuperAt f d) : ‚àÉ t, SuperNear f d t (1 / 2) (1 / 4) := by
  rcases s.fa0.exists_ball_analyticOnNhd with ‚ü®r0, r0p, fa‚ü©
  rcases Metric.continuousAt_iff.mp (s.ga_of_fa (fa 0 (mem_ball_self r0p))).continuousAt (1 / 4)
      (by norm_num) with
    ‚ü®r1, r1p, gs‚ü©
  set r := min r0 (min r1 (1 / 2))
  use ball 0 r
  have rp : 0 < r := by bound
  have r2 : r ‚â§ 1 / 2 := le_trans (min_le_right _ _) (min_le_right _ _)
  have rr1 : r ‚â§ r1 := le_trans (min_le_right r0 _) (min_le_left r1 _)
  simp only [g0, dist_zero_right, Complex.dist_eq] at gs
  exact s.super_on_ball rp r2 (b := 1 / 4) (by norm_num) (by norm_num) (by norm_num) (by norm_num)
    (by norm_num) (fa.mono (Metric.ball_subset_ball (min_le_left r0 _))) (fun {z} zr ‚Ü¶
    gs (lt_of_lt_of_le zr rr1))

/-- `g` is small near 0 -/
theorem SuperNear.gs (s : SuperNear f d t a b) {z : ‚ÑÇ} (zt : z ‚àà t) : ‚Äñg f d z - 1‚Äñ ‚â§ b := by
  by_cases z0 : z = 0
  ¬∑ simp only [z0, g0, sub_self, norm_zero, s.b0]
  ¬∑ simp only [g, z0, if_false, s.gs' z0 zt]

/-- `g` is nonzero -/
theorem SuperNear.g_ne_zero (s : SuperNear f d t a b) {z : ‚ÑÇ} (zt : z ‚àà t) : g f d z ‚â† 0 := by
  have h := s.gs zt; contrapose h; simp only [h]; norm_num [s.b1]

/-- `f` is zero only at zero -/
theorem SuperNear.f_ne_zero (s : SuperNear f d t a b) {z : ‚ÑÇ} (zt : z ‚àà t) (z0 : z ‚â† 0) :
    f z ‚â† 0 := by
  simp only [s.fg, mul_ne_zero (pow_ne_zero _ z0) (s.g_ne_zero zt), Ne, not_false_iff]

/-!
## The infinite product

To prove Bottcher's theorem for a monic, superattracting fixpoint, we start with

   `f z = z^d * g z`
   `g 0 = 1`

Ignoring multiple values when taking `d`th roots, we can derive the infinite product

  `(E n z)^(d^n) = f^[n] z`
  `E n z = (f^[n] z)^(1/d^n)`
  `E n z = (f (f^[n-1] z))^(1/d^n)`
  `E n z = (f ((E (n-1) z)^(d^(n-1))))^(1/d^n)`
  `E n z = ((E (n-1) z)^(d^n) * g ((E (n-1) z)^(d^(n-1))))^(1/d^n)`
  `E n z = E (n-1) z * (g ((E (n-1) z)^(d^(n-1))))^(1/d^n)`
  `E n z = E (n-1) z * (g (f^[n-1] z))^(1/d^n)`
  `E n z = z * prod_{1 < k ‚â§ n} (g (f^[k-1] z))^(1/d^k)`
-/

/-- `^d` shifts `term (n+1)` to `term n`:

    `(term n z)^d = (g (f^[n] z) ^ 1/d^(n+1))^d`
    `             = (g (f^[n-1] (f z)) ^ 1/d^n)`
    `             = term (n-1) (f z)` -/
theorem term_eqn (s : SuperNear f d t a b) : ‚àÄ n, term f d n (f z) = term f d (n + 1) z ^ d := by
  intro n
  simp only [term, ‚Üê Function.iterate_succ_apply, pow_mul_nat, div_mul, pow_succ' _ (n + 1),
    mul_div_cancel_left‚ÇÄ _ s.dz, Nat.succ_eq_add_one, Nat.cast_mul]

/-- The analogue of `term_eqn (-1)`:

    `(z * term 0 z)^d = (z * g z ^ 1/d)^d`
    `                 = z^d * g z`
    `                 = f z` -/
theorem term_base (s : SuperNear f d t a b) : f z = (z * term f d 0 z) ^ d := by
  rw [term]; simp only [Function.iterate_zero, id, one_div]
  rw [mul_pow, pow_mul_nat, zero_add, pow_one, inv_mul_cancel‚ÇÄ]
  ¬∑ rw [s.fg]; simp only [Complex.cpow_one]
  ¬∑ simp only [Ne, Nat.cast_eq_zero]
    have := s.d2
    omega

/-- `abs (f z) = abs (z^d * g z) ‚â§ c * (abs z)^d ‚â§ c * abs z` -/
theorem f_converges (s : SuperNear f d t a b) : z ‚àà t ‚Üí ‚Äñf z‚Äñ ‚â§ s.c * ‚Äñz‚Äñ := by
  intro zt
  simp only [s.fg, Complex.norm_mul, norm_pow]
  have gs : ‚Äñg f d z‚Äñ ‚â§ 1 + b := by
    calc ‚Äñg f d z‚Äñ
      _ = ‚Äñg f d z - 1 + 1‚Äñ := by ring_nf
      _ ‚â§ ‚Äñg f d z - 1‚Äñ + ‚Äñ(1 : ‚ÑÇ)‚Äñ := by bound
      _ ‚â§ b + ‚Äñ(1 : ‚ÑÇ)‚Äñ := by linarith [s.gs zt]
      _ ‚â§ (1 + b) := by simp only [norm_one, add_comm, le_refl]
  have az1 : ‚Äñz‚Äñ ‚â§ 1 := le_trans (s.t2 zt) s.a1.le
  calc ‚Äñz‚Äñ ^ d * ‚Äñg f d z‚Äñ
    _ ‚â§ ‚Äñz‚Äñ ^ 2 * (1 + b) := by bound
    _ = ‚Äñz‚Äñ * ‚Äñz‚Äñ * (1 + b) := by ring_nf
    _ ‚â§ a * ‚Äñz‚Äñ * (1 + b) := by bound [s.t2 zt]
    _ = a * (1 + b) * ‚Äñz‚Äñ := by ring

/-- Iterating f remains in t -/
theorem SuperNear.mapsTo (s : SuperNear f d t a b) (n : ‚Ñï) : MapsTo f^[n] t t := by
  induction' n with n h; simp only [Set.mapsTo_id, Function.iterate_zero]
  rw [Function.iterate_succ']; exact s.ft.comp h

/-- `‚Äñf^[n] z‚Äñ ‚â§ s.c ^ n * ‚Äñz‚Äñ` -/
public theorem iterates_converge (s : SuperNear f d t a b) :
    ‚àÄ n, z ‚àà t ‚Üí ‚Äñf^[n] z‚Äñ ‚â§ s.c ^ n * ‚Äñz‚Äñ := by
  intro n zt
  induction' n with n nh
  ¬∑ simp only [Function.iterate_zero, id, pow_zero, one_mul, le_refl]
  ¬∑ rw [Function.iterate_succ']
    trans s.c * ‚Äñf^[n] z‚Äñ
    ¬∑ exact f_converges s (s.mapsTo n zt)
    ¬∑ calc s.c * ‚Äñf^[n] z‚Äñ
        _ ‚â§ s.c * (s.c ^ n * ‚Äñz‚Äñ) := by bound
        _ = s.c * s.c ^ n * ‚Äñz‚Äñ := by ring
        _ = s.c ^ (n + 1) * ‚Äñz‚Äñ := by rw [‚Üê pow_succ']
        _ = s.c ^ n.succ * ‚Äñz‚Äñ := rfl

/-- Iterates are analytic -/
theorem iterates_analytic (s : SuperNear f d t a b) : ‚àÄ n, AnalyticOnNhd ‚ÑÇ f^[n] t := by
  intro n; induction' n with n h
  ¬∑ simp only [Function.iterate_zero]
    exact analyticOnNhd_id
  ¬∑ rw [Function.iterate_succ']
    intro z zt
    exact (s.fa _ (s.mapsTo n zt)).comp (h z zt)

/-- `term` is analytic close to 0 -/
theorem term_analytic (s : SuperNear f d t a b) : ‚àÄ n, AnalyticOnNhd ‚ÑÇ (term f d n) t := by
  intro n z zt
  refine AnalyticAt.cpow ?_ analyticAt_const ?_
  ¬∑ exact (s.ga _ (s.mapsTo n zt)).comp (iterates_analytic s n z zt)
  ¬∑ exact mem_slitPlane_of_near_one (lt_of_le_of_lt (s.gs (s.mapsTo n zt)) s.b1)

@[bound] public lemma SuperNear.kt_nonneg (s : SuperNear f d t a b) : 0 ‚â§ s.kt := by
  unfold kt; bound

/-- term converges to 1 exponentially: `‚Äñterm s n z - 1‚Äñ = O((1 / 2) ^ n)` -/
theorem term_converges (s : SuperNear f d t a b) (n : ‚Ñï) (zt : z ‚àà t) :
    ‚Äñterm f d n z - 1‚Äñ ‚â§ s.kt * (2‚Åª¬π : ‚Ñù) ^ n := by
  rw [term]
  trans psg b 2‚Åª¬π * ‚Äñg f d (f^[n] z) - 1‚Äñ * ‚Äñ(1 / (d ^ (n + 1) : ‚Ñï) : ‚ÑÇ)‚Äñ
  ¬∑ refine pow_small_general ?_ ?_ s.b1
    ¬∑ exact s.gs (s.mapsTo n zt)
    ¬∑ simp only [Nat.cast_pow, one_div, norm_inv, norm_pow, RCLike.norm_natCast]
      calc (d ^ (n + 1) : ‚Ñù)‚Åª¬π
        _ ‚â§ (2 ^ (n + 1))‚Åª¬π := by bound
        _ ‚â§ 2‚Åª¬π := by bound
  ¬∑ simp only [Nat.cast_pow, one_div, ‚Üê div_eq_mul_inv, norm_inv, norm_pow, norm_natCast, inv_pow]
    calc psg b 2‚Åª¬π * ‚Äñg f d (f^[n] z) - 1‚Äñ / d ^ (n + 1)
      _ ‚â§ psg b 2‚Åª¬π * b / 2 ^ (n + 1) := by bound [s.gs (s.mapsTo n zt)]
      _ = s.kt / 2 ^ n := by simp only [SuperNear.kt, pow_succ']; ring

/-- `term` is nonzero, sufficiently close to 0 -/
theorem term_nonzero (s : SuperNear f d t a b) : ‚àÄ n, z ‚àà t ‚Üí term f d n z ‚â† 0 := by
  intro n zt
  simp [term, s.g_ne_zero (s.mapsTo n zt)]

/-- The `term` product exists and is analytic -/
theorem term_prod (s : SuperNear f d t a b) :
    ProdExistsOn (term f d) t ‚àß AnalyticOnNhd ‚ÑÇ (tprodOn (term f d)) t ‚àß
      ‚àÄ z ‚àà t, tprodOn (term f d) z ‚â† 0 := by
  have a0 : 0 ‚â§ (2‚Åª¬π : ‚Ñù) := by norm_num
  obtain ‚ü®p, a, nz‚ü© := fast_products_converge_eventually' s.o a0 (by linarith) (term_analytic s)
    (.of_forall fun n z zt ‚Ü¶ term_converges s n zt)
  exact ‚ü®p, a, fun z zt ‚Ü¶ nz z zt (fun n ‚Ü¶ term_nonzero s n zt)‚ü©

/-- The `term` product exists -/
public theorem term_prod_exists (s : SuperNear f d t a b) : ProdExistsOn (term f d) t :=
  (term_prod s).1

/-- The `term` product is analytic in `z` -/
theorem term_prod_analytic_z (s : SuperNear f d t a b) : AnalyticOnNhd ‚ÑÇ (tprodOn (term f d)) t :=
  (term_prod s).2.1

/-- The `term` product is nonzero -/
theorem term_prod_ne_zero (s : SuperNear f d t a b) (zt : z ‚àà t) : tprodOn (term f d) z ‚â† 0 :=
  (term_prod s).2.2 _ zt

/-- `bottcherNear` satisfies `b (f z) = (b z)^d` near 0 -/
public theorem bottcherNear_eqn (s : SuperNear f d t a b) (zt : z ‚àà t) :
    bottcherNear f d (f z) = bottcherNear f d z ^ d := by
  simp_rw [bottcherNear]
  have pe := (term_prod_exists s) z zt
  simp only [mul_pow, product_pow' pe]
  have pe : ProdExists fun n ‚Ü¶ term f d n z ^ d := by
    rcases pe with ‚ü®g, hg‚ü©; exact ‚ü®_, product_pow d hg‚ü©
  simp only [product_split pe, ‚Üê term_eqn s, ‚Üê mul_assoc, ‚Üê mul_pow, ‚Üê term_base s]

/-- `bottcherNear_eqn`, iterated -/
public theorem bottcherNear_eqn_iter (s : SuperNear f d t a b) (zt : z ‚àà t) (n : ‚Ñï) :
    bottcherNear f d (f^[n] z) = bottcherNear f d z ^ d ^ n := by
  induction' n with n h; simp only [Function.iterate_zero, id, pow_zero, pow_one]
  simp only [Function.comp, Function.iterate_succ', pow_succ, pow_mul,
    bottcherNear_eqn s (s.mapsTo n zt), h]

/-- `f^[n] 0 = 0` -/
theorem iterates_at_zero (s : SuperNear f d t a b) : ‚àÄ n, f^[n] 0 = 0 := by
  intro n; induction' n with n h; simp only [Function.iterate_zero, id]
  simp only [Function.iterate_succ', Function.comp_apply, h, s.f0]

/-- `term s n 0 = 1` -/
theorem term_at_zero (s : SuperNear f d t a b) (n : ‚Ñï) : term f d n 0 = 1 := by
  simp only [term, iterates_at_zero s, g0, Complex.one_cpow]

/-- `prod (term s _ 0) = 1` -/
theorem term_prod_at_zero (s : SuperNear f d t a b) : tprodOn (term f d) 0 = 1 := by
  simp_rw [tprodOn, term_at_zero s, tprod_one]

/-- `bottcherNear' 0 = 1` (so in particular `bottcherNear` is a local isomorphism) -/
public theorem bottcherNear_monic (s : SuperNear f d t a b) :
    HasDerivAt (bottcherNear f d) 1 0 := by
  have dz : HasDerivAt (fun z : ‚ÑÇ ‚Ü¶ z) 1 0 := hasDerivAt_id 0
  have db := HasDerivAt.mul dz (term_prod_analytic_z s 0 s.t0).differentiableAt.hasDerivAt
  simp only [one_mul, MulZeroClass.zero_mul, add_zero] at db
  rw [term_prod_at_zero s] at db; exact db

/-- `bottcherNear 0 = 0` -/
@[simp] public lemma bottcherNear_zero : bottcherNear f d 0 = 0 := by
  simp only [bottcherNear, MulZeroClass.zero_mul]

/-- `z ‚â† 0 ‚Üí bottcherNear z ‚â† 0` -/
public theorem bottcherNear_ne_zero (s : SuperNear f d t a b) :
    z ‚àà t ‚Üí z ‚â† 0 ‚Üí bottcherNear f d z ‚â† 0 :=
  fun zt z0 ‚Ü¶ mul_ne_zero z0 (term_prod_ne_zero s zt)

/-- `bottcherNear` is analytic in `z` -/
public theorem bottcherNear_analytic_z (s : SuperNear f d t a b) :
    AnalyticOnNhd ‚ÑÇ (bottcherNear f d) t :=
  analyticOnNhd_id.mul (term_prod_analytic_z s)

/-- `f^[n] z ‚Üí 0` -/
public theorem iterates_tendsto (s : SuperNear f d t a b) (zt : z ‚àà t) :
    Tendsto (fun n ‚Ü¶ f^[n] z) atTop (ùìù 0) := by
  by_cases z0 : z = 0; simp only [z0, iterates_at_zero s, tendsto_const_nhds]
  rw [Metric.tendsto_atTop]; intro e ep
  simp only [Complex.dist_eq, sub_zero]
  have xp : e / ‚Äñz‚Äñ > 0 := div_pos ep (norm_pos_iff.mpr z0)
  rcases exists_pow_lt_of_lt_one xp s.c1 with ‚ü®N, Nb‚ü©
  simp only [lt_div_iff‚ÇÄ (norm_pos_iff.mpr z0)] at Nb
  use N; intro n nN
  refine lt_of_le_of_lt (iterates_converge s n zt) (lt_of_le_of_lt ?_ Nb)
  bound

/-- `bottcherNear < 1` -/
public theorem bottcherNear_lt_one (s : SuperNear f d t a b) (zt : z ‚àà t) :
    ‚ÄñbottcherNear f d z‚Äñ < 1 := by
  rcases Metric.continuousAt_iff.mp (bottcherNear_analytic_z s _ s.t0).continuousAt 1 zero_lt_one
    with ‚ü®r, rp, rs‚ü©
  simp only [Complex.dist_eq, sub_zero, bottcherNear_zero] at rs
  have b' : ‚àÄ·∂† n in atTop, ‚ÄñbottcherNear f d (f^[n] z)‚Äñ < 1 := by
    refine (Metric.tendsto_nhds.mp (iterates_tendsto s zt) r rp).mp (.of_forall fun n h ‚Ü¶ ?_)
    rw [Complex.dist_eq, sub_zero] at h; exact rs h
  rcases b'.exists with ‚ü®n, b‚ü©
  contrapose b; simp only [not_lt] at b ‚ä¢
  simp only [bottcherNear_eqn_iter s zt n, norm_pow, one_le_pow‚ÇÄ b]

/-- Linear bound on `bottcherNear` -/
public theorem bottcherNear_le (s : SuperNear f d t a b) (zt : z ‚àà t) :
    ‚ÄñbottcherNear f d z‚Äñ ‚â§ s.k * ‚Äñz‚Äñ := by
  simp only [bottcherNear, norm_mul]; rw [mul_comm]
  refine mul_le_mul_of_nonneg_right ?_ (norm_nonneg _)
  rcases term_prod_exists s _ zt with ‚ü®p, h‚ü©; rw [h.tprod_eq]; simp only [HasProd] at h
  apply le_of_tendsto' (Filter.Tendsto.comp continuous_norm.continuousAt h)
  intro A
  clear h
  simp only [Function.comp, norm_prod]
  have tb : ‚àÄ n, ‚Äñterm f d n z‚Äñ ‚â§ 1 + s.kt * (1 / 2 : ‚Ñù) ^ n := by
    intro n
    calc ‚Äñterm f d n z‚Äñ
      _ = ‚Äñ1 + (term f d n z - 1)‚Äñ := by ring_nf
      _ ‚â§ ‚Äñ(1 : ‚ÑÇ)‚Äñ + ‚Äñterm f d n z - 1‚Äñ := by bound
      _ = 1 + ‚Äñterm f d n z - 1‚Äñ := by norm_num
      _ ‚â§ 1 + s.kt * (1 / 2 : ‚Ñù) ^ n := by bound [term_converges s n zt]
  have p : ‚àÄ n : ‚Ñï, 0 < (1 : ‚Ñù) + s.kt * (1 / 2 : ‚Ñù) ^ n :=
    fun _ ‚Ü¶ by apply add_pos_of_pos_of_nonneg <;> bound
  have lb : ‚àÄ n : ‚Ñï, Real.log ((1 : ‚Ñù) + s.kt * (1 / 2 : ‚Ñù) ^ n) ‚â§ s.kt * (1 / 2 : ‚Ñù) ^ n :=
    fun n ‚Ü¶ le_trans (Real.log_le_sub_one_of_pos (p n)) (le_of_eq (by ring))
  refine le_trans (Finset.prod_le_prod (fun _ _ ‚Ü¶ norm_nonneg _) fun n _ ‚Ü¶ tb n) ?_
  rw [‚Üê Real.exp_log (Finset.prod_pos fun n _ ‚Ü¶ p n), Real.log_prod fun n _ ‚Ü¶ (p n).ne']
  refine le_trans (Real.exp_le_exp.mpr (Finset.sum_le_sum fun n _ ‚Ü¶ lb n)) ?_
  have geom := partial_scaled_geometric_bound (s.kt).toNNReal A one_half_pos.le
    one_half_lt_one
  simp only [Real.coe_toNNReal', s.kt_nonneg, sup_of_le_left,
    (by norm_num : (1 - 1 / 2 : ‚Ñù)‚Åª¬π = 2)] at geom
  refine le_trans (Real.exp_le_exp.mpr geom) (le_of_eq ?_)
  simp only [SuperNear.k, mul_comm]

end Bottcher

-- Next we prove that everything is analytic in an additional function parameter
section BottcherC

variable {f : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ}
variable {d : ‚Ñï}
variable {u : Set ‚ÑÇ}
variable {t : Set (‚ÑÇ √ó ‚ÑÇ)}
variable {a b : ‚Ñù}

/-- `SuperNearC ‚Üí SuperNear` at `p ‚àà t` -/
theorem SuperNearC.ts (s : SuperNearC f d u t a b) {p : ‚ÑÇ √ó ‚ÑÇ} (m : p ‚àà t) :
    SuperNear (f p.1) d {z | (p.1, z) ‚àà t} a b :=
  s.s (s.tc m)

/-- The parameter set `u` is open -/
theorem SuperNearC.ou (s : SuperNearC f d u t a b) : IsOpen u := by
  have e : u = Prod.fst '' t := by
    ext c; simp only [Set.mem_image, Prod.exists, exists_and_right, exists_eq_right]
    exact ‚ü®fun m ‚Ü¶ ‚ü®0, (s.s m).t0‚ü©, fun h ‚Ü¶ Exists.elim h fun z m ‚Ü¶ s.tc m‚ü©
  rw [e]; exact isOpenMap_fst _ s.o

/-- `SuperNearC ‚Üí SuperAtC` -/
public theorem SuperNearC.superAtC (s : SuperNearC f d u t a b) : SuperAtC f d u :=
  { o := s.ou
    s := by
      intro c m; have s := s.s m
      exact
        { d2 := s.d2
          fa0 := s.fa0
          fd := s.fd
          fc := s.fc }
    fa := fun {c} m ‚Ü¶ s.fa _ (s.s m).t0 }

/-- A Two-parameter version of `g` -/
def g2 (f : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ) (d : ‚Ñï) := fun p : ‚ÑÇ √ó ‚ÑÇ ‚Ü¶ g (f p.1) d p.2

/-- `g2` is jointly analytic where `f` is -/
theorem SuperAtC.ga_of_fa (s : SuperAtC f d u) {t : Set (‚ÑÇ √ó ‚ÑÇ)} (o : IsOpen t)
    (fa : AnalyticOnNhd ‚ÑÇ (uncurry f) t) (tc : ‚àÄ {p : ‚ÑÇ √ó ‚ÑÇ}, p ‚àà t ‚Üí p.1 ‚àà u) :
    AnalyticOnNhd ‚ÑÇ (g2 f d) t := by
  refine Pair.hartogs o ?_ ?_
  ¬∑ intro c z m
    simp only [g2, g]
    by_cases zero : z = 0; ¬∑ simp only [zero, if_true]; exact analyticAt_const
    ¬∑ simp only [zero, if_false]; refine AnalyticAt.div ?_ analyticAt_const (pow_ne_zero _ zero)
      refine (fa _ ?_).comp‚ÇÇ analyticAt_id analyticAt_const; exact m
  ¬∑ intro c z m; apply (s.s (tc m)).ga_of_fa
    refine (fa _ ?_).comp‚ÇÇ analyticAt_const analyticAt_id; exact m

/-- `g2` is jointly analytic -/
theorem SuperNearC.ga (s : SuperNearC f d u t a b) : AnalyticOnNhd ‚ÑÇ (g2 f d) t :=
  s.superAtC.ga_of_fa s.o s.fa fun {_} m ‚Ü¶ s.tc m

/-- `SuperNearC` commutes with unions -/
theorem SuperNearC.union {I : Type} {u : I ‚Üí Set ‚ÑÇ} {t : I ‚Üí Set (‚ÑÇ √ó ‚ÑÇ)}
    (s : ‚àÄ i, SuperNearC f d (u i) (t i) a b) : SuperNearC f d (‚ãÉ i, u i) (‚ãÉ i, t i) a b := by
  set tu := ‚ãÉ i, t i
  have o : IsOpen tu := isOpen_iUnion fun i ‚Ü¶ (s i).o
  have sm : ‚àÄ {c z : ‚ÑÇ},
      (c, z) ‚àà tu ‚Üí ‚àÉ u, z ‚àà u ‚àß u ‚äÜ {z | (c, z) ‚àà tu} ‚àß SuperNear (f c) d u a b := by
    intro c z m; rcases Set.mem_iUnion.mp m with ‚ü®i, m‚ü©; use{z | (c, z) ‚àà t i}
    simp only [Set.mem_setOf_eq, m, Set.mem_iUnion, Set.setOf_subset_setOf, true_and, tu]
    constructor
    ¬∑ exact fun z m ‚Ü¶ ‚ü®i, m‚ü©
    ¬∑ exact (s i).s ((s i).tc m)
  exact
    { o
      tc := by
        intro p m; rcases Set.mem_iUnion.mp m with ‚ü®i, m‚ü©
        exact Set.subset_iUnion _ i ((s i).tc m)
      fa := by intro p m; rcases Set.mem_iUnion.mp m with ‚ü®i, m‚ü©; exact (s i).fa _ m
      s := by
        intro c m; rcases Set.mem_iUnion.mp m with ‚ü®i, m‚ü©; have s := (s i).s m
        exact
          { d2 := s.d2
            fa0 := s.fa0
            fd := s.fd
            fc := s.fc
            o := o.snd_preimage c
            a1 := s.a1
            b0 := s.b0
            b1 := s.b1
            c1' := s.c1'
            t0 := Set.subset_iUnion _ i s.t0
            t2 := by intro z m; rcases sm m with ‚ü®u, m, _, s‚ü©; exact s.t2 m
            fa := by intro z m; rcases sm m with ‚ü®u, m, _, s‚ü©; exact s.fa _ m
            ft := by intro z m; rcases sm m with ‚ü®u, m, us, s‚ü©; exact us (s.ft m)
            gs' := by intro z z0 m; rcases sm m with ‚ü®u, m, _, s‚ü©; exact s.gs' z0 m } }

/-- `SuperAtC ‚Üí SuperNearC`, staying inside `w` -/
public theorem SuperAtC.superNearC' (s : SuperAtC f d u) {w : Set (‚ÑÇ √ó ‚ÑÇ)} (wo : IsOpen w)
    (wc : ‚àÄ c, c ‚àà u ‚Üí (c, (0 : ‚ÑÇ)) ‚àà w) : ‚àÉ t, t ‚äÜ w ‚àß SuperNearC f d u t (1 / 2) (1 / 4) := by
  have h : ‚àÄ c, c ‚àà u ‚Üí
      ‚àÉ r, r > 0 ‚àß ball c r ‚äÜ u ‚àß ball (c, 0) r ‚äÜ w ‚àß
        SuperNearC f d (ball c r) (ball (c, 0) r) (1 / 2) (1 / 4) := by
    intro c m
    rcases(s.fa m).exists_ball_analyticOnNhd with ‚ü®r0, r0p, fa‚ü©
    rcases Metric.isOpen_iff.mp s.o c m with ‚ü®r1, r1p, rc‚ü©
    set r2 := min r0 r1
    have fa := fa.mono (Metric.ball_subset_ball (min_le_left r0 r1))
    have rc : ball c r2 ‚äÜ u := le_trans (Metric.ball_subset_ball (by bound)) rc
    have ga := s.ga_of_fa isOpen_ball fa
        (by intro p m; simp only [‚Üê ball_prod_same, Set.mem_prod] at m; exact rc m.1)
    rcases Metric.isOpen_iff.mp wo (c, 0) (wc c m) with ‚ü®r3, r3p, rw‚ü©
    rcases Metric.continuousAt_iff.mp (ga (c, 0) (mem_ball_self (by bound))).continuousAt
        (1 / 4) (by norm_num) with ‚ü®r4, r4p, gs‚ü©
    set r := min (min r2 r3) (min r4 (1 / 2))
    have rp : 0 < r := by bound
    have rh : r ‚â§ 1 / 2 := le_trans (min_le_right _ _) (min_le_right _ _)
    have rr4 : r ‚â§ r4 := le_trans (min_le_right _ _) (min_le_left r4 _)
    have rc : ball c r ‚äÜ u := le_trans (Metric.ball_subset_ball (by bound)) rc
    have rw : ball (c, 0) r ‚äÜ w :=
      _root_.trans (Metric.ball_subset_ball (le_trans (min_le_left _ _) (min_le_right _ _))) rw
    use r, rp, rc, rw
    exact
      { o := isOpen_ball
        tc := by
          intro p m; simp only [‚Üê ball_prod_same, Set.mem_prod] at m
          exact Metric.ball_subset_ball (by linarith) m.1
        s := by
          intro c' m; simp only [‚Üê ball_prod_same, Set.mem_prod, m, true_and]
          apply (s.s (rc m)).super_on_ball rp rh (by norm_num) (by norm_num) (by norm_num)
              (by norm_num) (by norm_num)
          ¬∑ apply fa.comp‚ÇÇ analyticOnNhd_const analyticOnNhd_id
            intro z zm; apply Metric.ball_subset_ball (by bound : r ‚â§ r2)
            simp only [‚Üê ball_prod_same, Set.mem_prod, m, true_and]; exact zm
          ¬∑ simp only [Complex.dist_eq, Prod.dist_eq, sub_zero, max_lt_iff, and_imp, g2, g0] at gs
            simp only [Metric.mem_ball, Complex.dist_eq] at m
            intro z zr; exact @gs ‚ü®c', z‚ü© (lt_of_lt_of_le m rr4) (lt_of_lt_of_le zr rr4)
        fa := fa.mono (Metric.ball_subset_ball (min_le_of_left_le (min_le_left _ _))) }
  set r := fun c : u ‚Ü¶ choose (h _ c.mem)
  set v := fun c : u ‚Ü¶ ball (c : ‚ÑÇ) (r c)
  set t := fun c : u ‚Ü¶ ball ((c : ‚ÑÇ), (0 : ‚ÑÇ)) (r c)
  use‚ãÉ c : u, t c
  have e : u = ‚ãÉ c : u, v c := by
    apply Set.ext; intro c; rw [Set.mem_iUnion]; constructor
    ¬∑ intro m; use‚ü®c, m‚ü©; rcases choose_spec (h c m) with ‚ü®rp, _, _‚ü©
      exact mem_ball_self rp
    ¬∑ intro m; rcases m with ‚ü®i, m‚ü©; rcases choose_spec (h _ i.mem) with ‚ü®_, us, _‚ü©
      exact us m
  have tw : (‚ãÉ c : u, t c) ‚äÜ w := by
    apply Set.iUnion_subset; intro i; rcases choose_spec (h _ i.mem) with ‚ü®_, _, rw, _‚ü©; exact rw
  have si : ‚àÄ c : u, SuperNearC f d (v c) (t c) (1 / 2) (1 / 4) := by
    intro i; rcases choose_spec (h _ i.mem) with ‚ü®_, _, _, s‚ü©; exact s
  have s := SuperNearC.union si; simp only at s; rw [‚Üê e] at s
  exact ‚ü®tw, s‚ü©

/-- `SuperAtC ‚Üí SuperNearC` -/
theorem SuperAtC.superNearC (s : SuperAtC f d u) : ‚àÉ t, SuperNearC f d u t (1 / 2) (1 / 4) := by
  rcases s.superNearC' isOpen_univ fun _ _ ‚Ü¶ Set.mem_univ _ with ‚ü®t, _, s‚ü©; exact ‚ü®t, s‚ü©

theorem iterates_analytic_c (s : SuperNearC f d u t a b) {c z : ‚ÑÇ} (n : ‚Ñï) (m : (c, z) ‚àà t) :
    AnalyticAt ‚ÑÇ (fun c ‚Ü¶ (f c)^[n] z) c := by
  induction' n with n nh; ¬∑ simp only [Function.iterate_zero, id]; exact analyticAt_const
  ¬∑ simp_rw [Function.iterate_succ']; simp only [Function.comp_apply]
    refine (s.fa _ ?_).comp (analyticAt_id.prod nh)
    exact (s.ts m).mapsTo n m

theorem term_analytic_c (s : SuperNearC f d u t a b) {c z : ‚ÑÇ} (n : ‚Ñï) (m : (c, z) ‚àà t) :
    AnalyticAt ‚ÑÇ (fun c ‚Ü¶ term (f c) d n z) c := by
  refine AnalyticAt.cpow ?_ analyticAt_const ?_
  ¬∑ have e : (fun c ‚Ü¶ g (f c) d ((f c)^[n] z)) = fun c ‚Ü¶ g2 f d (c, (f c)^[n] z) := rfl
    rw [e]
    refine (s.ga _ ?_).comp ?_
    ¬∑ exact (s.ts m).mapsTo n m
    ¬∑ apply analyticAt_id.prod (iterates_analytic_c s n m)
  ¬∑ refine mem_slitPlane_of_near_one ?_
    exact lt_of_le_of_lt ((s.ts m).gs ((s.ts m).mapsTo n m)) (s.ts m).b1

/-- `term` prod is analytic in `c` -/
theorem term_prod_analytic_c (s : SuperNearC f d u t a b) {c z : ‚ÑÇ} (m : (c, z) ‚àà t) :
    AnalyticAt ‚ÑÇ (fun c ‚Ü¶ tprod fun n ‚Ü¶ term (f c) d n z) c := by
  have a0 : 0 ‚â§ (2‚Åª¬π : ‚Ñù) := by norm_num
  set t' := {c | (c, z) ‚àà t}
  have o' : IsOpen t' := s.o.preimage (by continuity)
  refine (fast_products_converge_eventually' o' a0 (by linarith) ?_
    (.of_forall fun n c m ‚Ü¶ term_converges (s.ts m) n m)).2.1 _ m
  exact fun n c m ‚Ü¶ term_analytic_c s n m

/-- `term` prod is jointly analytic (using Hartogs's theorem for simplicity) -/
theorem term_prod_analytic (s : SuperNearC f d u t a b) :
    AnalyticOnNhd ‚ÑÇ (fun p : ‚ÑÇ √ó ‚ÑÇ ‚Ü¶ tprod fun n ‚Ü¶ term (f p.1) d n p.2) t := by
  refine Pair.hartogs s.o ?_ ?_
  ¬∑ intro c z m; simp only; exact term_prod_analytic_c s m
  ¬∑ intro c z m; simp only; exact term_prod_analytic_z (s.ts m) _ m

/-- `bottcherNear` is analytic in `c` -/
public theorem bottcherNear_analytic_c (s : SuperNearC f d u t a b) {c z : ‚ÑÇ} (m : (c, z) ‚àà t) :
    AnalyticAt ‚ÑÇ (fun c ‚Ü¶ bottcherNear (f c) d z) c :=
  analyticAt_const.mul (term_prod_analytic_c s m)

/-- `bottcherNear` is jointly analytic -/
public theorem bottcherNear_analytic (s : SuperNearC f d u t a b) :
    AnalyticOnNhd ‚ÑÇ (fun p : ‚ÑÇ √ó ‚ÑÇ ‚Ü¶ bottcherNear (f p.1) d p.2) t := fun _ m ‚Ü¶
  analyticAt_snd.mul (term_prod_analytic s _ m)

/-- `deriv f` is nonzero away from 0 -/
public theorem df_ne_zero (s : SuperNearC f d u t a b) {c : ‚ÑÇ} (m : c ‚àà u) :
    ‚àÄ·∂† p : ‚ÑÇ √ó ‚ÑÇ in ùìù (c, 0), deriv (f p.1) p.2 = 0 ‚Üî p.2 = 0 := by
  have df : ‚àÄ e z, (e, z) ‚àà t ‚Üí
      deriv (f e) z = ‚Üëd * z ^ (d - 1) * g (f e) d z + z ^ d * deriv (g (f e) d) z := by
    intro e z m; apply HasDerivAt.deriv
    have fg : f e = fun z ‚Ü¶ z ^ d * g (f e) d z := by funext; rw [(s.ts m).fg]
    nth_rw 1 [fg]
    apply HasDerivAt.mul; apply hasDerivAt_pow
    rw [hasDerivAt_deriv_iff]; exact ((s.ts m).ga _ m).differentiableAt
  have small : ‚àÄ·∂† p : ‚ÑÇ √ó ‚ÑÇ in ùìù (c, 0),
      ‚Äñp.2 * deriv (g (f p.1) d) p.2‚Äñ < ‚Äñ‚Üëd * g (f p.1) d p.2‚Äñ := by
    have ga : AnalyticAt ‚ÑÇ (uncurry fun c z ‚Ü¶ g (f c) d z) (c, 0) := s.ga _ (s.s m).t0
    apply ContinuousAt.eventually_lt
    ¬∑ exact continuous_norm.continuousAt.comp (continuousAt_snd.mul ga.deriv2.continuousAt)
    ¬∑ exact continuous_norm.continuousAt.comp (continuousAt_const.mul ga.continuousAt)
    ¬∑ simp only [g0, MulZeroClass.zero_mul, norm_zero, Complex.norm_natCast, mul_one, Nat.cast_pos]
      exact (s.s m).dp
  apply small.mp
  apply (s.o.eventually_mem (s.s m).t0).mp
  refine .of_forall ?_; clear small
  intro ‚ü®e, w‚ü© m' small; simp only [df _ _ m'] at small ‚ä¢
  nth_rw 4 [‚Üê Nat.sub_add_cancel (Nat.succ_le_of_lt (s.s m).dp)]
  simp only [pow_add, pow_one, mul_comm _ (w ^ (d - 1)), mul_assoc (w ^ (d - 1)) _ _, ‚Üê
    left_distrib, mul_eq_zero, pow_eq_zero_iff (Nat.sub_pos_of_lt (s.s m).d2).ne']
  exact or_iff_left (add_ne_zero_of_abs_lt small)

end BottcherC
