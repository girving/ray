import Mathlib.Algebra.Order.Floor.Div
import Mathlib.Data.Real.Basic
import Ray.Approx.Bool
import Ray.Misc.Real

/-!
## `‚Ñö` machinery
-/

open Set

lemma Rat.abs_eq_div {x : ‚Ñö} : |x| = (x.num.natAbs : ‚Ñö) / x.den := by
  nth_rw 1 [‚ÜêRat.num_div_den x]
  have d0 : 0 < (x.den : ‚Ñö) := Nat.cast_pos.mpr x.den_pos
  rw [abs_div, abs_of_pos d0, ‚ÜêInt.cast_abs, Int.abs_eq_natAbs, Int.cast_Nat_cast]

lemma Rat.abs_eq_div' {ùïú : Type} [LinearOrderedField ùïú] {x : ‚Ñö} :
    (|x| : ùïú) = (x.num.natAbs : ùïú) / x.den := by
  nth_rw 1 [‚ÜêRat.num_div_den x]
  have d0 : 0 < (x.den : ùïú) := Nat.cast_pos.mpr x.den_pos
  simp only [cast_div, cast_coe_int, cast_coe_nat, abs_div, abs_of_pos d0, ‚ÜêInt.cast_abs,
    Int.abs_eq_natAbs, Int.cast_Nat_cast]

/-- `n` s.t. `2^n ‚â§ |x| < 2^(n+1)` if `n ‚â† 0` -/
@[irreducible] def Rat.log2 (x : ‚Ñö) : ‚Ñ§ :=
  -- Reduce to two possible answers
  let n := x.num.natAbs
  let a := n.log2  -- `2^a ‚â§ n < 2^(a+1)`
  let b := x.den.log2  -- `2^b ‚â§ d < 2^(b+1)`
  -- `2^(a-b-1) < n/d < 2^(a+1-b)`, so the answer is either `a-b-1` or `a-b`
  -- If `2^(a-b) ‚â§ n/d` then `a-b`, otherwise `a-b-1`
  let g : ‚Ñ§ := a - b
  let good := bif 0 ‚â§ g then decide (x.den <<< g.toNat ‚â§ n) else decide (x.den ‚â§ n <<< (-g).toNat)
  bif good then g else g - 1

lemma Rat.log2_correct {x : ‚Ñö} (x0 : x ‚â† 0) : |x| ‚àà Ico (2^x.log2) (2^(x.log2 + 1)) := by
  have t0 : (2:‚Ñö) ‚â† 0 := by norm_num
  rw [log2]
  simp only [sub_nonneg, Nat.cast_le, neg_sub, Bool.cond_decide, bif_eq_if, decide_eq_true_eq,
    Nat.shiftLeft_eq]
  generalize hn : x.num.natAbs = n
  generalize ha : n.log2 = a
  generalize hb : x.den.log2 = b
  have n0 : n ‚â† 0 := by rwa [‚Üêhn, Int.natAbs_ne_zero, Rat.num_ne_zero]
  have d0' : 0 < (x.den : ‚Ñö) := Nat.cast_pos.mpr x.den_pos
  have an := Nat.log2_self_le n0
  have bd := Nat.log2_self_le x.den_nz
  have na := Nat.lt_log2_self (n := n)
  have db := Nat.lt_log2_self (n := x.den)
  simp only [ha, hb] at an bd na db
  have ae : |x| = (n : ‚Ñö) / x.den := by rw [Rat.abs_eq_div, hn]
  have lo : 2^(a - b - 1 : ‚Ñ§) ‚â§ |x| := by
    rw [ae]
    refine le_trans ?_ (div_le_div (by positivity) (Nat.cast_le.mpr an) (by positivity)
      (Nat.cast_le.mpr db.le))
    simp only [Nat.cast_pow, Nat.cast_two, ‚Üêzpow_ofNat, ‚Üêzpow_sub‚ÇÄ t0, Nat.cast_add, sub_sub,
      Nat.cast_one, le_refl]
  have hi : |x| < 2^(a - b + 1 : ‚Ñ§) := by
    rw [ae]
    refine lt_of_lt_of_le ((div_lt_div_right d0').mpr (Nat.cast_lt.mpr na)) ?_
    refine le_trans (div_le_div_of_le_left (by positivity) (by positivity) (Nat.cast_le.mpr bd)) ?_
    simp only [Nat.cast_pow, Nat.cast_two, ‚Üêzpow_ofNat, ‚Üêzpow_sub‚ÇÄ t0, Nat.cast_add, sub_sub,
      Nat.cast_one, le_refl, add_sub_right_comm]
  simp only [‚ÜêNat.cast_le (Œ± := ‚Ñö), ‚ÜêNat.cast_lt (Œ± := ‚Ñö), ‚Üêae, mem_Ico,
    apply_ite (fun n : ‚Ñ§ ‚Ü¶ (2:‚Ñö)^n), apply_ite (fun y : ‚Ñö ‚Ü¶ y ‚â§ |x|),
    apply_ite (fun y : ‚Ñö ‚Ü¶ |x| < y), apply_ite (fun n : ‚Ñ§ ‚Ü¶ n + 1),
    Nat.cast_mul, Nat.cast_pow, Nat.cast_two, mul_comm (x.den : ‚Ñö),
    ‚Üêle_div_iff d0', lo, hi, sub_add_cancel]
  by_cases ba : b ‚â§ a
  ¬∑ simp only [Nat.cast_le, ba, ite_true, decide_eq_true_eq, sub_add_cancel, ‚ÜêNat.cast_sub ba,
      Int.toNat_ofNat, zpow_ofNat]
    split_ifs with h
    ¬∑ simp only [h, and_self]
    ¬∑ simp only [not_le.mp h, and_self]
  ¬∑ simp only [Nat.cast_le, ba, ite_false, decide_eq_true_eq, ‚ÜêNat.cast_sub (not_le.mp ba).le,
      Int.toNat_ofNat]
    split_ifs with h
    all_goals {
      rw [‚Üêzpow_ofNat, Nat.cast_sub (not_le.mp ba).le, ‚Üêneg_sub (a:‚Ñ§) (b:‚Ñ§), zpow_neg,
        ‚Üêdiv_eq_mul_inv, le_div_iff two_zpow_pos, mul_comm, ‚Üêle_div_iff d0', ‚Üêae] at h
      try simp only [not_le] at h
      simp only [h, and_self] }

lemma Rat.log2_self_le {x : ‚Ñö} (x0 : x ‚â† 0) : 2 ^ x.log2 ‚â§ |x| := (Rat.log2_correct x0).1

lemma Rat.lt_log2_self {x : ‚Ñö} : |x| < 2 ^ (x.log2 + 1) := by
  by_cases x0 : x = 0
  ¬∑ simp only [x0, abs_zero, two_zpow_pos]
  ¬∑ exact (Rat.log2_correct x0).2
