module
public import Mathlib.Analysis.Calculus.ContDiff.Defs
public import Mathlib.Analysis.Calculus.DSlope
public import Mathlib.Analysis.Calculus.IteratedDeriv.Defs
public import Ray.Analytic.Defs
import Mathlib.Analysis.Analytic.Basic
import Mathlib.Analysis.Analytic.Composition
import Mathlib.Analysis.Analytic.Constructions
import Mathlib.Analysis.Analytic.IsolatedZeros
import Mathlib.Analysis.Analytic.Linear
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.FDeriv.Pow
import Mathlib.Analysis.Calculus.FormalMultilinearSeries
import Mathlib.Data.Finset.Basic
import Mathlib.Data.Real.Basic
import Mathlib.Data.Set.Basic
import Mathlib.Data.Stream.Defs
import Mathlib.Tactic.Cases
import Mathlib.Topology.Basic
import Ray.Misc.Bounds
import Ray.Misc.Multilinear
import Ray.Misc.Topology

/-!
## Facts about analytic functions (general field case)
-/

open Classical
open Filter (atTop)
open Function (curry uncurry)
open Metric (ball closedBall sphere isOpen_ball)
open Set (univ)
open scoped ContDiff Real NNReal ENNReal Topology
noncomputable section

variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]
variable {E : Type} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

/-- Drop 'Within' from `AnalyticWithinAt` if we have a neighborhood -/
public lemma AnalyticWithinAt.analyticAt {f : E â†’ F} {s : Set E} {x : E}
    (fa : AnalyticWithinAt ğ•œ f s x) (xs : s âˆˆ ğ“ x) : AnalyticAt ğ•œ f x := by
  obtain âŸ¨p, r, fpâŸ© := fa
  obtain âŸ¨e, e0, esâŸ© := Metric.mem_nhds_iff.mp xs
  refine âŸ¨p, min r (.ofReal e),
    {r_le := by simp [fp.r_le], r_pos := by simp [fp.r_pos, e0], hasSum := fun {y} yr â†¦ ?_}âŸ©
  simp only [EMetric.mem_ball, edist_zero_right, lt_inf_iff] at yr
  obtain âŸ¨yr, yeâŸ© := yr
  simp only [â† ofReal_norm, ENNReal.ofReal_lt_ofReal_iff e0] at ye
  exact fp.hasSum (.inr (es (by simp [ye]))) (by simp [yr])

/-- Extract `AnalyticAt` from `ContDiffOn ğ•œ Ï‰` if we have a neighborhood -/
public lemma ContDiffOn.analyticAt {f : E â†’ F} {s : Set E} (fa : ContDiffOn ğ•œ Ï‰ f s) {x : E}
    (xs : s âˆˆ ğ“ x) : AnalyticAt ğ•œ f x :=
  (fa x (mem_of_mem_nhds xs)).analyticWithinAt.analyticAt xs

/-- Extract `AnalyticOnNhd` from `ContDiffOn ğ•œ Ï‰` if we're open -/
public lemma ContDiffOn.analyticOnNhd {f : E â†’ F} {s : Set E} (fa : ContDiffOn ğ•œ Ï‰ f s)
    (os : IsOpen s) : AnalyticOnNhd ğ•œ f s :=
  fun x xs â†¦ (fa x xs).analyticWithinAt.analyticAt (os.mem_nhds xs)

public lemma AnalyticAt.div_const {f : E â†’ ğ•œ} {c : E} (fa : AnalyticAt ğ•œ f c) {w : ğ•œ} :
    AnalyticAt ğ•œ (fun z â†¦ f z / w) c := by
  by_cases w0 : w = 0
  Â· simp only [w0, div_zero, analyticAt_const]
  Â· exact fa.div analyticAt_const w0

public lemma AnalyticAt.dslope {f : ğ•œ â†’ E} {c x : ğ•œ} (fa : AnalyticAt ğ•œ f x) :
    AnalyticAt ğ•œ (dslope f c) x := by
  by_cases e : x = c
  Â· obtain âŸ¨p,fpâŸ© := fa
    simp only [â† e]
    exact âŸ¨_, fp.has_fpower_series_dslope_fslopeâŸ©
  Â· rw [analyticAt_congr (dslope_eventuallyEq_slope_of_ne _ e)]
    apply AnalyticAt.smul
    Â· exact AnalyticAt.inv (by fun_prop) (by simpa only [ne_eq, sub_eq_zero])
    Â· simp only [vsub_eq_sub]
      exact fa.sub analyticAt_const

/-- Power series coefficients in terms of iterated derivatives -/
public lemma HasFPowerSeriesAt.coeff_eq_iteratedDeriv_div [CompleteSpace ğ•œ] [CharZero ğ•œ] {f : ğ•œ â†’ ğ•œ}
    {p : FormalMultilinearSeries ğ•œ ğ•œ ğ•œ} {c : ğ•œ} (fp : HasFPowerSeriesAt f p c) (n : â„•) :
    p.coeff n = iteratedDeriv n f c / n.factorial := by
  simp only [fp.eq_formalMultilinearSeries (AnalyticAt.hasFPowerSeriesAt âŸ¨_, fpâŸ©),
    FormalMultilinearSeries.coeff_ofScalars]

/-- `orderAt` is unique, since power series are -/
public theorem HasFPowerSeriesAt.orderAt_unique {f : ğ•œ â†’ E} {p : FormalMultilinearSeries ğ•œ ğ•œ E}
    {c : ğ•œ} (fp : HasFPowerSeriesAt f p c) : orderAt f c = p.order := by
  have fa : AnalyticAt ğ•œ f c := âŸ¨p, fpâŸ©
  simp only [orderAt, fa, dif_pos]
  have s := choose_spec fa
  generalize hq : choose fa = q
  simp_rw [hq] at s
  rw [fp.eq_formalMultilinearSeries s]

/-- `orderAt` is zero for nonzeros -/
public theorem orderAt_eq_zero {f : ğ•œ â†’ E} {c : ğ•œ} (f0 : f c â‰  0) : orderAt f c = 0 := by
  by_cases fp : AnalyticAt ğ•œ f c
  Â· rcases fp with âŸ¨p, fpâŸ©; rw [fp.orderAt_unique]; rw [â† fp.coeff_zero 1] at f0
    rw [FormalMultilinearSeries.order_eq_zero_iff']; right
    contrapose f0
    simp only [f0, ContinuousMultilinearMap.zero_apply]
  Â· simp [orderAt, fp]

/-- `orderAt = 0` means either `f = 0` or `f c â‰  0` -/
public theorem orderAt_eq_zero_iff {f : ğ•œ â†’ E} {c : ğ•œ} (fa : AnalyticAt ğ•œ f c) :
    orderAt f c = 0 â†” f =á¶ [ğ“ c] 0 âˆ¨ f c â‰  0 := by
  rcases fa with âŸ¨p, fpâŸ©
  simp only [fp.orderAt_unique, â†fp.coeff_zero fun _ â†¦ 0,
    FormalMultilinearSeries.order_eq_zero_iff']
  rw [â†@norm_ne_zero_iff _ _ (p 0 fun _ â†¦ 0), ContinuousMultilinearMap.fin0_apply_norm,
    norm_ne_zero_iff]
  apply or_congr_left'; intro _; exact fp.locally_zero_iff.symm

/-- `orderAt = 1 â†’ deriv â‰  0` -/
public theorem deriv_ne_zero_of_orderAt_eq_one {f : ğ•œ â†’ E} {c : ğ•œ} (o : orderAt f c = 1) :
    deriv f c â‰  0 := by
  by_cases fa : AnalyticAt ğ•œ f c
  Â· rcases fa with âŸ¨p, fpâŸ©
    rw [fp.orderAt_unique] at o
    have o0 : p.order â‰  0 := by rw [o]; exact one_ne_zero
    have p0 := FormalMultilinearSeries.apply_order_ne_zero' o0
    rw [o] at p0
    simpa only [fp.deriv, FormalMultilinearSeries.apply_eq_pow_smul_coeff, one_pow, one_smul,
      FormalMultilinearSeries.coeff_eq_zero, Ne]
  Â· simp only [orderAt, fa] at o; rw [dif_neg] at o; norm_num at o; exact not_false

/-- `leadingCoeff` for nonzeros -/
public theorem leadingCoeff_of_ne_zero {f : ğ•œ â†’ E} {c : ğ•œ} (f0 : f c â‰  0) :
    leadingCoeff f c = f c := by
  simp only [leadingCoeff, orderAt_eq_zero f0, Function.iterate_zero_apply]

/-- `f` is approximated by its leading monomial -/
public theorem AnalyticAt.leading_approx {f : ğ•œ â†’ E} {c : ğ•œ} (fa : AnalyticAt ğ•œ f c) :
    (fun z â†¦ f z - (z - c) ^ orderAt f c â€¢ leadingCoeff f c) =o[ğ“ c] fun z â†¦
      (z - c) ^ orderAt f c := by
  rcases fa with âŸ¨p, fpâŸ©
  generalize ha : leadingCoeff f c = a
  generalize hd : orderAt f c = d
  have ha' : (Function.swap _root_.dslope c)^[d] f c = a := by rw [â† ha, â† hd, leadingCoeff]
  have e := fp.eq_pow_order_mul_iterate_dslope
  simp_rw [â† fp.orderAt_unique, hd] at e
  apply Asymptotics.IsLittleO.of_isBigOWith; intro k kp
  rw [Asymptotics.isBigOWith_iff]
  apply e.mp
  have dc : ContinuousAt ((Function.swap _root_.dslope c)^[d] f) c :=
    (fp.has_fpower_series_iterate_dslope_fslope d).analyticAt.continuousAt
  rcases Metric.continuousAt_iff.mp dc k kp with âŸ¨r, rp, rhâŸ©
  rw [ha'] at rh
  generalize hg : (Function.swap _root_.dslope c)^[d] f = g; rw [hg] at rh
  rw [Metric.eventually_nhds_iff]; use r, rp; intro y yr fe; rw [fe]
  specialize rh yr; rw [dist_eq_norm] at rh
  calc â€–(y - c) ^ d â€¢ g y - (y - c) ^ d â€¢ aâ€–
    _ = â€–(y - c) ^ dâ€– * â€–g y - aâ€– := by rw [â†smul_sub, norm_smul]
    _ â‰¤ â€–(y - c) ^ dâ€– * k := mul_le_mul_of_nonneg_left rh.le (norm_nonneg _)
    _ = k * â€–(y - c) ^ dâ€– := by rw [mul_comm]

/-- `orderAt > 0` means `f` has a zero -/
public theorem AnalyticAt.zero_of_order_pos {f : ğ•œ â†’ E} {c : ğ•œ} (fa : AnalyticAt ğ•œ f c)
    (p : 0 < orderAt f c) : f c = 0 := by
  have a := (Asymptotics.isBigOWith_iff.mp (fa.leading_approx.forall_isBigOWith zero_lt_one)).self_of_nhds
  simp only [(pow_eq_zero_iff (Nat.pos_iff_ne_zero.mp p)).mpr, sub_self, zero_smul, sub_zero,
    norm_zero, MulZeroClass.mul_zero, norm_le_zero_iff] at a
  exact a

/-- The power series of `(z - c) â€¢ f z` -/
def FormalMultilinearSeries.unshift' (p : FormalMultilinearSeries ğ•œ ğ•œ E) (c : E) :
    FormalMultilinearSeries ğ•œ ğ•œ E :=
  ((ContinuousLinearMap.smulRightL ğ•œ ğ•œ E (ContinuousLinearMap.id ğ•œ ğ•œ)).compFormalMultilinearSeries
        p).unshift c

@[simp]
lemma FormalMultilinearSeries.unshift_coeff_zero (p : FormalMultilinearSeries ğ•œ ğ•œ E) (c : E) :
    (p.unshift' c).coeff 0 = c := by
  simp only [FormalMultilinearSeries.coeff, FormalMultilinearSeries.unshift',
    FormalMultilinearSeries.unshift, continuousMultilinearCurryFin0_symm_apply]

@[simp]
lemma FormalMultilinearSeries.unshift_coeff_succ (p : FormalMultilinearSeries ğ•œ ğ•œ E) (c : E)
    (n : â„•) : (p.unshift' c).coeff (n + 1) = p.coeff n := by
  simp only [FormalMultilinearSeries.coeff, FormalMultilinearSeries.unshift',
    FormalMultilinearSeries.unshift, ContinuousLinearMap.compFormalMultilinearSeries_apply]
  simp [ContinuousLinearMap.smulRightL, Finset.univ, Fintype.elems, Fin.init]

/-- The power series of `(z - c)^n â€¢ f z` -/
def FormalMultilinearSeries.unshiftIter (p : FormalMultilinearSeries ğ•œ ğ•œ E) (n : â„•) :=
  (fun p â†¦ FormalMultilinearSeries.unshift' p (0 : E))^[n] p

lemma FormalMultilinearSeries.unshiftIter_coeff (p : FormalMultilinearSeries ğ•œ ğ•œ E) (n : â„•)
    (i : â„•) : (p.unshiftIter n).coeff i = if i < n then 0 else p.coeff (i - n) := by
  revert i; induction' n with n h
  Â· simp only [FormalMultilinearSeries.unshiftIter, Function.iterate_zero, id_eq, not_lt_zero',
    tsub_zero, if_false, forall_const]
  Â· simp_rw [FormalMultilinearSeries.unshiftIter] at h
    simp only [FormalMultilinearSeries.unshiftIter, Function.iterate_succ', Function.comp]
    generalize hq : (fun p : FormalMultilinearSeries ğ•œ ğ•œ E â†¦ p.unshift' 0)^[n] p = q
    rw [hq] at h; clear hq
    intro i; induction' i with i _
    Â· simp only [FormalMultilinearSeries.unshift_coeff_zero, Nat.succ_pos', if_true]
    Â· simp only [Nat.succ_lt_succ_iff, h i, FormalMultilinearSeries.unshift_coeff_succ,
        Nat.succ_sub_succ_eq_sub]

/-- `unshift'` respects norm -/
lemma FormalMultilinearSeries.unshift_norm' (p : FormalMultilinearSeries ğ•œ ğ•œ E) (c : E) (n : â„•) :
    â€–p.unshift' c (n + 1)â€– = â€–p nâ€– := by
  simp only [FormalMultilinearSeries.norm_apply_eq_norm_coef,
    FormalMultilinearSeries.unshift_coeff_succ]

/-- `unshift'` respects radius -/
lemma FormalMultilinearSeries.unshift_radius' (p : FormalMultilinearSeries ğ•œ ğ•œ E) {c : E} :
    (p.unshift' c).radius = p.radius := by
  simp_rw [FormalMultilinearSeries.radius]
  apply le_antisymm
  Â· refine iSupâ‚‚_le ?_; intro r k; refine iSup_le ?_; intro h
    refine le_trans ?_ (le_iSupâ‚‚ r (k * â†‘râ»Â¹))
    have h := fun n â†¦ mul_le_mul_of_nonneg_right (h (n + 1)) (NNReal.coe_nonneg râ»Â¹)
    by_cases r0 : r = 0; Â· simp only [r0, ENNReal.coe_zero, ENNReal.iSup_zero, le_zero_iff]
    simp only [pow_succ, â†mul_assoc _ _ (r:â„), mul_assoc _ (r:â„) _,
      mul_inv_cancelâ‚€ (NNReal.coe_ne_zero.mpr r0), NNReal.coe_inv, mul_one, p.unshift_norm'] at h
    simp only [NNReal.coe_inv]
    convert le_iSup _ h; rfl
  Â· refine iSupâ‚‚_le ?_; intro r k; refine iSup_le ?_; intro h
    refine le_trans ?_ (le_iSupâ‚‚ r (max â€–câ€– (k * â†‘r)))
    have h' : âˆ€ n, â€–p.unshift' c nâ€– * (r:â„)^n â‰¤ max â€–câ€– (k * â†‘r) := by
      intro n; induction' n with n _
      Â· simp only [FormalMultilinearSeries.unshift_coeff_zero,
          FormalMultilinearSeries.norm_apply_eq_norm_coef, pow_zero, mul_one, le_max_iff, le_refl,
          true_or]
      Â· simp only [FormalMultilinearSeries.norm_apply_eq_norm_coef] at h
        simp only [FormalMultilinearSeries.unshift_coeff_succ, pow_succ, â† mul_assoc,
          FormalMultilinearSeries.norm_apply_eq_norm_coef, le_max_iff]
        right; exact mul_le_mul_of_nonneg_right (h n) (NNReal.coe_nonneg _)
    convert le_iSup _ h'; rfl

/-- The power series of `(z - c) â€¢ f z` is the unshifted power series -/
theorem HasFPowerSeriesOnBall.unshift' {f : ğ•œ â†’ E} {p : FormalMultilinearSeries ğ•œ ğ•œ E} {c : ğ•œ}
    {r : ENNReal} (fp : HasFPowerSeriesOnBall f p c r) :
    HasFPowerSeriesOnBall (fun z â†¦ (z - c) â€¢ f z) (p.unshift' 0) c r :=
  { r_le := le_trans fp.r_le (ge_of_eq p.unshift_radius')
    r_pos := fp.r_pos
    hasSum := by
      intro y yr; simp only [FormalMultilinearSeries.apply_eq_pow_smul_coeff, add_sub_cancel_left]
      generalize hs : (fun n â†¦ y ^ n â€¢ (p.unshift' 0).coeff n) = s
      have s0 : y â€¢ f (c + y) = y â€¢ f (c + y) + (Finset.range 1).sum s := by
        simp only [â† hs, p.unshift_coeff_zero, Finset.range_one, Finset.sum_singleton, smul_zero,
          add_zero]
      rw [s0, â† hasSum_nat_add_iff, â† hs]
      simp only [p.unshift_coeff_succ, pow_succ', â† smul_smul]; apply HasSum.const_smul
      have h := fp.hasSum yr; simp only [FormalMultilinearSeries.apply_eq_pow_smul_coeff] at h
      exact h }

theorem HasFPowerSeriesAt.unshift {f : ğ•œ â†’ E} {p : FormalMultilinearSeries ğ•œ ğ•œ E} {c : ğ•œ}
    (fp : HasFPowerSeriesAt f p c) :
    HasFPowerSeriesAt (fun z â†¦ (z - c) â€¢ f z) (p.unshift' 0) c := by
  rcases fp with âŸ¨r, faâŸ©; use r; exact fa.unshift'

theorem HasFPowerSeriesAt.unshiftIter {f : ğ•œ â†’ E} {p : FormalMultilinearSeries ğ•œ ğ•œ E} {c : ğ•œ}
    {n : â„•} (fp : HasFPowerSeriesAt f p c) :
    HasFPowerSeriesAt (fun z â†¦ (z - c) ^ n â€¢ f z) (p.unshiftIter n) c := by
  induction' n with n h; Â· simp only [pow_zero, one_smul]; exact fp
  Â· simp only [pow_succ', â† smul_smul, FormalMultilinearSeries.unshiftIter, Function.iterate_succ',
      Function.comp]
    exact h.unshift

/-- Power series terms are zero iff their coeffs are zero -/
theorem FormalMultilinearSeries.ne_zero_iff_coeff_ne_zero (p : FormalMultilinearSeries ğ•œ ğ•œ E)
    {n : â„•} : p n â‰  0 â†” p.coeff n â‰  0 := by
  constructor
  Â· intro h; contrapose h; exact coeff_eq_zero.mp h
  Â· intro h; contrapose h; exact coeff_eq_zero.mpr h

/-- The order of `(z - n)^n â€¢ f z` is `n` greater than `f`'s -/
public theorem AnalyticAt.monomial_mul_orderAt {f : ğ•œ â†’ E} {c : ğ•œ} (fa : AnalyticAt ğ•œ f c)
    (fnz : âˆƒá¶  z in ğ“ c, f z â‰  0) (n : â„•) :
    orderAt (fun z â†¦ (z - c) ^ n â€¢ f z) c = n + orderAt f c := by
  rcases fa with âŸ¨p, fpâŸ©
  have pnz : p â‰  0 := by
    contrapose fnz
    simpa only [HasFPowerSeriesAt.locally_zero_iff fp, Filter.not_frequently, not_not]
  have pe : âˆƒ i, p i â‰  0 := by rw [Function.ne_iff] at pnz; exact pnz
  have pne : âˆƒ i, (p.unshiftIter n) i â‰  0 := by
    rcases pe with âŸ¨i, piâŸ©; use n + i
    simp only [FormalMultilinearSeries.ne_zero_iff_coeff_ne_zero] at pi âŠ¢
    simpa only [p.unshiftIter_coeff, add_lt_iff_neg_left, add_tsub_cancel_left]
  have fq : HasFPowerSeriesAt (fun z â†¦ (z - c) ^ n â€¢ f z) (p.unshiftIter n) c := fp.unshiftIter
  rw [fp.orderAt_unique, fq.orderAt_unique]
  rw [FormalMultilinearSeries.order_eq_find pe, FormalMultilinearSeries.order_eq_find pne]
  rw [Nat.find_eq_iff]; constructor
  Â· have s := Nat.find_spec pe
    simp only [â† p.coeff_eq_zero, Ne] at s
    simp only [p.unshiftIter_coeff, â† FormalMultilinearSeries.coeff_eq_zero, s, Ne,
      add_lt_iff_neg_left, not_lt_zero', add_tsub_cancel_left, if_false, not_false_iff]
  Â· intro m mp; simp [â† FormalMultilinearSeries.coeff_eq_zero, p.unshiftIter_coeff]; intro mn
    generalize ha : m - n = a; have hm : m = n + a := by rw [â† ha, add_comm, Nat.sub_add_cancel mn]
    simp only [hm, add_lt_add_iff_left, Nat.lt_find_iff, not_not] at mp
    specialize mp a (le_refl _); rwa [FormalMultilinearSeries.coeff_eq_zero]

/-- The leading coefficient of `(z - n)^n â€¢ f z` is the same as `f`'s -/
public theorem AnalyticAt.monomial_mul_leadingCoeff {f : ğ•œ â†’ E} {c : ğ•œ} (fa : AnalyticAt ğ•œ f c)
    (fnz : âˆƒá¶  z in ğ“ c, f z â‰  0) (n : â„•) :
    leadingCoeff (fun z â†¦ (z - c) ^ n â€¢ f z) c = leadingCoeff f c := by
  simp [leadingCoeff, fa.monomial_mul_orderAt fnz n]; generalize orderAt f c = a
  induction' n with n h
  Â· simp only [zero_add, pow_zero, one_smul]
  Â· simp [pow_succ', â† smul_smul, Nat.succ_add]
    generalize hg : (fun z â†¦ (z - c) ^ n â€¢ f z) = g
    have hg' : âˆ€ z, (z - c) ^ n â€¢ f z = g z := by
      rw [â†hg]; simp only [forall_const]
    simp_rw [hg'] at h âŠ¢
    have e : (Function.swap _root_.dslope c fun z â†¦ (z - c) â€¢ g z) = g := by
      simp only [Function.swap, dslope_sub_smul, Function.update_eq_self_iff]
      rw [deriv_fun_smul]
      simp only [sub_self, zero_smul, deriv_fun_sub, differentiableAt_fun_id,
        differentiableAt_const, deriv_id'', deriv_const', sub_zero, one_smul, zero_add]
      exact differentiableAt_id.sub (differentiableAt_const _)
      rw [â†hg]
      exact ((differentiableAt_id.sub (differentiableAt_const _)).pow _).smul fa.differentiableAt
    rw [e, h]

/-- `deriv` in the second variable is analytic -/
public theorem AnalyticAt.deriv2 [CompleteSpace ğ•œ] {f : E â†’ ğ•œ â†’ ğ•œ} {c : E Ã— ğ•œ}
    (fa : AnalyticAt ğ•œ (uncurry f) c) :
    AnalyticAt ğ•œ (fun x : E Ã— ğ•œ â†¦ _root_.deriv (f x.1) x.2) c := by
  set p : (E Ã— ğ•œ â†’L[ğ•œ] ğ•œ) â†’L[ğ•œ] ğ•œ := ContinuousLinearMap.apply ğ•œ ğ•œ (0, 1)
  have e : âˆ€á¶  x : E Ã— ğ•œ in ğ“ c, _root_.deriv (f x.1) x.2 = p (_root_.fderiv ğ•œ (uncurry f) x) := by
    refine fa.eventually_analyticAt.mp (.of_forall ?_)
    intro âŸ¨x, yâŸ© fa; simp only [â† fderiv_apply_one_eq_deriv]
    have e : f x = uncurry f âˆ˜ fun y â†¦ (x, y) := rfl
    rw [e]; rw [fderiv_comp]
    have pd : _root_.fderiv ğ•œ (fun y : ğ•œ â†¦ (x, y)) y = ContinuousLinearMap.inr ğ•œ E ğ•œ := by
      apply HasFDerivAt.fderiv; apply hasFDerivAt_prodMk_right
    rw [pd, ContinuousLinearMap.comp_apply, ContinuousLinearMap.inr_apply,
      ContinuousLinearMap.apply_apply]
    Â· exact fa.differentiableAt
    Â· exact (differentiableAt_const _).prodMk differentiableAt_id
  rw [analyticAt_congr e]
  exact (p.analyticAt _).comp fa.fderiv

/-- Scaling commutes with power series -/
theorem HasFPowerSeriesAt.const_fun_smul {f : ğ•œ â†’ E} {c a : ğ•œ} {p : FormalMultilinearSeries ğ•œ ğ•œ E}
    (fp : HasFPowerSeriesAt f p c) : HasFPowerSeriesAt (fun z â†¦ a â€¢ f z) (fun n â†¦ a â€¢ p n) c := by
  rw [hasFPowerSeriesAt_iff] at fp âŠ¢; refine fp.mp (.of_forall fun z h â†¦ ?_)
  simp only [FormalMultilinearSeries.coeff, ContinuousMultilinearMap.smul_apply, smul_comm _ a]
  exact h.const_smul a

/-- Nonzero scaling does not change analyticitiy -/
theorem analyticAt_iff_const_smul {f : ğ•œ â†’ E} {c a : ğ•œ} (a0 : a â‰  0) :
    AnalyticAt ğ•œ (fun z â†¦ a â€¢ f z) c â†” AnalyticAt ğ•œ f c := by
  constructor
  Â· intro âŸ¨p, fpâŸ©
    have e : f = fun z â†¦ aâ»Â¹ â€¢ a â€¢ f z := by
      funext; simp only [â† smul_assoc, smul_eq_mul, inv_mul_cancelâ‚€ a0, one_smul]
    rw [e]; exact âŸ¨_, fp.const_smulâŸ©
  Â· intro âŸ¨p, fpâŸ©; exact âŸ¨_, fp.const_smulâŸ©

/-- Nonzero scaling does not change `orderAt` -/
public theorem orderAt_const_smul {f : ğ•œ â†’ E} {c a : ğ•œ} (a0 : a â‰  0) :
    orderAt (fun z â†¦ a â€¢ f z) c = orderAt f c := by
  by_cases fa : AnalyticAt ğ•œ f c
  Â· rcases fa with âŸ¨p, fpâŸ©
    have e : âˆ€ n, a â€¢ p n â‰  0 â†” p n â‰  0 := fun n â†¦ by
      simp only [a0, Ne, smul_eq_zero, false_or]
    simp only [fp.orderAt_unique, fp.const_fun_smul.orderAt_unique,
      FormalMultilinearSeries.order, e]
  Â· have ga := fa; rw [â† analyticAt_iff_const_smul a0] at ga
    simp only [orderAt, fa, ga]; rw [dif_neg, dif_neg]
    exact not_false; exact not_false

/-- The leading coefficient of zero is zero -/
theorem leadingCoeff.zero {c : ğ•œ} : leadingCoeff (fun _ : ğ•œ â†¦ (0 : E)) c = 0 := by
  simp only [leadingCoeff]
  generalize hn : orderAt (fun _ : ğ•œ â†¦ (0 : E)) c = n; clear hn
  induction' n with n h; simp only [Function.iterate_zero_apply]
  simp only [Function.iterate_succ_apply]; convert h
  simp only [Function.swap, dslope, deriv_const]
  simp only [slope_fun_def, vsub_eq_sub, sub_zero, smul_zero, Function.update_apply]
  split_ifs; rfl; rfl

/-- `leadingCoeff` has linear scaling -/
public theorem leadingCoeff_const_smul {f : ğ•œ â†’ E} {c a : ğ•œ} :
    leadingCoeff (fun z â†¦ a â€¢ f z) c = a â€¢ leadingCoeff f c := by
  by_cases a0 : a = 0; simp only [a0, zero_smul, leadingCoeff.zero]
  simp only [leadingCoeff, orderAt_const_smul a0]
  generalize hn : orderAt f c = n; clear hn
  have e : ((Function.swap dslope c)^[n] fun z : ğ•œ â†¦ a â€¢ f z) =
      a â€¢ (Function.swap dslope c)^[n] f := by
    induction' n with n h; funext; simp only [Function.iterate_zero_apply, Pi.smul_apply]
    generalize hg : (Function.swap dslope c)^[n] f = g
    simp only [Function.iterate_succ_apply', h, hg]
    funext x; simp only [Function.swap]
    by_cases cx : x = c
    Â· simp only [cx, dslope_same, Pi.smul_apply, Pi.smul_def, deriv_fun_const_smul']
    Â· simp only [dslope_of_ne _ cx, Pi.smul_apply, slope, vsub_eq_sub, â† smul_sub, smul_comm _ a]
  simp only [e, Pi.smul_apply]

/-- `leadingCoeff` is nonzero for nonzero order -/
public theorem leadingCoeff_ne_zero {f : ğ•œ â†’ E} {c : ğ•œ} (fa : AnalyticAt ğ•œ f c)
    (o0 : orderAt f c â‰  0) : leadingCoeff f c â‰  0 := by
  rcases fa with âŸ¨p, fpâŸ©
  simp only [fp.orderAt_unique, leadingCoeff] at o0 âŠ¢
  exact fp.iterate_dslope_fslope_ne_zero (FormalMultilinearSeries.ne_zero_of_order_ne_zero o0)
