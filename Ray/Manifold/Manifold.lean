module
public import Mathlib.Analysis.Normed.Field.Basic
public import Mathlib.Analysis.Normed.Module.Basic
public import Mathlib.Geometry.Manifold.Algebra.Structures
public import Ray.Manifold.Defs
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Geometry.Manifold.ChartedSpace
import Mathlib.Geometry.Manifold.ContMDiffMFDeriv
import Mathlib.Geometry.Manifold.LocalInvariantProperties
import Mathlib.Geometry.Manifold.VectorBundle.Tangent

/-!
## Manifold lemmas
-/

open ChartedSpace (chartAt)
open Function (uncurry)
open Set
open scoped Manifold Topology
noncomputable section

variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]
variable {E : Type} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {G : Type} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
variable {H : Type} [NormedAddCommGroup H] [NormedSpace ğ•œ H]

variable {A M : Type} [TopologicalSpace A] [TopologicalSpace M]
variable {B N : Type} [TopologicalSpace B] [TopologicalSpace N]
variable {C O : Type} [TopologicalSpace C] [TopologicalSpace O]
variable {D P : Type} [TopologicalSpace D] [TopologicalSpace P]

/-- Version of `ModelWithCorners.prod_apply` with `x âˆˆ H Ã— H'` rather than `ModelProd H H'`.  This
comes up because other simplification doesn't stay in `ModelProd`. -/
@[simp]
lemma ModelWithCorners.prod_apply' {E H E' H' : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H) [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
    [TopologicalSpace H'] (I' : ModelWithCorners ğ•œ E' H') (x : H Ã— H') :
    (I.prod I') x = (I x.1, I' x.2) :=
  ModelWithCorners.prod_apply _ _ _

variable {I : ModelWithCorners ğ•œ E A} [ChartedSpace A M]
variable {J : ModelWithCorners ğ•œ F B} [ChartedSpace B N]
variable {K : ModelWithCorners ğ•œ G C} [ChartedSpace C O]
variable {L : ModelWithCorners ğ•œ H D} [ChartedSpace D P]

section Nhds

/-- `extChartAt` as a `PartialHomeomorph` -/
@[expose] public def extChartAt' (I : ModelWithCorners ğ•œ E A) [I.Boundaryless] {M : Type}
    [TopologicalSpace M] [ChartedSpace A M] (x : M) : OpenPartialHomeomorph M E where
  toPartialEquiv := extChartAt I x
  open_source := isOpen_extChartAt_source x
  open_target := isOpen_extChartAt_target x
  continuousOn_toFun := continuousOn_extChartAt x
  continuousOn_invFun := continuousOn_extChartAt_symm x

/-- `extChartAt.symm` maps `ğ“` to `ğ“` -/
public theorem extChartAt_symm_map_nhds [I.Boundaryless] {x : M} {y : E}
    (m : y âˆˆ (extChartAt I x).target) :
    Filter.map (extChartAt I x).symm (ğ“ y) = ğ“ ((extChartAt I x).symm y) :=
  (extChartAt' I x).symm.map_nhds_eq m

/-- `extChartAt.symm` maps `ğ“` to `ğ“` -/
public theorem extChartAt_symm_map_nhds' (I : ModelWithCorners ğ•œ E A) [I.Boundaryless] {M : Type}
    [TopologicalSpace M] [ChartedSpace A M] (x : M) :
    Filter.map (extChartAt I x).symm (ğ“ (extChartAt I x x)) = ğ“ x := by
  convert extChartAt_symm_map_nhds (mem_extChartAt_target x)
  simp only [PartialEquiv.left_inv _ (mem_extChartAt_source x)]
  infer_instance

/-- Nontrivial manifolds have no isolated points.
    Unfortunately, making this an instance gives "cannot find synthesization order for instance" -/
public theorem AnalyticManifold.punctured_nhds_neBot (I : ModelWithCorners ğ•œ E A) [I.Boundaryless]
    [Nontrivial E] (x : M) : (ğ“[{x}á¶œ] x).NeBot := by
  have p := Module.punctured_nhds_neBot ğ•œ E (extChartAt I x x)
  simp only [â† Filter.frequently_true_iff_neBot, frequently_nhdsWithin_iff, â†
    extChartAt_symm_map_nhds' I x, Filter.frequently_map, true_and,
    mem_compl_singleton_iff] at p âŠ¢
  apply p.mp
  apply ((isOpen_extChartAt_target x).eventually_mem (mem_extChartAt_target x)).mp
  refine .of_forall fun y m h â†¦ ?_
  contrapose h; nth_rw 2 [â† h]
  rw [PartialEquiv.right_inv _ m]

end Nhds

section Deriv

/-- `TangentSpace` commutes with products -/
theorem tangentSpace_prod (x : M) (y : N) :
    TangentSpace (I.prod J) (x, y) = (TangentSpace I x Ã— TangentSpace J y) := by
  simp only [TangentSpace]

/-- `HasMFDerivAt` composition for curried functions.
    This was oddly difficult to prove. -/
theorem MDifferentiableAt.hasMFDerivAt_uncurry {f : N â†’ O â†’ P} {y : N} {z : O}
    (fd : MDifferentiableAt (J.prod K) L (uncurry f) (y, z))
    {df0 : TangentSpace J y â†’L[ğ•œ] TangentSpace L (f y z)}
    (fh0 : HasMFDerivAt J L (fun x â†¦ f x z) y df0)
    {df1 : TangentSpace K z â†’L[ğ•œ] TangentSpace L (f y z)}
    (fh1 : HasMFDerivAt K L (fun x â†¦ f y x) z df1) :
    HasMFDerivAt (J.prod K) L (uncurry f) (y, z)
      (df0.comp (ContinuousLinearMap.fst ğ•œ (TangentSpace J y) (TangentSpace K z)) +
        df1.comp (ContinuousLinearMap.snd ğ•œ (TangentSpace J y) (TangentSpace K z))) := by
  set fst := ContinuousLinearMap.fst ğ•œ (TangentSpace J y) (TangentSpace K z)
  set snd := ContinuousLinearMap.snd ğ•œ (TangentSpace J y) (TangentSpace K z)
  generalize hdf : mfderiv (J.prod K) L (uncurry f) (y, z) = df
  have fh := fd.hasMFDerivAt; rw [hdf] at fh
  suffices e : df = df0.comp fst + df1.comp snd by rw [e] at fh; exact fh
  apply ContinuousLinearMap.ext; intro âŸ¨u, vâŸ©
  simp only [Function.uncurry_apply_pair, ContinuousLinearMap.add_apply,
    ContinuousLinearMap.comp_apply]
  have hu : âˆ€ u : TangentSpace J y, df (u, 0) = df0 u := by
    intro u
    have d : HasMFDerivAt J L (uncurry f âˆ˜ fun x â†¦ (x, z)) y
        (df.comp ((ContinuousLinearMap.id ğ•œ (TangentSpace J y)).prod 0)) :=
      fh.comp y ((hasMFDerivAt_id _).prodMk (hasMFDerivAt_const _ _))
    simp only [hasMFDerivAt_unique fh0 d]
    refine Eq.trans (congr_arg _ ?_) (ContinuousLinearMap.comp_apply _ _ _).symm
    refine Eq.trans ?_ (ContinuousLinearMap.prod_apply _ _ _).symm
    simp only [ContinuousLinearMap.zero_apply, Prod.mk.injEq, and_true]
    exact rfl
  have hv : âˆ€ v : TangentSpace K z, df (0, v) = df1 v := by
    intro v
    have d : HasMFDerivAt K L (uncurry f âˆ˜ fun x â†¦ (y, x)) z (df.comp
        ((0 : TangentSpace K z â†’L[ğ•œ] TangentSpace J y).prod
          (ContinuousLinearMap.id ğ•œ (TangentSpace K z)))) :=
      fh.comp z ((hasMFDerivAt_const _ _).prodMk (hasMFDerivAt_id _))
    rw [hasMFDerivAt_unique fh1 d]
    refine Eq.trans (congr_arg _ ?_) (ContinuousLinearMap.comp_apply _ _ _).symm
    refine Eq.trans ?_ (ContinuousLinearMap.prod_apply _ _ _).symm
    simp only [Prod.mk.injEq]
    exact âŸ¨(ContinuousLinearMap.zero_apply _).symm, rflâŸ©
  have e : (u, v) = (u, 0) + (0, v) := by simp only [Prod.mk_add_mk, add_zero, zero_add]
  nth_rw 1 [e]
  rw [map_add]
  exact congr_argâ‚‚ _ (hu u) (hv v)

/-- `HasMFDerivAt` composition for curried functions -/
public theorem MDifferentiableAt.hasMFDerivAt_comp2 {f : N â†’ O â†’ P} {g : M â†’ N} {h : M â†’ O} {x : M}
    (fd : MDifferentiableAt (J.prod K) L (uncurry f) (g x, h x))
    {dg : TangentSpace I x â†’L[ğ•œ] TangentSpace J (g x)} (gh : HasMFDerivAt I J g x dg)
    {dh : TangentSpace I x â†’L[ğ•œ] TangentSpace K (h x)} (hh : HasMFDerivAt I K h x dh)
    {df0 : TangentSpace J (g x) â†’L[ğ•œ] TangentSpace L (f (g x) (h x))}
    (fh0 : HasMFDerivAt J L (fun y â†¦ f y (h x)) (g x) df0)
    {df1 : TangentSpace K (h x) â†’L[ğ•œ] TangentSpace L (f (g x) (h x))}
    (fh1 : HasMFDerivAt K L (fun y â†¦ f (g x) y) (h x) df1) :
    HasMFDerivAt I L (fun y â†¦ f (g y) (h y)) x (df0.comp dg + df1.comp dh) := by
  have fh := (fd.hasMFDerivAt_uncurry fh0 fh1).comp x (gh.prodMk hh)
  simp only [ContinuousLinearMap.add_comp, ContinuousLinearMap.comp_assoc] at fh
  exact fh

/-- More general version of `hasMFDerivAt_iff_hasDerivAt`.
    The mathlib version doesn't handle product spaces. -/
public theorem hasMFDerivAt_iff_hasFDerivAt' {I : ModelWithCorners ğ•œ E A} [I.Boundaryless]
    [ChartedSpace A E] [IsManifold I âŠ¤ E] [ExtChartEqRefl I]
    {J : ModelWithCorners ğ•œ F B} [J.Boundaryless] [ChartedSpace B F] [IsManifold J âŠ¤ F]
    [ExtChartEqRefl J] {f : E â†’ F} {x : E} {f' : E â†’L[ğ•œ] F} :
    HasMFDerivAt I J f x f' â†” HasFDerivAt f f' x := by
  simp only [HasMFDerivAt, ModelWithCorners.range_eq_univ, hasFDerivWithinAt_univ,
    writtenInExtChartAt, extChartAt_eq_refl, Function.comp_def, PartialEquiv.refl_coe,
    PartialEquiv.refl_symm, id]
  exact âŸ¨fun x â†¦ x.2, fun d â†¦ âŸ¨d.continuousAt, dâŸ©âŸ©

/-- Variant of `mfderiv_comp` that doesn't use `âˆ˜` for better inference -/
theorem mfderiv_comp' {f : M â†’ N} (x : M) {g : N â†’ O} (hg : MDifferentiableAt J K g (f x))
    (hf : MDifferentiableAt I J f x) :
    mfderiv I K (fun x â†¦ g (f x)) x = (mfderiv J K g (f x)).comp (mfderiv I J f x) :=
  mfderiv_comp _ hg hf

variable [IsManifold I âŠ¤ M] [IsManifold J âŠ¤ N] [IsManifold K âŠ¤ O] [IsManifold L âŠ¤ P]

/-- Chart derivatives are invertible (left inverse) -/
public theorem extChartAt_mderiv_left_inverse [I.Boundaryless] {x y : M}
    (m : y âˆˆ (extChartAt I x).source) :
    (mfderiv (modelWithCornersSelf ğ•œ E) I (extChartAt I x).symm (extChartAt I x y)).comp
        (mfderiv I (modelWithCornersSelf ğ•œ E) (extChartAt I x) y) =
      ContinuousLinearMap.id ğ•œ (TangentSpace I y) := by
  have m' : extChartAt I x y âˆˆ (extChartAt I x).target := PartialEquiv.map_source _ m
  have mc : y âˆˆ (chartAt A x).source := by simpa only [mfld_simps] using m
  have d0 := (contMDiffOn_extChartAt_symm (n := âŠ¤) _ _ m').mdifferentiableWithinAt (by decide)
  have d1 := (contMDiffAt_extChartAt' (I := I) (n := âŠ¤) mc).mdifferentiableWithinAt (by decide)
  replace d0 := d0.mdifferentiableAt (extChartAt_target_mem_nhds' m')
  simp only [mdifferentiableWithinAt_univ] at d1
  have c := mfderiv_comp y d0 d1
  refine Eq.trans c.symm ?_
  rw [â† mfderiv_id]
  apply Filter.EventuallyEq.mfderiv_eq
  rw [Filter.eventuallyEq_iff_exists_mem]; use(extChartAt I x).source
  use extChartAt_source_mem_nhds' m
  intro z zm
  simp only [Function.comp, id, PartialEquiv.left_inv _ zm]

/-- Chart derivatives are invertible (right inverse) -/
public theorem extChartAt_mderiv_right_inverse [I.Boundaryless] {x : M} {y : E}
    (m : y âˆˆ (extChartAt I x).target) :
    (mfderiv I (modelWithCornersSelf ğ•œ E) (extChartAt I x) ((extChartAt I x).symm y)).comp
        (mfderiv (modelWithCornersSelf ğ•œ E) I (extChartAt I x).symm y) =
      ContinuousLinearMap.id ğ•œ (TangentSpace (modelWithCornersSelf ğ•œ E) y) := by
  have m' : (extChartAt I x).symm y âˆˆ (extChartAt I x).source := PartialEquiv.map_target _ m
  have mc : (extChartAt I x).symm y âˆˆ (chartAt A x).source := by simpa only [mfld_simps] using m'
  have d0 := (contMDiffOn_extChartAt_symm (n := âŠ¤) _ _ m).mdifferentiableWithinAt (by decide)
  have d1 := (contMDiffAt_extChartAt' (I := I) (n := âŠ¤) mc).mdifferentiableWithinAt (by decide)
  replace d0 := d0.mdifferentiableAt (extChartAt_target_mem_nhds' m)
  simp only [mdifferentiableWithinAt_univ] at d1
  have c := mfderiv_comp y d1 d0
  refine Eq.trans c.symm ?_; clear c; rw [â† mfderiv_id]; apply Filter.EventuallyEq.mfderiv_eq
  rw [Filter.eventuallyEq_iff_exists_mem]; use(extChartAt I x).target
  have n := extChartAt_target_mem_nhdsWithin' m'
  simp only [ModelWithCorners.range_eq_univ, nhdsWithin_univ,
    PartialEquiv.right_inv _ m] at n
  use n; intro z zm
  simp only [Function.comp, id, PartialEquiv.right_inv _ zm, Function.comp]

/-- Chart derivatives are invertible (right inverse) -/
public theorem extChartAt_mderiv_right_inverse' [I.Boundaryless] {x y : M}
    (m : y âˆˆ (extChartAt I x).source) :
    (mfderiv I (modelWithCornersSelf ğ•œ E) (extChartAt I x) y).comp
        (mfderiv (modelWithCornersSelf ğ•œ E) I (extChartAt I x).symm (extChartAt I x y)) =
      ContinuousLinearMap.id ğ•œ (TangentSpace (modelWithCornersSelf ğ•œ E) (extChartAt I x y)) := by
  have h := extChartAt_mderiv_right_inverse (PartialEquiv.map_source _ m)
  rw [PartialEquiv.left_inv _ m] at h; exact h

end Deriv
