module
public import Mathlib.Analysis.Complex.Basic
public import Mathlib.Geometry.Manifold.ContMDiff.Defs
public import Ray.Manifold.Defs
import Ray.Dynamics.Multiple
import Ray.Manifold.Nontrivial
import Ray.Manifold.OpenMapping
import all Ray.Manifold.Inverse

/-!
## Global inverse functions theorems on 1D complex manifolds

Given a parameterized analytic function `f : ‚ÑÇ ‚Üí S ‚Üí T` where `(c,z) ‚Ü¶ (c, f c z)` is
injective, there exists a global inverse `g : ‚ÑÇ ‚Üí T ‚Üí S` to `f` with `g c (f c z) = z`.

We prove several versions of this result, with different hypotheses:
1. `global_complex_inverse_fun_open`: `f : ‚ÑÇ ‚Üí S ‚Üí T` is nonsingular and injective on an open set
2. `global_complex_inverse_fun_compact`: `f : S ‚Üí T` is nonsingular and injective on a compact set
3. `global_complex_inverse_fun_open': `f` is injective on an open set

These results follow straightforwardly by stitching together local inverses, except that
(3) needs the result from `AnalyticManifold.Multiple` that injectivity implies nonzero derivative.
-/

open Classical
open Filter (Tendsto)
open Function (uncurry)
open OneDimension
open Set
open scoped ContDiff Topology
noncomputable section

variable {S : Type} [TopologicalSpace S] [ChartedSpace ‚ÑÇ S] [cms : IsManifold I œâ S]
variable {T : Type} [TopologicalSpace T] [ChartedSpace ‚ÑÇ T] [cmt : IsManifold I œâ T]

/-- The global 1D inverse function theorem (parameterized, open case): if `f : ‚ÑÇ ‚Üí S ‚Üí T`
    is nonsingular and injective on an open set `s`, it has a global analytic inverse. -/
public theorem global_complex_inverse_fun_open {f : ‚ÑÇ ‚Üí S ‚Üí T} [Nonempty S] {s : Set (‚ÑÇ √ó S)}
    (fa : ContMDiffOn II I œâ (uncurry f) s) (nc : ‚àÄ p : ‚ÑÇ √ó S, p ‚àà s ‚Üí mfderiv I I (f p.1) p.2 ‚â† 0)
    (inj : InjOn (fun p : ‚ÑÇ √ó S ‚Ü¶ (p.1, f p.1 p.2)) s) (so : IsOpen s) :
    ‚àÉ g : ‚ÑÇ ‚Üí T ‚Üí S,
      ContMDiffOnNhd II I (uncurry g) ((fun p : ‚ÑÇ √ó S ‚Ü¶ (p.1, f p.1 p.2)) '' s) ‚àß
        ‚àÄ p : ‚ÑÇ √ó S, p ‚àà s ‚Üí g p.1 (f p.1 p.2) = p.2 := by
  --obtain blah := complex_inverse_fun
  have i : ‚àÄ p : ‚ÑÇ √ó S, p ‚àà s ‚Üí ComplexInverseFun.Cinv f p.1 p.2 := by
    intro ‚ü®c, z‚ü© m; exact
      { fa := fa.contMDiffAt (so.mem_nhds m)
        nc := nc _ m }
  generalize hg : (fun c w ‚Ü¶
    if h : ‚àÉ z, (c, z) ‚àà s ‚àß f c z = w then choose h else Classical.arbitrary S) = g
  have left : ‚àÄ c z, (c, z) ‚àà s ‚Üí g c (f c z) = z := by
    intro c z m
    have h : ‚àÉ x, (c, x) ‚àà s ‚àß f c x = f c z := ‚ü®z, m, rfl‚ü©
    simp only [‚Üê hg, dif_pos h]
    rcases choose_spec h with ‚ü®m0, w0‚ü©
    have left := (i _ m).left_inv.self_of_nhds
    simp only at left
    have e : (c, choose h) = (c, (i _ m).g c (f c z)) := by
      refine (inj.eq_iff m0 ?_).mp ?_
      ¬∑ simp only [left, m]
      ¬∑ simp only [left, w0]
    rw [left] at e; exact (Prod.ext_iff.mp e).2
  have ge : ‚àÄ (p : ‚ÑÇ √ó S) (m : p ‚àà s), ‚àÄ·∂† q : ‚ÑÇ √ó T in ùìù (p.1, f p.1 p.2),
      g q.1 q.2 = (i p m).g q.1 q.2 := by
    intro ‚ü®c, z‚ü© m; simp only
    have n := nontrivialMAnalyticAt_of_mfderiv_ne_zero
      (fa.contMDiffAt (so.mem_nhds m)).along_snd (nc _ m); simp only at n
    simp only [n.nhds_eq_map_nhds_param (fa.contMDiffAt (so.mem_nhds m)), Filter.eventually_map]
    apply (i _ m).left_inv.mp; apply (so.eventually_mem m).mp
    refine .of_forall fun ‚ü®e, w‚ü© wm gf ‚Ü¶ ?_
    simp only at gf
    simp only [left _ _ wm, gf]
  use g; constructor
  ¬∑ intro ‚ü®c, w‚ü© wm
    rcases(mem_image _ _ _).mp wm with ‚ü®‚ü®c', z‚ü©, zm, e‚ü©
    simp only [Prod.ext_iff] at e; simp only [e.1] at e zm; simp only [‚Üê e.2]
    exact ((i _ zm).ga.congr_of_eventuallyEq (ge _ zm)).contMDiffWithinAt
  ¬∑ intro ‚ü®c, z‚ü© m; exact left _ _ m

/-- The global 1D inverse function theorem (compact case): if `f : S ‚Üí T` is nonsingular and
    injective on a compact set `s`, it has a global analytic inverse. -/
public theorem global_complex_inverse_fun_compact {f : ‚ÑÇ ‚Üí S ‚Üí T} [Nonempty S] [T2Space T]
    {s : Set (‚ÑÇ √ó S)} (fa : ContMDiffOnNhd II I (uncurry f) s)
    (nc : ‚àÄ p : ‚ÑÇ √ó S, p ‚àà s ‚Üí mfderiv I I (f p.1) p.2 ‚â† 0)
    (inj : InjOn (fun p : ‚ÑÇ √ó S ‚Ü¶ (p.1, f p.1 p.2)) s) (sc : IsCompact s) :
    ‚àÉ g : ‚ÑÇ ‚Üí T ‚Üí S,
      ContMDiffOnNhd II I (uncurry g) ((fun p : ‚ÑÇ √ó S ‚Ü¶ (p.1, f p.1 p.2)) '' s) ‚àß
        ‚àÄ·∂† p : ‚ÑÇ √ó S in ùìùÀ¢ s, g p.1 (f p.1 p.2) = p.2 := by
  -- Enlarge s while preserving injectivity
  have t : ‚àÉ t, IsOpen t ‚àß s ‚äÜ t ‚àß InjOn (fun p : ‚ÑÇ √ó S ‚Ü¶ (p.1, f p.1 p.2)) t := by
    apply inj.exists_isOpen_superset sc (fun _ m ‚Ü¶ continuousAt_fst.prodMk (fa _ m).continuousAt)
    intro ‚ü®c, z‚ü© m; rcases complex_inverse_fun (fa _ m) (nc _ m) with ‚ü®g, _, gf, _‚ü©
    rcases eventually_nhds_iff.mp gf with ‚ü®t, gf, o, m‚ü©
    use t, o.mem_nhds m; intro ‚ü®c0, z0‚ü© m0 ‚ü®c1, z1‚ü© m1 e
    simp only [Prod.ext_iff] at e ‚ä¢; use e.1
    have e0 := gf _ m0; have e1 := gf _ m1; simp only at e0 e1
    rw [‚Üê e0, ‚Üê e1, e.2, ‚Üê e.1]
  rcases t with ‚ü®t, ot, st, ti‚ü©
  -- Shrink t to recover openness and deriv ‚â† 0
  set u := t ‚à© {p | ContMDiffAt II I œâ (uncurry f) p ‚àß mfderiv I I (f p.1) p.2 ‚â† 0}
  have tu : u ‚äÜ t := inter_subset_left
  have su : s ‚äÜ u := subset_inter st (subset_inter fa nc)
  have uo : IsOpen u := by
    apply ot.inter; rw [isOpen_iff_eventually]; intro ‚ü®c, z‚ü© ‚ü®fa, nc‚ü©
    refine fa.eventually.mp ((mfderiv_ne_zero_eventually' fa nc).mp (.of_forall ?_))
    intro ‚ü®c, z‚ü© nc fa; use fa, nc
  -- Find our inverse on u
  have fa' : ‚àÄ x ‚àà u, ContMDiffAt II I œâ (uncurry f) x := fun _ m ‚Ü¶ (inter_subset_right m).1
  have d0 : ‚àÄ (p : ‚ÑÇ √ó S), p ‚àà u ‚Üí mfderiv I I (f p.fst) p.snd ‚â† 0 :=
    fun _ m ‚Ü¶ (inter_subset_right m).2
  rcases global_complex_inverse_fun_open
    (fun x m ‚Ü¶ (fa' x m).contMDiffWithinAt) d0 (ti.mono tu) uo with ‚ü®g, ga, gf‚ü©
  exact ‚ü®g, ga.mono (image_mono su), Filter.eventually_of_mem (uo.mem_nhdsSet.mpr su) gf‚ü©

/-- The global 1D inverse function theorem (weak, open case): if `f : S ‚Üí T` is nonsingular
    and injective on an open set `s`, it has a global analytic inverse (we remove the need
    for nonsingularity below, by deriving it from injectivity). -/
theorem weak_global_complex_inverse_fun_open {f : S ‚Üí T} [Nonempty S] {s : Set S}
    (fa : ContMDiffOn I I œâ f s) (nc : ‚àÄ z, z ‚àà s ‚Üí mfderiv I I f z ‚â† 0) (inj : InjOn f s)
    (so : IsOpen s) : ‚àÉ g : T ‚Üí S, ContMDiffOnNhd I I g (f '' s) ‚àß ‚àÄ z, z ‚àà s ‚Üí g (f z) = z := by
  set f' := fun (_ : ‚ÑÇ) (z : S) ‚Ü¶ f z
  have nc' : ‚àÄ p : ‚ÑÇ √ó S, p ‚àà (univ : Set ‚ÑÇ) √óÀ¢ s ‚Üí mfderiv I I (f' p.1) p.2 ‚â† 0 := by
    intro ‚ü®c, z‚ü© ‚ü®_, zs‚ü©; exact nc _ zs
  have inj' : InjOn (fun p : ‚ÑÇ √ó S ‚Ü¶ (p.1, f' p.1 p.2)) (univ √óÀ¢ s) := by
    intro ‚ü®c0, z0‚ü© ‚ü®_, zs0‚ü© ‚ü®c1, z1‚ü© ‚ü®_, zs1‚ü© h; simp only [Prod.ext_iff] at h zs0 zs1
    rw [h.1, inj zs0 zs1]; exact h.2
  have fa' : ‚àÄ x ‚àà univ √óÀ¢ s, ContMDiffAt II I œâ (uncurry f') x := by
    intro ‚ü®c, z‚ü© ‚ü®_, zs‚ü©
    exact (fa.contMDiffAt (so.mem_nhds zs)).comp_of_eq contMDiffAt_snd rfl
  rcases global_complex_inverse_fun_open (fun x m ‚Ü¶ (fa' x m).contMDiffWithinAt)
    nc' inj' (isOpen_univ.prod so) with ‚ü®g, ga, gf‚ü©
  use g 0
  constructor
  ¬∑ intro z ‚ü®w, m‚ü©
    exact (ga ‚ü®0, z‚ü© (by aesop)).along_snd
  ¬∑ intro z m; exact gf ‚ü®0, z‚ü© ‚ü®mem_univ _, m‚ü©

/-- The global 1D inverse function theorem (open case): if `f : S ‚Üí T` is injective on an
    open set `s`, it has a global analytic inverse. -/
public theorem global_complex_inverse_fun_open' {f : S ‚Üí T} [Nonempty S] {s : Set S}
    (fa : ContMDiffOn I I œâ f s) (inj : InjOn f s) (so : IsOpen s) :
    ‚àÉ g : T ‚Üí S, ContMDiffOnNhd I I g (f '' s) ‚àß ‚àÄ z, z ‚àà s ‚Üí g (f z) = z :=
  weak_global_complex_inverse_fun_open fa
    (fun _ m ‚Ü¶ inj.mfderiv_ne_zero so m (fa.contMDiffAt (so.mem_nhds m))) inj so

/-- The global 1D inverse function theorem (open, complex case): if `f : ‚ÑÇ ‚Üí ‚ÑÇ` is injective on an
    open set `s`, it has a global analytic inverse. -/
public theorem global_complex_inverse_fun_open'' {f : ‚ÑÇ ‚Üí ‚ÑÇ} {s : Set ‚ÑÇ}
    (fa : AnalyticOnNhd ‚ÑÇ f s) (inj : InjOn f s) (so : IsOpen s) :
    ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, AnalyticOnNhd ‚ÑÇ g (f '' s) ‚àß ‚àÄ z, z ‚àà s ‚Üí g (f z) = z := by
  have ‚ü®g,ga,gf‚ü© := global_complex_inverse_fun_open' (f := f) ?_ inj so
  ¬∑ refine ‚ü®g, ?_, gf‚ü©
    intro z m
    exact (ga z m).analyticAt
  ¬∑ intro z m
    specialize fa z m
    rw [analyticAt_iff_mAnalyticAt (I := I) (J := I)] at fa
    exact fa.contMDiffWithinAt
