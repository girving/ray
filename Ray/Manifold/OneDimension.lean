module
public import Mathlib.Analysis.Complex.Basic
public import Mathlib.Geometry.Manifold.ContMDiff.Defs
public import Mathlib.Geometry.Manifold.IsManifold.Basic
public import Mathlib.Geometry.Manifold.MFDeriv.Defs
public import Ray.Manifold.Defs
import Mathlib.Geometry.Manifold.ContMDiff.Atlas
import Mathlib.Geometry.Manifold.LocalInvariantProperties
import Mathlib.Geometry.Manifold.MFDeriv.SpecificFunctions
import Mathlib.Tactic.Cases
import Ray.Analytic.Analytic
import Ray.Hartogs.Osgood
import Ray.Manifold.Analytic
import Ray.Manifold.Manifold
import Ray.Misc.Multilinear

/-!
## Special properties of 1D complex manifolds

One complex dimension is special, and 1D complex manifolds inherit this specialness.

Unfortunately, a lot of proofs here are messy, as we abuse the definitional quality
of `TangentSpace I z = ‚ÑÇ` to do noncanonical field arithmetic over `‚ÑÇ`.
-/

open Filter (Tendsto)
open Function (uncurry)
open OneDimension
open Set
open scoped ContDiff Manifold Topology
noncomputable section

variable {S : Type} [TopologicalSpace S] [cs : ChartedSpace ‚ÑÇ S]
variable {T : Type} [TopologicalSpace T] [ct : ChartedSpace ‚ÑÇ T]
variable {U : Type} [TopologicalSpace U] [cu : ChartedSpace ‚ÑÇ U]

/-- 1D tangent spaces are nontrivial -/
instance one_dimension_tangentSpace_nontrivial (z : S) : Nontrivial (TangentSpace I z) := by
  simp only [TangentSpace]; infer_instance

/-- 1D tangent spaces are `NormedAddCommGroup`s -/
instance oneDimensionTangentSpaceNormedAddCommGroup (z : S) :
    NormedAddCommGroup (TangentSpace I z) := by
  simp only [TangentSpace]; infer_instance

/-- 1D tangent spaces are `NormedSpace`s -/
instance oneDimensionTangentSpaceNormedSpace (z : S) : NormedSpace ‚ÑÇ (TangentSpace I z) := by
  simp only [TangentSpace]; infer_instance

/-- The tangent space norm is `abs`, if we unpack types -/
theorem tangentSpace_norm_eq_complex_norm (z : S) (x : TangentSpace I z) :
    ‚Äñx‚Äñ = Complex.instNorm.norm x := rfl

/-- 1D tangent space maps are (noncanonically!) equivalent to `‚ÑÇ` (linear equivalence) -/
def mderivToScalar' (z : S) (w : T) : (TangentSpace I z ‚ÜíL[‚ÑÇ] TangentSpace I w) ‚âÉ‚Çó[‚ÑÇ] ‚ÑÇ where
  toFun := by intro x; have y : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ := x; exact y 1
  invFun := by intro x; have y : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ := x ‚Ä¢ ContinuousLinearMap.id ‚ÑÇ ‚ÑÇ; exact y
  map_add' x y := ContinuousLinearMap.add_apply _ _ _
  map_smul' s x := by
    simp only [RingHom.id_apply, smul_eq_mul]
    exact Eq.trans (ContinuousLinearMap.smul_apply _ _ _) (smul_eq_mul _ _)
  left_inv := by
    intro x; simp only; apply ContinuousLinearMap.ext; intro s
    simp only [ContinuousLinearMap.smul_apply, ContinuousLinearMap.id_apply, smul_eq_mul, mul_comm]
    exact Eq.trans (smul_eq_mul _ _).symm (Eq.trans (ContinuousLinearMap.map_smul _ _ _).symm
      (congr_arg _ (mul_one _)))
  right_inv := by
    intro x
    simp only [ContinuousLinearMap.smul_apply, ContinuousLinearMap.id_apply, smul_eq_mul, mul_one]

/-- 1D tangent space maps are (noncanonically!) equivalent to `‚ÑÇ` (continuous linear equivalence) -/
def mderivToScalar (z : S) (w : T) : (TangentSpace I z ‚ÜíL[‚ÑÇ] TangentSpace I w) ‚âÉL[‚ÑÇ] ‚ÑÇ where
  toLinearEquiv := mderivToScalar' z w
  continuous_toFun := by
    simp only [mderivToScalar']
    rw [Metric.continuous_iff]; intro x e ep; use e / 2, half_pos ep; intro y xy
    simp only [dist_eq_norm] at xy ‚ä¢
    have b := ContinuousLinearMap.le_of_opNorm_le _ xy.le (1 : ‚ÑÇ)
    simp only [tangentSpace_norm_eq_complex_norm, norm_one, mul_one] at b ‚ä¢
    exact lt_of_le_of_lt b (half_lt_self ep)
  continuous_invFun := by
    simp only [mderivToScalar']
    rw [Metric.continuous_iff]; intro x e ep; use e / 2, half_pos ep; intro y xy
    simp only [dist_eq_norm] at xy ‚ä¢
    refine lt_of_le_of_lt ?_ (half_lt_self ep)
    apply ContinuousLinearMap.opNorm_le_bound' _ (half_pos ep).le; intro s _
    -- Something's wrong with the type at this point, so rewrite it to make things go through
    have h : ‚Äñ(y ‚Ä¢ ContinuousLinearMap.id ‚ÑÇ ‚ÑÇ - x ‚Ä¢ ContinuousLinearMap.id ‚ÑÇ ‚ÑÇ) s‚Äñ ‚â§
        e/2 * ‚Äñs‚Äñ := by
      rw [ContinuousLinearMap.sub_apply, ContinuousLinearMap.smul_apply,
        ContinuousLinearMap.smul_apply]
      simp only [ContinuousLinearMap.id_apply, smul_eq_mul, ‚Üê mul_sub_right_distrib, norm_mul,
        tangentSpace_norm_eq_complex_norm] at xy ‚ä¢
      bound
    exact h

/-- Given nonzero `u`, a tangent space map `x` is `0` iff `x u = 0` -/
theorem mderiv_eq_zero_iff {z : S} {w : T} (f : TangentSpace I z ‚ÜíL[‚ÑÇ] TangentSpace I w)
    (u : TangentSpace I z) : f u = 0 ‚Üî f = 0 ‚à® u = 0 := by
  constructor
  ¬∑ rw [or_iff_not_imp_right]; intro f0 u0
    apply ContinuousLinearMap.ext; intro v
    simp only [TangentSpace] at f u v u0
    have e : v = (v * u‚Åª¬π) ‚Ä¢ u := by simp only [smul_eq_mul, mul_assoc, inv_mul_cancel‚ÇÄ u0, mul_one]
    rw [ContinuousLinearMap.zero_apply, e]
    refine Eq.trans (f.map_smul _ _) ?_
    rw [smul_eq_zero]; right; exact f0
  ¬∑ intro h; cases' h with h h
    simp only [h, ContinuousLinearMap.zero_apply]
    simp only [h, ContinuousLinearMap.map_zero]

/-- Given nonzero `u`, a tangent space map `x` is `0` iff `x u = 0` -/
theorem mderiv_eq_zero_iff' {z : S} {w : T} {f : TangentSpace I z ‚ÜíL[‚ÑÇ] TangentSpace I w}
    {u : TangentSpace I z} (u0 : u ‚â† 0) : f u = 0 ‚Üî f = 0 := by
  simp only [mderiv_eq_zero_iff, u0, or_false]

/-- Given nonzero `u`, a tangent space map `x` is `‚â† 0` iff `x u ‚â† 0` -/
theorem mderiv_ne_zero_iff {z : S} {w : T} (f : TangentSpace I z ‚ÜíL[‚ÑÇ] TangentSpace I w)
    (u : TangentSpace I z) : f u ‚â† 0 ‚Üî f ‚â† 0 ‚àß u ‚â† 0 := by
  simp only [‚Üê not_or]; exact Iff.not (mderiv_eq_zero_iff _ _)

/-- Given nonzero `u`, a tangent space map `x` is `‚â† 0` iff `x u ‚â† 0` -/
theorem mderiv_ne_zero_iff' {z : S} {w : T} {f : TangentSpace I z ‚ÜíL[‚ÑÇ] TangentSpace I w}
    {u : TangentSpace I z} (u0 : u ‚â† 0) : f u ‚â† 0 ‚Üî f ‚â† 0 := by
  simp only [ne_eq, mderiv_ne_zero_iff, u0, not_false_eq_true, and_true]

/-- 1D map composition is zero iff either side is -/
public theorem mderiv_comp_eq_zero_iff {x : S} {y : T} {z : U}
    (f : TangentSpace I y ‚ÜíL[‚ÑÇ] TangentSpace I z) (g : TangentSpace I x ‚ÜíL[‚ÑÇ] TangentSpace I y) :
    f.comp g = 0 ‚Üî f = 0 ‚à® g = 0 := by
  rcases exists_ne (0 : TangentSpace I x) with ‚ü®t, t0‚ü©
  constructor
  ¬∑ intro h; simp only [‚Üê mderiv_eq_zero_iff' t0, ContinuousLinearMap.comp_apply] at h
    by_cases g0 : g t = 0
    right; rw [mderiv_eq_zero_iff' t0] at g0; exact g0
    left; rwa [‚Üê mderiv_eq_zero_iff' g0]
  ¬∑ intro h; cases' h with h h; simp only [h, g.zero_comp]; simp only [h, f.comp_zero]

/-- 1D map composition is nonzero if both sides are -/
public theorem mderiv_comp_ne_zero {x : S} {y : T} {z : U}
    (f : TangentSpace I y ‚ÜíL[‚ÑÇ] TangentSpace I z) (g : TangentSpace I x ‚ÜíL[‚ÑÇ] TangentSpace I y) :
    f ‚â† 0 ‚Üí g ‚â† 0 ‚Üí f.comp g ‚â† 0 := by
  intro f0 g0; simp only [Ne, mderiv_comp_eq_zero_iff, f0, g0, or_self_iff, not_false_iff]

/-- Nonzero `mfderiv` implies differentiability -/
theorem has_mfderiv_at_of_mderiv_ne_zero {f : S ‚Üí T} {x : S} (d0 : mfderiv I I f x ‚â† 0) :
    MDifferentiableAt I I f x := by
  contrapose d0
  simp only [mfderiv, d0, if_false]

/-- If two functions have nonzero derivative, their composition has nonzero derivative -/
public theorem mderiv_comp_ne_zero' {f : T ‚Üí U} {g : S ‚Üí T} {x : S} :
    mfderiv I I f (g x) ‚â† 0 ‚Üí mfderiv I I g x ‚â† 0 ‚Üí mfderiv I I (fun x ‚Ü¶ f (g x)) x ‚â† 0 := by
  intro df dg
  have e : (fun x ‚Ü¶ f (g x)) = f ‚àò g := rfl
  rw [e, mfderiv_comp x (has_mfderiv_at_of_mderiv_ne_zero df) (has_mfderiv_at_of_mderiv_ne_zero dg)]
  exact mderiv_comp_ne_zero _ _ df dg

/-- Nonzero 1D derivatives are invertible -/
@[expose] public def mderivEquiv {z : S} {w : T} (f : TangentSpace I z ‚ÜíL[‚ÑÇ] TangentSpace I w)
    (f0 : f ‚â† 0) : TangentSpace I z ‚âÉL[‚ÑÇ] TangentSpace I w where
  toFun := f
  map_add' := f.map_add'
  map_smul' := f.map_smul'
  invFun := by
    intro x
    have f' : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ := f
    unfold TangentSpace at f x
    exact (f' 1)‚Åª¬π * x
  left_inv := by
    generalize hu : (1:‚ÑÇ) = u
    have u0 : u ‚â† 0 := by rw [‚Üêhu]; norm_num
    have h := mderiv_ne_zero_iff' (f := f) (u := (u : TangentSpace I z)) u0
    intro x; simp only [TangentSpace] at f x ‚ä¢
    have e : f x = (f u) * x := by
      rw [mul_comm, ‚Üê smul_eq_mul, ‚Üê f.map_smul, smul_eq_mul, ‚Üêhu, mul_one]
    simp only [e, ‚Üê mul_assoc]
    rw [inv_mul_cancel‚ÇÄ, one_mul]
    exact h.mpr f0
  right_inv := by
    generalize hu : (1:‚ÑÇ) = u
    have u0 : u ‚â† 0 := by rw [‚Üêhu]; norm_num
    have h := mderiv_ne_zero_iff' (f := f) (u := (u : TangentSpace I z)) u0
    intro x; simp only [TangentSpace] at f x ‚ä¢
    have e : ‚àÄ y : ‚ÑÇ, f y = (f u) * y := by
      intro y; rw [mul_comm, ‚Üê smul_eq_mul, ‚Üê f.map_smul, smul_eq_mul, ‚Üêhu, mul_one]
    rw [e ((f u)‚Åª¬π * x), ‚Üê mul_assoc, mul_inv_cancel‚ÇÄ, one_mul]
    exact h.mpr f0
  continuous_toFun := f.cont
  continuous_invFun := by
    simp only [TangentSpace] at f ‚ä¢; exact Continuous.mul continuous_const continuous_id

public theorem mderivEquiv_apply {z : S} {w : T} {f : TangentSpace I z ‚ÜíL[‚ÑÇ] TangentSpace I w}
    (f0 : f ‚â† 0) (x : TangentSpace I z) : mderivEquiv f f0 x = f x := by rfl

public theorem mderivEquiv_eq {z : S} {w : T} (f : TangentSpace I z ‚ÜíL[‚ÑÇ] TangentSpace I w)
    (f0 : f ‚â† 0) : ‚Üë(mderivEquiv f f0) = f := by
  apply ContinuousLinearMap.ext; intro x; rfl

/-- Identity derivatives are nonzero -/
public theorem id_mderiv_ne_zero {z : S} : mfderiv I I (fun z ‚Ü¶ z) z ‚â† 0 := by
  have d : MDifferentiableAt I I (fun z ‚Ü¶ z) z := mdifferentiableAt_id
  simp only [mfderiv, d, if_true, writtenInExtChartAt, ModelWithCorners.Boundaryless.range_eq_univ,
    fderivWithin_univ]
  have e : (fun w ‚Ü¶ extChartAt I z ((extChartAt I z).symm w)) =·∂†[ùìù (extChartAt I z z)] id := by
    apply ((isOpen_extChartAt_target z).eventually_mem (mem_extChartAt_target z)).mp
    refine .of_forall fun w m ‚Ü¶ ?_
    simp only [id, PartialEquiv.right_inv _ m]
  simp only [e.fderiv_eq, fderiv_id, Ne, ContinuousLinearMap.ext_iff, not_forall,
    ContinuousLinearMap.id_apply, Function.comp_def]
  use 1, one_ne_zero

/-- Critical points of iterations are precritical points -/
public theorem critical_iter {f : S ‚Üí S} {n : ‚Ñï} {z : S} (fa : ContMDiff I I œâ f)
    (c : Critical f^[n] z) : Precritical f z := by
  induction' n with n h
  ¬∑ rw [Function.iterate_zero, Critical, mfderiv_id, ‚Üê ContinuousLinearMap.opNorm_zero_iff,
      ContinuousLinearMap.norm_id] at c
    norm_num at c
  ¬∑ rw [Function.iterate_succ', Critical,
      mfderiv_comp z ((fa _).mdifferentiableAt (by decide))
       ((fa.iterate _ _).mdifferentiableAt (by decide)),
      mderiv_comp_eq_zero_iff] at c
    cases' c with c c; use n, c; exact h c

variable [IsManifold I œâ S] [IsManifold I œâ T] [IsManifold I œâ U]

/-- Chart derivatives are nonzero -/
public theorem extChartAt_mderiv_ne_zero' {z w : S} (m : w ‚àà (extChartAt I z).source) :
    mfderiv I I (extChartAt I z) w ‚â† 0 := by
  rcases exists_ne (0 : TangentSpace I z) with ‚ü®t, t0‚ü©
  rw [‚Üê mderiv_ne_zero_iff' t0]; contrapose t0
  have h := ContinuousLinearMap.ext_iff.mp (extChartAt_mderiv_left_inverse m) t
  simp only [ContinuousLinearMap.comp_apply] at h
  rw [‚Üêh.trans (ContinuousLinearMap.id_apply _), ContinuousLinearMap.apply_eq_zero_of_eq_zero]
  exact t0

/-- Chart derivatives are nonzero -/
public theorem extChartAt_symm_mderiv_ne_zero' {z : S} {w : ‚ÑÇ} (m : w ‚àà (extChartAt I z).target) :
    mfderiv I I (extChartAt I z).symm w ‚â† 0 := by
  rcases exists_ne (0 : TangentSpace I (extChartAt I z z)) with ‚ü®t, t0‚ü©
  rw [‚Üê mderiv_ne_zero_iff' t0]; contrapose t0
  have h := ContinuousLinearMap.ext_iff.mp (extChartAt_mderiv_right_inverse m) t
  simp only [ContinuousLinearMap.comp_apply] at h
  rw [‚Üêh.trans (ContinuousLinearMap.id_apply _), ContinuousLinearMap.apply_eq_zero_of_eq_zero]
  exact t0

/-- Chart derivatives are nonzero -/
public theorem extChartAt_mderiv_ne_zero (z : S) : mfderiv I I (extChartAt I z) z ‚â† 0 :=
  extChartAt_mderiv_ne_zero' (mem_extChartAt_source z)

/-- Chart derivatives are nonzero -/
public theorem extChartAt_symm_mderiv_ne_zero (z : S) :
    mfderiv I I (extChartAt I z).symm (extChartAt I z z) ‚â† 0 :=
  extChartAt_symm_mderiv_ne_zero' (mem_extChartAt_target z)

/-- Nonzeroness of `mfderiv` reduces to nonzeroness of `deriv` -/
public theorem mfderiv_eq_zero_iff_deriv_eq_zero {f : ‚ÑÇ ‚Üí ‚ÑÇ} {z : ‚ÑÇ} :
    mfderiv I I f z = 0 ‚Üî deriv f z = 0 := by
  by_cases d : DifferentiableAt ‚ÑÇ f z
  ¬∑ constructor
    ¬∑ have h := d.mdifferentiableAt.hasMFDerivAt; intro e; rw [e] at h
      have p := h.hasFDerivAt.hasDerivAt
      simp only at p; exact p.deriv
    ¬∑ have h := d.hasDerivAt
      intro e
      rw [e] at h
      have p := h.hasFDerivAt.hasMFDerivAt
      simp only [ContinuousLinearMap.toSpanSingleton_zero] at p
      exact p.mfderiv
  ¬∑ have d' : ¬¨MDifferentiableAt I I f z := by
      contrapose d; exact d.differentiableAt
    simp only [deriv_zero_of_not_differentiableAt d, mfderiv_zero_of_not_mdifferentiableAt d']

/-- `mfderiv ‚â† 0` iff `deriv ‚â† 0` -/
public theorem mfderiv_ne_zero_iff_deriv_ne_zero {f : ‚ÑÇ ‚Üí ‚ÑÇ} {z : ‚ÑÇ} :
    mfderiv I I f z ‚â† 0 ‚Üî deriv f z ‚â† 0 := by rw [not_iff_not, mfderiv_eq_zero_iff_deriv_eq_zero]

/-!
## Facts about `mfderiv` related to continuity and analyticity

These facts would ideally follow from continuity and analyticity of `mfderiv`, but we can't
express that directly as `mfderiv` lives in a different type at each point.  Rather than detour
into the necessary theory, I'm going to express what I need in coordinates for now.
-/

/-- A curried function in coordinates -/
@[expose] public def inChart (f : ‚ÑÇ ‚Üí S ‚Üí T) (c : ‚ÑÇ) (z : S) : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ := fun e w ‚Ü¶
  extChartAt I (f c z) (f e ((extChartAt I z).symm w))

/-- `inChart` is analytic -/
public theorem ContMDiffAt.inChart {f : ‚ÑÇ ‚Üí S ‚Üí T} {c : ‚ÑÇ} {z : S}
    (fa : ContMDiffAt II I œâ (uncurry f) (c, z)) :
    AnalyticAt ‚ÑÇ (uncurry (inChart f c z)) (c, _root_.extChartAt I z z) := by
  apply ContMDiffAt.analyticAt II I
  apply (contMDiffAt_extChartAt' (extChartAt_source I (f c z) ‚ñ∏
    (mem_extChartAt_source (f c z)))).comp_of_eq
  apply fa.comp‚ÇÇ_of_eq contMDiffAt_fst
  apply ((contMDiffOn_extChartAt_symm _).contMDiffAt
    (extChartAt_target_mem_nhds' (mem_extChartAt_target z))).comp_of_eq contMDiffAt_snd
  repeat' simp only [PartialEquiv.left_inv _ (mem_extChartAt_source z)]

/-- `inChart` preserves critical points locally -/
public theorem inChart_critical {f : ‚ÑÇ ‚Üí S ‚Üí T} {c : ‚ÑÇ} {z : S}
    (fa : ContMDiffAt II I œâ (uncurry f) (c, z)) :
    ‚àÄ·∂† p : ‚ÑÇ √ó S in ùìù (c, z),
      mfderiv I I (f p.1) p.2 = 0 ‚Üî deriv (inChart f c z p.1) (extChartAt I z p.2) = 0 := by
  apply (fa.continuousAt.eventually_mem ((isOpen_extChartAt_source (f c z)).mem_nhds
    (mem_extChartAt_source (I := I) (f c z)))).mp
  apply ((isOpen_extChartAt_source (c, z)).eventually_mem (mem_extChartAt_source (I := II) _)).mp
  refine fa.eventually.mp (.of_forall ?_); intro ‚ü®e, w‚ü© fa m fm
  simp only [extChartAt_prod, PartialEquiv.prod_source, extChartAt_eq_refl,
    PartialEquiv.refl_source, mem_prod, mem_univ, true_and] at m
  simp only [uncurry] at fm
  have m' := PartialEquiv.map_source _ m
  simp only [‚Üê mfderiv_eq_zero_iff_deriv_eq_zero]
  have cd : ContMDiffAt I I œâ (extChartAt I (f c z)) (f e w) := contMDiffAt_extChartAt' (extChartAt_source I (f c z) ‚ñ∏ fm)
  have fd : ContMDiffAt I I œâ (f e ‚àò (extChartAt I z).symm) (extChartAt I z w) := by
    simp only [Function.comp_def]
    exact ContMDiffAt.comp_of_eq fa.along_snd ((contMDiffOn_extChartAt_symm _).contMDiffAt
      (extChartAt_target_mem_nhds' m'))
      (PartialEquiv.right_inv _ m)
  have ce : inChart f c z e = extChartAt I (f c z) ‚àò f e ‚àò (extChartAt I z).symm := rfl
  rw [ce,
    mfderiv_comp_of_eq (cd.mdifferentiableAt (by decide)) (fd.mdifferentiableAt (by decide)) _,
    mfderiv_comp_of_eq (fa.along_snd.mdifferentiableAt (by decide))
      (((contMDiffOn_extChartAt_symm _).contMDiffAt
        (extChartAt_target_mem_nhds' m')).mdifferentiableAt WithTop.top_ne_zero)]
  ¬∑ simp only [mderiv_comp_eq_zero_iff, Function.comp]
    rw [(extChartAt I z).left_inv m]
    simp only [extChartAt_mderiv_ne_zero' fm, false_or]
    constructor
    ¬∑ intro h; left; exact h
    ¬∑ intro h; cases' h with h h; exact h; simpa only using extChartAt_symm_mderiv_ne_zero' m' h
  ¬∑ exact PartialEquiv.left_inv _ m
  ¬∑ simp only [Function.comp, PartialEquiv.left_inv _ m]

/-- `mfderiv` is nonzero near where it is nonzero (parameterized version) -/
public theorem mfderiv_ne_zero_eventually' {f : ‚ÑÇ ‚Üí S ‚Üí T} {c : ‚ÑÇ} {z : S}
    (fa : ContMDiffAt II I œâ (uncurry f) (c, z)) (f0 : mfderiv I I (f c) z ‚â† 0) :
    ‚àÄ·∂† p : ‚ÑÇ √ó S in ùìù (c, z), mfderiv I I (f p.1) p.2 ‚â† 0 := by
  set g := inChart f c z
  have g0 := inChart_critical fa
  have g0n : ‚àÄ·∂† p : ‚ÑÇ √ó S in ùìù (c, z), deriv (g p.1) (extChartAt I z p.2) ‚â† 0 := by
    refine ContinuousAt.eventually_ne ?_ ?_
    ¬∑ have e : (fun p : ‚ÑÇ √ó S ‚Ü¶ deriv (g p.1) (extChartAt I z p.2)) =
        (fun p : ‚ÑÇ √ó ‚ÑÇ ‚Ü¶ deriv (g p.1) p.2) ‚àò fun p : ‚ÑÇ √ó S ‚Ü¶ (p.1, extChartAt I z p.2) := rfl
      rw [e]
      exact fa.inChart.deriv2.continuousAt.comp_of_eq
        (continuousAt_fst.prodMk ((continuousAt_extChartAt z).comp_of_eq continuousAt_snd rfl))
        rfl
    ¬∑ contrapose f0; rw [g0.self_of_nhds]; exact f0
  refine g0.mp (g0n.mp (.of_forall fun w g0 e ‚Ü¶ ?_))
  rw [Ne, e]; exact g0

/-- `mfderiv` is nonzero near where it is nonzero -/
public theorem mfderiv_ne_zero_eventually {f : S ‚Üí T} {z : S} (fa : ContMDiffAt I I œâ f z)
    (f0 : mfderiv I I f z ‚â† 0) : ‚àÄ·∂† w in ùìù z, mfderiv I I f w ‚â† 0 := by
  set c : ‚ÑÇ := 0
  set g : ‚ÑÇ ‚Üí S ‚Üí T := fun _ z ‚Ü¶ f z
  have ga : ContMDiffAt II I œâ (uncurry g) (c, z) := by
    have e : uncurry g = f ‚àò fun p ‚Ü¶ p.2 := rfl; rw [e]
    apply ContMDiffAt.comp_of_eq fa contMDiffAt_snd; simp only
  have pc : Tendsto (fun z ‚Ü¶ (c, z)) (ùìù z) (ùìù (c, z)) := continuousAt_const.prodMk continuousAt_id
  exact pc.eventually (mfderiv_ne_zero_eventually' ga f0)

/-- The set of noncritical points is open -/
public theorem isOpen_noncritical {f : ‚ÑÇ ‚Üí S ‚Üí T} (fa : ContMDiff II I œâ (uncurry f)) :
    IsOpen {p : ‚ÑÇ √ó S | ¬¨Critical (f p.1) p.2} := by
  rw [isOpen_iff_eventually]; intro ‚ü®c, z‚ü© m; exact mfderiv_ne_zero_eventually' (fa _) m

/-- The set of critical points is closed -/
public theorem isClosed_critical {f : ‚ÑÇ ‚Üí S ‚Üí T} (fa : ContMDiff II I œâ (uncurry f)) :
    IsClosed {p : ‚ÑÇ √ó S | Critical (f p.1) p.2} := by
  have c := (isOpen_noncritical fa).isClosed_compl
  simp only [compl_setOf, not_not] at c; exact c

/-- Osgood's theorem on 2D product manifolds: separate analyticity + continuity
    implies joint analyticity.  I'm not sure if a Hartogs' analogue is possible,
    since we use continuity to remain within the right charts. -/
public theorem osgoodManifold {f : S √ó T ‚Üí U} (fc : Continuous f)
    (f0 : ‚àÄ x y, ContMDiffAt I I œâ (fun x ‚Ü¶ f (x, y)) x)
    (f1 : ‚àÄ x y, ContMDiffAt I I œâ (fun y ‚Ü¶ f (x, y)) y) : ContMDiff II I œâ f := by
  rw [mAnalytic_iff_of_boundaryless]; use fc; intro p; apply osgood_at'
  have fm : ‚àÄ·∂† q in ùìù (extChartAt II p p),
      f ((extChartAt II p).symm q) ‚àà (extChartAt I (f p)).source := by
    refine (fc.continuousAt.comp (continuousAt_extChartAt_symm p)).eventually_mem
        ((isOpen_extChartAt_source (f p)).mem_nhds ?_)
    simp only [Function.comp, (extChartAt II p).left_inv (mem_extChartAt_source _)]
    apply mem_extChartAt_source
  apply ((isOpen_extChartAt_target p).eventually_mem (mem_extChartAt_target p)).mp
  refine fm.mp (.of_forall fun q fm m ‚Ü¶ ‚ü®?_, ?_, ?_‚ü©)
  ¬∑ exact (continuousAt_extChartAt' fm).comp_of_eq
        (fc.continuousAt.comp (continuousAt_extChartAt_symm'' m)) rfl
  ¬∑ apply ContMDiffAt.analyticAt I I
    refine (contMDiffAt_extChartAt' (extChartAt_source I (f p) ‚ñ∏ fm)).comp_of_eq ?_ rfl
    rw [extChartAt_prod] at m
    simp only [Function.comp, extChartAt_prod, PartialEquiv.prod_symm, PartialEquiv.prod_coe,
      PartialEquiv.prod_target, mem_prod_eq] at m ‚ä¢
    exact (f0 _ _).comp _ ((contMDiffOn_extChartAt_symm _).contMDiffAt
      (extChartAt_target_mem_nhds' m.1))
  ¬∑ apply ContMDiffAt.analyticAt I I
    refine (contMDiffAt_extChartAt' (extChartAt_source I (f p) ‚ñ∏ fm)).comp_of_eq ?_ rfl
    rw [extChartAt_prod] at m
    simp only [Function.comp, extChartAt_prod, PartialEquiv.prod_symm, PartialEquiv.prod_coe,
      PartialEquiv.prod_target, mem_prod_eq] at m ‚ä¢
    exact (f1 _ _).comp _ ((contMDiffOn_extChartAt_symm _).contMDiffAt
      (extChartAt_target_mem_nhds' m.2))
