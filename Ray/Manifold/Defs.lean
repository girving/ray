module
public import Mathlib.Analysis.Complex.Basic
public import Mathlib.Geometry.Manifold.ContMDiff.Defs
public import Mathlib.Geometry.Manifold.MFDeriv.Defs

/-!
## Manifold definitions, allowing minimal public imports
-/

open scoped ContDiff Manifold Topology
noncomputable section

/-!
## General manifolds
-/

variable {ğ•œ E A F B M N : Type} [NontriviallyNormedField ğ•œ]

/-- Analyticity in a neighborhood of a set (the manifold analogue of `AnalyticOnNhd`) -/
@[expose] public def ContMDiffOnNhd {ğ•œ E A F B M N : Type} [NontriviallyNormedField ğ•œ]
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
    [TopologicalSpace A] [TopologicalSpace B] [TopologicalSpace M] [TopologicalSpace N]
    [ChartedSpace A M] [ChartedSpace B N] (I : ModelWithCorners ğ•œ E A) (J : ModelWithCorners ğ•œ F B)
    (f : M â†’ N) (s : Set M) : Prop := âˆ€ x âˆˆ s, ContMDiffAt I J Ï‰ f x

/-- A typeclass for trivial manifolds where `extChartAt` is the identity.
    In this case, `extChartAt I : E â†’ E`, but the intermediate space `H` might be different.
    This is necessary to handle product spaces, where the intermediate space may be `ModelProd`. -/
public class ExtChartEqRefl [NormedAddCommGroup E] [NormedSpace ğ•œ E] [TopologicalSpace A]
    (I : ModelWithCorners ğ•œ E A) [ChartedSpace A E] : Prop where
  eq_refl : âˆ€ x, extChartAt I x = PartialEquiv.refl E

/-- `extChartAt I x = refl` given [ExtChartEqRefl] -/
public theorem extChartAt_eq_refl [NormedAddCommGroup E] [NormedSpace ğ•œ E] [TopologicalSpace A]
    [ChartedSpace A E] {I : ModelWithCorners ğ•œ E A} [e : ExtChartEqRefl I] (x : E) :
    extChartAt I x = PartialEquiv.refl E :=
  e.eq_refl x

/-- `extChartAt = refl` for `I = modelWithCornersSelf ğ•œ E` -/
public instance extChartEqReflSelf [NormedAddCommGroup E] [NormedSpace ğ•œ E] :
    ExtChartEqRefl (modelWithCornersSelf ğ•œ E) := âŸ¨by
  simp only [OpenPartialHomeomorph.singletonChartedSpace_chartAt_eq,
    OpenPartialHomeomorph.refl_partialEquiv, PartialEquiv.refl_source, forall_const, extChartAt,
    OpenPartialHomeomorph.extend, modelWithCornersSelf_partialEquiv, PartialEquiv.refl_trans]âŸ©

/-- `extChartAt = refl` extends to products -/
public instance extChartEqReflProd [NormedAddCommGroup E] [NormedSpace ğ•œ E] [TopologicalSpace A]
    [NormedAddCommGroup F] [NormedSpace ğ•œ F] [TopologicalSpace B] [ChartedSpace A E]
    [ChartedSpace B F] {I : ModelWithCorners ğ•œ E A} [ExtChartEqRefl I] {J : ModelWithCorners ğ•œ F B}
    [ExtChartEqRefl J] : ExtChartEqRefl (I.prod J) :=
  âŸ¨fun x â†¦ by simp_rw [extChartAt_prod, extChartAt_eq_refl, PartialEquiv.refl_prod_refl]âŸ©

/-!
## One dimension
-/

variable {S : Type} [TopologicalSpace S] [ChartedSpace â„‚ S]
variable {T : Type} [TopologicalSpace T] [ChartedSpace â„‚ T]

/-- Abbreviation for `ğ“˜(â„‚,â„‚) = modelWithCornersSelf â„‚ â„‚` -/
public abbrev OneDimension.I := modelWithCornersSelf â„‚ â„‚

/-- Abbreviation for `ğ“˜(â„‚,â„‚).prod ğ“˜(â„‚,â„‚)` -/
public abbrev OneDimension.II := I.prod I

open OneDimension

/-- A critical point is where the derivative of `f` vanishes -/
@[expose] public def Critical (f : S â†’ T) (z : S) :=
  mfderiv I I f z = 0

/-- A precritical point is an iterated preimage of a critical point -/
@[expose] public def Precritical (f : S â†’ S) (z : S) :=
  âˆƒ n, Critical f (f^[n] z)

/-!
## Nontrivial analyticity
-/

/-- A nontrivial analytic function is one which is not locally constant -/
public structure NontrivialAnalyticOn (f : â„‚ â†’ â„‚) (s : Set â„‚) : Prop where
  analyticOn : AnalyticOnNhd â„‚ f s
  nonconst : âˆ€ x, x âˆˆ s â†’ âˆƒá¶  y in ğ“ x, f y â‰  f x

/-- A analytic function that is nonconstant near a point -/
public structure NontrivialMAnalyticAt (f : S â†’ T) (z : S) : Prop where
  mAnalyticAt : ContMDiffAt I I Ï‰ f z
  nonconst : âˆƒá¶  w in ğ“ z, f w â‰  f z

/-- `f` is nontrivial analytic everyone in `s` -/
@[expose] public def NontrivialMAnalyticOn (f : S â†’ T) (s : Set S) : Prop :=
  âˆ€ z, z âˆˆ s â†’ NontrivialMAnalyticAt f z
