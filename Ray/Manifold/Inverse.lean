module
public import Ray.Manifold.Defs
import Mathlib.Analysis.Calculus.InverseFunctionTheorem.ContDiff
import Mathlib.Geometry.Manifold.ContMDiff.Atlas
import Mathlib.Geometry.Manifold.MFDeriv.SpecificFunctions
import Ray.Analytic.Holomorphic
import Ray.Manifold.Analytic
import Ray.Manifold.Manifold
import Ray.Manifold.OneDimension

/-!
## The parameterized inverse function theorem on 1D complex manifolds

Given `f : ‚ÑÇ √ó S ‚Üí T`, we seek `g : ‚ÑÇ √ó T ‚Üí S` s.t. `g c (f c z) = z`.

The key theorems are `complex_inverse_fun` and `complex_inverse_fun'`; everything else is
intermediate lemmas.

These results are straightforward consequences of the 2D inverse function theorem
applied to `(c,z) ‚Ü¶ (c, f c z)` mapped to charts, but (at least for me)
this takes a while to write out.  A subtlety is that `TangentSpace I z` for
`z ‚àà ‚ÑÇ` is definitionally and canonically `‚ÑÇ`, and we take advantage of this
to express manifold invertibility in charts as nonmanifold invertibility.  But
this means that the type signatures on all the small definitions are very important
to make `simp` go through correctly.
-/

open Classical
open Filter (Tendsto)
open Function (uncurry)
open OneDimension
open Set
open scoped ContDiff OneDimension Topology
noncomputable section

variable {S : Type} [TopologicalSpace S] [ChartedSpace ‚ÑÇ S]
variable {T : Type} [TopologicalSpace T] [ChartedSpace ‚ÑÇ T]

namespace ComplexInverseFun

/-- Data for our 1D inverse function theorem -/
structure Cinv (f : ‚ÑÇ ‚Üí S ‚Üí T) (c : ‚ÑÇ) (z : S) : Prop where
  fa : ContMDiffAt II I œâ (uncurry f) (c, z)
  nc : mfderiv I I (f c) z ‚â† 0

variable {f : ‚ÑÇ ‚Üí S ‚Üí T} {c : ‚ÑÇ} {z : S}

/-- `z` in charts -/
@[nolint unusedArguments] def Cinv.z' (_ : Cinv f c z) : ‚ÑÇ := extChartAt I z z

/-- `f z` in charts -/
@[nolint unusedArguments] def Cinv.fz' (_ : Cinv f c z) : ‚ÑÇ := extChartAt I (f c z) (f c z)

lemma Cinv.zz (i : Cinv f c z) : (extChartAt I z).symm (c, i.z').snd = z := by
  simp only [Cinv.z', PartialEquiv.left_inv _ (mem_extChartAt_source _)]

/-- `f` in coordinates -/
@[nolint unusedArguments] def Cinv.f' (_ : Cinv f c z) : ‚ÑÇ √ó ‚ÑÇ ‚Üí ‚ÑÇ := fun x ‚Ü¶
  extChartAt I (f c z) (f x.1 ((extChartAt I z).symm x.2))

/-- `(c,z) ‚Üí (c, f c z)`, in coordinates.  We will show this function is invertible. -/
def Cinv.h (i : Cinv f c z) : ‚ÑÇ √ó ‚ÑÇ ‚Üí ‚ÑÇ √ó ‚ÑÇ := fun x ‚Ü¶ (x.1, i.f' x)

-- f' and h are analytic
theorem Cinv.fa' (i : Cinv f c z) : AnalyticAt ‚ÑÇ i.f' (c, i.z') := by
  have fa := i.fa
  simp only [mAnalyticAt_iff_of_boundaryless, uncurry, extChartAt_prod, PartialEquiv.prod_coe_symm,
    PartialEquiv.prod_coe] at fa
  exact fa.2
theorem Cinv.ha (i : Cinv f c z) : AnalyticAt ‚ÑÇ i.h (c, i.z') := analyticAt_fst.prod i.fa'

/-- The key nonzero derivative: `d(f c z)/dz` -/
@[nolint unusedArguments]
def Cinv.dfz (_ : Cinv f c z) : TangentSpace I z ‚ÜíL[‚ÑÇ] TangentSpace I (f c z) := mfderiv I I (f c) z

/-- The inverse of the key nonzero derivative: `(d(f c z)/dz)‚Åª¬π` -/
def Cinv.dfzi (i : Cinv f c z) :
    TangentSpace I (f c z) ‚ÜíL[‚ÑÇ] TangentSpace I z := (mderivEquiv i.dfz i.nc).symm

lemma Cinv.dfzi_dfz (i : Cinv f c z) : ‚àÄ t, i.dfzi (i.dfz t) = t :=
    fun _ ‚Ü¶ (mderivEquiv _ i.nc).left_inv _
lemma Cinv.dfz_dfzi (i : Cinv f c z) : ‚àÄ t, i.dfz (i.dfzi t) = t :=
    fun _ ‚Ü¶ (mderivEquiv _ i.nc).right_inv _

-- The derivative i.dh of i.h
--   dh = dc.prod (i.de'.comp (i.dfc.comp dc + i.dfz.comp (i.de.comp dz)))
--      = (    1               0      )
--        (de' ‚àò dfc    de' ‚àò dfz ‚àò de)

/-- The inverse chart derivative at `z` -/
def Cinv.de (i : Cinv f c z) : ‚ÑÇ ‚ÜíL[‚ÑÇ] TangentSpace I z := mfderiv I I (extChartAt I z).symm i.z'
/-- The chart derivative at `f c z` -/
def Cinv.de' (_ : Cinv f c z) :
    TangentSpace I (f c z) ‚ÜíL[‚ÑÇ] ‚ÑÇ := mfderiv I I (extChartAt I (f c z)) (f c z)
/-- The derivative of `(c,z) ‚Ü¶ c` is `fst` -/
def dc : ‚ÑÇ √ó ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ := ContinuousLinearMap.fst ‚ÑÇ ‚ÑÇ ‚ÑÇ
/-- The derivative of `(c,z) ‚Ü¶ z` is `snd` -/
def dz : ‚ÑÇ √ó ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ := ContinuousLinearMap.snd ‚ÑÇ ‚ÑÇ ‚ÑÇ
/-- `d(f c z)/dc` -/
def Cinv.dfc (_ : Cinv f c z) : ‚ÑÇ ‚ÜíL[‚ÑÇ] TangentSpace I (f c z) := mfderiv I I (fun c : ‚ÑÇ ‚Ü¶ f c z) c
/-- `df = d(f c z)/dc dc + d(f c z)/dz dz` -/
def Cinv.df (i : Cinv f c z) :
    ‚ÑÇ √ó ‚ÑÇ ‚ÜíL[‚ÑÇ] TangentSpace I (f c z) := i.dfc.comp dc + i.dfz.comp (i.de.comp dz)
/-- `df` in charts -/
def Cinv.df' (i : Cinv f c z) : ‚ÑÇ √ó ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ := i.de'.comp i.df
/-- `dh` (in charts) -/
def Cinv.dh (i : Cinv f c z) : ‚ÑÇ √ó ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ √ó ‚ÑÇ := dc.prod i.df'

-- dh is invertible
--   dh (u,v) = (a,b)
--   (u, (de' ‚àò dfc)u + (de' ‚àò dfz ‚àò de)v) = (a,b)
--   u = a
--   (de' ‚àò dfc)a + (de' ‚àò dfz ‚àò de)v = b
--   v = (de' ‚àò dfz ‚àò de)‚Åª¬π (b - (de' ‚àò dfc)a)
--   v = (de‚Åª¬π  ‚àò dfz‚Åª¬π ‚àò de'‚Åª¬π) (b - (de' ‚àò dfc)a)
/-- The chart derivative at `z` -/
def Cinv.dei (_ : Cinv f c z) :
    TangentSpace I z ‚ÜíL[‚ÑÇ] ‚ÑÇ := mfderiv I I (extChartAt I z) z
/-- The inverse chart derivative at `z` -/
def Cinv.dei' (i : Cinv f c z) :
    ‚ÑÇ ‚ÜíL[‚ÑÇ] TangentSpace I (f c z) := mfderiv I I (extChartAt I (f c z)).symm i.fz'
/-- The key inverse derivative of `f` w.r.t. `z`, in charts -/
def Cinv.dfi' (i : Cinv f c z) : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ := (i.dei.comp i.dfzi).comp i.dei'
/-- The overall inverse derivative of `h` -/
def Cinv.dhi (i : Cinv f c z) :
    ‚ÑÇ √ó ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ √ó ‚ÑÇ := dc.prod (i.dfi'.comp (dz - (i.de'.comp i.dfc).comp dc))

variable [cms : IsManifold I œâ S]

lemma Cinv.dei_de (i : Cinv f c z) : ‚àÄ t, i.dei (i.de t) = t := by
  intro t
  have h := ContinuousLinearMap.ext_iff.mp
    (extChartAt_mderiv_right_inverse' (mem_extChartAt_source (I := I) z)) t
  simp only [ContinuousLinearMap.comp_apply, ContinuousLinearMap.id_apply] at h; exact h

variable [cmt : IsManifold I œâ T]

lemma Cinv.has_df' (i : Cinv f c z) : HasMFDerivAt II I i.f' (c, i.z') i.df' := by
  apply HasMFDerivAt.comp (I' := I) (c, i.z')
  ¬∑ rw [i.zz]
    exact ((contMDiffAt_extChartAt' (mem_chart_source _ _)).mdifferentiableAt one_ne_zero).hasMFDerivAt
  ¬∑ simp only [Cinv.df]
    have fd := i.fa.mdifferentiableAt (by decide)
    rw [‚Üê i.zz] at fd
    apply MDifferentiableAt.hasMFDerivAt_comp2 fd
    ¬∑ apply hasMFDerivAt_fst
    ¬∑ refine HasMFDerivAt.comp _ ?_ (hasMFDerivAt_snd _)
      exact (((contMDiffOn_extChartAt_symm _).contMDiffAt
        (extChartAt_target_mem_nhds'
        (mem_extChartAt_target _))).mdifferentiableAt one_ne_zero).hasMFDerivAt
    ¬∑ rw [i.zz]; exact (i.fa.along_fst.mdifferentiableAt (by decide)).hasMFDerivAt
    ¬∑ rw [i.zz]; exact (i.fa.along_snd.mdifferentiableAt (by decide)).hasMFDerivAt

lemma Cinv.has_dh (i : Cinv f c z) : HasMFDerivAt II II i.h (c, i.z') i.dh := by
  refine HasMFDerivAt.prodMk ?_ i.has_df'; apply hasMFDerivAt_fst

omit cms in
lemma Cinv.dei_de' (i : Cinv f c z) : ‚àÄ t, i.dei' (i.de' t) = t := by
  intro t
  have h := ContinuousLinearMap.ext_iff.mp (extChartAt_mderiv_left_inverse
    (mem_extChartAt_source (f c z))) t
  simp only [ContinuousLinearMap.comp_apply] at h; exact h

omit cmt in
lemma Cinv.de_dei (i : Cinv f c z) : ‚àÄ t, i.de (i.dei t) = t := by
  intro t
  have h := ContinuousLinearMap.ext_iff.mp (extChartAt_mderiv_left_inverse
    (mem_extChartAt_source z)) t
  simp only [ContinuousLinearMap.comp_apply] at h; exact h

omit cms in
lemma Cinv.de_dei' (i : Cinv f c z) : ‚àÄ t, i.de' (i.dei' t) = t := by
  intro t
  have h := ContinuousLinearMap.ext_iff.mp (extChartAt_mderiv_right_inverse'
    (mem_extChartAt_source (I := I) (f c z))) t
  simp only [ContinuousLinearMap.comp_apply, ContinuousLinearMap.id_apply] at h; exact h

lemma Cinv.dhi_dh (i : Cinv f c z) : ‚àÄ t, i.dhi (i.dh t) = t := by
  intro ‚ü®u, v‚ü©
  simp only [Cinv.dh, Cinv.dhi, dc, dz, Cinv.dfi', Cinv.df', Cinv.df, i.dei_de', i.dei_de,
    i.dfzi_dfz, ContinuousLinearMap.comp_apply, ContinuousLinearMap.prod_apply,
    ContinuousLinearMap.sub_apply, ContinuousLinearMap.coe_fst', ContinuousLinearMap.coe_snd',
    ContinuousLinearMap.add_apply, ContinuousLinearMap.map_add, add_sub_cancel_left]

lemma Cinv.dh_dhi (i : Cinv f c z) : ‚àÄ t, i.dh (i.dhi t) = t := by
  intro ‚ü®u, v‚ü©
  simp only [Cinv.dh, Cinv.dhi, dc, dz, Cinv.dfi', Cinv.df', Cinv.df, i.de_dei', i.de_dei,
    i.dfz_dfzi, ContinuousLinearMap.comp_apply, ContinuousLinearMap.prod_apply,
    ContinuousLinearMap.sub_apply, ContinuousLinearMap.coe_fst', ContinuousLinearMap.coe_snd',
    ContinuousLinearMap.add_apply, ContinuousLinearMap.map_add, ContinuousLinearMap.map_sub,
    add_sub_cancel_left, ‚Üê add_sub_assoc]

/-- `dh` as a `ContinuousLinearEquiv` -/
def Cinv.dhe (i : Cinv f c z) : (‚ÑÇ √ó ‚ÑÇ) ‚âÉL[‚ÑÇ] ‚ÑÇ √ó ‚ÑÇ :=
  ContinuousLinearEquiv.equivOfInverse i.dh i.dhi i.dhi_dh i.dh_dhi

lemma Cinv.has_dhe (i : Cinv f c z) : HasFDerivAt i.h (i.dhe : ‚ÑÇ √ó ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ √ó ‚ÑÇ) (c, i.z') :=
  hasMFDerivAt_iff_hasFDerivAt'.mp i.has_dh

/-- `h` as a `PartialHomeomorph` -/
def Cinv.he (i : Cinv f c z) :=
  ContDiffAt.toOpenPartialHomeomorph i.h i.ha.contDiffAt i.has_dhe one_ne_zero

/-- `h` inverts at the point -/
theorem Cinv.inv_at (i : Cinv f c z) :
    (i.he.symm (c, extChartAt I (f c z) (f c z))).2 = extChartAt I z z := by
  have a := ContDiffAt.localInverse_apply_image i.ha.contDiffAt i.has_dhe one_ne_zero
  have e : ContDiffAt.localInverse i.ha.contDiffAt i.has_dhe one_ne_zero = i.he.symm := rfl
  rw [e] at a; clear e
  simp only [Cinv.z', Cinv.h, Cinv.f', PartialEquiv.left_inv _ (mem_extChartAt_source _)] at a
  rw [a]

/-- Our inverse function! -/
def Cinv.g (i : Cinv f c z) : ‚ÑÇ ‚Üí T ‚Üí S := fun b w ‚Ü¶
  (extChartAt I z).symm (i.he.symm (b, extChartAt I (f c z) w)).2

/-- `g` is a local left inverse -/
theorem Cinv.left_inv (i : Cinv f c z) : ‚àÄ·∂† x : ‚ÑÇ √ó S in ùìù (c, z), i.g x.1 (f x.1 x.2) = x.2 := by
  generalize ht :
      ((extChartAt II (c, z)).source ‚à© extChartAt II (c, z) ‚Åª¬π' i.he.source : Set (‚ÑÇ √ó S)) = t
  have o : IsOpen t := by
    rw [‚Üê ht]
    exact (continuousOn_extChartAt _).isOpen_inter_preimage (isOpen_extChartAt_source _)
      i.he.open_source
  have m : (c, z) ‚àà t := by
    simp only [mem_inter_iff, mem_preimage, mem_extChartAt_source, true_and, ‚Üê ht]
    exact ContDiffAt.mem_toOpenPartialHomeomorph_source i.ha.contDiffAt i.has_dhe one_ne_zero
  apply Filter.eventuallyEq_of_mem (o.mem_nhds m); intro x m
  simp only [mem_inter_iff, mem_preimage, extChartAt_prod, extChartAt_eq_refl, ‚Üê ht,
    PartialEquiv.prod_source, PartialEquiv.refl_source, mem_prod_eq, mem_univ, true_and,
    PartialEquiv.prod_coe, PartialEquiv.refl_coe, id] at m
  have inv := i.he.left_inv m.2
  simp only [Cinv.g]
  generalize hq : i.he.symm = q; rw [hq] at inv
  rw [Cinv.he, ContDiffAt.toOpenPartialHomeomorph_coe i.ha.contDiffAt i.has_dhe one_ne_zero] at inv
  simp only [Cinv.h, Cinv.f', PartialEquiv.left_inv _ m.1] at inv
  simp only [inv, PartialEquiv.left_inv _ m.1]

/-- `h‚Åª¬π` passes through its first argument -/
theorem Cinv.inv_fst (i : Cinv f c z) : ‚àÄ x, x ‚àà i.he.target ‚Üí (i.he.symm x).1 = x.1 := by
  intro x m
  have e : i.he (i.he.symm x) = x := i.he.right_inv m
  generalize hq : i.he.symm x = q; rw [hq] at e
  rw [Cinv.he, ContDiffAt.toOpenPartialHomeomorph_coe i.ha.contDiffAt i.has_dhe one_ne_zero,
    Cinv.h] at e
  rw [‚Üê e]

/-- `g` is a local right inverse -/
theorem Cinv.right_inv (i : Cinv f c z) :
    ‚àÄ·∂† x : ‚ÑÇ √ó T in ùìù (c, f c z), f x.1 (i.g x.1 x.2) = x.2 := by
  generalize ht : ((extChartAt II (c, f c z)).source ‚à© extChartAt II (c, f c z) ‚Åª¬π' i.he.target
      : Set (‚ÑÇ √ó T)) = t
  have o : IsOpen t := by
    rw [‚Üê ht]
    exact (continuousOn_extChartAt _).isOpen_inter_preimage (isOpen_extChartAt_source _)
      i.he.open_target
  have m' : (c, extChartAt I (f c z) (f c z)) ‚àà i.he.toPartialEquiv.target := by
    have m := ContDiffAt.image_mem_toOpenPartialHomeomorph_target i.ha.contDiffAt i.has_dhe
      one_ne_zero
    have e : i.h (c, i.z') = (c, extChartAt I (f c z) (f c z)) := by
      simp only [Cinv.h, Cinv.z', Cinv.f', PartialEquiv.left_inv _ (mem_extChartAt_source _)]
    rw [e] at m; exact m
  have m : (c, f c z) ‚àà t := by
    simp only [m', mem_inter_iff, mem_preimage, mem_extChartAt_source, true_and, ‚Üê ht,
      extChartAt_prod, PartialEquiv.prod_coe, extChartAt_eq_refl, PartialEquiv.refl_coe, id,
      PartialEquiv.prod_source, prodMk_mem_set_prod_eq, PartialEquiv.refl_source, mem_univ]
  have fm : ‚àÄ·∂† x : ‚ÑÇ √ó T in ùìù (c, f c z),
      f x.1 ((extChartAt I z).symm (i.he.symm (x.1, extChartAt I (f c z) x.2)).2) ‚àà
        (extChartAt I (f c z)).source := by
    refine ContinuousAt.eventually_mem ?_ (extChartAt_source_mem_nhds' ?_)
    ¬∑ apply i.fa.continuousAt.comp‚ÇÇ_of_eq continuousAt_fst
      ¬∑ refine ContinuousAt.comp ?_ ?_
        ¬∑ simp only [i.inv_at]; exact continuousAt_extChartAt_symm _
        ¬∑ apply continuousAt_snd.comp
          ¬∑ refine (OpenPartialHomeomorph.continuousAt i.he.symm ?_).comp ?_
            ¬∑ simp only [m', (he i).symm_source]
            ¬∑ apply continuousAt_fst.prodMk
              apply (continuousAt_extChartAt _).comp_of_eq
              ¬∑ exact continuousAt_snd
              ¬∑ rfl
      ¬∑ simp only [i.inv_at, PartialEquiv.left_inv _ (mem_extChartAt_source _)]
    ¬∑ simp only [i.inv_at, PartialEquiv.left_inv _ (mem_extChartAt_source _)]
      apply mem_extChartAt_source
  refine fm.mp (Filter.eventually_of_mem (o.mem_nhds m) ?_)
  intro x m mf
  simp only [mem_inter_iff, mem_preimage, extChartAt_prod, extChartAt_eq_refl,
    PartialEquiv.prod_source, PartialEquiv.refl_source, mem_prod_eq, mem_univ, true_and,
    PartialEquiv.prod_coe, PartialEquiv.refl_coe, id, ‚Üê ht] at m
  have inv := i.he.right_inv m.2
  simp only [Cinv.g]
  generalize hq : i.he.symm = q; rw [hq] at inv mf
  rw [Cinv.he, ContDiffAt.toOpenPartialHomeomorph_coe i.ha.contDiffAt i.has_dhe one_ne_zero] at inv
  have q1 : (q (x.1, extChartAt I (f c z) x.2)).1 = x.1 := by simp only [‚Üê hq, i.inv_fst _ m.2]
  simp only [Cinv.h, Cinv.f', Prod.eq_iff_fst_eq_snd_eq, q1] at inv
  nth_rw 2 [‚Üê PartialEquiv.left_inv _ m.1]; nth_rw 2 [‚Üê inv.2]
  refine (PartialEquiv.left_inv _ mf).symm

theorem Cinv.he_symm_mAnalytic (i : Cinv f c z) : ContMDiffAt II II œâ i.he.symm (c, i.fz') := by
  have d : ContDiffAt ‚ÑÇ œâ i.he.symm _ :=
    ContDiffAt.to_localInverse i.ha.contDiffAt i.has_dhe (by decide)
  have e : i.h (c, i.z') = (c, i.fz') := by
    simp only [Cinv.h, Cinv.fz', Cinv.f']
    simp only [Cinv.z', (extChartAt I z).left_inv (mem_extChartAt_source _)]
  rw [e] at d
  rw [‚Üê analyticAt_iff_mAnalyticAt]
  exact (contDiffAt_iff_analytic_at2 le_top).mp d

/-- Our inverse `g` is analytic -/
theorem Cinv.ga (i : Cinv f c z) : ContMDiffAt II I œâ (uncurry i.g) (c, f c z) := by
  apply ((contMDiffOn_extChartAt_symm _).contMDiffAt
    (extChartAt_target_mem_nhds' (mem_extChartAt_target z))).comp_of_eq
  ¬∑ refine contMDiffAt_snd.comp _ (i.he_symm_mAnalytic.comp_of_eq ?_ ?_)
    ¬∑ apply contMDiffAt_fst.prodMk
      refine (contMDiffAt_extChartAt' ?_).comp _ contMDiffAt_snd
      exact mem_chart_source _ _
    ¬∑ rfl
  ¬∑ exact i.inv_at

end ComplexInverseFun

variable [IsManifold I œâ S] [IsManifold I œâ T]

/-- The 1D inverse function theorem for complex manifolds (parameterized version):
    If `f : ‚ÑÇ ‚Üí S ‚Üí T` is analytic with nonzero derivative (w.r.t. the second
    argument) at a point `(c,z)`, it is a parameterized local inverse `g : ‚ÑÇ ‚Üí T ‚Üí S` s.t.
    `g c (f c z) = z` and `f c (g c z) = z` locally. -/
public theorem complex_inverse_fun {f : ‚ÑÇ ‚Üí S ‚Üí T} {c : ‚ÑÇ} {z : S}
    (fa : ContMDiffAt II I œâ (uncurry f) (c, z)) (nc : mfderiv I I (f c) z ‚â† 0) :
    ‚àÉ g : ‚ÑÇ ‚Üí T ‚Üí S,
      ContMDiffAt II I œâ (uncurry g) (c, f c z) ‚àß
        (‚àÄ·∂† x : ‚ÑÇ √ó S in ùìù (c, z), g x.1 (f x.1 x.2) = x.2) ‚àß
          ‚àÄ·∂† x : ‚ÑÇ √ó T in ùìù (c, f c z), f x.1 (g x.1 x.2) = x.2 := by
  have i : ComplexInverseFun.Cinv f c z :=
    { fa
      nc }
  use i.g, i.ga, i.left_inv, i.right_inv

/-- The 1D inverse function theorem for complex manifolds (nonparameterized version):
    If `f : S ‚Üí T` is analytic with nonzero derivative, it has a local inverse `g : T ‚Üí S`. -/
public theorem complex_inverse_fun' {f : S ‚Üí T} {z : S} (fa : ContMDiffAt I I œâ f z)
    (nc : mfderiv I I f z ‚â† 0) :
    ‚àÉ g : T ‚Üí S,
      ContMDiffAt I I œâ g (f z) ‚àß (‚àÄ·∂† x in ùìù z, g (f x) = x) ‚àß ‚àÄ·∂† x in ùìù (f z), f (g x) = x := by
  set f' : ‚ÑÇ ‚Üí S ‚Üí T := fun _ z ‚Ü¶ f z
  have fa' : ContMDiffAt II I œâ (uncurry f') (0, z) := fa.comp_of_eq contMDiffAt_snd rfl
  rcases complex_inverse_fun fa' nc with ‚ü®g, ga, gf, fg‚ü©
  use g 0, ga.comp _ (contMDiffAt_const.prodMk contMDiffAt_id),
    (continuousAt_const.prodMk continuousAt_id).eventually gf,
    (continuousAt_const.prodMk continuousAt_id).eventually fg
