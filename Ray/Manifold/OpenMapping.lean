module
public import Ray.Manifold.Defs
import Mathlib.Analysis.Complex.OpenMapping
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.RingTheory.RootsOfUnity.Complex
import Mathlib.Tactic.Cases
import Ray.Analytic.Holomorphic
import Ray.Manifold.Analytic
import Ray.Manifold.Manifold
import Ray.Manifold.Nontrivial
import Ray.Manifold.OneDimension
import Ray.Misc.Connected
import Ray.Misc.Topology
import Ray.Misc.TotallyDisconnected

/-!
## The open mapping theorem on 1D complex manifolds

`AnalyticAt.eventually_constant_or_nhds_le_map_nhds` shows that `‚ÑÇ ‚Üí ‚ÑÇ` analytic
functions are either locally constant or locally open (mapping open neighborhoods to
open neighborhoods).  We slightly generalize this result, to

1. Parameterized analytic maps `f : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ`, where the analogue of openness for `f`
   is openness of `(c,z) ‚Ü¶ (c, f c z)`.
2. MAnalytic maps `S ‚Üí T` where `S, T` are 1D analytic manifolds
3. (1) and (2) together: parameterized analytic maps `f : ‚ÑÇ ‚Üí S ‚Üí T`, where
   `S, T` are 1D analytic manifolds.

The parameterized versions follow straightforwardly from effective versions of the
unparameterized version, and specificaly our underlying workhorse is
`DiffContOnCl.ball_subset_image_closedBall`.  The manifold versions are straightforward
extentions of the flat versions lifted to charts.
-/

open Classical
open Complex
open Filter (Tendsto)
open Function (curry uncurry)
open Metric (ball closedBall isOpen_ball mem_ball mem_closedBall mem_ball_self
  mem_closedBall_self mem_sphere sphere)
open OneDimension
open Set
open scoped ContDiff Real Topology Manifold
noncomputable section

variable {X : Type} [TopologicalSpace X]
variable {S : Type} [TopologicalSpace S] [ChartedSpace ‚ÑÇ S]
variable {T : Type} [TopologicalSpace T] [ChartedSpace ‚ÑÇ T]
variable {U : Type} [TopologicalSpace U] [ChartedSpace ‚ÑÇ U]

/-- Nontriviality at a point from nontriviality on a sphere -/
theorem nontrivial_local_of_global {f : ‚ÑÇ ‚Üí ‚ÑÇ} {z : ‚ÑÇ} {e r : ‚Ñù}
    (fa : AnalyticOnNhd ‚ÑÇ f (closedBall z r))
    (rp : 0 < r) (ep : 0 < e) (ef : ‚àÄ w, w ‚àà sphere z r ‚Üí e ‚â§ ‚Äñf w - f z‚Äñ) :
    NontrivialMAnalyticAt f z := by
  replace fa : ‚àÉ t, r < t ‚àß AnalyticOnNhd ‚ÑÇ f (ball z t) :=
    exists_ball_superset fa (isOpen_analyticAt ‚ÑÇ f)
  obtain ‚ü®t, rt, fa‚ü© := fa
  have fh : ‚àÄ x ‚àà ball z t, ContMDiffAt I I œâ f x := fun _ m ‚Ü¶ (fa _ m).mAnalyticAt I I
  have zs : z ‚àà ball z t := mem_ball_self (by linarith)
  use fh _ zs
  contrapose ef
  simp only [Filter.not_frequently, not_not] at ef
  simp only [not_forall, not_le]
  have zrs : z + r ‚àà sphere z r := by
    simp only [mem_sphere, Complex.dist_eq, add_sub_cancel_left, Complex.norm_real, abs_of_pos rp,
      Real.norm_eq_abs]
  use z + r, zrs
  have lc := ContMDiffOn.const_of_locally_const (fun _ m ‚Ü¶ (fh _ m).contMDiffWithinAt) zs
      isOpen_ball (convex_ball z t).isPreconnected ef (z + r) (Metric.sphere_subset_ball rt zrs)
  simp only [lc, sub_self, norm_zero, ep]

/-- The effective parameterized open mapping theorem for analytic `f : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ`.
    We lose more effectiveness than is optimal, since our goal is ineffective versions. -/
theorem AnalyticOnNhd.ball_subset_image_closedBall_param {f : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ} {c z : ‚ÑÇ} {e r : ‚Ñù}
    {u : Set ‚ÑÇ} (fa : AnalyticOnNhd ‚ÑÇ (uncurry f) (u √óÀ¢ closedBall z r)) (rp : 0 < r) (ep : 0 < e)
    (un : u ‚àà ùìù c) (ef : ‚àÄ d, d ‚àà u ‚Üí ‚àÄ w, w ‚àà sphere z r ‚Üí e ‚â§ ‚Äñf d w - f d z‚Äñ) :
    (fun p : ‚ÑÇ √ó ‚ÑÇ ‚Ü¶ (p.1, f p.1 p.2)) '' u √óÀ¢ closedBall z r ‚àà ùìù (c, f c z) := by
  have fn : ‚àÄ d, d ‚àà u ‚Üí ‚àÉ·∂† w in ùìù z, f d w ‚â† f d z := by
    refine fun d m ‚Ü¶ (nontrivial_local_of_global (fa.along_snd.mono ?_) rp ep (ef d m)).nonconst
    simp only [mem_prod_eq, setOf_mem_eq, (iff_true _).mpr m, true_and, subset_refl]
  have op : ‚àÄ d, d ‚àà u ‚Üí ball (f d z) (e / 2) ‚äÜ f d '' closedBall z r := by
    intro d du; refine DiffContOnCl.ball_subset_image_closedBall ?_ rp (ef d du) (fn d du)
    have e : f d = uncurry f ‚àò fun w ‚Ü¶ (d, w) := rfl
    rw [e]; apply DifferentiableOn.diffContOnCl; apply AnalyticOnNhd.differentiableOn
    refine fa.comp (analyticOnNhd_const.prod analyticOnNhd_id) ?_
    intro w wr; simp only [closure_ball _ rp.ne'] at wr
    simp only [mem_prod_eq, wr, true_and, du]
  rcases Metric.continuousAt_iff.mp
      (fa (c, z) (mk_mem_prod (mem_of_mem_nhds un) (mem_closedBall_self rp.le))).continuousAt
      (e / 4) (by linarith) with
    ‚ü®s, sp, sh‚ü©
  rw [mem_nhds_prod_iff]
  refine ‚ü®u ‚à© ball c s, Filter.inter_mem un (Metric.ball_mem_nhds c (by linarith)), ?_‚ü©
  use ball (f c z) (e / 4), Metric.ball_mem_nhds _ (by linarith)
  intro ‚ü®d, w‚ü© m
  simp only [mem_inter_iff, mem_prod_eq, mem_image, @mem_ball _ _ c] at m op ‚ä¢
  have wm : w ‚àà ball (f d z) (e / 2) := by
    simp only [mem_ball] at m ‚ä¢
    specialize @sh ‚ü®d, z‚ü©; simp only [Prod.dist_eq, dist_self, Function.uncurry] at sh
    specialize sh (max_lt m.1.2 sp); rw [dist_comm] at sh
    calc dist w (f d z)
      _ ‚â§ dist w (f c z) + dist (f c z) (f d z) := by bound
      _ < e / 4 + dist (f c z) (f d z) := by linarith [m.2]
      _ ‚â§ e / 4 + e / 4 := by linarith [sh]
      _ = e / 2 := by ring
  specialize op d m.1.1 wm
  rcases (mem_image _ _ _).mp op with ‚ü®y, yr, yw‚ü©
  use‚ü®d, y‚ü©
  simp only [yw, and_true, yr, m.1.1]

/-- A trivial lemma used repeatedly below -/
theorem norm_sub_self_lt {z : ‚ÑÇ} {r : ‚Ñù} (rp : 0 < r) : ‚Äñz - z‚Äñ < r := by
  simp [sub_self, norm_zero, rp]

/-- The parameterized open mapping theorem for analytic `f : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ`:
    `(c,z) ‚Ü¶ (c, f c z)` sends neighborhoods to neighborhoods if `f` is nontrivial. -/
public theorem NontrivialMAnalyticAt.nhds_le_map_nhds_param' {f : ‚ÑÇ ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ} {c z : ‚ÑÇ}
    (n : NontrivialMAnalyticAt (f c) z) (fa : AnalyticAt ‚ÑÇ (uncurry f) (c, z)) :
    ùìù (c, f c z) ‚â§ Filter.map (fun p : ‚ÑÇ √ó ‚ÑÇ ‚Ü¶ (p.1, f p.1 p.2)) (ùìù (c, z)) := by
  -- Reduce to a neighborhood of (c,z) on which f is analytic
  rw [Filter.le_map_iff]
  intro s' sn
  generalize hs : s' ‚à© {p | AnalyticAt ‚ÑÇ (uncurry f) p} = s
  have ss : s ‚äÜ s' := by rw [‚Üê hs]; apply inter_subset_left
  replace sn : s ‚àà ùìù (c, z) := by rw [‚Üê hs]; exact Filter.inter_mem sn fa.eventually_analyticAt
  replace fa : AnalyticOnNhd ‚ÑÇ (uncurry f) s := by rw [‚Üê hs]; apply inter_subset_right
  refine Filter.mem_of_superset ?_ (image_mono ss)
  clear ss hs s'
  rcases Metric.mem_nhds_iff.mp sn with ‚ü®e, ep, es‚ü©
  -- Find a radius within s where f c is nontrivial
  have er : ‚àÉ r, 0 < r ‚àß closedBall (c, z) r ‚äÜ s ‚àß f c z ‚àâ f c '' sphere z r := by
    have h := n.eventually_ne; contrapose h
    simp only [not_exists, not_not, not_and, not_exists] at h
    simp only [_root_.not_imp, not_not, Filter.eventually_iff, Metric.mem_nhds_iff, not_exists,
      not_subset, mem_setOf, not_and]
    intro r rp; specialize h (min (e/2) (r/2)) ?_ ?_
    ¬∑ bound
    ¬∑ exact _root_.trans (Metric.closedBall_subset_ball (lt_of_le_of_lt (min_le_left _ _)
        (half_lt_self ep))) es
    ¬∑ rcases (mem_image _ _ _).mp h with ‚ü®w, ws, wz‚ü©
      use w; refine ‚ü®?_, ?_, wz‚ü©
      ¬∑ exact Metric.closedBall_subset_ball (lt_of_le_of_lt (min_le_right _ _) (half_lt_self rp))
          (Metric.sphere_subset_closedBall ws)
      ¬∑ contrapose ws
        simp only [ws, Metric.mem_sphere, dist_self]
        exact ne_of_lt (by bound)
  rcases er with ‚ü®r, rp, rs, fr‚ü©
  -- Get a lower bound of f c '' sphere z r, then extend to a neighborhood of c
  have fc : ContinuousOn (fun w ‚Ü¶ ‚Äñf c w - f c z‚Äñ) (sphere z r) := by
    apply ContinuousOn.norm; refine ContinuousOn.sub ?_ continuousOn_const
    apply fa.along_snd.continuousOn.mono; intro x xs; apply rs
    simp only [‚Üê closedBall_prod_same, mem_prod_eq]
    use Metric.mem_closedBall_self rp.le, Metric.sphere_subset_closedBall xs
  rcases (isCompact_sphere _ _).exists_isMinOn (NormedSpace.sphere_nonempty.mpr rp.le) fc with
    ‚ü®x, xs, xm‚ü©
  generalize he : ‚Äñf c x - f c z‚Äñ = e
  have ep : 0 < e := by
    contrapose fr
    simp only [norm_pos_iff, sub_ne_zero, not_not, mem_image, ‚Üê he] at fr ‚ä¢
    use x, xs, fr
  rcases Metric.uniformContinuousOn_iff.mp
      ((isCompact_closedBall _ _).uniformContinuousOn_of_continuous (fa.continuousOn.mono rs))
      (e / 4) (by linarith) with
    ‚ü®t, tp, ft‚ü©
  have ef : ‚àÄ d, d ‚àà ball c (min t r) ‚Üí ‚àÄ w, w ‚àà sphere z r ‚Üí e / 2 ‚â§ ‚Äñf d w - f d z‚Äñ := by
    intro d dt w wr
    simp only [Complex.dist_eq, Prod.forall, mem_closedBall, Prod.dist_eq, max_le_iff, max_lt_iff,
      Function.uncurry, and_imp] at ft
    simp only [mem_ball, Complex.dist_eq, lt_min_iff] at dt
    have a1 : ‚Äñf d w - f c w‚Äñ ‚â§ e / 4 :=
      (ft d w dt.2.le (le_of_eq wr) c w (norm_sub_self_lt rp).le (le_of_eq wr) dt.1
        (norm_sub_self_lt tp)).le
    have a2 : ‚Äñf c z - f d z‚Äñ ‚â§ e / 4 := by
      refine (ft c z (norm_sub_self_lt rp).le (norm_sub_self_lt rp).le d z
          dt.2.le (norm_sub_self_lt rp).le ?_ (norm_sub_self_lt tp)).le
      rw [‚Üê neg_sub, norm_neg]; exact dt.1
    calc ‚Äñf d w - f d z‚Äñ
      _ = ‚Äñf c w - f c z + (f d w - f c w) + (f c z - f d z)‚Äñ := by ring_nf
      _ ‚â• ‚Äñf c w - f c z + (f d w - f c w)‚Äñ - ‚Äñf c z - f d z‚Äñ := by bound
      _ ‚â• ‚Äñf c w - f c z‚Äñ - ‚Äñf d w - f c w‚Äñ - ‚Äñf c z - f d z‚Äñ := by bound
      _ ‚â• e - e / 4 - e / 4 := by rw [‚Üê he] at a1 a2 ‚ä¢; exact sub_le_sub (sub_le_sub (xm wr) a1) a2
      _ = e / 2 := by ring
  -- Apply the partially effective parameterized open mapping theorem
  have ss : ball c (min t r) √óÀ¢ closedBall z r ‚äÜ s := by
    refine _root_.trans ?_ rs; rw [‚Üê closedBall_prod_same]; apply prod_mono_left
    exact _root_.trans (Metric.ball_subset_ball (min_le_right _ _)) Metric.ball_subset_closedBall
  exact Filter.mem_of_superset ((fa.mono ss).ball_subset_image_closedBall_param rp (half_pos ep)
    (Metric.ball_mem_nhds _ (by bound)) ef) (image_mono ss)

/-- If `f : S ‚Üí T` is nontrivial, it is nontrivial when written in charts -/
theorem NontrivialMAnalyticAt.inCharts {f : S ‚Üí T} {z : S} (n : NontrivialMAnalyticAt f z) :
    NontrivialMAnalyticAt (fun w ‚Ü¶ extChartAt I (f z) (f ((extChartAt I z).symm w)))
      (extChartAt I z z) := by
  use (mAnalyticAt_iff_of_boundaryless.mp n.mAnalyticAt).2.mAnalyticAt I I
  have c := n.nonconst; contrapose c
  simp only [Filter.not_frequently, not_not, ‚Üê map_extChartAt_nhds_of_boundaryless z,
    Filter.eventually_map] at c ‚ä¢
  apply c.mp
  apply ((isOpen_extChartAt_source z).eventually_mem (mem_extChartAt_source (I := I) z)).mp
  apply (n.mAnalyticAt.continuousAt.eventually_mem (extChartAt_source_mem_nhds (I := I) (f z))).mp
  refine .of_forall fun w fm m fn ‚Ü¶ ?_
  simp only at fm m fn
  rw [PartialEquiv.left_inv _ m, PartialEquiv.left_inv _ (mem_extChartAt_source z)] at fn
  exact ((PartialEquiv.injOn _).eq_iff fm (mem_extChartAt_source _)).mp fn

/-- The local open mapping theorem, manifold version: if `f : S ‚Üí T` is nontrivial,
    `f` sends neighborhoods to neighborhoods.  This is a manifold version of
    `AnalyticAt.eventually_constant_or_nhds_le_map_nhds`. -/
public theorem NontrivialMAnalyticAt.nhds_eq_map_nhds [IsManifold I œâ T] {f : S ‚Üí T} {z : S}
    (n : NontrivialMAnalyticAt f z) : ùìù (f z) = Filter.map f (ùìù z) := by
  refine le_antisymm ?_ n.mAnalyticAt.continuousAt
  generalize hg : (fun x ‚Ü¶ extChartAt I (f z) (f ((extChartAt I z).symm x))) = g
  have ga : AnalyticAt ‚ÑÇ g (extChartAt I z z) := by
    rw [‚Üê hg]; exact (mAnalyticAt_iff_of_boundaryless.mp n.mAnalyticAt).2
  cases' ga.eventually_constant_or_nhds_le_map_nhds with h h
  ¬∑ contrapose h; clear h; simp only [Filter.not_eventually]
    apply n.inCharts.nonconst.mp; simp only [‚Üê hg, Ne, imp_self, Filter.eventually_true]
  ¬∑ -- The open mapping theorem for g = c ‚àò f ‚àò c‚Åª¬π (with charts c) is
    --   ùìù (g (c z)) ‚â§ map g (ùìù (c z))
    -- We have
    --   map c‚Åª¬π (ùìù (g (c z))) ‚â§ map c‚Åª¬π (map g (ùìù (c z))  -- Monotonicity of map
    --   ùìù (c‚Åª¬π (g (c z))) ‚â§ map (c' ‚àò g ‚àò c) (ùìù z)        -- Charts map ùìù to ùìù
    --   ùìù (f z) ‚â§ map f (ùìù z)                             -- Congruence
    simp only [‚Üê map_extChartAt_nhds_of_boundaryless z, Filter.map_map] at h
    replace h := @Filter.map_mono _ _ (extChartAt I (f z)).symm _ _ h
    simp only [‚Üê hg] at h; rw [PartialEquiv.left_inv _ (mem_extChartAt_source z)] at h
    simp only [extChartAt_symm_map_nhds' I (f z), Filter.map_map, Function.comp_def] at h
    have e : (fun w ‚Ü¶ (extChartAt I (f z)).symm
        (extChartAt I (f z) (f ((extChartAt I z).symm (extChartAt I z w))))) =·∂†[ùìù z] f := by
      apply ((isOpen_extChartAt_source z).eventually_mem (mem_extChartAt_source (I := I) z)).mp
      apply (n.mAnalyticAt.continuousAt.eventually_mem
        (extChartAt_source_mem_nhds (I := I) (f z))).mp
      refine .of_forall fun w fm m ‚Ü¶ ?_
      simp only [PartialEquiv.left_inv _ m, PartialEquiv.left_inv _ fm]
    rw [Filter.map_congr e] at h; exact h

/-- Special case of `Filter.prod_map_map_eq` where the first map is `id` -/
theorem Filter.prod_map_id_map_eq {A B C : Type} {f : Filter A} {g : Filter B} {m : B ‚Üí C} :
    f √óÀ¢ (Filter.map m g) = Filter.map (fun p : A √ó B ‚Ü¶ (p.1, m p.2)) (f √óÀ¢ g) :=
  Filter.prod_map_map_eq (f‚ÇÅ := f) (f‚ÇÇ := g) (m‚ÇÅ := id) (m‚ÇÇ := m)

/-- The local open mapping theorem, parameterized manifold version: if `f : ‚ÑÇ ‚Üí S ‚Üí T` is
    nontrivial, then `(c,z) ‚Ü¶ (c, f c z)` sends neighborhoods to neighborhoods. -/
public theorem NontrivialMAnalyticAt.nhds_eq_map_nhds_param [IsManifold I œâ T] {f : ‚ÑÇ ‚Üí S ‚Üí T}
    {c : ‚ÑÇ} {z : S} (n : NontrivialMAnalyticAt (f c) z)
    (fa : ContMDiffAt II I œâ (uncurry f) (c, z)) :
    ùìù (c, f c z) = Filter.map (fun p : ‚ÑÇ √ó S ‚Ü¶ (p.1, f p.1 p.2)) (ùìù (c, z)) := by
  refine le_antisymm ?_ (continuousAt_fst.prodMk fa.continuousAt)
  generalize hg : (fun e x ‚Ü¶ extChartAt I (f c z) (f e ((extChartAt I z).symm x))) = g
  have ga : AnalyticAt ‚ÑÇ (uncurry g) (c, extChartAt I z z) := by
    rw [‚Üê hg]; exact (mAnalyticAt_iff_of_boundaryless.mp fa).2
  have gn : NontrivialMAnalyticAt (g c) (extChartAt I z z) := by rw [‚Üê hg]; exact n.inCharts
  have h := gn.nhds_le_map_nhds_param' ga
  -- We follow the ùìù ‚â§ ùìù argument of nontrivial_mAnalytic_at.nhds_le_map_nhds
  -- above, but a bit more complicated due to the parameterization.
  simp only [nhds_prod_eq, ‚Üê map_extChartAt_nhds_of_boundaryless z, Filter.map_map,
    Filter.prod_map_id_map_eq] at h
  replace h := @Filter.map_mono _ _ (fun p : ‚ÑÇ √ó ‚ÑÇ ‚Ü¶ (p.1, (extChartAt I (f c z)).symm p.2)) _ _ h
  simp only [‚Üê hg] at h; rw [PartialEquiv.left_inv _ (mem_extChartAt_source z)] at h
  have pe := Filter.prod_map_id_map_eq (f := ùìù c) (g := ùìù (extChartAt I (f c z) (f c z)))
    (m := fun x ‚Ü¶ (extChartAt I (f c z)).symm x)
  rw [extChartAt_symm_map_nhds', ‚Üênhds_prod_eq] at pe
  refine _root_.trans (le_of_eq pe) (_root_.trans h (le_of_eq ?_)); clear h pe
  rw [‚Üênhds_prod_eq, Filter.map_map]; apply Filter.map_congr
  apply ((isOpen_extChartAt_source (c, z)).eventually_mem
    (mem_extChartAt_source (I := II) (c, z))).mp
  apply (fa.continuousAt.eventually_mem (extChartAt_source_mem_nhds (I := I) (f c z))).mp
  refine .of_forall fun ‚ü®e, w‚ü© fm m ‚Ü¶ ?_
  simp only [uncurry, extChartAt_prod, PartialEquiv.prod_source, mem_prod_eq] at fm m
  simp only [Function.comp, PartialEquiv.left_inv _ m.2, PartialEquiv.left_inv _ fm]
