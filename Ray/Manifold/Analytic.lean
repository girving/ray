import Mathlib.Analysis.Analytic.Basic
import Mathlib.Analysis.Analytic.Constructions
import Mathlib.Analysis.Complex.Basic
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Analysis.SpecialFunctions.Complex.Analytic
import Mathlib.Data.Complex.Basic
import Mathlib.Geometry.Manifold.ChartedSpace
import Mathlib.Geometry.Manifold.ContMDiffMFDeriv
import Mathlib.Geometry.Manifold.LocalInvariantProperties
import Mathlib.Geometry.Manifold.VectorBundle.Tangent
import Ray.Analytic.Within
import Ray.Manifold.ManifoldUpstream

/-!
## Facts about analytic manifolds

This file used to define `AnalyticManifold`, but now `IsManifold I Ï‰ M` handles that natively!
-/

open ChartedSpace (chartAt)
open Function (uncurry)
open Set
open scoped ContDiff Manifold Topology
noncomputable section

variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]

variable {E A : Type} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F B : Type} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {G C : Type} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
variable {H D : Type} [NormedAddCommGroup H] [NormedSpace ğ•œ H]
variable [TopologicalSpace A] [TopologicalSpace B] [TopologicalSpace C] [TopologicalSpace D]
variable {M : Type} {I : ModelWithCorners ğ•œ E A} [TopologicalSpace M]
variable {N : Type} {J : ModelWithCorners ğ•œ F B} [TopologicalSpace N]
variable {O : Type} {K : ModelWithCorners ğ•œ G C} [TopologicalSpace O]
variable {P : Type} {L : ModelWithCorners ğ•œ H D} [TopologicalSpace P]
variable [ChartedSpace A M] [ChartedSpace B N] [ChartedSpace C O] [ChartedSpace D P]

-- begin #28286

/-- Functions are `ContMDiffAt` iff they are continuous and analytic in charts -/
theorem mAnalyticAt_iff {f : M â†’ N} {x : M} [CompleteSpace F] :
    ContMDiffAt I J Ï‰ f x â†” ContinuousAt f x âˆ§
      AnalyticWithinAt ğ•œ (extChartAt J (f x) âˆ˜ f âˆ˜ (extChartAt I x).symm) (range I)
      (extChartAt I x x) := by
  rw [contMDiffAt_iff, contDiffWithinAt_omega_iff_analyticWithinAt]

/-- Functions are `ContMDiffAt` iff they are continuous and analytic in charts -/
theorem mAnalyticAt_iff_of_boundaryless [I.Boundaryless] [CompleteSpace F] {f : M â†’ N} {x : M} :
    ContMDiffAt I J Ï‰ f x â†” ContinuousAt f x âˆ§
      AnalyticAt ğ•œ (extChartAt J (f x) âˆ˜ f âˆ˜ (extChartAt I x).symm) (extChartAt I x x) := by
  simp only [mAnalyticAt_iff, I.range_eq_univ, analyticWithinAt_univ]

/-- Functions are `ContMDiff` iff they are continuous and analytic in charts everywhere -/
theorem mAnalytic_iff {f : M â†’ N} [CompleteSpace F] [IsManifold I Ï‰ M] [IsManifold J Ï‰ N] :
    ContMDiff I J Ï‰ f â†” Continuous f âˆ§
      âˆ€ x : M, AnalyticWithinAt ğ•œ (extChartAt J (f x) âˆ˜ f âˆ˜ (extChartAt I x).symm)
        (range I) (extChartAt I x x) := by
  simp only [ContMDiff, contMDiffAt_iff, continuous_iff_continuousAt,
    contDiffWithinAt_omega_iff_analyticWithinAt]
  aesop

/-- Functions are `ContMDiff` iff they are continuous and analytic in charts everywhere -/
theorem mAnalytic_iff_of_boundaryless [I.Boundaryless] [IsManifold I Ï‰ M] [IsManifold J Ï‰ N]
    [CompleteSpace F] {f : M â†’ N} :
    ContMDiff I J Ï‰ f â†” Continuous f âˆ§
      âˆ€ x : M, AnalyticAt ğ•œ (extChartAt J (f x) âˆ˜ f âˆ˜ (extChartAt I x).symm)
        (extChartAt I x x) := by
  simp only [mAnalytic_iff, I.range_eq_univ, analyticWithinAt_univ]

-- end #28286

/- /-- ContMDiff functions are continuous (explicit `I`, `J` version) -/
theorem ContMDiffAt.continuousAt' (I : ModelWithCorners ğ•œ E A) (J : ModelWithCorners ğ•œ F B)
    {M N : Type} [TopologicalSpace M] [ChartedSpace A M] [TopologicalSpace N] [ChartedSpace B N]
    [I.Boundaryless] [J.Boundaryless] [CompleteSpace F]
    {f : M â†’ N} {x : M} (h : ContMDiffAt I J Ï‰ f x) :
    ContinuousAt f x := h.continuousAt -/

/- /-- `I.toPartialEquiv = I` in terms of `coe` -/
lemma ModelWithCorners.coe_coe (I : ModelWithCorners ğ•œ E A) :
    â‡‘I.toPartialEquiv = (I : A â†’ E) := rfl

/-- `I.toPartialEquiv.symm = I.symm` in terms of `coe` -/
theorem ModelWithCorners.coe_coe_symm (I : ModelWithCorners ğ•œ E A) :
    â‡‘I.toPartialEquiv.symm = (I.symm : E â†’ A) := rfl -/

/- /-- `extChartAt` is analytic (boundary or not) -/
lemma ContMDiffAt.extChartAt [CompleteSpace E] [cm : IsManifold I âŠ¤ M]
    {x y : M} (ys : y âˆˆ (extChartAt I x).source) :
    ContMDiffAt I ğ“˜(ğ•œ, E) Ï‰ (extChartAt I x) y := by
  refine contMDiffOn_extChartAt.contMDiffAt ?_

  -- simp only [extChartAt_source] at ys
  -- exact contMDiffAt_extChartAt' ys -/

/- /-- `I` preserves `ğ“` if it is boundaryless -/
lemma ModelWithCorners.map_nhds_eq_of_boundaryless [I.Boundaryless] {x : A} :
    (ğ“ x).map I = ğ“ (I x) := by
  simp only [I.map_nhds_eq, I.range_eq_univ, nhdsWithin_univ] -/

/-- `extChartAt.symm` is analytic if we're boundaryless -/
theorem ContMDiffAt.extChartAt_symm [CompleteSpace E] [I.Boundaryless] [cm : IsManifold I Ï‰ M]
    {x : M} {y : E} (ys : y âˆˆ (_root_.extChartAt I x).target) :
    ContMDiffAt ğ“˜(ğ•œ, E) I Ï‰ (_root_.extChartAt I x).symm y := by
  suffices h : ContMDiffWithinAt ğ“˜(ğ•œ, E) I Ï‰ (_root_.extChartAt I x).symm (range I) y by
    simp only [mfld_simps, mAnalyticAt_iff, contMDiffWithinAt_iff, I.range_eq_univ,
      contDiffWithinAt_univ, analyticWithinAt_univ, continuousWithinAt_univ] at h âŠ¢
    exact âŸ¨h.1, h.2.analyticAtâŸ©
  exact contMDiffWithinAt_extChartAt_symm_range x ys

/- /-- `ContMDiffAt` depends only on local values -/
theorem ContMDiffAt.congr [CompleteSpace F] {f g : M â†’ N} {x : M} (fa : ContMDiffAt I J Ï‰ f x)
    (e : f =á¶ [ğ“ x] g) : ContMDiffAt I J Ï‰ g x :=
  ContMDiffAt.congr_of_eventuallyEq fa (id (Filter.EventuallyEq.symm e)) -/

-- begin #28292

/-- `ContMDiffAt.comp` for a function of two arguments -/
theorem ContMDiffAt.compâ‚‚ [IsManifold I Ï‰ M] [IsManifold J Ï‰ N] [IsManifold K Ï‰ O]
    [IsManifold L Ï‰ P] {h : N Ã— M â†’ P} {f : O â†’ N} {g : O â†’ M} {x : O}
    (ha : ContMDiffAt (J.prod I) L Ï‰ h (f x, g x)) (fa : ContMDiffAt K J Ï‰ f x)
    (ga : ContMDiffAt K I Ï‰ g x) : ContMDiffAt K L Ï‰ (fun x â†¦ h (f x, g x)) x :=
  ha.comp (f := fun x â†¦ (f x, g x)) _ (fa.prodMk ga)

/-- `ContMDiffAt.compâ‚‚`, with a separate argument for point equality -/
theorem ContMDiffAt.compâ‚‚_of_eq [IsManifold I Ï‰ M] [IsManifold J Ï‰ N] [IsManifold K Ï‰ O]
    [IsManifold L Ï‰ P] {h : N Ã— M â†’ P} {f : O â†’ N} {g : O â†’ M} {x : O} {y : N Ã— M}
    (ha : ContMDiffAt (J.prod I) L Ï‰ h y) (fa : ContMDiffAt K J Ï‰ f x)
    (ga : ContMDiffAt K I Ï‰ g x) (e : (f x, g x) = y) :
    ContMDiffAt K L Ï‰ (fun x â†¦ h (f x, g x)) x := by
  rw [â† e] at ha
  exact ha.compâ‚‚ fa ga

-- end #28292

section Iff

variable (I J)

/-- Analytic functions are analytic, and vice versa -/
theorem analyticAt_iff_mAnalyticAt [I.Boundaryless] [ChartedSpace A E] [IsManifold I Ï‰ E]
    [ChartedSpace B F] [IsManifold J Ï‰ F] [ExtChartEqRefl I] [ExtChartEqRefl J] [CompleteSpace F]
    {f : E â†’ F} {x : E} : AnalyticAt ğ•œ f x â†” ContMDiffAt I J Ï‰ f x := by
  simp only [mAnalyticAt_iff_of_boundaryless, extChartAt_eq_refl, PartialEquiv.refl_coe,
    PartialEquiv.refl_symm, Function.id_comp, Function.comp_id, id_eq, iff_and_self]
  exact AnalyticAt.continuousAt

end Iff

/-- Analytic functions are analytic -/
theorem AnalyticAt.mAnalyticAt {f : E â†’ F} {x : E} (fa : AnalyticAt ğ•œ f x) [CompleteSpace F]
    (I : ModelWithCorners ğ•œ E A) [ChartedSpace A E] [IsManifold I Ï‰ E] [ExtChartEqRefl I]
    (J : ModelWithCorners ğ•œ F B) [ChartedSpace B F] [IsManifold J Ï‰ F] [ExtChartEqRefl J] :
    ContMDiffAt I J Ï‰ f x := by
  simp only [mAnalyticAt_iff, fa.continuousAt, true_and, extChartAt_eq_refl, PartialEquiv.refl_coe,
    PartialEquiv.refl_symm, Function.id_comp, Function.comp_id, id_eq]
  exact fa.analyticWithinAt

/-- ContMDiff functions are analytic -/
theorem ContMDiffAt.analyticAt [CompleteSpace F] (I : ModelWithCorners ğ•œ E A) [I.Boundaryless]
    [ChartedSpace A E] [IsManifold I Ï‰ E] [ExtChartEqRefl I] (J : ModelWithCorners ğ•œ F B)
    [ChartedSpace B F] [IsManifold J Ï‰ F] [ExtChartEqRefl J] {f : E â†’ F} {x : E} :
    ContMDiffAt I J Ï‰ f x â†’ AnalyticAt ğ•œ f x :=
  (analyticAt_iff_mAnalyticAt _ _).mpr

-- begin #28292

/-- Curried analytic functions are analytic in the first coordinate -/
theorem ContMDiffAt.along_fst [CompleteSpace G] [CompleteSpace H] [IsManifold I Ï‰ M]
    [IsManifold K Ï‰ O] [IsManifold L Ï‰ P]
    {f : M â†’ O â†’ P} {x : M} {y : O} (fa : ContMDiffAt (I.prod K) L Ï‰ (uncurry f) (x, y)) :
    ContMDiffAt I L Ï‰ (fun x â†¦ f x y) x :=
  fa.compâ‚‚ contMDiffAt_id contMDiffAt_const

/-- Curried analytic functions are analytic in the second coordinate -/
theorem ContMDiffAt.along_snd [CompleteSpace G] [IsManifold I Ï‰ M] [IsManifold J Ï‰ N]
    [IsManifold K Ï‰ O] [CompleteSpace E] {f : M â†’ N â†’ O} {x : M} {y : N}
    (fa : ContMDiffAt (I.prod J) K Ï‰ (uncurry f) (x, y)) :
    ContMDiffAt J K Ï‰ (fun y â†¦ f x y) y :=
  fa.compâ‚‚ contMDiffAt_const contMDiffAt_id

/-- Curried analytic functions are analytic in the first coordinate -/
theorem ContMDiff.along_fst [CompleteSpace G] [CompleteSpace H] [IsManifold I Ï‰ M]
    [IsManifold K Ï‰ O] [IsManifold L Ï‰ P]
    {f : M â†’ O â†’ P} (fa : ContMDiff (I.prod K) L Ï‰ (uncurry f)) {y : O} :
    ContMDiff I L Ï‰ (fun x â†¦ f x y) :=
  fun _ â†¦ (fa _).along_fst

/-- Curried analytic functions are analytic in the second coordinate -/
theorem ContMDiff.along_snd [CompleteSpace G] [IsManifold I Ï‰ M] [IsManifold J Ï‰ N]
    [IsManifold K Ï‰ O] [CompleteSpace E] {f : M â†’ N â†’ O} {x : M}
    (fa : ContMDiff (I.prod J) K Ï‰ (uncurry f)) :
    ContMDiff J K Ï‰ (fun y â†¦ f x y) :=
  fun _ â†¦ (fa _).along_snd

-- end #28292

/-

/-- Addition is analytic -/
theorem ContMDiffAt.add [CompleteSpace F] [CompleteSpace G] [IsManifold K Ï‰ O] {f g : O â†’ F} {x : O}
    (fa : ContMDiffAt K (ğ“˜(ğ•œ, F)) Ï‰ f x) (ga : ContMDiffAt K (ğ“˜(ğ•œ, F)) Ï‰ g x) :
    ContMDiffAt K (ğ“˜(ğ•œ, F)) Ï‰ (fun x â†¦ f x + g x) x := by
  have e : (fun x â†¦ f x + g x) = (fun p : F Ã— F â†¦ p.1 + p.2) âˆ˜ fun x â†¦ (f x, g x) := rfl
  rw [e]
  exact ((analyticAt_fst.add analyticAt_snd).mAnalyticAt _ _).comp _ (fa.prodMk ga)

/-- Subtraction is analytic -/
theorem ContMDiffAt.sub [CompleteSpace F] [CompleteSpace G] [IsManifold K Ï‰ O] {f g : O â†’ F} {x : O}
    (fa : ContMDiffAt K (ğ“˜(ğ•œ, F)) Ï‰ f x) (ga : ContMDiffAt K (ğ“˜(ğ•œ, F)) Ï‰ g x) :
    ContMDiffAt K (ğ“˜(ğ•œ, F)) Ï‰ (fun x â†¦ f x - g x) x :=
  ((analyticAt_fst.sub analyticAt_snd).mAnalyticAt _ _).comp _ (fa.prodMk ga)

/-- Multiplication is analytic -/
theorem ContMDiffAt.mul' [CompleteSpace ğ•œ] [CompleteSpace G] [IsManifold K Ï‰ O] {f g : O â†’ ğ•œ} {x : O}
    (fa : ContMDiffAt K (ğ“˜(ğ•œ, ğ•œ)) Ï‰ f x) (ga : ContMDiffAt K (ğ“˜(ğ•œ, ğ•œ)) Ï‰ g x) :
    ContMDiffAt K (ğ“˜(ğ•œ, ğ•œ)) Ï‰ (fun x â†¦ f x * g x) x :=
  ((analyticAt_fst.mul analyticAt_snd).mAnalyticAt _ _).comp _ (fa.prodMk ga)

/-- Inverse is analytic away from zeros -/
theorem ContMDiffAt.inv [CompleteSpace ğ•œ] [CompleteSpace G] [IsManifold K Ï‰ O] {f : O â†’ ğ•œ} {x : O}
    (fa : ContMDiffAt K (ğ“˜(ğ•œ, ğ•œ)) Ï‰ f x) (f0 : f x â‰  0) :
    ContMDiffAt K (ğ“˜(ğ•œ, ğ•œ)) Ï‰ (fun x â†¦ (f x)â»Â¹) x :=
  ((analyticAt_id.inv f0).mAnalyticAt _ _).comp _ fa

/-- Division is analytic away from denominator zeros -/
theorem ContMDiffAt.div [CompleteSpace ğ•œ] [CompleteSpace G] [IsManifold K Ï‰ O] {f g : O â†’ ğ•œ} {x : O}
    (fa : ContMDiffAt K (ğ“˜(ğ•œ, ğ•œ)) Ï‰ f x) (ga : ContMDiffAt K (ğ“˜(ğ•œ, ğ•œ)) Ï‰ g x) (g0 : g x â‰  0) :
    ContMDiffAt K (ğ“˜(ğ•œ, ğ•œ)) Ï‰ (fun x â†¦ f x / g x) x := by
  simp only [div_eq_mul_inv]; exact fa.mul (ga.inv g0)

-/

/-- Powers are analytic -/
theorem ContMDiffAt.pow [CompleteSpace ğ•œ] [CompleteSpace G] [IsManifold K Ï‰ O] {f : O â†’ ğ•œ} {x : O}
    (fa : ContMDiffAt K (ğ“˜(ğ•œ, ğ•œ)) Ï‰ f x) {n : â„•} :
    ContMDiffAt K (ğ“˜(ğ•œ, ğ•œ)) Ï‰ (fun x â†¦ f x ^ n) x := by
  have e : (fun x â†¦ f x ^ n) = (fun z : ğ•œ â†¦ z ^ n) âˆ˜ f := rfl
  rw [e]; exact ((analyticAt_id.pow _).mAnalyticAt _ _).comp _ fa

/-- Complex powers `f x ^ g x` are analytic if `f x` avoids the negative real axis  -/
theorem ContMDiffAt.cpow [NormedSpace â„‚ E] [CompleteSpace E] {I : ModelWithCorners â„‚ E A}
    [IsManifold I Ï‰ M] {f g : M â†’ â„‚} {x : M} (fa : ContMDiffAt I (ğ“˜(â„‚, â„‚)) Ï‰ f x)
    (ga : ContMDiffAt I (ğ“˜(â„‚, â„‚)) Ï‰ g x) (a : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
    ContMDiffAt I (ğ“˜(â„‚, â„‚)) Ï‰ (fun x â†¦ f x ^ g x) x := by
  have e : (fun x â†¦ f x ^ g x) = (fun p : â„‚ Ã— â„‚ â†¦ p.1 ^ p.2) âˆ˜ fun x â†¦ (f x, g x) := rfl
  rw [e]
  refine ((analyticAt_fst.cpow analyticAt_snd ?_).mAnalyticAt _ _).comp _ (fa.prodMk ga)
  exact a

/-- Iterated analytic functions are analytic -/
theorem ContMDiff.iter {f : M â†’ M} (fa : ContMDiff I I Ï‰ f) (n : â„•) :
    ContMDiff I I Ï‰ (f^[n]) := by
  induction' n with n h; simp only [Function.iterate_zero]; exact contMDiff_id
  simp only [Function.iterate_succ']; exact fa.comp h

/-- If we're analytic at a point, we're locally analytic.
This is true even with boundary, but for now we prove only the `Boundaryless` case. -/
theorem ContMDiffAt.eventually [I.Boundaryless] [J.Boundaryless] [CompleteSpace E] [CompleteSpace F]
    [IsManifold I Ï‰ M] [IsManifold J Ï‰ N] {f : M â†’ N} {x : M} (fa : ContMDiffAt I J Ï‰ f x) :
    âˆ€á¶  y in ğ“ x, ContMDiffAt I J Ï‰ f y := by
  have ea := (mAnalyticAt_iff_of_boundaryless.mp fa).2.eventually_analyticAt
  simp only [â† extChartAt_map_nhds', Filter.eventually_map] at ea
  filter_upwards [ea, (fa.continuousAt.eventually_mem ((isOpen_extChartAt_source (f x)).mem_nhds
    (mem_extChartAt_source (I := J) (f x)))).eventually_nhds,
    (isOpen_extChartAt_source x).eventually_mem (mem_extChartAt_source (I := I) x)]
  intro y a fm m
  have h := a.mAnalyticAt (modelWithCornersSelf ğ•œ E) (modelWithCornersSelf ğ•œ F)
  clear a
  have h' := (ContMDiffAt.extChartAt_symm (PartialEquiv.map_source _ fm.self_of_nhds)).comp_of_eq
      (h.comp _ (contMDiffAt_extChartAt' (extChartAt_source I x â–¸ m))) ?_
  Â· apply h'.congr_of_eventuallyEq
    clear h h'
    apply ((isOpen_extChartAt_source x).eventually_mem m).mp
    refine fm.mp (.of_forall fun z mf m â†¦ ?_)
    simp only [PartialEquiv.left_inv _ m, PartialEquiv.left_inv _ mf, Function.comp_def]
  Â· simp only [Function.comp, PartialEquiv.left_inv _ m]

/-- The domain of analyticity is open -/
theorem isOpen_mAnalyticAt [I.Boundaryless] [J.Boundaryless] [CompleteSpace E] [CompleteSpace F]
    [IsManifold I Ï‰ M] [IsManifold J Ï‰ N] {f : M â†’ N} :
    IsOpen {x | ContMDiffAt I J Ï‰ f x} := by
  rw [isOpen_iff_eventually]; intro x fa; exact fa.eventually

/-- Analyticity in a neighborhood of a set (the manifold analogue of `AnalyticOnNhd`) -/
def ContMDiffOnNhd (I : ModelWithCorners ğ•œ E A) (J : ModelWithCorners ğ•œ F B)
    (f : M â†’ N) (s : Set M) : Prop := âˆ€ x âˆˆ s, ContMDiffAt I J Ï‰ f x

/-- `ContMDiffOnNhd` restricts to subsets -/
lemma ContMDiffOnNhd.mono {f : M â†’ N} {s t : Set M} (fa : ContMDiffOnNhd I J f s) (st : t âŠ† s) :
    ContMDiffOnNhd I J f t := fun x m â†¦ fa x (st m)

/-- `ContMDiffOnNhd` extends `ContMDiffOn` -/
lemma ContMDiffOnNhd.contMDiffOn {f : M â†’ N} {s : Set M} (fa : ContMDiffOnNhd I J f s) :
    ContMDiffOn I J Ï‰ f s := fun x m â†¦ (fa x m).contMDiffWithinAt

/-- `ContMDiffOnNhd` implies analyticity -/
lemma ContMDiffOnNhd.contMDiffAt {f : M â†’ N} {s : Set M} (fa : ContMDiffOnNhd I J f s) {x : M}
    (xs : x âˆˆ s) : ContMDiffAt I J Ï‰ f x := fa x xs

/-- `ContMDiffOnNhd` implies continuity -/
lemma ContMDiffOnNhd.continuousAt {f : M â†’ N} {s : Set M} (fa : ContMDiffOnNhd I J f s) {x : M}
    (xs : x âˆˆ s) : ContinuousAt f x := (fa x xs).continuousAt

/-- `ContMDiffOnNhd` implies continuity on the domain -/
lemma ContMDiffOnNhd.continuousOn {f : M â†’ N} {s : Set M} (fa : ContMDiffOnNhd I J f s) :
    ContinuousOn f s := fun x m â†¦ (fa x m).continuousAt.continuousWithinAt
