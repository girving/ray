module
public import Mathlib.Analysis.Analytic.Basic
public import Mathlib.Analysis.Complex.Basic
public import Mathlib.MeasureTheory.Integral.Average
public import Ray.Misc.Defs
public import Ray.Misc.Measure
import Mathlib.Analysis.Convex.Integral
import Mathlib.Analysis.Fourier.AddCircle
import Mathlib.Analysis.SpecialFunctions.Complex.Analytic
import Mathlib.Analysis.SpecialFunctions.Integrals.Basic
import Mathlib.MeasureTheory.Measure.Lebesgue.VolumeOfBalls
import Mathlib.Tactic.Bound
import Mathlib.Tactic.Cases
import Ray.Analytic.Analytic
import Ray.Analytic.Holomorphic
import Ray.Hartogs.Duals
import Ray.Hartogs.FubiniBall
import Ray.Hartogs.MaxLog
import Ray.Misc.Complex
import Ray.Misc.Max
import Ray.Misc.Multilinear

/-!
## Subharmonic and harmonic functions and Hartogs' lemma

We define subharmonic and harmonic functions from `C ‚Üí E` for any separable normed space `E`:

1. `f : ‚ÑÇ ‚Üí E` is harmonic if its circle averages are equal to the center value
2. `f : ‚ÑÇ ‚Üí ‚Ñù` is subharmonic if its circle averages are greater than or equal to the center value

For harmonic functions on `s`, we require (1) for any circle bounding a ball contained in `s`,
because this is easy to prove in the cases we need (harmonic functions derived from analytic
functions).  For subharmonic functions, we require (2) to hold only for small balls near each point
in the interior of `s`.

The usual definition of subharmonic allows `f z = -‚àû`, but we found it quite difficult to work
over the extended reals when developing the theory.  Thus, we instead restrict to `‚Üí ‚Ñù`.  Our
main application of Hartogs' theorem uses subharmonicity of `log ‚Äñf z‚Äñ` which would hit `-‚àû`
at any zero of `f`; we work around this by replace `log ‚Äñf z‚Äñ` with `max b (log ‚Äñf z‚Äñ)` for
suitable `b : ‚Ñù`.

Possibly we could avoid this by working over `‚Ñù‚â•0‚àû` using superharmonic functions instead of
harmonic functions, but (1) I'm not sure that's desired and (2) I didn't think of it until late
in the Hartogs' theorem proof.  We do define `SuperharmonicOn f s` for `f : ‚ÑÇ ‚Üí ‚Ñù‚â•0‚àû` late in
the file.

After deriving the basic theory, we prove Hartogs' lemma as `SubharmonicOn.hartogs`:
subharmonic functions that are bounded above and limsup bounded pointwise are limsup bounded
uniformly.  This is the key piece of measure theory needed for Hartogs' theorem.
-/

open Complex (exp I log)
open Filter (Tendsto liminf limsup atTop)
open Function (uncurry)
open MeasureTheory
open Metric (ball closedBall sphere)
open Set (Ioc Icc univ)
open scoped Real NNReal ENNReal Topology ComplexConjugate
noncomputable section

variable {S : Type} [_root_.RCLike S] [SMulCommClass ‚Ñù S S]
variable {T : Type} [_root_.RCLike T] [SMulCommClass ‚Ñù T T]
variable {E : Type} [NormedAddCommGroup E] [CompleteSpace E] [NormedSpace ‚Ñù E]
variable {F : Type} [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
variable {H : Type} [NormedAddCommGroup H] [NormedSpace ‚ÑÇ H]

/-- `f : ‚ÑÇ ‚Üí E` is harmonic if it is continuous and equal to means on circles.
    We require the mean property for large circles because it is easy to prove
    for the cases we need, and will be needed for the large submean theorem
    for subharmonic functions. -/
public structure HarmonicOn (f : ‚ÑÇ ‚Üí E) (s : Set ‚ÑÇ) : Prop where
  /-- `f` is continuous throughout `s` -/
  cont : ContinuousOn f s
  /-- If a circle bounds a disk contained in `s`, the circle mean is equal to the center value. -/
  mean : ‚àÄ (c : ‚ÑÇ) (r : ‚Ñù), r > 0 ‚Üí closedBall c r ‚äÜ s ‚Üí f c = ‚®ç t in itau, f (circleMap c r t)

/-- `f : ‚ÑÇ ‚Üí ‚Ñù` is subharmonic if it is upper semicontinuous and is below means on small disks.
    We require the submean property only locally in the definition (for sufficiently small circles
    in the interior of `s`), but prove the global version below.

    Out of laziness, we assume continuity as well for now.  Ideally we'd allow `-‚àû` as values, but
    using `ereal` instead of `‚Ñù` adds many annoying technicalities. -/
public structure SubharmonicOn (f : ‚ÑÇ ‚Üí ‚Ñù) (s : Set ‚ÑÇ) : Prop where
  /-- The usual definition requires upper semicontinuity; we use continuity out of laziness. -/
  cont : ContinuousOn f s
  /-- Near each `c ‚àà interior s`, for sufficiently small radii, `f c ‚â§` the circle mean of `f` -/
  submean' : ‚àÄ c, c ‚àà interior s ‚Üí ‚àÉ r, 0 < r ‚àß
    ‚àÄ s, 0 < s ‚Üí s < r ‚Üí f c ‚â§ ‚®ç t in itau, f (circleMap c s t)

/-- Subharmonic functions are subharmonic on smaller sets -/
public theorem SubharmonicOn.mono {f : ‚ÑÇ ‚Üí ‚Ñù} {s t : Set ‚ÑÇ} (fs : SubharmonicOn f s) (ts : t ‚äÜ s) :
    SubharmonicOn f t :=
  { cont := fs.cont.mono ts
    submean' := fun c cs ‚Ü¶ fs.submean' c (interior_mono ts cs) }

/-- Convex functions of harmonic functions are subharmonic -/
theorem HarmonicOn.convex {f : ‚ÑÇ ‚Üí E} {s : Set ‚ÑÇ} {g : E ‚Üí ‚Ñù} (fh : HarmonicOn f s)
    (c : Continuous g) (gc : ConvexOn ‚Ñù Set.univ g) : SubharmonicOn (fun z ‚Ü¶ g (f z)) s :=
  { cont := c.comp_continuousOn fh.cont
    submean' := by
      intro z zs
      rcases Metric.isOpen_iff.mp isOpen_interior z zs with ‚ü®r, rp, rh‚ü©
      exists r, rp; intro t tp tr
      have cs : closedBall z t ‚äÜ s :=
        _root_.trans (Metric.closedBall_subset_ball tr) (_root_.trans rh interior_subset)
      simp only [fh.mean z t tp cs]
      have n := NiceVolume.itau
      apply ConvexOn.map_set_average_le gc c.continuousOn isClosed_univ n.ne_zero n.ne_top
      simp only [Set.mem_univ, Filter.eventually_true]
      exact (fh.cont.mono cs).integrableOn_sphere tp
      exact ((c.comp_continuousOn fh.cont).mono cs).integrableOn_sphere tp }

/-- Harmonic functions are subharmonic -/
public theorem HarmonicOn.subharmonicOn {f : ‚ÑÇ ‚Üí ‚Ñù} {s : Set ‚ÑÇ} (h : HarmonicOn f s) :
    SubharmonicOn (fun z ‚Ü¶ f z) s := by
  have e : (fun z ‚Ü¶ f z) = fun z ‚Ü¶ (fun x ‚Ü¶ x) (f z) := rfl
  rw [e]; exact h.convex continuous_id (convexOn_id convex_univ)

/-- Norms of harmonic functions are subharmonic -/
public theorem HarmonicOn.norm {f : ‚ÑÇ ‚Üí E} {s : Set ‚ÑÇ} (h : HarmonicOn f s) :
    SubharmonicOn (fun z ‚Ü¶ ‚Äñf z‚Äñ) s :=
  h.convex continuous_norm (convexOn_norm convex_univ)

/-- `SubharmonicOn` depends only on values in `s` (`‚Üí` version) -/
public theorem SubharmonicOn.congr {f g : ‚ÑÇ ‚Üí ‚Ñù} {s : Set ‚ÑÇ} (fs : SubharmonicOn f s)
    (h : Set.EqOn g f s) : SubharmonicOn g s :=
  { cont := fs.cont.congr h
    submean' := by
      intro c cs
      rcases Metric.isOpen_iff.mp isOpen_interior c cs with ‚ü®r0, r0p, r0s‚ü©
      rcases fs.submean' c cs with ‚ü®r1, r1p, sm‚ü©
      have r01p : min r0 r1 > 0 := by bound
      exists min r0 r1, r01p
      intro t tp tr
      specialize sm t tp (lt_of_lt_of_le tr (by bound))
      have hs : (fun u ‚Ü¶ f (circleMap c t u)) =·µê[volume.restrict itau]
          fun u ‚Ü¶ g (circleMap c t u) := by
        rw [Filter.EventuallyEq]; rw [ae_restrict_iff' measurableSet_itau]
        apply Filter.Eventually.of_forall
        intro u _; apply h.symm
        apply _root_.trans r0s interior_subset
        simp only [Metric.mem_ball, Complex.dist_eq, circleMap_sub_center, norm_circleMap_zero,
          abs_of_pos tp]
        exact lt_of_lt_of_le tr (by bound)
      rw [average_eq] at sm ‚ä¢
      rwa [‚Üê h.symm (interior_subset cs), ‚Üê integral_congr_ae hs] }

/-- `SubharmonicOn` depends only on values in `s` (`‚Üî` version) -/
public theorem subharmonicOn_congr {f g : ‚ÑÇ ‚Üí ‚Ñù} {s : Set ‚ÑÇ} (h : Set.EqOn f g s) :
    SubharmonicOn f s ‚Üî SubharmonicOn g s :=
  ‚ü®fun fs ‚Ü¶ fs.congr h.symm, fun gs ‚Ü¶ gs.congr h‚ü©

/-- Constants are harmonic -/
public theorem HarmonicOn.const (a : E) {s : Set ‚ÑÇ} : HarmonicOn (fun _ ‚Ü¶ a) s :=
  { cont := continuousOn_const
    mean := by
      intro c r _ _
      rw [average_eq]
      simp [‚Üê smul_assoc, smul_eq_mul]
      field_simp [NiceVolume.itau.real_pos.ne']
      simp }

/-- Differences are harmonic -/
public theorem HarmonicOn.sub {f g : ‚ÑÇ ‚Üí F} {s : Set ‚ÑÇ} (fh : HarmonicOn f s)
    (gh : HarmonicOn g s) : HarmonicOn (f - g) s :=
  { cont := ContinuousOn.sub fh.cont gh.cont
    mean := by
      intro c r rp cs; simp [fh.mean c r rp cs, gh.mean c r rp cs]
      rw [Average.sub ((fh.cont.mono cs).integrableOn_sphere rp)
        ((gh.cont.mono cs).integrableOn_sphere rp)] }

/-- Subharmonic functions add (note that they don't subtract) -/
public theorem SubharmonicOn.add {f g : ‚ÑÇ ‚Üí ‚Ñù} {s : Set ‚ÑÇ} (fs : SubharmonicOn f s)
    (gs : SubharmonicOn g s) : SubharmonicOn (fun z ‚Ü¶ f z + g z) s :=
  { cont := fs.cont.add gs.cont
    submean' := by
      intro c cs
      rcases fs.submean' c cs with ‚ü®r0, r0p, r0m‚ü©
      rcases gs.submean' c cs with ‚ü®r1, r1p, r1m‚ü©
      rcases Metric.isOpen_iff.mp isOpen_interior c cs with ‚ü®r2, r2p, r2s‚ü©
      set r := min r0 (min r1 r2)
      have rr1 : r ‚â§ r1 := le_trans (min_le_right _ _) (by bound)
      have rr2 : r ‚â§ r2 := le_trans (min_le_right _ _) (by bound)
      use r; use by bound
      intro u up ur
      have us : closedBall c u ‚äÜ s :=
        _root_.trans (Metric.closedBall_subset_ball (lt_of_lt_of_le ur (by bound)))
          (_root_.trans r2s interior_subset)
      rw [Average.add ((fs.cont.mono us).integrableOn_sphere up)
          ((gs.cont.mono us).integrableOn_sphere up)]
      have m0 := r0m u up (lt_of_lt_of_le ur (by bound))
      have m1 := r1m u up (lt_of_lt_of_le ur (by bound))
      exact add_le_add m0 m1 }

/-- Negations are harmonic -/
public theorem HarmonicOn.neg {f : ‚ÑÇ ‚Üí E} {s : Set ‚ÑÇ} (fh : HarmonicOn f s) :
    HarmonicOn (-f) s := by
  have nh := HarmonicOn.sub (HarmonicOn.const (0 : E)) fh
  have e : (fun _ : ‚ÑÇ ‚Ü¶ (0 : E)) - f = -f := by ext; simp
  rwa [‚Üê e]

/-- Additions are harmonic -/
public theorem HarmonicOn.add {f g : ‚ÑÇ ‚Üí E} {s : Set ‚ÑÇ} (fh : HarmonicOn f s)
    (gh : HarmonicOn g s) : HarmonicOn (f + g) s := by
  have e : f + g = f - -g := by ext; simp
  rw [e]; exact fh.sub gh.neg

/-- Scalar multiples are harmonic -/
public theorem HarmonicOn.const_mul {f : ‚ÑÇ ‚Üí S} {s : Set ‚ÑÇ} (fh : HarmonicOn f s) (a : S) :
    HarmonicOn (fun z ‚Ü¶ a * f z) s :=
  { cont := ContinuousOn.mul continuousOn_const fh.cont
    mean := by
      intro c r rp cs; rw [average_eq]
      simp_rw [‚Üê smul_eq_mul, integral_smul, smul_comm _ a, ‚Üê average_eq, ‚Üê fh.mean c r rp cs] }

/-- Nonnegative scalar multiples are subharmonic -/
public theorem SubharmonicOn.const_mul {f : ‚ÑÇ ‚Üí ‚Ñù} {s : Set ‚ÑÇ} {a : ‚Ñù} (fs : SubharmonicOn f s)
    (ap : 0 ‚â§ a) : SubharmonicOn (fun z ‚Ü¶ a * f z) s :=
  { cont := ContinuousOn.mul continuousOn_const fs.cont
    submean' := by
      intro c cs; rcases fs.submean' c cs with ‚ü®r, rp, rm‚ü©; use r, rp; intro s sp sr
      specialize rm s sp sr
      simp only [average_eq, MeasurableSet.univ, measureReal_restrict_apply, Set.univ_inter,
        smul_eq_mul] at rm ‚ä¢
      calc a * f c
        _ ‚â§ a * ((volume.real itau)‚Åª¬π * ‚à´ t in itau, f (circleMap c s t)) := by bound
        _ = (volume.real itau)‚Åª¬π * (a * ‚à´ t in itau, f (circleMap c s t)) := by ring
        _ = (volume.real itau)‚Åª¬π * ‚à´ t in itau, a * f (circleMap c s t) := by
          rw [integral_const_mul] }

/-- Analytic functions equal circle means -/
theorem AnalyticOnNhd.circle_mean_eq [CompleteSpace H] {f : ‚ÑÇ ‚Üí H} {c : ‚ÑÇ} {r : ‚Ñù}
    (fa : AnalyticOnNhd ‚ÑÇ f (closedBall c r)) (rp : r > 0) :
    ‚®ç t in itau, f (circleMap c r t) = f c := by
  have h := Complex.circleIntegral_sub_inv_smul_of_differentiable_on_off_countable
    Set.countable_empty (Metric.mem_ball_self rp) fa.continuousOn ?_
  ¬∑ simp_rw [circleIntegral, deriv_circleMap, circleMap_sub_center, smul_smul, mul_comm _ I] at h
    field_simp [circleMap_ne_center rp.ne'] at h
    simp only [intervalIntegral.integral_smul] at h
    rw [mul_assoc, ‚Üê smul_smul, IsUnit.smul_left_cancel (Ne.isUnit Complex.I_ne_zero)] at h
    rw [intervalIntegral.integral_of_le Real.two_pi_pos.le] at h
    rw [average_eq, itau, h]
    simp only [MeasurableSet.univ, measureReal_restrict_apply, Set.univ_inter, Real.volume_real_Ioc,
      sub_zero]
    rw [max_eq_left Real.two_pi_pos.le]
    rw [‚Üê smul_assoc, Complex.real_smul]
    field_simp [Real.pi_ne_zero]
    simp
  ¬∑ intro z zs; rw [Set.diff_empty] at zs
    exact (fa z (Metric.ball_subset_closedBall zs)).differentiableAt

/-- Analytic functions are harmonic -/
theorem AnalyticOnNhd.harmonicOn [CompleteSpace H] {f : ‚ÑÇ ‚Üí H} {s : Set ‚ÑÇ} (fa : AnalyticOnNhd ‚ÑÇ f s) :
    HarmonicOn f s :=
  { cont := fa.continuousOn
    mean := by intro c r rp cs; rw [(fa.mono cs).circle_mean_eq rp] }

/-- Harmonic functions compose with linear maps -/
theorem HarmonicOn.linear [CompleteSpace F] {f : ‚ÑÇ ‚Üí E} {s : Set ‚ÑÇ} (fh : HarmonicOn f s)
    (g : E ‚ÜíL[‚Ñù] F) : HarmonicOn (fun z ‚Ü¶ g (f z)) s :=
  { cont := g.continuous.comp_continuousOn fh.cont
    mean := by
      intro c r rp cs
      rw [average_linear_comm ((fh.cont.mono cs).integrableOn_sphere rp)]
      rw [fh.mean c r rp cs] }

/-- Real parts of harmonic functions are harmonic -/
theorem HarmonicOn.re {f : ‚ÑÇ ‚Üí ‚ÑÇ} {s : Set ‚ÑÇ} (fh : HarmonicOn f s) :
    HarmonicOn (fun z ‚Ü¶ (f z).re) s := by simp only [‚Üê Complex.reCLM_apply]; exact fh.linear _

/-- Complex conjugates of harmonic functions are harmonic (since `conj` is linear) -/
theorem HarmonicOn.conj {f : ‚ÑÇ ‚Üí ‚ÑÇ} {s : Set ‚ÑÇ} (fh : HarmonicOn f s) :
    HarmonicOn (fun z ‚Ü¶ conj (f z)) s := by simp only [‚Üê conjCLM_apply]; exact fh.linear _

/-- Real parts of analytic functions are subharmonic -/
theorem AnalyticOnNhd.reSubharmonicOn {f : ‚ÑÇ ‚Üí ‚ÑÇ} {s : Set ‚ÑÇ} (fa : AnalyticOnNhd ‚ÑÇ f s) :
    SubharmonicOn (fun z ‚Ü¶ (f z).re) s :=
  fa.harmonicOn.re.subharmonicOn

/-- The submean property holds at minima -/
theorem Minimum.submean {f : ‚ÑÇ ‚Üí ‚Ñù} {s : Set ‚ÑÇ} {c : ‚ÑÇ} (fc : ContinuousOn f s)
    (cs : c ‚àà interior s) (fm : ‚àÄ z, f c ‚â§ f z) :
    ‚àÉ (r : _), 0 < r ‚àß ‚àÄ s, 0 < s ‚Üí s < r ‚Üí f c ‚â§ ‚®ç t in itau, f (circleMap c s t) := by
  rcases Metric.isOpen_iff.mp isOpen_interior c cs with ‚ü®r, rp, rs‚ü©
  use r, rp; intro t tp tr; rw [average_eq]
  have fg : ‚àÄ (u) (_ : u ‚àà itau), f c ‚â§ f (circleMap c t u) := fun _ _ ‚Ü¶ fm _
  have ss : closedBall c t ‚äÜ s :=
    _root_.trans (Metric.closedBall_subset_ball tr) (_root_.trans rs interior_subset)
  have n := NiceVolume.itau
  have m := setIntegral_ge_of_const_le n.measurable n.ne_top fg
    ((fc.mono ss).integrableOn_sphere tp)
  simpa only [MeasurableSet.univ, measureReal_restrict_apply, Set.univ_inter, smul_eq_mul,
    le_inv_mul_iff‚ÇÄ n.real_pos, mul_comm, ge_iff_le] using m

/-- `max b (log ‚Äñf z‚Äñ)` is subharmonic for analytic `f` (`‚ÑÇ` case) -/
theorem AnalyticOnNhd.maxLogAbsSubharmonicOn {f : ‚ÑÇ ‚Üí ‚ÑÇ} {s : Set ‚ÑÇ} (fa : AnalyticOnNhd ‚ÑÇ f s)
    (b : ‚Ñù) : SubharmonicOn (fun z ‚Ü¶ maxLog b ‚Äñf z‚Äñ) s :=
  { cont := fa.continuousOn.maxLog_norm b
    submean' := by
      intro c cs
      by_cases bf : b.exp ‚â• ‚Äñf c‚Äñ
      ¬∑ apply Minimum.submean (fa.continuousOn.maxLog_norm b) cs
        intro z; simp only [maxLog_eq_b bf, le_maxLog]
      simp only [not_le] at bf
      have anz : ‚Äñf c‚Äñ ‚â† 0 := (lt_trans (Real.exp_pos _) bf).ne'
      have fac : ContinuousAt f c :=
        fa.continuousOn.continuousAt (mem_interior_iff_mem_nhds.mp cs)
      -- We define g carefully to avoid the logarithmic branch cut
      generalize hh : (fun z ‚Ü¶ Complex.log (‚Äñf c‚Äñ / f c * f z)) = h
      generalize hg : (fun z ‚Ü¶ (h z).re) = g
      have ha : AnalyticAt ‚ÑÇ h c := by
        rw [‚Üê hh]
        apply (analyticAt_const.mul (fa c (interior_subset cs))).clog
        simp only [Pi.mul_apply]
        field_simp [norm_ne_zero_iff.mp anz]
        exact Complex.ofReal_mem_slitPlane.mpr
          (norm_pos_iff.mpr (ne_zero_of_norm_ne_zero anz))
      rcases Metric.isOpen_iff.mp (isOpen_analyticAt ‚ÑÇ h) c ha with ‚ü®r0, r0p, r0a‚ü©
      rcases Metric.continuousAt_iff.mp fac (‚Äñf c‚Äñ - b.exp) (sub_pos.mpr bf) with
        ‚ü®r1, r1p, r1h‚ü©
      set r := min r0 r1
      have fg : Set.EqOn (fun z ‚Ü¶ maxLog b ‚Äñf z‚Äñ) g (ball c r) := by
        intro z zs
        simp only [Metric.mem_ball, Complex.dist_eq] at zs r1h
        specialize r1h (lt_of_lt_of_le zs (by bound))
        have zp : ‚Äñf z‚Äñ > b.exp := by
          calc ‚Äñf z‚Äñ
            _ = ‚Äñf c + (f z - f c)‚Äñ := by ring_nf
            _ ‚â• ‚Äñf c‚Äñ - ‚Äñf z - f c‚Äñ := by bound
            _ > ‚Äñf c‚Äñ - (‚Äñf c‚Äñ - b.exp) := by bound
            _ = b.exp := by abel
        simp only [maxLog_eq_log zp.le]
        rw [‚Üêhg, ‚Üêhh]
        simp only [Complex.log_re, norm_mul, norm_div, Complex.norm_real,
          norm_norm, div_self anz, one_mul]
      have gs : SubharmonicOn g (ball c r) := by
        rw [‚Üê hg]; apply AnalyticOnNhd.reSubharmonicOn; intro z zs
        exact r0a (Metric.ball_subset_ball (by bound) zs)
      rw [subharmonicOn_congr fg.symm] at gs
      refine gs.submean' c ?_
      rw [Metric.isOpen_ball.interior_eq]; exact Metric.mem_ball_self (by bound) }

/-- If a subharmonic function is maximal at the center of a ball, it is constant on the ball. -/
theorem SubharmonicOn.maximum_principle_ball {f : ‚ÑÇ ‚Üí ‚Ñù} {c : ‚ÑÇ} {r : ‚Ñù}
    (fs : SubharmonicOn f (closedBall c r)) (rp : r > 0) :
    IsMaxOn f (closedBall c r) c ‚Üí ‚àÄ z, z ‚àà closedBall c r ‚Üí f c = f z := by
  intro cm g gs
  by_cases gc : g = c; ¬∑ rw [gc]
  generalize hu : ‚Äñg - c‚Äñ = u
  have u0 : u > 0 := by
    simp only [‚Üê hu, gt_iff_lt, norm_pos_iff, Ne]
    contrapose gc; simp only [sub_eq_zero] at gc ‚ä¢; exact gc
  have ur : u ‚â§ r := by
    simp only [Complex.dist_eq, Metric.mem_closedBall] at gs; simp only [‚Üêhu, gs]
  generalize hy : (g - c) / u = y
  have y1 : ‚Äñy‚Äñ = 1 := by
    simp only [‚Üê hy, ‚Üê hu, Complex.norm_div, Complex.norm_real, norm_norm, ne_eq,
      norm_sub_eq_zero_iff.not.mpr gc, not_false_eq_true, div_self]
  generalize hs : (fun t : ‚Ñù ‚Ü¶ f (c + t * y)) ‚Åª¬π' {f c} = s
  have s0 : (0 : ‚Ñù) ‚àà s := by
    simp only [‚Üê hs, Set.mem_preimage, Complex.ofReal_zero, MulZeroClass.zero_mul, add_zero,
      Set.mem_singleton]
  have us : u ‚àà s := by
    refine IsClosed.mem_of_ge_of_forall_exists_gt ?_ s0 u0.le ?_
    ¬∑ rw [‚Üê hs]; rw [Set.inter_comm]
      refine ContinuousOn.preimage_isClosed_of_isClosed ?_ isClosed_Icc isClosed_singleton
      apply fs.cont.comp (Continuous.continuousOn _) _
      ¬∑ exact continuous_const.add (Continuous.mul Complex.continuous_ofReal continuous_const)
      ¬∑ intro t ts
        simp only [Set.mem_Icc] at ts
        simp only [y1, abs_of_nonneg ts.1, _root_.trans ts.2 ur, Metric.mem_closedBall,
          dist_self_add_left, norm_mul, Complex.norm_real, Real.norm_eq_abs,
          mul_one]
    ¬∑ intro t ts
      simp only [‚Üê hs, Set.mem_inter_iff, Set.mem_preimage, Set.mem_singleton_iff,
        Set.mem_Ico] at ts
      generalize hz : c + t * y = z
      rcases ts with ‚ü®fz, tp, tu‚ü©
      have tz : ‚Äñz - c‚Äñ = t := by
        simp only [y1, abs_of_nonneg tp, add_sub_cancel_left, norm_mul, Complex.norm_real,
          Real.norm_eq_abs, mul_one, ‚Üê hz]
      have zs : z ‚àà ball c r := by
        simp only [y1, abs_of_nonneg tp, Metric.mem_ball, dist_self_add_left, norm_mul,
          Complex.norm_real, Real.norm_eq_abs, mul_one, ‚Üê hz]
        exact lt_of_lt_of_le tu ur
      rw [‚Üê interior_closedBall _ rp.ne'] at zs
      rcases fs.submean' z zs with ‚ü®e, ep, lo‚ü©
      generalize he' : min (e / 2) (u - t) = e'
      have e'p : e' > 0 := by rw [‚Üêhe']; exact lt_min (half_pos ep) (by linarith)
      have teu : t + e' ‚â§ u := by
        rw [‚Üê he']; trans t + (u - t)
        ¬∑ exact add_le_add_right (min_le_right _ _) _
        ¬∑ simp only [add_sub_cancel, le_refl]
      have e's : e' < e := by rw [‚Üê he']; exact lt_of_le_of_lt (min_le_left _ _) (half_lt_self ep)
      specialize lo e' e'p e's
      rw [‚Üê hz, fz] at lo
      have ss : closedBall z e' ‚äÜ closedBall c r := by
        apply Metric.closedBall_subset_closedBall'; rw [Complex.dist_eq, tz]; linarith
      have hi : ‚àÄ x, x ‚àà itau ‚Üí f (circleMap z e' x) ‚â§ f c := by
        intro x _; apply isMaxOn_iff.mp cm; apply ss
        simp only [Complex.dist_eq, Metric.mem_closedBall, circleMap_sub_center,
          norm_circleMap_zero, abs_of_pos e'p, le_refl]
      have fcc : ContinuousOn (fun a ‚Ü¶ f (circleMap z e' a)) itau := by
        apply (fs.cont.mono ss).comp (continuous_circleMap _ _).continuousOn; intro a _
        simp only [Complex.dist_eq, abs_of_pos e'p, Metric.mem_closedBall, circleMap_sub_center,
          norm_circleMap_zero, le_refl]
      rw [hz] at lo
      have fw := mean_squeeze NiceVolume.itau LocalVolume.itau fcc
        ((fs.cont.mono ss).integrableOn_sphere e'p) lo hi
      have eys : z + e' * y ‚àà sphere z e' := by
        simp only [Real.norm_eq_abs, abs_of_pos e'p, y1, mem_sphere_iff_norm, add_sub_cancel_left,
          norm_mul, Complex.norm_real, mul_one]
      rcases circleMap_Ioc eys with ‚ü®a, as, aey‚ü©
      specialize fw a as; simp only [‚Üê aey] at fw
      use t + e'
      simp only [Set.mem_inter_iff, Set.mem_Ioc, lt_add_iff_pos_right]
      refine ‚ü®?_, e'p, teu‚ü©
      simp only [‚Üê hs, right_distrib, Set.mem_preimage, Complex.ofReal_add, Set.mem_singleton_iff]
      rw [‚Üê add_assoc, hz]; exact fw
  simp only [‚Üê hs, ‚Üê hy, Set.mem_preimage, Set.mem_singleton_iff] at us
  have unz : (u : ‚ÑÇ) ‚â† 0 := by
    simp only [u0.ne', Ne, Complex.ofReal_eq_zero, not_false_iff]
  field_simp [unz] at us
  simpa using us.symm

/-- A subharmonic function achieves its maximum on the boundary -/
theorem SubharmonicOn.maximum_principle {f : ‚ÑÇ ‚Üí ‚Ñù} {s : Set ‚ÑÇ} (fs : SubharmonicOn f s)
    (sc : IsCompact s) (sn : s.Nonempty) : ‚àÉ w, w ‚àà frontier s ‚àß IsMaxOn f s w := by
  rcases sc.exists_isMaxOn sn fs.cont with ‚ü®x, xs, xm‚ü©
  rcases exists_mem_frontier_infDist_compl_eq_dist xs sc.ne_univ with ‚ü®w, wb, h‚ü©
  exists w, wb
  generalize hr : ‚Äñw - x‚Äñ = r
  by_cases wx : w = x; ¬∑ rwa [wx]
  have rp : r > 0 := by
    simp only [‚Üê hr, norm_pos_iff, Ne]; exact sub_ne_zero.mpr wx
  rw [dist_comm, Complex.dist_eq, hr] at h
  have rs : closedBall x r ‚äÜ s := by
    rw [‚Üê closure_ball x rp.ne', ‚Üê sc.isClosed.closure_eq]; apply closure_mono
    rw [‚Üê h]; apply Metric.ball_infDist_compl_subset
  have rm : IsMaxOn f (closedBall x r) x := by intro y ys; exact xm (rs ys)
  have wx : f x = f w := by
    apply SubharmonicOn.maximum_principle_ball (fs.mono rs) rp rm
    simp only [Complex.dist_eq, Metric.mem_closedBall, hr, le_refl]
  intro y ys; rw [‚Üê wx]; exact xm ys

/-- A harmonic function achieves its maximum norm on the boundary -/
theorem HarmonicOn.maximum_principle {f : ‚ÑÇ ‚Üí E} {s : Set ‚ÑÇ} (fh : HarmonicOn f s)
    (sc : IsCompact s) (sn : s.Nonempty) : ‚àÉ w, w ‚àà frontier s ‚àß ‚àÄ z, z ‚àà s ‚Üí ‚Äñf z‚Äñ ‚â§ ‚Äñf w‚Äñ := by
  rcases fh.norm.maximum_principle sc sn with ‚ü®w, wf, wh‚ü©; exists w, wf

omit [CompleteSpace E] in
/-- Uniform limits of harmonic functions are harmonic -/
theorem uniform_harmonic_lim [SecondCountableTopology E] {f : ‚Ñï ‚Üí ‚ÑÇ ‚Üí E} {g : ‚ÑÇ ‚Üí E} {s : Set ‚ÑÇ}
    (h : ‚àÄ n, HarmonicOn (f n) s) (u : TendstoUniformlyOn f g atTop s) : HarmonicOn g s :=
  { cont := u.continuousOn (Filter.Eventually.of_forall fun n ‚Ü¶ (h n).cont).frequently
    mean := by
      intro c r rp cs
      have m := fun n ‚Ü¶ (h n).mean c r rp cs
      simp_rw [average_eq] at m ‚ä¢
      have se : itau =·µê[volume] Icc 0 (2 * œÄ) := Ioc_ae_eq_Icc
      simp only [MeasurableSet.univ, measureReal_restrict_apply, Set.univ_inter,
        setIntegral_congr_set se] at m ‚ä¢
      generalize hv : volume.real itau = v; simp_rw [hv] at m ‚ä¢; clear hv
      have cc : Icc 0 (2 * œÄ) ‚äÜ circleMap c r ‚Åª¬π' s := by
        rw [Set.subset_def]; intro t _; simp only [Set.mem_preimage]; apply cs
        simp only [Complex.dist_eq, abs_of_pos rp, Metric.mem_closedBall, circleMap_sub_center,
          norm_circleMap_zero, le_refl]
      have fu := (u.comp (circleMap c r)).mono cc
      have fc : ‚àÄ n, ContinuousOn (fun t ‚Ü¶ f n (circleMap c r t)) (Icc 0 (2 * œÄ)) := by
        intro n; apply Continuous.continuousOn
        apply ((h n).cont.mono cs).comp_continuous (continuous_circleMap _ _); intro t
        simp only [Complex.dist_eq, abs_of_pos rp, Metric.mem_closedBall, circleMap_sub_center,
          norm_circleMap_zero, le_refl]
      have ti' := fu.integral_tendsto fc isCompact_Icc
      have ti := ti'.const_smul v‚Åª¬π; clear ti'
      have ci := u.tendsto_at (cs (Metric.mem_closedBall_self (by linarith)))
      simp only [‚Üê m, Function.comp_apply] at ti ‚ä¢
      exact tendsto_nhds_unique ci ti }

section HarmonicExtension

/-!
## Harmonic extensions

We show that continuous functions on circles extend to harmonic functions on the interior disk,
by showing that all trigonometric polynomials extend and taking limits.

This is needed below for the global submean property for subharmonic functions.
-/

variable {c : ‚ÑÇ} {r : ‚Ñù}

theorem rri (rp : 0 < r) (z : ‚ÑÇ) : c + r * ((‚Üër)‚Åª¬π * (z - c)) = z := by
  ring_nf; field_simp [rp.ne']; simp

theorem rir (rp : r > 0) (z : ‚ÑÇ) : (‚Üër)‚Åª¬π * (c + r * z - c) = z := by
  ring_nf; field_simp [rp.ne']

/-- The continuous function `f` on the circle has a Harmonic extension `g` on the disk -/
structure HasExtension (f : C(AddCircle (2*œÄ), S)) (g : ‚ÑÇ ‚Üí S) (c : ‚ÑÇ) (r : ‚Ñù) : Prop where
  gh : HarmonicOn g (closedBall c r)
  b : ‚àÄ t, f t = g (c + r * t.toCircle)

/-- `f` has some harmonic extension to the disk -/
def Extendable (f : C(AddCircle (2*œÄ), S)) (c : ‚ÑÇ) (r : ‚Ñù) :=
  ‚àÉ g : ‚ÑÇ ‚Üí S, HasExtension f g c r

/-- `HasExtension` is linear -/
theorem HasExtension.sub {f0 f1 : C(AddCircle (2*œÄ), ‚ÑÇ)} {g0 g1 : ‚ÑÇ ‚Üí ‚ÑÇ}
    (e0 : HasExtension f0 g0 c r) (e1 : HasExtension f1 g1 c r) :
    HasExtension (f0 - f1) (g0 - g1) c r :=
  { gh := e0.gh.sub e1.gh
    b := by simp only [e0.b, e1.b, ContinuousMap.coe_sub, Pi.sub_apply, forall_const] }

theorem mem_addCircle_iff_abs {z : ‚ÑÇ} : ‚Äñz‚Äñ = 1 ‚Üî ‚àÉ t : AddCircle (2 * œÄ), z = t.toCircle := by
  constructor
  ¬∑ intro az; rcases (Complex.norm_eq_one_iff z).mp az with ‚ü®t, h‚ü©; use t
    simp only [‚Üê h, AddCircle.toCircle, Function.Periodic.lift_coe, Circle.coe_exp,
      Complex.ofReal_mul, Complex.ofReal_div]
    field_simp [Real.pi_pos.ne']
  ¬∑ intro h; rcases h with ‚ü®t, h‚ü©; simp only [h, Circle.norm_coe]

/-- The extension is bounded by values on the circle -/
theorem Extension.maximum_principle {f : C(Real.Angle, ‚ÑÇ)} {g : ‚ÑÇ ‚Üí ‚ÑÇ} (e : HasExtension f g c r)
    {b : ‚Ñù} (fb : ‚àÄ z, ‚Äñf z‚Äñ ‚â§ b) (rp : 0 < r) : ‚àÄ z, z ‚àà closedBall c r ‚Üí ‚Äñg z‚Äñ ‚â§ b := by
  rcases e.gh.maximum_principle (isCompact_closedBall _ _) (Metric.nonempty_closedBall.mpr rp.le)
    with ‚ü®w, wf, wh‚ü©
  intro z zs; specialize wh z zs
  rw [frontier_closedBall _ rp.ne'] at wf; simp at wf
  generalize hw' : (‚Üër)‚Åª¬π * (w - c) = w'
  have wf' : ‚Äñw'‚Äñ = 1 := by
    simp only [‚Üê hw', Complex.norm_mul, norm_inv, Complex.norm_real, Real.norm_eq_abs,
      abs_of_pos rp, wf]
    field_simp [rp.ne']
  rcases mem_addCircle_iff_abs.mp wf' with ‚ü®t, tw‚ü©
  have fg := e.b t
  simp only [‚Üê tw, rri rp, ‚Üê hw'] at fg
  rw [‚Üê fg] at wh
  exact le_trans wh (fb _)

@[instance]
theorem Real.Angle.compactSpace : CompactSpace Real.Angle :=
  @AddCircle.compactSpace _ (fact_iff.mpr Real.two_pi_pos)

/-- `Extendable` is closed (limits of extendable functions are extendable) -/
theorem IsClosed.extendable {s : Set C(Real.Angle, ‚ÑÇ)} (e : ‚àÄ f, f ‚àà s ‚Üí Extendable f c r)
    (rp : r > 0) : ‚àÄ f, f ‚àà closure s ‚Üí Extendable f c r := by
  intro F Fe
  have fu : FrechetUrysohnSpace C(Real.Angle, ‚ÑÇ) := by
    apply @FirstCountableTopology.frechetUrysohnSpace
  rw [‚Üê seqClosure_eq_closure] at Fe
  rcases Fe with ‚ü®f, fs, fF‚ü©
  simp only [ContinuousMap.tendsto_iff_tendstoLocallyUniformly,
    tendstoLocallyUniformly_iff_tendstoUniformly_of_compactSpace] at fF
  generalize hg : (fun n ‚Ü¶ Classical.choose (e _ (fs n))) = g
  have gs : ‚àÄ n, HasExtension (f n) (g n) c r := by
    rw [‚Üê hg]; exact fun n ‚Ü¶ Classical.choose_spec (e _ (fs n))
  have cauchy : UniformCauchySeqOn g atTop (closedBall c r) := by
    rw [Metric.uniformCauchySeqOn_iff]
    simp_rw [Metric.tendstoUniformly_iff, Filter.eventually_atTop] at fF
    intro t tp; rcases fF (t / 4) (by linarith) with ‚ü®N, H‚ü©; exists N
    intro a aN b bN z zs
    have eab := (gs a).sub (gs b)
    have fab : ‚àÄ u : Real.Angle, ‚Äñf a u - f b u‚Äñ ‚â§ t / 2 := by
      intro u
      have ta := H a aN u
      have tb := H b bN u
      rw [‚Üê dist_eq_norm]; rw [dist_comm] at ta
      calc dist (f a u) (f b u)
        _ ‚â§ dist (f a u) (F u) + dist (F u) (f b u) := dist_triangle _ _ _
        _ ‚â§ t / 4 + t / 4 := by linarith
        _ = t / 2 := by ring_nf
    have m := Extension.maximum_principle eab fab rp z zs
    simp only [Complex.dist_eq, Pi.sub_apply] at m ‚ä¢
    exact lt_of_le_of_lt m (by linarith)
  set G := fun z ‚Ü¶ limUnder atTop fun n ‚Ü¶ g n z
  have gG : TendstoUniformlyOn g G atTop (closedBall c r) := by
    apply UniformCauchySeqOn.tendstoUniformlyOn_of_tendsto cauchy
    intro z zs; exact (cauchy.cauchySeq zs).tendsto_limUnder
  exists G
  exact
    { gh := uniform_harmonic_lim (fun n ‚Ü¶ (gs n).gh) gG
      b := by
        intro z
        refine (Filter.Tendsto.limUnder_eq ?_).symm
        simp_rw [‚Üê (gs _).b]
        exact fF.tendsto_at z }

theorem toCircle_neg {T : ‚Ñù} (x : AddCircle T) : (-x).toCircle = x.toCircle‚Åª¬π := by
  induction x using QuotientAddGroup.induction_on
  rw [‚ÜêAddCircle.coe_neg]
  simp only [AddCircle.toCircle, Function.Periodic.lift_coe, mul_neg, Circle.exp_neg]

theorem toCircle_smul {T : ‚Ñù} (n : ‚Ñï) (x : AddCircle T) : (n ‚Ä¢ x).toCircle = x.toCircle ^ n := by
  induction' x using QuotientAddGroup.induction_on with z
  rw [‚ÜêAddCircle.coe_nsmul]; simp only [AddCircle.toCircle, Function.Periodic.lift_coe]
  induction' n with n h
  ¬∑ simp only [Circle.exp_zero, MulZeroClass.mul_zero, pow_zero, zero_smul]
  ¬∑ simp only [succ_nsmul, left_distrib, Circle.exp_add, h, pow_succ]

@[simp] lemma Circle.pow_coe (z : Circle) (n : ‚Ñï) : (‚Üë(z ^ n) : ‚ÑÇ) = z ^ n := rfl

/-- Fourier terms extend -/
theorem fourierExtend' (rp : r > 0) (n : ‚Ñ§) : Extendable (fourier n) c r := by
  have mh : ‚àÄ n : ‚Ñï, HarmonicOn (fun z ‚Ü¶ ((‚Üër)‚Åª¬π * (z - c)) ^ n) (closedBall c r) := by
    intro n; apply AnalyticOnNhd.harmonicOn; refine AnalyticOnNhd.mono ?_ (Set.subset_univ _)
    rw [Complex.analyticOnNhd_iff_differentiableOn isOpen_univ]; apply Differentiable.differentiableOn
    apply Differentiable.pow; apply Differentiable.mul (differentiable_const _)
    apply Differentiable.sub differentiable_id (differentiable_const _)
  rcases n.eq_nat_or_neg with ‚ü®k, (e | e)‚ü©
  ¬∑ simp only [e]
    exists fun z : ‚ÑÇ ‚Ü¶ ((‚Üër)‚Åª¬π * (z - c)) ^ k
    exact
      { gh := mh k
        b := by
          intro t; rw [rir rp]
          apply Eq.trans fourier_apply
          simp only [natCast_zsmul, toCircle_smul]
          rfl }
  ¬∑ simp only [e]
    exists fun z : ‚ÑÇ ‚Ü¶ conj (((‚Üër)‚Åª¬π * (z - c)) ^ k)
    exact
      { gh := (mh k).conj
        b := by
          intro t; rw [rir rp]
          apply Eq.trans fourier_apply
          simp only [neg_smul, natCast_zsmul, toCircle_neg, toCircle_smul, Circle.coe_inv,
            Circle.pow_coe, Complex.inv_def, map_pow, Circle.normSq_coe, one_pow, inv_one,
            Complex.ofReal_one, mul_one] }

/-- Fourier sums extend -/
theorem fourierExtend {f : C(Real.Angle, ‚ÑÇ)} (rp : r > 0)
    (s : f ‚àà Submodule.span ‚ÑÇ (Set.range (@fourier (2 * œÄ)))) : Extendable f c r := by
  apply Submodule.span_induction (p := fun f _ ‚Ü¶ Extendable f c r) (hx := s)
  ¬∑ intro g gs
    simp only [Set.mem_range] at gs
    rcases gs with ‚ü®n, ng‚ü©
    rw [‚Üê ng]
    exact fourierExtend' rp _
  ¬∑ use fun _ ‚Ü¶ 0
    exact
      { gh := HarmonicOn.const _
        b := by simp only [ContinuousMap.coe_zero, Pi.zero_apply, forall_const] }
  ¬∑ intro x y _ _ ‚ü®x',xh,xb‚ü© ‚ü®y',yh,yb‚ü©
    use fun z ‚Ü¶ x' z + y' z
    exact
      { gh := xh.add yh
        b := by
          simp only [xb, yb, ContinuousMap.coe_add, Pi.add_apply, forall_const] }
  ¬∑ intro a x _ ‚ü®x', xh, xb‚ü©
    use fun z ‚Ü¶ a * x' z
    exact
      { gh := xh.const_mul _
        b := by
          simp only [xb, ContinuousMap.coe_smul, Pi.smul_apply, smul_eq_mul, forall_const] }

/-- All continuous functions on the circle extend to harmonic functions on the disk -/
theorem continuousExtend (f : C(Real.Angle, ‚ÑÇ)) (c : ‚ÑÇ) (rp : r > 0) : Extendable f c r := by
  set s : Submodule ‚ÑÇ C(Real.Angle, ‚ÑÇ) := Submodule.span ‚ÑÇ (Set.range (@fourier (2 * œÄ)))
  have se : ‚àÄ f, f ‚àà s.carrier ‚Üí Extendable f c r := fun f fs ‚Ü¶ fourierExtend rp fs
  have ce : ‚àÄ f, f ‚àà closure s.carrier ‚Üí Extendable f c r := IsClosed.extendable se rp
  have e : closure s.carrier = s.topologicalClosure.carrier := rfl
  rw [e, @span_fourier_closure_eq_top _ (fact_iff.mpr Real.two_pi_pos)] at ce
  apply ce; simp only [Submodule.mem_carrier]; trivial

end HarmonicExtension

/-- Everything is "harmonic" on singletons -/
theorem HarmonicOn.subsingleton {S : Type} [NormedAddCommGroup S] [NormedSpace ‚Ñù S]
    {f : ‚ÑÇ ‚Üí S} {s : Set ‚ÑÇ} (ss : s.Subsingleton) : HarmonicOn f s :=
  { cont := ss.continuousOn _
    mean := by
      intro c r rp cs
      have cm : c ‚àà s := cs (Metric.mem_closedBall_self (by linarith))
      have rm : c + r ‚àà s := cs (by
        simp only [Metric.mem_closedBall, dist_self_add_left, Complex.norm_real, Real.norm_eq_abs,
          abs_of_pos rp, le_refl])
      have e : c = c + r := ss cm rm
      simp [rp.ne'] at e }

/-- Continuous functions on the sphere extend to harmonic functions on the ball (`‚ÑÇ` case) -/
theorem continuous_to_harmonic_complex {f : ‚ÑÇ ‚Üí ‚ÑÇ} {c : ‚ÑÇ} {r : ‚Ñù}
    (fc : ContinuousOn f (sphere c r)) :
    ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, HarmonicOn g (closedBall c r) ‚àß ‚àÄ z, z ‚àà sphere c r ‚Üí f z = g z := by
  by_cases rp : r ‚â§ 0
  ¬∑ use f; use HarmonicOn.subsingleton (Metric.subsingleton_closedBall _ rp); intros; rfl
  simp only [not_le] at rp
  generalize hf' : (fun t : AddCircle (2 * œÄ) ‚Ü¶ f (c + r * t.toCircle)) = f'
  have fc' : Continuous f' := by
    rw [‚Üê hf']; apply fc.comp_continuous
    ¬∑ exact continuous_const.add
        (continuous_const.mul (continuous_subtype_val.comp AddCircle.continuous_toCircle))
    ¬∑ simp only [mem_sphere_iff_norm, add_sub_cancel_left, Complex.norm_mul, Complex.norm_real,
        Real.norm_eq_abs, norm_eq_of_mem_sphere, mul_one, abs_eq_self, rp.le, implies_true]
  rcases continuousExtend ‚ü®f', fc'‚ü© c rp with ‚ü®g, e‚ü©
  use g, e.gh; intro z zs
  generalize hz' : (‚Üër)‚Åª¬π * (z - c) = z'
  have za' : ‚Äñz'‚Äñ = 1 := by
    simp only [mem_sphere_iff_norm] at zs
    simp only [‚Üê hz', Complex.norm_mul, norm_inv, Complex.norm_real, Real.norm_eq_abs,
      abs_of_pos rp, zs, inv_mul_cancel‚ÇÄ rp.ne']
  rcases mem_addCircle_iff_abs.mp za' with ‚ü®t, tz‚ü©
  have rr : c + r * t.toCircle = z := by rw [‚Üê tz, ‚Üê hz']; exact rri rp _
  have h := e.b t
  simp only [ContinuousMap.coe_mk] at h
  nth_rw 2 [‚Üê rr]
  rw [‚Üê h]
  simp only [‚Üê hf', rr]

/-- Continuous functions on the sphere extend to harmonic functions on the ball (`‚Ñù` case) -/
theorem continuous_to_harmonic_real {f : ‚ÑÇ ‚Üí ‚Ñù} {c : ‚ÑÇ} {r : ‚Ñù} (fc : ContinuousOn f (sphere c r)) :
    ‚àÉ g : ‚ÑÇ ‚Üí ‚Ñù, HarmonicOn g (closedBall c r) ‚àß ‚àÄ z, z ‚àà sphere c r ‚Üí f z = g z := by
  generalize hf' : (fun z ‚Ü¶ (f z : ‚ÑÇ)) = f'
  have fc' : ContinuousOn f' (sphere c r) := by
    rw [‚Üê hf']; exact Complex.continuous_ofReal.comp_continuousOn fc
  rcases continuous_to_harmonic_complex fc' with ‚ü®g, gh, b‚ü©
  use fun z ‚Ü¶ (g z).re, gh.re
  intro z zs; simp only [‚Üê b z zs, Complex.ofReal_re, ‚Üê hf']

/-- If `f : ‚ÑÇ ‚Üí ‚Ñù` is subharmonic on a disk, its center value is `‚â§` the circle mean.

    This is the global version of the local submean property in the definition. -/
theorem SubharmonicOn.submean {f : ‚ÑÇ ‚Üí ‚Ñù} {c : ‚ÑÇ} {r : ‚Ñù} (fs : SubharmonicOn f (closedBall c r))
    (rp : r > 0) : f c ‚â§ ‚®ç t in itau, f (circleMap c r t) := by
  rcases continuous_to_harmonic_real (fs.cont.mono Metric.sphere_subset_closedBall) with ‚ü®g, gh, fg‚ü©
  generalize hd : (fun z ‚Ü¶ f z - g z) = d
  have ds : SubharmonicOn d (closedBall c r) := by rw [‚Üê hd]; apply fs.add gh.neg.subharmonicOn
  have dz : ‚àÄ z, z ‚àà sphere c r ‚Üí d z = 0 := by intro z zs; rw [‚Üê hd]; simp only [fg z zs, sub_self]
  have dz' : ‚àÄ·µê t, t ‚àà itau ‚Üí d (circleMap c r t) = 0 := by
    apply ae_of_all; intro t _; apply dz
    simp only [mem_sphere_iff_norm, circleMap_sub_center, norm_circleMap_zero,
      abs_eq_self]
    linarith
  rcases ds.maximum_principle (isCompact_closedBall _ _) ‚ü®c, Metric.mem_closedBall_self rp.le‚ü©
    with ‚ü®w, wf, wm‚ü©
  rw [frontier_closedBall _ rp.ne'] at wf
  have fd : f = fun z ‚Ü¶ d z + g z := by funext z; rw [‚Üê hd]; simp only [sub_add_cancel]
  simp_rw [fd, Average.add (ds.cont.integrableOn_sphere rp) (gh.cont.integrableOn_sphere rp)]
  simp only [‚Üê gh.mean c r rp (subset_refl _), add_le_add_iff_right]
  simp only [average_congr_on NiceVolume.itau dz', average_zero]
  rw [‚Üê dz w wf]; apply wm (Metric.mem_closedBall_self rp.le)

/-- A continuous function is subharmonic if it is globally subharmonic.
    This is useful since there are sometimes fewer technicalities in proving global
    subharmonicity. -/
theorem subharmonicOn_iff_submean {f : ‚ÑÇ ‚Üí ‚Ñù} {s : Set ‚ÑÇ} (fc : ContinuousOn f s) :
    SubharmonicOn f s ‚Üî ‚àÄ (c : ‚ÑÇ) (r : ‚Ñù), r > 0 ‚Üí
      closedBall c r ‚äÜ s ‚Üí f c ‚â§ ‚®ç t in itau, f (circleMap c r t) := by
  constructor; ¬∑ intro fs c r rp cs; exact (fs.mono cs).submean rp
  ¬∑ intro sm
    exact
      { cont := fc
        submean' := by
          intro c ci
          rcases Metric.isOpen_iff.mp isOpen_interior c ci with ‚ü®r, rp, rs‚ü©
          use r, rp; intro t tp tr; apply sm c t tp
          exact _root_.trans (Metric.closedBall_subset_ball tr) (_root_.trans rs interior_subset) }

/-- If `f : ‚ÑÇ ‚Üí ‚Ñù` is subharmonic on a disk, its center valus is `‚â§` the disk average.

    This is the submean property applied to disks, rather than circles. -/
theorem SubharmonicOn.submean_disk {f : ‚ÑÇ ‚Üí ‚Ñù} {c : ‚ÑÇ} {r : ‚Ñù}
    (fs : SubharmonicOn f (closedBall c r)) (rp : r > 0) : f c ‚â§ ‚®ç z in closedBall c r, f z := by
  simp only [average_eq, MeasurableSet.univ, measureReal_restrict_apply, Set.univ_inter,
    smul_eq_mul]
  rw [Complex.volume_closedBall' rp.le, fubini_ball fs.cont]
  have m : (fun s ‚Ü¶ (2 * œÄ * s) ‚Ä¢ f c) ‚â§·µê[volume.restrict (Ioc 0 r)] fun s ‚Ü¶
      s ‚Ä¢ ‚à´ t : ‚Ñù in Set.Ioc 0 (2 * œÄ), f (circleMap c s t) := by
    rw [Filter.EventuallyLE]; rw [ae_restrict_iff' measurableSet_Ioc]; apply ae_of_all; intro s sr
    simp only [Set.mem_Ioc] at sr
    have e := (fs.mono (Metric.closedBall_subset_closedBall sr.2)).submean sr.1
    rw [smul_eq_mul, ‚Üê itau]
    simp only [average_eq, MeasurableSet.univ, measureReal_restrict_apply, Set.univ_inter,
      smul_eq_mul, itau_real_volume] at e
    generalize hi : ‚à´ t in itau, f (circleMap c s t) = i
    rw [hi] at e
    calc 2 * œÄ * s * f c
      _ ‚â§ 2 * œÄ * s * ((2 * œÄ)‚Åª¬π * i) := by bound
      _ = s * (2 * œÄ * (2 * œÄ)‚Åª¬π) * i := by ring_nf
      _ ‚â§ s * i := by field_simp [Real.two_pi_pos.ne']; rfl
  have im := integral_mono_ae ?_ ?_ m
  ¬∑ generalize hi : ‚à´ s in Ioc 0 r, s ‚Ä¢ ‚à´ t in Ioc 0 (2 * œÄ), f (circleMap c s t) = i
    rw [hi] at im; clear hi m
    simp only [‚Üê intervalIntegral.integral_of_le rp.le, smul_eq_mul,
      intervalIntegral.integral_mul_const] at im
    rw [intervalIntegral.integral_const_mul] at im
    simp only [integral_id] at im
    ring_nf at im ‚ä¢
    calc f c
      _ = œÄ‚Åª¬π * r‚Åª¬π^2 * (œÄ * r^2 * f c) := by ring_nf; field_simp [rp.ne', Real.pi_pos.ne']
      _ ‚â§ œÄ‚Åª¬π * r‚Åª¬π^2 * i := by bound
  ¬∑ apply Continuous.integrableOn_Ioc; continuity
  ¬∑ refine IntegrableOn.mono_set ?_ Set.Ioc_subset_Icc_self
    apply ContinuousOn.integrableOn_Icc; apply ContinuousOn.smul continuousOn_id
    simp_rw [‚Üê intervalIntegral.integral_of_le Real.two_pi_pos.le]
    refine ContinuousOn.intervalIntegral ?_ isCompact_Icc Real.two_pi_pos.le
    simp only [Set.Icc_prod_Icc]
    refine fs.cont.comp (Continuous.continuousOn (by continuity)) ?_
    intro (a,b) ts
    simp only [Prod.mk_le_mk, Set.mem_Icc] at ts
    simp only [Metric.mem_closedBall, Complex.dist_eq, circleMap_sub_center, norm_circleMap_zero,
      abs_of_nonneg ts.1.1, ts.2.1]

/-- The max of two subharmonic functions is subharmonic -/
theorem SubharmonicOn.max {f g : ‚ÑÇ ‚Üí ‚Ñù} {s : Set ‚ÑÇ} (fs : SubharmonicOn f s)
    (gs : SubharmonicOn g s) : SubharmonicOn (fun z ‚Ü¶ max (f z) (g z)) s := by
  have pc : ContinuousOn (fun z ‚Ü¶ (f z, g z)) s := fs.cont.prodMk gs.cont
  have mc : ContinuousOn (fun z ‚Ü¶ Max.max (f z) (g z)) s := continuous_max.comp_continuousOn pc
  rw [subharmonicOn_iff_submean mc]
  intro c r rp cs
  have pi : IntegrableOn (fun t ‚Ü¶ (f (circleMap c r t), g (circleMap c r t))) itau := by
    refine ContinuousOn.integrableOn_sphere (f := fun z ‚Ü¶ (f z, g z)) ?_ rp
    exact pc.mono cs
  refine _root_.trans ?_ (ConvexOn.map_set_average_le convexOn_max continuous_max.continuousOn
      isClosed_univ ?_ ?_ ?_ pi ?_)
  ¬∑ apply max_le_max
    ¬∑ have e : ‚àÄ p : ‚Ñù √ó ‚Ñù, p.fst = ContinuousLinearMap.fst ‚Ñù ‚Ñù ‚Ñù p := fun p ‚Ü¶ by
        simp only [ContinuousLinearMap.fst, ContinuousLinearMap.coe_mk', LinearMap.fst_apply]
      rw [e]; rw [‚Üê average_linear_comm pi]
      simp only [ContinuousLinearMap.fst, ContinuousLinearMap.coe_mk', LinearMap.fst_apply]
      exact (fs.mono cs).submean rp
    ¬∑ have e : ‚àÄ p : ‚Ñù √ó ‚Ñù, p.snd = ContinuousLinearMap.snd ‚Ñù ‚Ñù ‚Ñù p := fun p ‚Ü¶ by
        simp only [ContinuousLinearMap.snd, ContinuousLinearMap.coe_mk', LinearMap.snd_apply]
      rw [e]; rw [‚Üê average_linear_comm pi]
      simp only [ContinuousLinearMap.snd, ContinuousLinearMap.coe_mk', LinearMap.snd_apply]
      exact (gs.mono cs).submean rp
  ¬∑ simp only [Ne]; exact NiceVolume.itau.ne_zero
  ¬∑ exact NiceVolume.itau.ne_top
  ¬∑ simp only [Set.mem_univ, Filter.eventually_true]
  ¬∑ exact (mc.mono cs).integrableOn_sphere rp

/-- The max of a finite set of subharmonic functions is subharmonic -/
theorem SubharmonicOn.partialSups {f : ‚Ñï ‚Üí ‚ÑÇ ‚Üí ‚Ñù} {s : Set ‚ÑÇ} (fs : ‚àÄ n, SubharmonicOn (f n) s)
    (n : ‚Ñï) : SubharmonicOn (fun z ‚Ü¶ partialSups (fun k ‚Ü¶ f k z) n) s := by
  induction' n with n h
  ¬∑ simp only [fs 0, partialSups_zero]
  ¬∑ simp only [‚Üê Order.succ_eq_add_one, partialSups_succ]
    exact h.max (fs (n + 1))

/-- Continuous, monotonic limits of subharmonic functions are subharmonic -/
theorem SubharmonicOn.monotone_lim {f : ‚Ñï ‚Üí ‚ÑÇ ‚Üí ‚Ñù} {g : ‚ÑÇ ‚Üí ‚Ñù} {s : Set ‚ÑÇ}
    (fs : ‚àÄ n, SubharmonicOn (f n) s) (fm : Monotone f)
    (ft : ‚àÄ z, z ‚àà s ‚Üí Tendsto (fun n ‚Ü¶ f n z) atTop (ùìù (g z))) (gc : ContinuousOn g s) :
    SubharmonicOn g s := by
  rw [subharmonicOn_iff_submean gc]; intro c r rp cs
  have sm := fun n ‚Ü¶ ((fs n).mono cs).submean rp
  have r0 : 0 ‚â§ r := rp.le
  have cts : ‚àÄ t, circleMap c r t ‚àà s := fun _ ‚Ü¶ cs (circleMap_mem_closedBall _ r0 _)
  have mt : Tendsto (fun n ‚Ü¶ ‚®ç t in itau, f n (circleMap c r t)) atTop
      (ùìù (‚®ç t in itau, g (circleMap c r t))) := by
    simp_rw [average_eq]; apply Filter.Tendsto.const_smul
    set b' := fun z ‚Ü¶ |f 0 z| + |g z|
    set b := fun t ‚Ü¶ b' (circleMap c r t)
    have bc' : ContinuousOn b' (closedBall c r) :=
      ContinuousOn.add ((fs 0).mono cs).cont.abs (gc.mono cs).abs
    have fcc : ‚àÄ n, Continuous fun t ‚Ü¶ f n (circleMap c r t) := fun n ‚Ü¶
      ((fs n).cont.mono cs).comp_continuous (continuous_circleMap _ _) fun t ‚Ü¶
        circleMap_mem_closedBall _ r0 _
    apply tendsto_integral_of_dominated_convergence b; ¬∑ intro n; exact (fcc n).aestronglyMeasurable
    ¬∑ exact bc'.integrableOn_sphere rp
    ¬∑ intro n; rw [ae_restrict_iff' measurableSet_itau]; apply ae_of_all; intro t _
      generalize hz : circleMap c r t = z
      have zs : z ‚àà s := by rw [‚Üê hz]; apply cts
      rw [Real.norm_eq_abs]; rw [abs_le]; constructor
      ¬∑ calc -b t
          _ ‚â§ -(|f 0 z| + 0) := by rw [‚Üêhz]; bound
          _ = -|f 0 z| := by simp only [add_zero]
          _ ‚â§ f 0 z := (neg_abs_le _)
          _ ‚â§ f n z := fm (by simp only [zero_le']) _
      ¬∑ have mn : Monotone fun n ‚Ü¶ f n z := fun _ _ ab ‚Ü¶ fm ab z
        calc f n z
          _ ‚â§ g z := Monotone.ge_of_tendsto (f := fun n ‚Ü¶ f n z) mn (ft z zs) n
          _ ‚â§ |g z| := le_abs_self _
          _ = 0 + |g z| := by ring
          _ ‚â§ b t := by rw [‚Üê hz]; apply add_le_add; apply abs_nonneg; apply le_refl
    ¬∑ rw [ae_restrict_iff' measurableSet_itau]; apply ae_of_all; intro t _; exact ft _ (cts _)
  exact le_of_tendsto_of_tendsto' (ft c (cs (Metric.mem_closedBall_self r0))) mt sm

/-- `max b (log ‚Äñf z‚Äñ)` is subharmonic for analytic `f : ‚ÑÇ ‚Üí H`, for a separable Banach space `H`.

    Some machinery is required to handle general Banach spaces: we rewrite `‚Äñf z‚Äñ` as the limit
    of norms along larger and larger finite subspaces, and use the fact that `linear ‚àò analytic`
    is analytic to reduce to the case of `H = ‚ÑÇ`. -/
public theorem AnalyticOnNhd.maxLog_norm_subharmonicOn [SecondCountableTopology H] {f : ‚ÑÇ ‚Üí H}
    {s : Set ‚ÑÇ} (fa : AnalyticOnNhd ‚ÑÇ f s) (b : ‚Ñù) : SubharmonicOn (fun z ‚Ü¶ maxLog b ‚Äñf z‚Äñ) s := by
  have gc := fa.continuousOn.maxLog_norm b
  have ft := fun z (_ : z ‚àà s) ‚Ü¶ duals_lim_tendsto_maxLog_norm b (f z)
  have fs : ‚àÄ n, SubharmonicOn (fun z ‚Ü¶ partialSups (fun k ‚Ü¶ maxLog b ‚Äñduals k (f z)‚Äñ) n) s := by
    intro m; apply SubharmonicOn.partialSups; intro n
    exact ((duals n).comp_analyticOnNhd fa).maxLogAbsSubharmonicOn b
  refine SubharmonicOn.monotone_lim fs ?_ ft gc
  ¬∑ intro a b ab z
    apply (partialSups _).monotone ab

/-- limsup -f = -liminf f -/
theorem Limsup.neg {f : ‚Ñï ‚Üí ‚Ñù} : (atTop.limsup fun n ‚Ü¶ f n) = -atTop.liminf fun n ‚Ü¶ -f n := by
  rw [Filter.limsup_eq]; rw [Filter.liminf_eq]; rw [Real.sInf_def]
  have ns : -{a | ‚àÄ·∂† n in atTop, a ‚â§ -f n} = {a | ‚àÄ·∂† n in atTop, f n ‚â§ a} := by
    apply Set.ext
    simp only [Set.mem_neg, Set.mem_setOf_eq, neg_le_neg_iff, iff_self, forall_const]
  simp_rw [‚Üê ns]; simp only [neg_neg]

/-- `p : ENNReal ‚Üí Prop` is true for all `ENNReal`s if it is true for `‚ä§` and positive reals -/
theorem ENNReal.induction {p : ENNReal ‚Üí Prop} (pi : p ‚ä§)
    (pf : ‚àÄ (x : ‚Ñù), 0 ‚â§ x ‚Üí p (ENNReal.ofReal x)) : ‚àÄ e, p e := by
  rw [ENNReal.forall_ennreal]; refine ‚ü®?_, pi‚ü©; rw [NNReal.forall]
  simpa only [‚Üê ENNReal.ofReal_eq_coe_nnreal]

theorem le_of_lt_imp_le {L : Type} [LinearOrder L] [DenselyOrdered L] {a b : L}
    (h : ‚àÄ c, c < a ‚Üí c ‚â§ b) : a ‚â§ b := by
  contrapose h
  simp only [not_forall, not_le, exists_prop] at h ‚ä¢; rcases exists_between h with ‚ü®x, bx, xa‚ü©
  exact ‚ü®x, xa, bx‚ü©

/-- A simple characterization of `c ‚â§ liminf` -/
theorem le_liminf.simple {L : Type} [CompleteLinearOrder L] [DenselyOrdered L] {f : ‚Ñï ‚Üí L} {c : L} :
    c ‚â§ atTop.liminf f ‚Üî ‚àÄ d, d < c ‚Üí ‚àÄ·∂† n in atTop, d ‚â§ f n := by
  constructor
  ¬∑ intro h d dc; rw [Filter.liminf_eq, le_sSup_iff, upperBounds] at h
    simp only [Filter.eventually_atTop, ge_iff_le, Set.mem_setOf_eq, forall_exists_index] at h
    specialize h d; contrapose h
    simp only [dc, not_forall, not_le, exists_prop, and_true, Filter.eventually_atTop,
      ge_iff_le, not_exists] at h ‚ä¢
    intro a n an; rcases h n with ‚ü®m, nm, fmd‚ü©
    exact _root_.trans (an m nm) fmd.le
  ¬∑ intro h; rw [Filter.liminf_eq, le_sSup_iff, upperBounds]
    simp only [Filter.eventually_atTop, ge_iff_le, Set.mem_setOf_eq, forall_exists_index]
    intro a ah; apply le_of_lt_imp_le; intro d dc
    rcases Filter.eventually_atTop.mp (h d dc) with ‚ü®n, hn‚ü©; exact ah n hn

theorem ENNReal.ofReal_neg_lt_ofReal_neg {x y : ‚Ñù} (xy : x < y) (xn : x < 0) :
    ENNReal.ofReal (-y) < ENNReal.ofReal (-x) := by
  apply (ENNReal.ofReal_lt_ofReal_iff _).mpr
  simp only [xy, neg_lt_neg_iff]; simp only [xn, Right.neg_pos_iff]

/-- Superharmonic `ENNReal` functions, which are allowed to take the value `‚àû` and required
    only to be measurable (which is not good: the right definition would require lower
    semicontinuity). -/
structure SuperharmonicOn (f : ‚ÑÇ ‚Üí ENNReal) (s : Set ‚ÑÇ) : Prop where
  AEMeasurable : AEMeasurable f (volume.restrict s)
  supmean :
    ‚àÄ (c : ‚ÑÇ) (r : ‚Ñù),
      r > 0 ‚Üí closedBall c r ‚äÜ s ‚Üí f c ‚â• ENNReal.ofReal (œÄ * r ^ 2)‚Åª¬π * ‚à´‚Åª z in closedBall c r, f z

/-- `ENNReal.ofReal (-f)` is superharmonic if `f` is negative superharmonic -/
theorem SubharmonicOn.neg {f : ‚ÑÇ ‚Üí ‚Ñù} {s : Set ‚ÑÇ} (fs : SubharmonicOn f s)
    (fn : ‚àÄ z, z ‚àà s ‚Üí f z ‚â§ 0) (sm : MeasurableSet s) :
    SuperharmonicOn (fun z ‚Ü¶ ENNReal.ofReal (-f z)) s :=
  { AEMeasurable := by
      apply ENNReal.measurable_ofReal.aemeasurable.comp_aemeasurable
      apply fs.cont.neg.aemeasurable sm
    supmean := by
      intro c r rp cs
      rw [‚Üê ofReal_integral_eq_lintegral_ofReal]
      ¬∑ rw [‚Üê ENNReal.ofReal_mul]; apply ENNReal.ofReal_le_ofReal
        rw [integral_neg, mul_neg]; apply neg_le_neg
        rw [‚ÜêComplex.volume_closedBall' rp.le, ‚Üêsmul_eq_mul, ‚ÜêsetAverage_eq]
        exact (fs.mono cs).submean_disk rp; bound
      ¬∑ exact (fs.mono cs).cont.neg.integrableOn_closedBall
      ¬∑ rw [Filter.EventuallyLE]; rw [ae_restrict_iff' measurableSet_closedBall]
        apply Filter.Eventually.of_forall
        intro z zs; simp only [Pi.zero_apply, Right.nonneg_neg_iff]; exact fn z (cs zs) }

lemma NNReal.pi_eq_ofReal_pi : (NNReal.pi : ENNReal) = .ofReal œÄ := by
  rw [‚ÜêNNReal.coe_real_pi, ENNReal.ofReal_coe_nnreal]

/-- Hartogs's lemma, superharmonic `‚Ñù‚â•0‚àû` case: superharmonic functions that are bounded below
    and liminf bounded pointwise are liminf bounded uniformly.

    See https://www-users.cse.umn.edu/~garrett/m/complex/hartogs.pdf -/
theorem SuperharmonicOn.hartogs {f : ‚Ñï ‚Üí ‚ÑÇ ‚Üí ENNReal} {s k : Set ‚ÑÇ} {c : ENNReal}
    (fs : ‚àÄ n, SuperharmonicOn (f n) s) (fc : ‚àÄ z, z ‚àà s ‚Üí (atTop.liminf fun n ‚Ü¶ f n z) ‚â• c)
    (ck : IsCompact k) (ks : k ‚äÜ interior s) :
    ‚àÄ d, d < c ‚Üí ‚àÄ·∂† n in atTop, ‚àÄ z, z ‚àà k ‚Üí f n z ‚â• d := by
  -- Prepare d and c
  intro d dc
  by_cases dz : d = 0
  ¬∑ simp only [dz, ge_iff_le, zero_le', imp_true_iff, Filter.eventually_atTop, exists_const]
  have dp : d > 0 := pos_iff_ne_zero.mpr dz
  have df : d ‚â† ‚ä§ := ne_top_of_lt dc
  have drp : d.toReal > 0 := ENNReal.toReal_pos dz df
  -- Choose e ‚àà (c,d) so that c ‚Üí e is due to Fatou, and e ‚Üí d is due to area bounding
  rcases exists_between dc with ‚ü®e, de, ec‚ü©
  have ep : e > 0 := _root_.trans de dp
  have ez : e ‚â† 0 := pos_iff_ne_zero.mp ep
  have ef : e ‚â† ‚ä§ := ne_top_of_lt ec
  have erp : e.toReal > 0 := ENNReal.toReal_pos ez ef
  -- Handle induction up from small balls
  apply ck.induction_on (p := fun s ‚Ü¶ ‚àÄ·∂† (n : ‚Ñï) in atTop, ‚àÄ (z : ‚ÑÇ), z ‚àà s ‚Üí f n z ‚â• d)
  ¬∑ simp only [Set.mem_empty_iff_false, IsEmpty.forall_iff, Filter.eventually_atTop, imp_true_iff,
      exists_const]
  ¬∑ intro k0 k1 k01 h1
    refine h1.mp (.of_forall ?_)
    exact fun n a1 z z0 ‚Ü¶ a1 z (k01 z0)
  ¬∑ intro k0 k1 h0 h1
    refine (h0.and h1).mp (.of_forall ?_)
    intro n h z zs; cases' zs with zs zs; exact h.1 z zs; exact h.2 z zs
  -- Base case: Hartogs's lemma near a point.  We choose radii r1 < r2 within s, apply
  -- Fatou's lemma at r1, use monotonicity to bound by r2 integrals, and apply the submean
  -- property with radius r2 to get Hartogs's within radius r2-r1.
  intro z zs
  rcases Metric.isOpen_iff.mp isOpen_interior z (ks zs) with ‚ü®r, rp, rs‚ü©
  generalize hr2 : r / 2 = r2
  -- We'll use the submean property on disks of radius r2 < r
  generalize hr1 : r2 * Real.sqrt (d.toReal / e.toReal) = r1
  -- We'll apply Fatou's lemma to a disk of radius r1 < r2 < r
  have dep : d.toReal / e.toReal > 0 := div_pos drp erp
  have r2p : r2 > 0 := by rw [‚Üê hr2]; exact half_pos rp
  have r1p : r1 > 0 := by rw [‚Üê hr1]; exact mul_pos r2p (Real.sqrt_pos_of_pos (div_pos drp erp))
  have r12 : r1 < r2 := by
    rw [‚Üê hr1]; apply mul_lt_of_lt_one_right r2p; rw [Real.sqrt_lt dep.le zero_le_one]
    simp only [one_pow]
    apply (div_lt_one erp).mpr; exact (ENNReal.toReal_lt_toReal df ef).mpr de
  have r1r : r1 < r := by apply _root_.trans r12; rw [‚Üê hr2]; exact half_lt_self rp
  have r1s : closedBall z r1 ‚äÜ s :=
    _root_.trans (Metric.closedBall_subset_ball r1r) (_root_.trans rs interior_subset)
  have rde : d = e * (ENNReal.ofReal (œÄ * r1 ^ 2) * ENNReal.ofReal (œÄ * r2 ^ 2)‚Åª¬π) := by
    rw [‚Üê ENNReal.ofReal_mul (by bound : œÄ * r1 ^ 2 ‚â• 0), ‚Üê hr1, mul_pow, Real.sq_sqrt dep.le]
    have smash : œÄ * (r2 ^ 2 * (d.toReal / e.toReal)) * (œÄ * r2 ^ 2)‚Åª¬π = d.toReal / e.toReal := by
      calc œÄ * (r2 ^ 2 * (d.toReal / e.toReal)) * (œÄ * r2 ^ 2)‚Åª¬π
        _ = œÄ * (r2 ^ 2 * (d.toReal / e.toReal)) * (œÄ‚Åª¬π * (r2 ^ 2)‚Åª¬π) := by simp_rw [mul_inv]
        _ = d.toReal / e.toReal * (œÄ * œÄ‚Åª¬π) * (r2 ^ 2 * (r2 ^ 2)‚Åª¬π) := by ring_nf
        _ = d.toReal / e.toReal := by simp only [mul_inv_cancel‚ÇÄ Real.pi_pos.ne',
            mul_inv_cancel‚ÇÄ (pow_ne_zero _ r2p.ne'), mul_one]
    rw [smash, ENNReal.ofReal_div_of_pos erp, ENNReal.ofReal_toReal df, ENNReal.ofReal_toReal ef]
    rw [ENNReal.mul_div_cancel ez ef]
  have s12 : ‚àÄ w, w ‚àà closedBall z (r2 - r1) ‚Üí closedBall z r1 ‚äÜ closedBall w r2 := by
    intro w wr; apply Metric.closedBall_subset_closedBall'
    simp only [dist_comm, Metric.mem_closedBall, le_sub_iff_add_le] at wr; rwa [add_comm]
  have r2s : ‚àÄ w, w ‚àà closedBall z (r2 - r1) ‚Üí closedBall w r2 ‚äÜ s := by
    intro w ws; refine _root_.trans ?_ (_root_.trans rs interior_subset)
    simp only [Complex.dist_eq, ‚Üê hr2, Metric.mem_closedBall] at ws ‚ä¢
    apply Metric.closedBall_subset_ball'; simp only [Complex.dist_eq]
    calc r / 2 + ‚Äñw - z‚Äñ
      _ ‚â§ r / 2 + (r / 2 - r1) := by bound
      _ = r - r1 := by ring_nf
      _ < r := sub_lt_self _ r1p
  -- Apply Fatou's lemma to closedBall z (r/2)
  set fi := fun z ‚Ü¶ atTop.liminf fun n ‚Ü¶ f n z
  have fm : ‚àÄ n, _root_.AEMeasurable (f n) (volume.restrict (closedBall z r1)) := fun n ‚Ü¶
    AEMeasurable.mono_set r1s (fs n).AEMeasurable
  have fatou' := @lintegral_liminf_le' _ _ (volume.restrict (closedBall z r1)) f fm
  have im := @set_lintegral_mono_aEMeasurable _ _ (closedBall z r1) (fun _ ‚Ü¶ c) _
    measurableSet_closedBall fun _ zs ‚Ü¶ fc _ (r1s zs)
  simp only [lintegral_const, Measure.restrict_apply, MeasurableSet.univ, Set.univ_inter] at im
  have vec : e * volume (closedBall z r1) < c * volume (closedBall z r1) :=
    haveI n := NiceVolume.closedBall z r1p
    (ENNReal.mul_lt_mul_iff_left n.ne_zero n.ne_top).mpr ec
  have fatou := le_liminf.simple.mp (_root_.trans im fatou') (e * volume (closedBall z r1)) vec
  rw [Complex.volume_closedBall, NNReal.pi_eq_ofReal_pi, ‚ÜêENNReal.ofReal_pow r1p.le,
    ‚ÜêENNReal.ofReal_mul' Real.pi_nonneg, mul_comm _ œÄ] at fatou
  clear fatou' im fc vec
  -- Within radius r2-r1, Fatou's lemma implies local Hartogs's
  use closedBall z (r2 - r1),
    mem_nhdsWithin_of_mem_nhds (Metric.closedBall_mem_nhds _ (by bound))
  refine fatou.mp (.of_forall ?_)
  intro n fn w ws
  calc d
    _ = e * (ENNReal.ofReal (œÄ * r1 ^ 2) * ENNReal.ofReal (œÄ * r2 ^ 2)‚Åª¬π) := by rw [rde]
    _ = e * ENNReal.ofReal (œÄ * r1 ^ 2) * ENNReal.ofReal (œÄ * r2 ^ 2)‚Åª¬π := by rw [mul_assoc]
    _ ‚â§ (‚à´‚Åª v in closedBall z r1, f n v) * ENNReal.ofReal (œÄ * r2 ^ 2)‚Åª¬π := (mul_left_mono fn)
    _ ‚â§ (‚à´‚Åª v in closedBall w r2, f n v) * ENNReal.ofReal (œÄ * r2 ^ 2)‚Åª¬π :=
      (mul_left_mono (lintegral_mono_set (s12 w ws)))
    _ = ENNReal.ofReal (œÄ * r2 ^ 2)‚Åª¬π * ‚à´‚Åª v in closedBall w r2, f n v := by rw [mul_comm]
    _ ‚â§ f n w := (fs n).supmean w r2 r2p (r2s w ws)

/-- Hartogs's lemma, subharmonic case: subharmonic functions that are bounded above and linsup
    bounded pointwise are linsup bounded uniformly.  We write out the definition of `limsup ‚â§ c`
    since `‚Ñù` not being complete makes it complicated otherwise.

    See https://www-users.cse.umn.edu/~garrett/m/complex/hartogs.pdf -/
public theorem SubharmonicOn.hartogs {f : ‚Ñï ‚Üí ‚ÑÇ ‚Üí ‚Ñù} {s k : Set ‚ÑÇ} {c b : ‚Ñù}
    (fs : ‚àÄ n, SubharmonicOn (f n) s) (fb : ‚àÄ n z, z ‚àà s ‚Üí f n z ‚â§ b)
    (fc : ‚àÄ z, z ‚àà s ‚Üí ‚àÄ d, d > c ‚Üí ‚àÄ·∂† n in atTop, f n z ‚â§ d) (ck : IsCompact k)
    (ks : k ‚äÜ interior s) : ‚àÄ d, d > c ‚Üí ‚àÄ·∂† n in atTop, ‚àÄ z, z ‚àà k ‚Üí f n z ‚â§ d := by
  -- Deal with degenerate b ‚â§ c case
  by_cases bc : b ‚â§ c
  ¬∑ exact fun d dc ‚Ü¶ .of_forall fun n z zk ‚Ü¶
      le_trans (fb n z (_root_.trans ks interior_subset zk)) (_root_.trans bc dc.le)
  simp only [not_le] at bc
  -- Port subharmonic problem to superharmonic ennreal problem
  generalize hf' : (fun n z ‚Ü¶ f n z - b) = f'
  generalize hg : (fun n z ‚Ü¶ ENNReal.ofReal (-f' n z)) = g
  have fs' : ‚àÄ n, SubharmonicOn (f' n) s := by
    rw [‚Üê hf']; exact fun n ‚Ü¶ (fs n).add (HarmonicOn.const _).subharmonicOn
  have fn' : ‚àÄ n z, z ‚àà interior s ‚Üí f' n z ‚â§ 0 := fun n z zs ‚Ü¶ by
    simp only [‚Üê hf', fb n z (interior_subset zs), sub_nonpos]
  have gs : ‚àÄ n, SuperharmonicOn (g n) (interior s) := by
    rw [‚Üê hg]; exact fun n ‚Ü¶ ((fs' n).mono interior_subset).neg (fn' n) measurableSet_interior
  have gc : ‚àÄ z, z ‚àà interior s ‚Üí (atTop.liminf fun n ‚Ü¶ g n z) ‚â• ENNReal.ofReal (b - c) := by
    intro z zs; specialize fc z (interior_subset zs); refine le_liminf.simple.mpr ?_
    intro d dc
    have df : d ‚â† ‚ä§ := ne_top_of_lt dc
    have dc' : b - d.toReal > c := by
      calc b - d.toReal
        _ > b - (ENNReal.ofReal (b - c)).toReal :=
          sub_lt_sub_left ((ENNReal.toReal_lt_toReal df ENNReal.ofReal_ne_top).mpr dc) b
        _ = b - (b - c) := by rw [ENNReal.toReal_ofReal (sub_pos.mpr bc).le]
        _ = c := by ring_nf
    refine (fc _ dc').mp (.of_forall ?_); intro n fb
    calc
      g n z = ENNReal.ofReal (b - f n z) := by simp only [‚Üê hg, ‚Üê hf', neg_sub]
      _ ‚â• ENNReal.ofReal (b - (b - d.toReal)) := by bound
      _ = ENNReal.ofReal d.toReal := by ring_nf
      _ = d := by rw [ENNReal.ofReal_toReal df]
  -- Apply Hartogs's lemma to g
  have ks' := ks
  rw [‚Üê interior_interior] at ks'
  have h := SuperharmonicOn.hartogs gs gc ck ks'
  -- Finish up
  intro d dc
  have dc' : ENNReal.ofReal (b - d) < ENNReal.ofReal (b - c) := by
    rw [ENNReal.ofReal_lt_ofReal_iff (sub_pos.mpr bc)]; simpa only [sub_lt_sub_iff_left]
  refine (h _ dc').mp (.of_forall ?_)
  intro n hn z zk; specialize hn z zk
  simp only [‚Üê hg, ‚Üê hf', neg_sub, ge_iff_le] at hn
  rw [ENNReal.ofReal_le_ofReal_iff (sub_nonneg.mpr (fb n z (interior_subset (ks zk))))] at hn
  rwa [‚Üê sub_le_sub_iff_left]
