import Mathlib.Analysis.Analytic.Basic
import Mathlib.Analysis.Analytic.Constructions
import Mathlib.Analysis.Complex.Basic
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Data.Complex.Basic
import Mathlib.Geometry.Manifold.AnalyticManifold
import Mathlib.Geometry.Manifold.ChartedSpace
import Mathlib.Geometry.Manifold.ContMDiffMFDeriv
import Mathlib.Geometry.Manifold.LocalInvariantProperties
import Mathlib.Geometry.Manifold.SmoothManifoldWithCorners
import Mathlib.Geometry.Manifold.VectorBundle.Tangent
import Ray.Analytic.HolomorphicUpstream
import Ray.AnalyticManifold.SmoothManifoldWithCorners

/-!
## Analytic manifolds

We define `AnalyticManifold`s over any complete, nontrivially normed field `ğ•œ`, as
charted spaces where all charts conversions are locally analytic.  We consider only
the boundaryless case for simplicity, though the `analyticGroupoid` is defined in the
boundary case too so this is a future generalization.  We then define the analogs to
`Smooth` for the analytic case:

1. `MAnalyticAt I J f x` means `f` is locally analytic at `x`
2. `MAnalyticOn I J f s` means `f` is locally analytic at each `x âˆˆ s`
3. `MAnalytic I J f` means `f` is analytic everywhere in the manifold

Possibly these should be renamed to `MAnalytic...`, though that name sounds bad.

Other things we show:

1. `extChartAt` and `.symm` are analytic with invertible derivatives
2. Arithmetic on analytic functions into `â„‚` are analytic
3. MAnalytic functions are differentiable, smooth, etc.
4. A variety of other small things
-/

open ChartedSpace (chartAt)
open Function (uncurry)
open Set
open scoped Manifold Topology
noncomputable section

variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]

/-- Normed spaces are analytic manifolds over themselves -/
instance AnalyticManifold.self_of_nhds {E : Type} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    [CompleteSpace E] : AnalyticManifold (modelWithCornersSelf ğ•œ E) E :=
  AnalyticManifold.mk

/-- Version of `ModelWithCorners.prod_apply` with `x âˆˆ H Ã— H'` rather than `ModelProd H H'`.  This
comes up because other simplification doesn't stay in `ModelProd`. -/
@[simp]
lemma ModelWithCorners.prod_apply' {E H E' H' : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H) [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
    [TopologicalSpace H'] (I' : ModelWithCorners ğ•œ E' H') (x : H Ã— H') :
    (I.prod I') x = (I x.1, I' x.2) :=
  ModelWithCorners.prod_apply _ _ _

/-- Charts are analytic w.r.t. themselves.
    This lemma helps when proving particular spaces are analytic manifolds. -/
theorem extChartAt_self_analytic {E : Type} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {M : Type} [TopologicalSpace M] (f : PartialHomeomorph M E) :
    AnalyticOn ğ•œ (ğ“˜(ğ•œ, E) âˆ˜ (f.symm.trans f) âˆ˜ â‡‘ğ“˜(ğ•œ, E).symm)
      (ğ“˜(ğ•œ, E) '' (f.symm.trans f).toPartialEquiv.source) := by
  apply AnalyticOn.congr (f := fun z â†¦ z)
  Â· simp only [modelWithCornersSelf_coe, id_eq, image_id', PartialHomeomorph.trans_toPartialEquiv,
      PartialHomeomorph.symm_toPartialEquiv, PartialEquiv.trans_source, PartialEquiv.symm_source,
      PartialHomeomorph.coe_coe_symm]
    exact f.isOpen_inter_preimage_symm f.open_source
  Â· exact analyticOn_id _
  Â· intro x m
    simp only [modelWithCornersSelf_coe, id, image_id', PartialHomeomorph.trans_toPartialEquiv,
      PartialHomeomorph.symm_toPartialEquiv, PartialEquiv.trans_source, PartialEquiv.symm_source,
      PartialHomeomorph.coe_coe_symm, mem_inter_iff, mem_preimage, Function.comp,
      modelWithCornersSelf_coe_symm, PartialHomeomorph.coe_trans] at m âŠ¢
    rw [f.right_inv m.1]

variable {E A : Type} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F B : Type} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {G C : Type} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
variable {H D : Type} [NormedAddCommGroup H] [NormedSpace ğ•œ H]
variable [TopologicalSpace A] [TopologicalSpace B] [TopologicalSpace C] [TopologicalSpace D]
variable {M : Type} {I : ModelWithCorners ğ•œ E A} [TopologicalSpace M]
variable {N : Type} {J : ModelWithCorners ğ•œ F B} [TopologicalSpace N]
variable {O : Type} {K : ModelWithCorners ğ•œ G C} [TopologicalSpace O]
variable {P : Type} {L : ModelWithCorners ğ•œ H D} [TopologicalSpace P]
variable [ChartedSpace A M] [ChartedSpace B N] [ChartedSpace C O] [ChartedSpace D P]

/-- MAnalytic at a point -/
def MAnalyticAt (I : ModelWithCorners ğ•œ E A) (J : ModelWithCorners ğ•œ F B)
    {M N : Type} [TopologicalSpace M] [ChartedSpace A M] [TopologicalSpace N] [ChartedSpace B N]
    (f : M â†’ N) (x : M) :=
  ChartedSpace.LiftPropAt (fun f _ x â†¦ AnalyticAt ğ•œ (J âˆ˜ f âˆ˜ I.symm) (I x)) f x

/-- MAnalytic on a set -/
def MAnalyticOn (I : ModelWithCorners ğ•œ E A) (J : ModelWithCorners ğ•œ F B)
    {M N : Type} [TopologicalSpace M] [ChartedSpace A M] [TopologicalSpace N] [ChartedSpace B N]
    (f : M â†’ N) (s : Set M) :=
  âˆ€ x, x âˆˆ s â†’ MAnalyticAt I J f x

/-- MAnalytic everywhere -/
def MAnalytic (I : ModelWithCorners ğ•œ E A) (J : ModelWithCorners ğ•œ F B)
    {M N : Type} [TopologicalSpace M] [ChartedSpace A M] [TopologicalSpace N] [ChartedSpace B N]
    (f : M â†’ N) :=
  âˆ€ x, MAnalyticAt I J f x

/-- `MAnalyticOn` is monotonic in the set -/
theorem MAnalyticOn.mono {f : M â†’ N} {s t : Set M} (fa : MAnalyticOn I J f s) (ts : t âŠ† s) :
    MAnalyticOn I J f t := fun _ m â†¦ fa _ (ts m)

/-- Functions are `MAnalyticAt` iff they are continuous and analytic in charts -/
theorem mAnalyticAt_iff {f : M â†’ N} {x : M} :
    MAnalyticAt I J f x â†” ContinuousAt f x âˆ§
      AnalyticAt ğ•œ (extChartAt J (f x) âˆ˜ f âˆ˜ (extChartAt I x).symm) (extChartAt I x x) := by
  simp only [MAnalyticAt, ChartedSpace.liftPropAt_iff, extChartAt, PartialHomeomorph.extend_coe,
    PartialHomeomorph.extend_coe_symm, Function.comp]

/-- Functions are `MAnalytic` iff they are continuous and analytic in charts everywhere -/
theorem mAnalytic_iff {f : M â†’ N} :
    MAnalytic I J f â†” Continuous f âˆ§
      âˆ€ x : M, AnalyticAt ğ•œ (extChartAt J (f x) âˆ˜ f âˆ˜ (extChartAt I x).symm)
        (extChartAt I x x) := by
  simp only [MAnalytic, mAnalyticAt_iff, continuous_iff_continuousAt]; exact forall_and

/-- MAnalytic functions are continuous -/
theorem MAnalyticAt.continuousAt {f : M â†’ N} {x : M} (h : MAnalyticAt I J f x) :
    ContinuousAt f x := (mAnalyticAt_iff.mp h).1

/-- MAnalytic functions are continuous -/
theorem MAnalytic.continuous {f : M â†’ N} (h : MAnalytic I J f) : Continuous f :=
  (mAnalytic_iff.mp h).1

/-- MAnalytic functions are continuous (explicit `I`, `J` version) -/
theorem MAnalyticAt.continuousAt' (I : ModelWithCorners ğ•œ E A) (J : ModelWithCorners ğ•œ F B)
    {M N : Type} [TopologicalSpace M] [ChartedSpace A M] [TopologicalSpace N] [ChartedSpace B N]
    [I.Boundaryless] [J.Boundaryless]
    {f : M â†’ N} {x : M} (h : MAnalyticAt I J f x) :
    ContinuousAt f x := h.continuousAt

/-- `MAnalyticOn` functions are `ContinuousOn` -/
theorem MAnalyticOn.continuousOn {f : M â†’ N} {s : Set M} (h : MAnalyticOn I J f s) :
    ContinuousOn f s := fun x m â†¦ (h x m).continuousAt.continuousWithinAt

/-- MAnalytic functions compose -/
theorem MAnalyticAt.comp {f : N â†’ O} {g : M â†’ N} {x : M} (fh : MAnalyticAt J K f (g x))
    (gh : MAnalyticAt I J g x) : MAnalyticAt I K (fun x â†¦ f (g x)) x := by
  rw [mAnalyticAt_iff] at fh gh âŠ¢; use fh.1.comp gh.1
  have e : extChartAt J (g x) (g x) =
      (extChartAt J (g x) âˆ˜ g âˆ˜ (extChartAt I x).symm) (extChartAt I x x) := by
    simp only [Function.comp_apply, PartialEquiv.left_inv _ (mem_extChartAt_source I x)]
  rw [e] at fh; apply (fh.2.comp gh.2).congr; clear e fh
  simp only [Function.comp]
  have m : âˆ€á¶  y in ğ“ (extChartAt I x x), g ((extChartAt I x).symm y) âˆˆ
      (extChartAt J (g x)).source := by
    apply ContinuousAt.eventually_mem
    Â· apply ContinuousAt.comp
      Â· rw [PartialEquiv.left_inv _ (mem_extChartAt_source _ _)]; exact gh.1
      Â· exact continuousAt_extChartAt_symm I x
    Â· rw [PartialEquiv.left_inv _ (mem_extChartAt_source _ _)]
      exact extChartAt_source_mem_nhds _ _
  refine m.mp (.of_forall fun y m â†¦ ?_)
  simp_rw [PartialEquiv.left_inv _ m]

/-- MAnalytic functions compose -/
theorem MAnalytic.comp {f : N â†’ O} {g : M â†’ N} (fh : MAnalytic J K f) (gh : MAnalytic I J g) :
    MAnalytic I K fun x â†¦ f (g x) := fun _ â†¦ (fh _).comp (gh _)

/-- MAnalytic functions compose at a point, with a separate argument for point equality -/
theorem MAnalyticAt.comp_of_eq {f : N â†’ O} {g : M â†’ N} {x : M} {y : N}
    (fh : MAnalyticAt J K f y) (gh : MAnalyticAt I J g x) (e : g x = y) :
    MAnalyticAt I K (fun x â†¦ f (g x)) x := by
  rw [â† e] at fh; exact fh.comp gh

/-- `MAnalyticAt` for `x â†¦ (f x, g x)` -/
theorem MAnalyticAt.prod {f : M â†’ N} {g : M â†’ O} {x : M} (fh : MAnalyticAt I J f x)
    (gh : MAnalyticAt I K g x) : MAnalyticAt I (J.prod K) (fun x â†¦ (f x, g x)) x := by
  rw [mAnalyticAt_iff] at fh gh âŠ¢; use fh.1.prod gh.1
  refine (fh.2.prod gh.2).congr (.of_forall fun y â†¦ ?_)
  simp only [extChartAt_prod, Function.comp, PartialEquiv.prod_coe]

/-- `MAnalyticAt.comp` for a curried function -/
theorem MAnalyticAt.compâ‚‚ {h : N â†’ O â†’ P} {f : M â†’ N} {g : M â†’ O} {x : M}
    (ha : MAnalyticAt (J.prod K) L (uncurry h) (f x, g x)) (fa : MAnalyticAt I J f x)
    (ga : MAnalyticAt I K g x) : MAnalyticAt I L (fun x â†¦ h (f x) (g x)) x :=
  ha.comp (g := fun _ â†¦ (_, _)) (fa.prod ga)

/-- `MAnalytic` for `x â†¦ (f x, g x)` -/
theorem MAnalytic.prod {f : M â†’ N} {g : M â†’ O} (fh : MAnalytic I J f) (gh : MAnalytic I K g) :
    MAnalytic I (J.prod K) fun x â†¦ (f x, g x) := fun _ â†¦ (fh _).prod (gh _)

/-- `MAnalyticAt.compâ‚‚`, with a separate argument for point equality -/
theorem MAnalyticAt.compâ‚‚_of_eq {h : N â†’ O â†’ P} {f : M â†’ N} {g : M â†’ O} {x : M} {y : N Ã— O}
    (ha : MAnalyticAt (J.prod K) L (uncurry h) y) (fa : MAnalyticAt I J f x)
    (ga : MAnalyticAt I K g x) (e : (f x, g x) = y) :
    MAnalyticAt I L (fun x â†¦ h (f x) (g x)) x := by rw [â† e] at ha; exact ha.compâ‚‚ fa ga

/-- Constants are analytic -/
theorem mAnalyticAt_const {x : M} {c : N} : MAnalyticAt I J (fun _ â†¦ c) x := by
  rw [mAnalyticAt_iff]; use continuousAt_const
  simp only [Prod.fst_comp_mk, Function.comp]; exact analyticAt_const

/-- Constants are analytic -/
theorem mAnalytic_const {c : N} : MAnalytic I J fun _ : M â†¦ c := fun _ â†¦ mAnalyticAt_const

/-- `fst` is analytic -/
theorem mAnalyticAt_fst [I.Boundaryless] [J.Boundaryless] {x : M Ã— N} :
    MAnalyticAt (I.prod J) I (fun p : M Ã— N â†¦ p.fst) x := by
  rw [mAnalyticAt_iff]; use continuousAt_fst; refine (analyticAt_fst _).congr ?_
  filter_upwards [((isOpen_extChartAt_target _ x).eventually_mem (mem_extChartAt_target _ _))]
  intro y m
  rw [extChartAt_prod] at m
  simp only [PartialHomeomorph.prod_toPartialEquiv, PartialEquiv.prod_target, mem_prod] at m
  simp only [extChartAt_prod, Function.comp, PartialEquiv.prod_coe_symm]
  exact ((extChartAt I x.1).right_inv m.1).symm

/-- `snd` is analytic -/
theorem mAnalyticAt_snd [I.Boundaryless] [J.Boundaryless] {x : M Ã— N} :
    MAnalyticAt (I.prod J) J (fun p : M Ã— N â†¦ p.snd) x := by
  rw [mAnalyticAt_iff]; use continuousAt_snd; refine (analyticAt_snd _).congr ?_
  filter_upwards [((isOpen_extChartAt_target _ x).eventually_mem (mem_extChartAt_target _ _))]
  intro y m
  rw [extChartAt_prod] at m
  simp only [PartialHomeomorph.prod_toPartialEquiv, PartialEquiv.prod_target, mem_prod] at m
  simp only [extChartAt_prod, Function.comp, PartialEquiv.prod_coe_symm]
  exact ((extChartAt J x.2).right_inv m.2).symm

/-- `fst` is analytic -/
theorem mAnalytic_fst [I.Boundaryless] [J.Boundaryless] :
    MAnalytic (I.prod J) I fun p : M Ã— N â†¦ p.fst := fun _ â†¦ mAnalyticAt_fst

/-- `snd` is analytic -/
theorem mAnalytic_snd [I.Boundaryless] [J.Boundaryless] :
    MAnalytic (I.prod J) J fun p : M Ã— N â†¦ p.snd := fun _ â†¦ mAnalyticAt_snd

/-- `I.toPartialEquiv = I` in terms of `coe` -/
lemma ModelWithCorners.coe_coe (I : ModelWithCorners ğ•œ E A) :
    â‡‘I.toPartialEquiv = (I : A â†’ E) := rfl

/-- `I.toPartialEquiv.symm = I.symm` in terms of `coe` -/
theorem ModelWithCorners.coe_coe_symm (I : ModelWithCorners ğ•œ E A) :
    â‡‘I.toPartialEquiv.symm = (I.symm : E â†’ A) := rfl

/-- `extChartAt` is analytic -/
theorem MAnalyticAt.extChartAt [CompleteSpace E] [I.Boundaryless] [cm : AnalyticManifold I M]
    {x y : M} (ys : y âˆˆ (extChartAt I x).source) :
    MAnalyticAt I (modelWithCornersSelf ğ•œ E) (extChartAt I x) y := by
  rw [mAnalyticAt_iff]; use continuousAt_extChartAt' I ys
  simp only [Function.comp, extChartAt, PartialHomeomorph.extend, PartialEquiv.coe_trans,
    PartialHomeomorph.toFun_eq_coe, ModelWithCorners.toPartialEquiv_coe,
    PartialHomeomorph.refl_partialEquiv, PartialEquiv.refl_source,
    PartialHomeomorph.singletonChartedSpace_chartAt_eq, modelWithCornersSelf_partialEquiv,
    PartialEquiv.trans_refl, PartialEquiv.trans_symm_eq_symm_trans_symm,
    ModelWithCorners.toPartialEquiv_coe_symm, PartialHomeomorph.coe_coe_symm,
    PartialEquiv.refl_coe, id, _root_.extChartAt]
  have a : (chartAt A x).symm â‰«â‚• chartAt A y âˆˆ analyticGroupoid I := by
    apply StructureGroupoid.compatible_of_mem_maximalAtlas
    exact (@StructureGroupoid.chart_mem_maximalAtlas _ _ _ _ _ (analyticGroupoid I)
      cm.toHasGroupoid x)
    exact (@StructureGroupoid.chart_mem_maximalAtlas _ _ _ _ _ (analyticGroupoid I)
      cm.toHasGroupoid y)
  simp only [mem_analyticGroupoid_of_boundaryless, PartialHomeomorph.trans_symm_eq_symm_trans_symm,
    Function.comp, PartialHomeomorph.trans_apply] at a
  apply a.2; clear a; use chartAt A y y; aesop

/-- `extChartAt.symm` is analytic -/
theorem MAnalyticAt.extChartAt_symm [CompleteSpace E] [I.Boundaryless] [cm : AnalyticManifold I M]
    {x : M} {y : E} (ys : y âˆˆ (_root_.extChartAt I x).target) :
    MAnalyticAt (modelWithCornersSelf ğ•œ E) I (_root_.extChartAt I x).symm y := by
  rw [mAnalyticAt_iff]; use continuousAt_extChartAt_symm'' I ys
  simp only [extChartAt_eq_refl, PartialEquiv.refl_coe, Function.comp, id, extChartAt,
    PartialHomeomorph.extend, PartialEquiv.coe_trans, PartialEquiv.coe_trans_symm,
    PartialHomeomorph.coe_coe, PartialHomeomorph.coe_coe_symm, ModelWithCorners.coe_coe,
    ModelWithCorners.coe_coe_symm, modelWithCornersSelf_coe, chartAt_self_eq,
    PartialHomeomorph.refl_apply, PartialHomeomorph.refl_symm, modelWithCornersSelf_coe_symm]
  set y' := (chartAt A x).symm (I.symm y)
  have a : (chartAt A x).symm â‰«â‚• chartAt A ((chartAt A x).symm (I.symm y)) âˆˆ
      analyticGroupoid I := by
    apply StructureGroupoid.compatible_of_mem_maximalAtlas
    exact @StructureGroupoid.chart_mem_maximalAtlas _ _ _ _ _ (analyticGroupoid I)
      cm.toHasGroupoid x
    exact @StructureGroupoid.chart_mem_maximalAtlas _ _ _ _ _ (analyticGroupoid I)
      cm.toHasGroupoid y'
  simp only [mem_analyticGroupoid_of_boundaryless, PartialHomeomorph.trans_symm_eq_symm_trans_symm,
    Function.comp] at a
  apply a.1; clear a; use I.symm y; aesop

/-- MAnalytic functions are smooth -/
theorem MAnalyticAt.smoothAt [CompleteSpace F] {f : M â†’ N} {x : M} (fa : MAnalyticAt I J f x) :
    SmoothAt I J f x := by
  rw [mAnalyticAt_iff] at fa; simp only [SmoothAt, contMDiffAt_iff]
  use fa.1; use fa.2.contDiffAt.contDiffWithinAt

/-- MAnalytic functions are smooth -/
theorem MAnalyticOn.smoothOn [CompleteSpace F] {f : M â†’ N} {s : Set M}
    (fa : MAnalyticOn I J f s) : SmoothOn I J f s :=
  fun x m â†¦ (fa x m).smoothAt.smoothWithinAt

/-- MAnalytic functions are differentiable -/
theorem MAnalyticAt.mdifferentiableAt [CompleteSpace F] {f : M â†’ N} {x : M}
    (fa : MAnalyticAt I J f x) : MDifferentiableAt I J f x :=
  fa.smoothAt.mdifferentiableAt

/-- `MAnalyticAt` depends only on local values -/
theorem MAnalyticAt.congr {f g : M â†’ N} {x : M} (fa : MAnalyticAt I J f x) (e : f =á¶ [ğ“ x] g) :
    MAnalyticAt I J g x := by
  rw [mAnalyticAt_iff] at fa âŠ¢; use fa.1.congr e; apply fa.2.congr
  rw [e.self_of_nhds]; refine Filter.EventuallyEq.fun_comp ?_ (_root_.extChartAt J (g x))
  have t := (continuousAt_extChartAt_symm I x).tendsto
  rw [PartialEquiv.left_inv _ (mem_extChartAt_source I x)] at t
  exact e.comp_tendsto t

variable [CompleteSpace E] [CompleteSpace F]

section Iff

variable (I J)

/-- Analytic functions are analytic, and vice versa -/
theorem analyticAt_iff_mAnalyticAt [ChartedSpace A E] [AnalyticManifold I E] [ChartedSpace B F]
    [AnalyticManifold J F] [ExtChartEqRefl I] [ExtChartEqRefl J] {f : E â†’ F} {x : E} :
    AnalyticAt ğ•œ f x â†” MAnalyticAt I J f x := by
  simp only [mAnalyticAt_iff, extChartAt_eq_refl, PartialEquiv.refl_coe, PartialEquiv.refl_symm,
    Function.id_comp, Function.comp_id, id_eq, iff_and_self]
  exact AnalyticAt.continuousAt

end Iff

/-- Analytic functions are analytic -/
theorem AnalyticAt.mAnalyticAt {f : E â†’ F} {x : E} (fa : AnalyticAt ğ•œ f x)
    (I : ModelWithCorners ğ•œ E A) [I.Boundaryless] [ChartedSpace A E] [AnalyticManifold I E]
    [ExtChartEqRefl I]
    (J : ModelWithCorners ğ•œ F B) [J.Boundaryless] [ChartedSpace B F] [AnalyticManifold J F]
    [ExtChartEqRefl J] :
    MAnalyticAt I J f x :=
  (analyticAt_iff_mAnalyticAt _ _).mp fa

/-- MAnalytic functions are analytic -/
theorem MAnalyticAt.analyticAt
    (I : ModelWithCorners ğ•œ E A) [I.Boundaryless] [ChartedSpace A E] [AnalyticManifold I E]
    [ExtChartEqRefl I]
    (J : ModelWithCorners ğ•œ F B) [J.Boundaryless] [ChartedSpace B F] [AnalyticManifold J F]
    [ExtChartEqRefl J]
    {f : E â†’ F} {x : E} : MAnalyticAt I J f x â†’ AnalyticAt ğ•œ f x :=
  (analyticAt_iff_mAnalyticAt _ _).mpr

/-- `id` is analytic -/
theorem mAnalyticAt_id [I.Boundaryless] [AnalyticManifold I M] {x : M} :
    MAnalyticAt I I (fun x â†¦ x) x := by
  rw [mAnalyticAt_iff]; use continuousAt_id; apply (analyticAt_id _ _).congr
  filter_upwards [((isOpen_extChartAt_target I x).eventually_mem (mem_extChartAt_target I x))]
  intro y m
  simp only [Function.comp, PartialEquiv.right_inv _ m, id]

/-- `id` is analytic -/
theorem mAnalytic_id [I.Boundaryless] [AnalyticManifold I M] : MAnalytic I I fun x : M â†¦ x :=
  fun _ â†¦ mAnalyticAt_id

/-- Curried analytic functions are analytic in the first coordinate -/
theorem MAnalyticAt.along_fst [I.Boundaryless] [AnalyticManifold I M] {f : M â†’ O â†’ P} {x : M}
    {y : O} (fa : MAnalyticAt (I.prod K) L (uncurry f) (x, y)) :
    MAnalyticAt I L (fun x â†¦ f x y) x :=
  MAnalyticAt.compâ‚‚ fa mAnalyticAt_id mAnalyticAt_const

/-- Curried analytic functions are analytic in the second coordinate -/
theorem MAnalyticAt.along_snd [I.Boundaryless] [J.Boundaryless] [AnalyticManifold I M]
    [AnalyticManifold J N] {f : M â†’ N â†’ O} {x : M} {y : N}
    (fa : MAnalyticAt (I.prod J) K (uncurry f) (x, y)) : MAnalyticAt J K (fun y â†¦ f x y) y :=
  MAnalyticAt.compâ‚‚ fa mAnalyticAt_const mAnalyticAt_id

/-- Curried analytic functions are analytic in the first coordinate -/
theorem MAnalytic.along_fst [I.Boundaryless] [AnalyticManifold I M] {f : M â†’ O â†’ P}
    (fa : MAnalytic (I.prod K) L (uncurry f)) {y : O} :
    MAnalytic I L fun x â†¦ f x y := fun _ â†¦ (fa _).along_fst

/-- Curried analytic functions are analytic in the second coordinate -/
theorem MAnalytic.along_snd [I.Boundaryless] [J.Boundaryless] [AnalyticManifold I M]
    [AnalyticManifold J N] {f : M â†’ N â†’ O} {x : M}
    (fa : MAnalytic (I.prod J) K (uncurry f)) : MAnalytic J K fun y â†¦ f x y := fun _ â†¦
  (fa _).along_snd

/-- Addition is analytic -/
theorem MAnalyticAt.add {f g : O â†’ F} {x : O}
    (fa : MAnalyticAt K (modelWithCornersSelf ğ•œ F) f x)
    (ga : MAnalyticAt K (modelWithCornersSelf ğ•œ F) g x) :
    MAnalyticAt K (modelWithCornersSelf ğ•œ F) (fun x â†¦ f x + g x) x := by
  have e : (fun x â†¦ f x + g x) = (fun p : F Ã— F â†¦ p.1 + p.2) âˆ˜ fun x â†¦ (f x, g x) := rfl
  rw [e]; exact (((analyticAt_fst _).add (analyticAt_snd _)).mAnalyticAt _ _).comp (fa.prod ga)

/-- Subtraction is analytic -/
theorem MAnalyticAt.sub {f g : O â†’ F} {x : O}
    (fa : MAnalyticAt K (modelWithCornersSelf ğ•œ F) f x)
    (ga : MAnalyticAt K (modelWithCornersSelf ğ•œ F) g x) :
    MAnalyticAt K (modelWithCornersSelf ğ•œ F) (fun x â†¦ f x - g x) x :=
  (((analyticAt_fst _).sub (analyticAt_snd _)).mAnalyticAt _ _).comp (fa.prod ga)

/-- Multiplication is analytic -/
theorem MAnalyticAt.mul [CompleteSpace ğ•œ] {f g : O â†’ ğ•œ} {x : O}
    (fa : MAnalyticAt K (modelWithCornersSelf ğ•œ ğ•œ) f x)
    (ga : MAnalyticAt K (modelWithCornersSelf ğ•œ ğ•œ) g x) :
    MAnalyticAt K (modelWithCornersSelf ğ•œ ğ•œ) (fun x â†¦ f x * g x) x :=
  (((analyticAt_fst _).mul (analyticAt_snd _)).mAnalyticAt _ _).comp (fa.prod ga)

/-- Inverse is analytic away from zeros -/
theorem MAnalyticAt.inv [CompleteSpace ğ•œ] {f : O â†’ ğ•œ} {x : O}
    (fa : MAnalyticAt K (modelWithCornersSelf ğ•œ ğ•œ) f x) (f0 : f x â‰  0) :
    MAnalyticAt K (modelWithCornersSelf ğ•œ ğ•œ) (fun x â†¦ (f x)â»Â¹) x :=
  (((analyticAt_id _ _).inv f0).mAnalyticAt _ _).comp fa

/-- Division is analytic away from denominator zeros -/
theorem MAnalyticAt.div [CompleteSpace ğ•œ] {f g : O â†’ ğ•œ} {x : O}
    (fa : MAnalyticAt K (modelWithCornersSelf ğ•œ ğ•œ) f x)
    (ga : MAnalyticAt K (modelWithCornersSelf ğ•œ ğ•œ) g x) (g0 : g x â‰  0) :
    MAnalyticAt K (modelWithCornersSelf ğ•œ ğ•œ) (fun x â†¦ f x / g x) x := by
  simp only [div_eq_mul_inv]; exact fa.mul (ga.inv g0)

/-- Powers are analytic -/
theorem MAnalyticAt.pow [CompleteSpace ğ•œ] {f : O â†’ ğ•œ} {x : O}
    (fa : MAnalyticAt K (modelWithCornersSelf ğ•œ ğ•œ) f x) {n : â„•} :
    MAnalyticAt K (modelWithCornersSelf ğ•œ ğ•œ) (fun x â†¦ f x ^ n) x := by
  have e : (fun x â†¦ f x ^ n) = (fun z : ğ•œ â†¦ z ^ n) âˆ˜ f := rfl
  rw [e]; exact (((analyticAt_id _ _).pow _).mAnalyticAt _ _).comp fa

/-- Complex powers `f x ^ g x` are analytic if `f x` avoids the negative real axis  -/
theorem MAnalyticAt.cpow {E A M : Type} [NormedAddCommGroup E] [NormedSpace â„‚ E] [CompleteSpace E]
    [TopologicalSpace A] {I : ModelWithCorners â„‚ E A} [TopologicalSpace M] [ChartedSpace A M]
    [I.Boundaryless] [AnalyticManifold I M]
    {f g : M â†’ â„‚} {x : M}
    (fa : MAnalyticAt I (modelWithCornersSelf â„‚ â„‚) f x)
    (ga : MAnalyticAt I (modelWithCornersSelf â„‚ â„‚) g x) (a : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
    MAnalyticAt I (modelWithCornersSelf â„‚ â„‚) (fun x â†¦ f x ^ g x) x := by
  have e : (fun x â†¦ f x ^ g x) = (fun p : â„‚ Ã— â„‚ â†¦ p.1 ^ p.2) âˆ˜ fun x â†¦ (f x, g x) := rfl
  rw [e]
  refine (((analyticAt_fst _).cpow (analyticAt_snd _) ?_).mAnalyticAt _ _).comp (fa.prod ga)
  exact a

/-- Iterated analytic functions are analytic -/
theorem MAnalytic.iter [I.Boundaryless] [AnalyticManifold I M] {f : M â†’ M}
    (fa : MAnalytic I I f) (n : â„•) :
    MAnalytic I I f^[n] := by
  induction' n with n h; simp only [Function.iterate_zero]; exact mAnalytic_id
  simp only [Function.iterate_succ']; exact fa.comp h

/-- Chart derivatives are invertible (left inverse) -/
theorem extChartAt_mderiv_left_inverse [I.Boundaryless] [AnalyticManifold I M]
    {x y : M} (m : y âˆˆ (extChartAt I x).source) :
    (mfderiv (modelWithCornersSelf ğ•œ E) I (extChartAt I x).symm (extChartAt I x y)).comp
        (mfderiv I (modelWithCornersSelf ğ•œ E) (extChartAt I x) y) =
      ContinuousLinearMap.id ğ•œ (TangentSpace I y) := by
  have m' : extChartAt I x y âˆˆ (extChartAt I x).target := PartialEquiv.map_source _ m
  have c := mfderiv_comp y (MAnalyticAt.extChartAt_symm m').mdifferentiableAt
    (MAnalyticAt.extChartAt m).mdifferentiableAt
  refine _root_.trans c.symm ?_; clear c; rw [â†mfderiv_id]; apply Filter.EventuallyEq.mfderiv_eq
  rw [Filter.eventuallyEq_iff_exists_mem]; use(extChartAt I x).source
  use extChartAt_source_mem_nhds' I m
  intro z zm; simp only [Function.comp, id, PartialEquiv.left_inv _ zm]

/-- Chart derivatives are invertible (right inverse) -/
theorem extChartAt_mderiv_right_inverse [I.Boundaryless] [AnalyticManifold I M]
    {x : M} {y : E} (m : y âˆˆ (extChartAt I x).target) :
    (mfderiv I (modelWithCornersSelf ğ•œ E) (extChartAt I x) ((extChartAt I x).symm y)).comp
        (mfderiv (modelWithCornersSelf ğ•œ E) I (extChartAt I x).symm y) =
      ContinuousLinearMap.id ğ•œ (TangentSpace (modelWithCornersSelf ğ•œ E) y) := by
  have m' : (extChartAt I x).symm y âˆˆ (extChartAt I x).source := PartialEquiv.map_target _ m
  have c := mfderiv_comp y (MAnalyticAt.extChartAt m').mdifferentiableAt
    (MAnalyticAt.extChartAt_symm m).mdifferentiableAt
  refine _root_.trans c.symm ?_; clear c; rw [â† mfderiv_id]; apply Filter.EventuallyEq.mfderiv_eq
  rw [Filter.eventuallyEq_iff_exists_mem]; use(extChartAt I x).target
  have n := extChartAt_target_mem_nhdsWithin' I m'
  simp only [ModelWithCorners.range_eq_univ, nhdsWithin_univ,
    PartialEquiv.right_inv _ m] at n
  use n; intro z zm
  simp only [Function.comp, id, PartialEquiv.right_inv _ zm, Function.comp]

/-- Chart derivatives are invertible (right inverse) -/
theorem extChartAt_mderiv_right_inverse' [I.Boundaryless] [AnalyticManifold I M]
    {x y : M} (m : y âˆˆ (extChartAt I x).source) :
    (mfderiv I (modelWithCornersSelf ğ•œ E) (extChartAt I x) y).comp
        (mfderiv (modelWithCornersSelf ğ•œ E) I (extChartAt I x).symm (extChartAt I x y)) =
      ContinuousLinearMap.id ğ•œ (TangentSpace (modelWithCornersSelf ğ•œ E) (extChartAt I x y)) := by
  have h := extChartAt_mderiv_right_inverse (PartialEquiv.map_source _ m)
  rw [PartialEquiv.left_inv _ m] at h; exact h

/-- If we're analytic at a point, we're locally analytic -/
theorem MAnalyticAt.eventually [I.Boundaryless] [J.Boundaryless] [AnalyticManifold I M]
    [AnalyticManifold J N] {f : M â†’ N} {x : M} (fa : MAnalyticAt I J f x) :
    âˆ€á¶  y in ğ“ x, MAnalyticAt I J f y := by
  apply (fa.continuousAt.eventually_mem ((isOpen_extChartAt_source J (f x)).mem_nhds
    (mem_extChartAt_source J (f x)))).eventually_nhds.mp
  apply ((isOpen_extChartAt_source I x).eventually_mem (mem_extChartAt_source I x)).mp
  apply ((continuousAt_extChartAt I x).eventually
    ((isOpen_analyticAt _ _).eventually_mem (mAnalyticAt_iff.mp fa).2)).mp
  refine .of_forall fun y a m fm â†¦ ?_
  simp only at a m fm; rw [mem_setOf] at a
  have h := a.mAnalyticAt (modelWithCornersSelf ğ•œ E) (modelWithCornersSelf ğ•œ F); clear a
  have h' := (MAnalyticAt.extChartAt_symm (PartialEquiv.map_source _ fm.self_of_nhds)).comp_of_eq
      (h.comp (MAnalyticAt.extChartAt m)) ?_
  swap; simp only [Function.comp, PartialEquiv.left_inv _ m]
  apply h'.congr; clear h h'; simp only [Function.comp]
  apply ((isOpen_extChartAt_source I x).eventually_mem m).mp
  refine fm.mp (.of_forall fun z mf m â†¦ ?_)
  simp only [PartialEquiv.left_inv _ m, PartialEquiv.left_inv _ mf]

/-- The domain of analyticity is open -/
theorem isOpen_mAnalyticAt [I.Boundaryless] [J.Boundaryless] [AnalyticManifold I M]
    [AnalyticManifold J N] {f : M â†’ N} : IsOpen {x | MAnalyticAt I J f x} := by
  rw [isOpen_iff_eventually]; intro x fa; exact fa.eventually

/-- MAnalytic functions have continuous tangent maps.
    Obviously more is true and the tangent map is analytic, but I don't need that yet -/
theorem MAnalyticOn.continuousOn_tangentMap [I.Boundaryless] [J.Boundaryless] [AnalyticManifold I M]
    [AnalyticManifold J N] {f : M â†’ N} {s : Set M} (fa : MAnalyticOn I J f s) :
    ContinuousOn (tangentMap I J f) (Bundle.TotalSpace.proj â»Â¹' s) := by
  generalize ht : {x | MAnalyticAt I J f x} = t
  have o : IsOpen t := by rw [â† ht]; exact isOpen_mAnalyticAt
  have sub : s âŠ† t := by rw [â† ht]; exact fa
  replace fa : MAnalyticOn I J f t := by
    simp only [MAnalyticOn, mem_setOf_eq, imp_self, implies_true, â† ht]
  refine ContinuousOn.mono ?_ (preimage_mono sub)
  apply (fa.smoothOn.contMDiffOn.continuousOn_tangentMapWithin le_top o.uniqueMDiffOn).congr
  intro x m; simp only [mem_preimage] at m
  rw [tangentMapWithin_eq_tangentMap (o.uniqueMDiffOn _ m) (fa _ m).mdifferentiableAt]
