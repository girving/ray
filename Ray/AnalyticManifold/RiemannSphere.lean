import Mathlib.Analysis.Analytic.Basic
import Mathlib.Analysis.Complex.Basic
import Mathlib.Analysis.Complex.RemovableSingularity
import Mathlib.Data.Complex.Basic
import Mathlib.Topology.Compactification.OnePoint
import Ray.Analytic.Analytic
import Ray.AnalyticManifold.AnalyticManifold
import Ray.AnalyticManifold.OneDimension
import Ray.Misc.AtInf

/-!
## The Riemann sphere

We give `OnePoint â„‚` the natural analytic manifold structure with two charts,
namely `coe` and `inv âˆ˜ coe`, giving the Riemann sphere `ğ•Š`.
-/

open Classical
open Complex (abs)
open Filter (eventually_of_forall Tendsto atTop)
open Function (curry uncurry)
open Set
open scoped Topology OnePoint
noncomputable section

/-- A left inverse to `coe : â„‚ â†’ ğ•Š`.
    We put this outside the `RiemannSphere` namespace so that `z.toComplex` works. -/
def OnePoint.toComplex (z : OnePoint â„‚) : â„‚ := z.rec 0 id

namespace RiemannSphere

/-- The Riemann sphere, as a complex manifold -/
scoped notation "ğ•Š" => OnePoint â„‚

-- Basic instances for ğ•Š
instance : Zero ğ•Š := âŸ¨((0 : â„‚) : ğ•Š)âŸ©
instance : Inhabited ğ•Š := âŸ¨0âŸ©
@[simp] theorem coe_zero : ((0 : â„‚) : ğ•Š) = (0 : ğ•Š) := rfl
@[simp] theorem coe_eq_coe {z w : â„‚} : (z : ğ•Š) = w â†” z = w := OnePoint.coe_eq_coe
@[simp] theorem coe_eq_zero (z : â„‚) : (z : ğ•Š) = (0 : ğ•Š) â†” z = 0 := by
  simp only [â† coe_zero, coe_eq_coe]

/-- `coe : â„‚ â†’ ğ•Š` is injective -/
theorem injective_coe : Function.Injective (fun z : â„‚ â†¦ (z : ğ•Š)) := OnePoint.coe_injective

/-- `coe : â„‚ â†’ ğ•Š` is continuous -/
theorem continuous_coe : Continuous (fun z : â„‚ â†¦ (z : ğ•Š)) := OnePoint.continuous_coe

-- Recursion lemmas
@[simp] theorem rec_coe {C : ğ•Š â†’ Sort*} {i : C âˆ} {f : âˆ€ z : â„‚, C (z : ğ•Š)} (z : â„‚) :
    (z : ğ•Š).rec i f = f z := rfl
@[simp] theorem rec_inf {C : ğ•Š â†’ Sort*} {i : C âˆ} {f : âˆ€ z : â„‚, C (z : ğ•Š)} :
    (âˆ : ğ•Š).rec i f = i := rfl
theorem map_rec {A B : Sort*} (g : A â†’ B) {f : â„‚ â†’ A} {i : A} {z : ğ•Š} :
    g (z.rec i f) = (z.rec (g i) (g âˆ˜ f)) := by
  induction z using OnePoint.rec
  Â· simp only [rec_inf]
  Â· simp only [rec_coe, Function.comp]

-- âˆ is not 0 or finite
theorem inf_ne_coe {z : â„‚} : (âˆ : ğ•Š) â‰  â†‘z := by
  simp only [Ne.def, OnePoint.infty_ne_coe, not_false_iff]
theorem inf_ne_zero : (âˆ : ğ•Š) â‰  (0 : ğ•Š) := by
  have e : (0 : ğ•Š) = ((0 : â„‚) : ğ•Š) := rfl; rw [e]; exact inf_ne_coe
theorem coe_ne_inf {z : â„‚} : (z : ğ•Š) â‰  âˆ := inf_ne_coe.symm
theorem coe_eq_inf_iff {z : â„‚} : (z : ğ•Š) = âˆ â†” False := âŸ¨coe_ne_inf, False.elimâŸ©

-- Conversion to â„‚, sending âˆ to 0
@[simp] theorem toComplex_coe {z : â„‚} : (z : ğ•Š).toComplex = z := rfl
@[simp] theorem toComplex_inf : (âˆ : ğ•Š).toComplex = 0 := rfl
theorem coe_toComplex {z : ğ•Š} (h : z â‰  âˆ) : â†‘z.toComplex = z := by
  induction z using OnePoint.rec
  Â· simp only [ne_eq, not_true_eq_false] at h
  Â· simp only [toComplex_coe]
@[simp] theorem toComplex_zero : (0 : ğ•Š).toComplex = 0 := by rw [â† coe_zero, toComplex_coe]
theorem continuousAt_toComplex {z : â„‚} : ContinuousAt OnePoint.toComplex z := by
  simp only [OnePoint.continuousAt_coe, Function.comp, toComplex_coe]; exact continuousAt_id
theorem continuousOn_toComplex : ContinuousOn OnePoint.toComplex ({âˆ}á¶œ) := by
  intro z m; induction z using OnePoint.rec
  Â· simp only [mem_compl_iff, mem_singleton_iff, not_true] at m
  Â· exact continuousAt_toComplex.continuousWithinAt

/-- Inversion in `ğ•Š`, interchanging `0` and `âˆ` -/
def inv (z : ğ•Š) : ğ•Š := if z = 0 then âˆ else â†‘z.toComplexâ»Â¹
instance : Inv ğ•Š := âŸ¨RiemannSphere.invâŸ©
theorem inv_def (z : ğ•Š) : zâ»Â¹ = RiemannSphere.inv z := rfl
instance : InvolutiveInv ğ•Š where
  inv := Inv.inv
  inv_inv := by
    simp_rw [inv_def, inv]; apply OnePoint.rec
    Â· simp only [inf_ne_zero, toComplex_inf, inv_zero, coe_zero, ite_false, toComplex_zero,
        ite_true]
    Â· intro z; by_cases z0 : z = 0
      Â· simp only [z0, coe_zero, toComplex_zero, inv_zero, ite_true, inf_ne_zero, toComplex_inf,
          ite_false]
      Â· simp only [coe_eq_zero, z0, toComplex_coe, ite_false, inv_eq_zero, inv_inv]
@[simp] theorem inv_zero' : (0 : ğ•Š)â»Â¹ = âˆ := by simp only [inv_def, inv, eq_self_iff_true, if_true]
@[simp] theorem inv_inf : ((âˆ : ğ•Š)â»Â¹ : ğ•Š) = 0 := by simp [inv_def, inv, inf_ne_zero]

theorem inv_coe {z : â„‚} (z0 : z â‰  0) : (z : ğ•Š)â»Â¹ = â†‘(z : â„‚)â»Â¹ := by
  simp only [inv_def, inv, z0, WithTop.coe_eq_zero, toComplex_coe, if_false, coe_eq_zero]
@[simp] theorem inv_eq_inf {z : ğ•Š} : zâ»Â¹ = âˆ â†” z = 0 := by
  induction z using OnePoint.rec
  Â· simp only [inv_inf]; exact âŸ¨Eq.symm, Eq.symmâŸ©
  Â· simp only [inv_def, inv, not_not, imp_false, ite_eq_left_iff, OnePoint.coe_ne_infty]
@[simp] theorem inv_eq_zero {z : ğ•Š} : zâ»Â¹ = 0 â†” z = âˆ := by
  induction' z using OnePoint.rec with z
  Â· simp only [inv_inf, eq_self_iff_true]
  Â· simp only [inv_def, inv, toComplex_coe]
    by_cases z0 : (z : ğ•Š) = 0; simp only [if_pos, z0, inf_ne_zero, inf_ne_zero.symm]
    simp only [if_neg z0, coe_ne_inf, iff_false_iff]; rw [coe_eq_zero, _root_.inv_eq_zero]
    simpa only [coe_eq_zero] using z0
theorem toComplex_inv {z : ğ•Š} : zâ»Â¹.toComplex = z.toComplexâ»Â¹ := by
  induction' z using OnePoint.rec with z
  Â· simp only [inv_inf, toComplex_zero, toComplex_inf, inv_zero', inv_zero, eq_self_iff_true]
  Â· by_cases z0 : z = 0
    Â· simp only [z0, coe_zero, inv_zero', toComplex_inf, toComplex_zero, inv_zero]
    Â· simp only [z0, inv_coe, Ne.def, not_false_iff, toComplex_coe]

/-- `coe` tends to `âˆ` `atInf` -/
theorem coe_tendsto_inf : Tendsto (fun z : â„‚ â†¦ (z : ğ•Š)) atInf (ğ“ âˆ) := by
  rw [Filter.tendsto_iff_comap, OnePoint.comap_coe_nhds_infty, Filter.coclosedCompact_eq_cocompact]
  exact atInf_le_cocompact

/-- `coe` tends to `âˆ` `atInf`, but without touching `âˆ` -/
theorem coe_tendsto_inf' : Tendsto (fun z : â„‚ â†¦ (z : ğ•Š)) atInf (ğ“[{âˆ}á¶œ] âˆ) := by
  have e : {(âˆ : ğ•Š)}á¶œ = range (fun z : â„‚ â†¦ (z : ğ•Š)) := by
    ext z; induction' z using OnePoint.rec with z
    Â· simp only [mem_compl_iff, mem_singleton_iff, not_true, mem_range, OnePoint.coe_ne_infty,
        exists_false]
    Â· simp only [mem_compl_iff, mem_singleton_iff, OnePoint.coe_ne_infty, not_false_eq_true,
        mem_range, coe_eq_coe, exists_eq]
  simp only [e, tendsto_nhdsWithin_range, coe_tendsto_inf]

/-- Inversion is continuous -/
theorem continuous_inv : Continuous fun z : ğ•Š â†¦ zâ»Â¹ := by
  rw [continuous_iff_continuousOn_univ]; intro z _; apply ContinuousAt.continuousWithinAt
  induction' z using OnePoint.rec with z
  Â· simp only [OnePoint.continuousAt_infty', Function.comp, Filter.coclosedCompact_eq_cocompact,
      inv_inf, â† atInf_eq_cocompact]
    have e : âˆ€á¶  z : â„‚ in atInf, â†‘zâ»Â¹ = (â†‘z : ğ•Š)â»Â¹ := by
      refine' (eventually_atInf 0).mp (eventually_of_forall fun z z0 â†¦ _)
      simp only [gt_iff_lt, Complex.norm_eq_abs, AbsoluteValue.pos_iff] at z0; rw [inv_coe z0]
    apply Filter.Tendsto.congr' e
    exact Filter.Tendsto.comp continuous_coe.continuousAt inv_tendsto_atInf'
  Â· simp only [OnePoint.continuousAt_coe, Function.comp, inv_def, inv, WithTop.coe_eq_zero,
      toComplex_coe]
    by_cases z0 : z = 0
    Â· simp only [z0, ContinuousAt, OnePoint.nhds_infty_eq, eq_self_iff_true, if_true,
        Filter.coclosedCompact_eq_cocompact]
      simp only [â† nhdsWithin_compl_singleton_sup_pure, Filter.tendsto_sup]
      constructor
      Â· refine' Filter.Tendsto.mono_right _ le_sup_left
        apply tendsto_nhdsWithin_congr (f := fun z : â„‚ â†¦ (â†‘zâ»Â¹ : ğ•Š))
        Â· intro z m; rw [mem_compl_singleton_iff] at m; simp only [coe_eq_zero, m, ite_false]
        Â· simp only [coe_zero, ite_true]; apply coe_tendsto_inf'.comp
          rw [â† @tendsto_atInf_iff_tendsto_nhds_zero â„‚ â„‚ _ _ fun z : â„‚ â†¦ z]
          exact Filter.tendsto_id
      Â· refine' Filter.Tendsto.mono_right _ le_sup_right
        simp only [Filter.pure_zero, Filter.tendsto_pure, ite_eq_left_iff, Filter.eventually_zero,
          eq_self_iff_true, not_true, IsEmpty.forall_iff]
    Â· have e : âˆ€á¶  w : â„‚ in ğ“ z, (if w = 0 then âˆ else â†‘wâ»Â¹ : ğ•Š) = â†‘wâ»Â¹ := by
        refine' (continuousAt_id.eventually_ne z0).mp (eventually_of_forall fun w w0 â†¦ _)
        simp only [Ne.def, id.def] at w0; simp only [w0, if_false]
      simp only [coe_eq_zero, continuousAt_congr e]
      exact continuous_coe.continuousAt.comp (tendsto_invâ‚€ z0)
instance : ContinuousInv ğ•Š := âŸ¨continuous_invâŸ©

/-- Inversion as an equivalence -/
def invEquiv : ğ•Š â‰ƒ ğ•Š where
  toFun := Inv.inv
  invFun := Inv.inv
  left_inv := inv_inv
  right_inv := inv_inv

/-- Inversion as a homeomorphism -/
def invHomeomorph : ğ•Š â‰ƒâ‚œ ğ•Š where
  toEquiv := invEquiv
  continuous_toFun := continuous_inv
  continuous_invFun := continuous_inv
@[simp] theorem invEquiv_apply (z : ğ•Š) : invEquiv z = zâ»Â¹ := by
  simp only [invEquiv, Equiv.coe_fn_mk]
@[simp] theorem invEquiv_symm : invEquiv.symm = invEquiv := by
  simp only [Equiv.ext_iff, invEquiv, Equiv.coe_fn_symm_mk, Equiv.coe_fn_mk, eq_self_iff_true,
    forall_const]
@[simp] theorem invHomeomorph_apply (z : ğ•Š) : invHomeomorph z = zâ»Â¹ := by
  simp only [invHomeomorph, Homeomorph.homeomorph_mk_coe, invEquiv_apply]
@[simp] theorem invHomeomorph_symm : invHomeomorph.symm = invHomeomorph := Homeomorph.ext (by
  simp only [invHomeomorph, Homeomorph.homeomorph_mk_coe_symm, invEquiv_symm,
    Homeomorph.homeomorph_mk_coe, eq_self_iff_true, forall_const])

/-- `coe : â„‚ â†’ ğ•Š` as an equivalence -/
def coePartialEquiv : PartialEquiv â„‚ ğ•Š where
  toFun := fun x : â„‚ â†¦ x
  invFun := OnePoint.toComplex
  source := univ
  target := {âˆ}á¶œ
  map_source' z _ := by
    simp only [mem_compl_iff, mem_singleton_iff, OnePoint.coe_ne_infty, not_false_iff]
  map_target' z _ := mem_univ _
  left_inv' z _ := toComplex_coe
  right_inv' z m := coe_toComplex m

/-- `coe : â„‚ â†’ ğ•Š` as a partial homeomorphism.  This is the first chart of `ğ•Š`. -/
def coePartialHomeomorph : PartialHomeomorph â„‚ ğ•Š where
  toPartialEquiv := coePartialEquiv
  open_source := isOpen_univ
  open_target := isOpen_compl_singleton
  continuousOn_toFun := continuous_coe.continuousOn
  continuousOn_invFun := continuousOn_toComplex

/-- `inv âˆ˜ coe : â„‚ â†’ ğ•Š` as a partial homeomorphism.  This is the second chart of `ğ•Š`. -/
def invCoePartialHomeomorph : PartialHomeomorph â„‚ ğ•Š :=
  coePartialHomeomorph.trans invHomeomorph.toPartialHomeomorph

@[simp] lemma coePartialEquiv_target : coePartialEquiv.target = {âˆ}á¶œ := rfl
@[simp] lemma coePartialHomeomorph_target : coePartialHomeomorph.target = {âˆ}á¶œ := by
  simp only [coePartialHomeomorph, coePartialEquiv_target]
@[simp] lemma invCoePartialHomeomorph_target : invCoePartialHomeomorph.target = {0}á¶œ := by
  ext z; simp only [invCoePartialHomeomorph, PartialHomeomorph.trans_toPartialEquiv,
    PartialEquiv.trans_target, Homeomorph.toPartialHomeomorph_target, PartialHomeomorph.coe_coe_symm,
    Homeomorph.toPartialHomeomorph_symm_apply, invHomeomorph_symm, coePartialHomeomorph_target,
    preimage_compl, univ_inter, mem_compl_iff, mem_preimage, invHomeomorph_apply, mem_singleton_iff,
    inv_eq_inf]
@[simp] lemma coePartialEquiv_apply (z : â„‚) : coePartialEquiv z = â†‘z := rfl
@[simp] lemma coePartialEquiv_symm_apply (z : ğ•Š) : coePartialEquiv.symm z = z.toComplex := rfl
@[simp] lemma invCoePartialHomeomorph_apply (z : â„‚) : invCoePartialHomeomorph z = (z : ğ•Š)â»Â¹ := rfl
@[simp] lemma invCoePartialHomeomorph_symm_apply (z : ğ•Š) :
    invCoePartialHomeomorph.symm z = (zâ»Â¹).toComplex := rfl

/-- Chart structure for `ğ•Š` -/
instance : ChartedSpace â„‚ ğ•Š where
  atlas := {e | e = coePartialHomeomorph.symm âˆ¨ e = invCoePartialHomeomorph.symm}
  chartAt z := z.rec invCoePartialHomeomorph.symm (fun _ â†¦ coePartialHomeomorph.symm)
  mem_chart_source := by
    intro z; induction z using OnePoint.rec
    Â· simp only [rec_inf, PartialHomeomorph.symm_toPartialEquiv, PartialEquiv.symm_source,
        invCoePartialHomeomorph_target, mem_compl_iff, mem_singleton_iff, inf_ne_zero,
        not_false_eq_true]
    Â· simp only [rec_coe, PartialHomeomorph.symm_toPartialEquiv, PartialEquiv.symm_source,
        coePartialHomeomorph_target, mem_compl_iff, mem_singleton_iff, OnePoint.coe_ne_infty,
        not_false_eq_true]
  chart_mem_atlas := by
    intro z; induction z using OnePoint.rec
    Â· simp only [rec_inf, eq_self_iff_true, mem_setOf_eq, or_true_iff]
    Â· simp only [rec_coe, mem_setOf_eq, eq_self_iff_true, true_or_iff]

/-- There are just two charts on `ğ•Š` -/
theorem two_charts {e : PartialHomeomorph ğ•Š â„‚} (m : e âˆˆ atlas â„‚ ğ•Š) :
    e = coePartialHomeomorph.symm âˆ¨ e = invCoePartialHomeomorph.symm := m

-- Chart simplification lemmas
@[simp] theorem chartAt_coe {z : â„‚} : chartAt â„‚ (z : ğ•Š) = coePartialHomeomorph.symm := rfl
@[simp] theorem chartAt_inf : @chartAt â„‚ _ ğ•Š _ _ âˆ = invCoePartialHomeomorph.symm := rfl
theorem extChartAt_coe {z : â„‚} : extChartAt I (z : ğ•Š) = coePartialEquiv.symm := by
  simp only [coePartialHomeomorph, extChartAt, PartialHomeomorph.extend, chartAt_coe,
    PartialHomeomorph.symm_toPartialEquiv, modelWithCornersSelf_partialEquiv,
    PartialEquiv.trans_refl]
theorem extChartAt_zero : extChartAt I (0 : ğ•Š) = coePartialEquiv.symm := by
  simp only [â† coe_zero, extChartAt_coe]
theorem extChartAt_inf : extChartAt I (âˆ : ğ•Š) = invEquiv.toPartialEquiv.trans coePartialEquiv.symm := by
  apply PartialEquiv.ext
  Â· intro z
    simp only [extChartAt, invCoePartialHomeomorph, coePartialHomeomorph, invHomeomorph,
      PartialHomeomorph.extend, chartAt_inf, PartialHomeomorph.symm_toPartialEquiv,
      PartialHomeomorph.trans_toPartialEquiv, modelWithCornersSelf_partialEquiv,
      PartialEquiv.trans_refl, PartialEquiv.coe_trans_symm, PartialHomeomorph.coe_coe_symm,
      Homeomorph.toPartialHomeomorph_symm_apply, Homeomorph.homeomorph_mk_coe_symm, invEquiv_symm,
      PartialEquiv.coe_trans, Equiv.toPartialEquiv_apply]
  Â· intro z
    simp only [extChartAt, invCoePartialHomeomorph, coePartialHomeomorph, invHomeomorph,
      invEquiv, PartialHomeomorph.extend, chartAt_inf, PartialHomeomorph.symm_toPartialEquiv,
      PartialHomeomorph.trans_toPartialEquiv, modelWithCornersSelf_partialEquiv,
      PartialEquiv.trans_refl, PartialEquiv.symm_symm, PartialEquiv.coe_trans,
      PartialHomeomorph.coe_coe, Homeomorph.toPartialHomeomorph_apply, Homeomorph.homeomorph_mk_coe,
      Equiv.coe_fn_mk, PartialEquiv.coe_trans_symm, Equiv.toPartialEquiv_symm_apply,
      Equiv.coe_fn_symm_mk]
  Â· simp only [extChartAt, invCoePartialHomeomorph, coePartialHomeomorph, invHomeomorph,
      PartialHomeomorph.extend, chartAt_inf, PartialHomeomorph.symm_toPartialEquiv,
      PartialHomeomorph.trans_toPartialEquiv, modelWithCornersSelf_partialEquiv,
      PartialEquiv.trans_refl,
      PartialEquiv.symm_source, PartialEquiv.trans_target, Homeomorph.toPartialHomeomorph_target,
      PartialHomeomorph.coe_coe_symm, Homeomorph.toPartialHomeomorph_symm_apply,
      Homeomorph.homeomorph_mk_coe_symm, invEquiv_symm, PartialEquiv.trans_source,
      Equiv.toPartialEquiv_source, Equiv.toPartialEquiv_apply]
theorem extChartAt_inf_apply {x : ğ•Š} : extChartAt I âˆ x = xâ»Â¹.toComplex := by
  simp only [extChartAt_inf, PartialEquiv.trans_apply, coePartialEquiv_symm_apply,
    Equiv.toPartialEquiv_apply, invEquiv_apply]

/-- `ğ•Š`'s charts have analytic groupoid structure -/
instance : HasGroupoid ğ•Š (analyticGroupoid I) where
  compatible := by
    have e0 : ((fun z : â„‚ â†¦ (z : ğ•Š)) â»Â¹' {0})á¶œ = {(0 : â„‚)}á¶œ := by
      ext; simp only [mem_compl_iff, mem_preimage, mem_singleton_iff, coe_eq_zero]
    have e1 : ((fun z : â„‚ â†¦ (z : ğ•Š)â»Â¹) â»Â¹' {âˆ})á¶œ = {(0 : â„‚)}á¶œ := by
      ext; simp only [mem_compl_iff, mem_preimage, mem_singleton_iff, inv_eq_inf, coe_eq_zero]
    have a : AnalyticOn â„‚ (fun z : â„‚ â†¦ OnePoint.toComplex (z : ğ•Š)â»Â¹) {0}á¶œ := by
      apply AnalyticOn.congr (f := fun z â†¦ zâ»Â¹)
      Â· exact isOpen_compl_singleton
      Â· apply analyticOn_inv
      Â· intro z z0; simp only [mem_compl_iff, mem_singleton_iff] at z0
        simp only [inv_coe z0, toComplex_coe]
    intro f g fa ga; simp only [mem_analyticGroupoid_of_boundaryless]
    cases' two_charts fa with fh fh
    Â· cases' two_charts ga with gh gh
      Â· simp only [â†fh, gh]; constructor; repeat apply extChartAt_self_analytic
      Â· simp [fh, gh, invCoePartialHomeomorph, coePartialHomeomorph, coePartialEquiv, invHomeomorph,
          invEquiv, Function.comp, e0, e1, and_self, a]
    Â· cases' two_charts ga with gh gh
      Â· simp [fh, gh, invCoePartialHomeomorph, coePartialHomeomorph, coePartialEquiv, invHomeomorph,
          invEquiv, Function.comp, e0, e1, and_self, a]
      Â· simp only [â†fh, gh]; constructor; repeat apply extChartAt_self_analytic

/-- `ğ•Š` is an analytic manifold -/
instance : AnalyticManifold I ğ•Š where

/-- Composing with `coe` turns convergence `atInf` into convergence to `ğ“ âˆ` -/
theorem tendsto_inf_iff_tendsto_atInf {X : Type} {f : Filter X} {g : X â†’ â„‚} :
    Tendsto (fun x â†¦ (g x : ğ•Š)) f (ğ“ âˆ) â†” Tendsto (fun x â†¦ g x) f atInf := by
  constructor
  Â· intro t; simp only [Filter.tendsto_iff_comap] at t âŠ¢
    rw [â†Function.comp_def, â†Filter.comap_comap, OnePoint.comap_coe_nhds_infty,
      Filter.coclosedCompact_eq_cocompact, â†atInf_eq_cocompact] at t
    exact t
  Â· exact fun h â†¦ coe_tendsto_inf.comp h

variable {X : Type} [TopologicalSpace X]
variable {Y : Type} [TopologicalSpace Y]
variable {T : Type} [TopologicalSpace T] [ChartedSpace â„‚ T] [AnalyticManifold I T]

/-- `coe : â„‚ â†’ ğ•Š` is an open map -/
theorem isOpenMap_coe : IsOpenMap (fun z : â„‚ â†¦ (z : ğ•Š)) := by
  intro s o
  have e : (fun z : â„‚ â†¦ (z : ğ•Š)) '' s = {âˆ}á¶œ âˆ© OnePoint.toComplex â»Â¹' s := by
    apply Set.ext; intro z
    simp only [mem_image, mem_inter_iff, mem_compl_singleton_iff, mem_preimage]
    constructor
    intro âŸ¨x, m, eâŸ©; simp only [â† e, toComplex_coe, m, and_true_iff]; exact inf_ne_coe.symm
    intro âŸ¨n, mâŸ©; use z.toComplex, m, coe_toComplex n
  rw [e]; exact continuousOn_toComplex.isOpen_inter_preimage isOpen_compl_singleton o

theorem prod_nhds_eq {x : X} {z : â„‚} :
    ğ“ (x, (z : ğ•Š)) = Filter.map (fun p : X Ã— â„‚ â†¦ (p.1, â†‘p.2)) (ğ“ (x, z)) := by
  refine le_antisymm ?_ (continuousAt_fst.prod (continuous_coe.continuousAt.comp continuousAt_snd))
  apply IsOpenMap.nhds_le; exact IsOpenMap.id.prod isOpenMap_coe

theorem mem_inf_of_mem_atInf {s : Set â„‚} (f : s âˆˆ @atInf â„‚ _) :
    (fun z : â„‚ â†¦ (z : ğ•Š)) '' s âˆª {âˆ} âˆˆ ğ“ (âˆ : ğ•Š) := by
  simp only [OnePoint.nhds_infty_eq, Filter.mem_sup, Filter.coclosedCompact_eq_cocompact, â†
    atInf_eq_cocompact, Filter.mem_map]
  exact âŸ¨Filter.mem_of_superset f fun _ m â†¦ Or.inl (mem_image_of_mem _ m), Or.inr rflâŸ©

theorem prod_mem_inf_of_mem_atInf {s : Set (X Ã— â„‚)} {x : X} (f : s âˆˆ (ğ“ x).prod (@atInf â„‚ _)) :
    (fun p : X Ã— â„‚ â†¦ (p.1, (p.2 : ğ•Š))) '' s âˆª univ Ã—Ë¢ {âˆ} âˆˆ ğ“ (x, (âˆ : ğ•Š)) := by
  rcases Filter.mem_prod_iff.mp f with âŸ¨t, tx, u, ui, subâŸ©
  rw [nhds_prod_eq]
  refine Filter.mem_prod_iff.mpr âŸ¨t, tx, (fun z : â„‚ â†¦ (z : ğ•Š)) '' u âˆª {âˆ}, mem_inf_of_mem_atInf ui,
    ?_âŸ©
  intro âŸ¨y, zâŸ© âŸ¨yt, mâŸ©
  simp only [mem_prod_eq, mem_image, mem_union, mem_singleton_iff, mem_univ, true_and_iff,
    Prod.ext_iff] at yt m âŠ¢
  induction' z using OnePoint.rec with z
  Â· simp only [eq_self_iff_true, or_true_iff]
  Â· simp only [coe_eq_inf_iff, or_false_iff, coe_eq_coe] at m âŠ¢
    rcases m with âŸ¨w, wu, wzâŸ©; refine âŸ¨âŸ¨y, zâŸ©, sub (mk_mem_prod yt ?_), rfl, rflâŸ©; rw [â† wz]
    exact wu

/-- `coe : â„‚ â†’ ğ•Š` is holomorphic -/
theorem holomorphic_coe : Holomorphic I I (fun z : â„‚ â†¦ (z : ğ•Š)) := by
  rw [holomorphic_iff]; use continuous_coe; intro z
  simp only [extChartAt_coe, extChartAt_eq_refl, PartialEquiv.refl_symm, PartialEquiv.refl_coe,
    Function.comp_id, id.def, Function.comp, PartialEquiv.invFun_as_coe]
  rw [â† PartialEquiv.invFun_as_coe]; simp only [coePartialEquiv, toComplex_coe]; apply analyticAt_id

/-- `OnePoint.toComplex : ğ•Š â†’ â„‚` is holomorphic except at `âˆ` -/
theorem holomorphicAt_toComplex {z : â„‚} : HolomorphicAt I I (OnePoint.toComplex : ğ•Š â†’ â„‚) z := by
  rw [holomorphicAt_iff]; use continuousAt_toComplex
  simp only [toComplex_coe, Function.comp, extChartAt_coe, extChartAt_eq_refl, PartialEquiv.refl_coe,
    id, PartialEquiv.symm_symm, coePartialEquiv_apply, coePartialEquiv_symm_apply]
  apply analyticAt_id

/-- Inversion is holomorphic -/
theorem holomorphic_inv : Holomorphic I I fun z : ğ•Š â†¦ zâ»Â¹ := by
  rw [holomorphic_iff]; use continuous_inv; intro z; induction' z using OnePoint.rec with z
  Â· simp only [inv_inf, extChartAt_inf, â† coe_zero, extChartAt_coe, Function.comp,
      PartialEquiv.trans_apply, Equiv.toPartialEquiv_apply, invEquiv_apply, coePartialEquiv_symm_apply,
      toComplex_coe, PartialEquiv.coe_trans_symm, PartialEquiv.symm_symm, coePartialEquiv_apply,
      Equiv.toPartialEquiv_symm_apply, invEquiv_symm, inv_inv]
    apply analyticAt_id
  Â· simp only [extChartAt_coe, PartialEquiv.symm_symm, Function.comp, coePartialEquiv_apply,
      coePartialEquiv_symm_apply, toComplex_coe]
    by_cases z0 : z = 0
    Â· simp only [z0, coe_zero, extChartAt_inf, PartialEquiv.trans_apply, coePartialEquiv_symm_apply,
        invEquiv_apply, Equiv.toPartialEquiv_apply, inv_zero', inv_inv, toComplex_coe]
      apply analyticAt_id
    Â· simp only [inv_coe z0, extChartAt_coe, coePartialEquiv_symm_apply]
      refine' ((analyticAt_id _ _).inv z0).congr _
      refine (continuousAt_id.eventually_ne z0).mp (eventually_of_forall fun w w0 â†¦ ?_)
      rw [id] at w0; simp only [inv_coe w0, toComplex_coe, id]

/-- Given `f : â„‚ â†’ X`, fill in the value at `âˆ` to get `ğ•Š â†’ X` -/
def fill {X : Type} (f : â„‚ â†’ X) (y : X) : ğ•Š â†’ X := fun z â†¦ z.rec y f

/-- Lift `f : â„‚ â†’ â„‚` to `ğ•Š â†’ ğ•Š` by filling in a value at `âˆ` -/
def lift (f : â„‚ â†’ â„‚) (y : ğ•Š) : ğ•Š â†’ ğ•Š := fun z â†¦ z.rec y (fun z â†¦ f z)

/-- Lift `f : X â†’ â„‚ â†’ â„‚` to `X â†’ ğ•Š â†’ ğ•Š` by filling in a value at `âˆ` -/
def lift' (f : X â†’ â„‚ â†’ â„‚) (y : ğ•Š) : X â†’ ğ•Š â†’ ğ•Š := fun x z â†¦ z.rec y (fun z â†¦ f x z)

variable {f : â„‚ â†’ â„‚}
variable {g : X â†’ â„‚ â†’ â„‚}
variable {y : ğ•Š} {x : X} {z : â„‚}

-- Values of `fill` and `lift` at `coe` and `âˆ`
@[simp] lemma fill_coe {f : â„‚ â†’ X} {y : X} : fill f y z = f z := rfl
@[simp] lemma fill_inf {f : â„‚ â†’ X} {y : X} : fill f y âˆ = y := rfl
@[simp] lemma lift_coe : lift f y z = â†‘(f z) := rfl
@[simp] lemma lift_coe' : lift' g y x z = â†‘(g x z) := rfl
@[simp] lemma lift_inf : lift f y âˆ = y := rfl
@[simp] lemma lift_inf' : lift' g y x âˆ = y := rfl

/-- `lift` in terms of `fill` -/
theorem lift_eq_fill : lift f y = fill (fun z â†¦ (f z : ğ•Š)) y := rfl

/-- `fill` is continuous at finite values -/
theorem continuousAt_fill_coe {f : â„‚ â†’ X} {y : X} (fc : ContinuousAt f z) :
    ContinuousAt (fill f y) z := by
  simp only [OnePoint.continuousAt_coe, Function.comp, fill_coe, fc]

/-- `fill` is continuous at `âˆ` -/
theorem continuousAt_fill_inf {f : â„‚ â†’ X} {y : X} (fi : Tendsto f atInf (ğ“ y)) :
    ContinuousAt (fill f y) âˆ := by
  simp only [OnePoint.continuousAt_infty', lift_inf, Filter.coclosedCompact_eq_cocompact, â†
    atInf_eq_cocompact, Function.comp, fill_coe, fill_inf, fi]

/-- `fill` is continuous -/
theorem continuous_fill {f : â„‚ â†’ X} {y : X} (fc : Continuous f) (fi : Tendsto f atInf (ğ“ y)) :
    Continuous (fill f y) := by
  rw [continuous_iff_continuousAt]; intro z; induction z using OnePoint.rec
  Â· exact continuousAt_fill_inf fi
  Â· exact continuousAt_fill_coe fc.continuousAt

/-- `fill` is holomorphic at finite values -/
theorem holomorphicAt_fill_coe {f : â„‚ â†’ T} {y : T} (fa : HolomorphicAt I I f z) :
    HolomorphicAt I I (fill f y) z := by
  have e : (fun x : ğ•Š â†¦ f x.toComplex) =á¶ [ğ“ â†‘z] fill f y := by
    simp only [OnePoint.nhds_coe_eq, Filter.EventuallyEq, Filter.eventually_map, toComplex_coe,
      fill_coe, eq_self_iff_true, Filter.eventually_true]
  refine' HolomorphicAt.congr _ e
  refine' fa.comp_of_eq holomorphicAt_toComplex _
  simp only [toComplex_coe]

/-- `fill` is holomorphic at `âˆ` -/
theorem holomorphicAt_fill_inf {f : â„‚ â†’ T} {y : T} (fa : âˆ€á¶  z in atInf, HolomorphicAt I I f z)
    (fi : Tendsto f atInf (ğ“ y)) : HolomorphicAt I I (fill f y) âˆ := by
  rw [holomorphicAt_iff]; use continuousAt_fill_inf fi
  simp only [Function.comp, extChartAt, PartialHomeomorph.extend, fill, rec_inf,
    modelWithCornersSelf_partialEquiv, PartialEquiv.trans_refl, chartAt_inf,
    PartialHomeomorph.symm_toPartialEquiv, PartialEquiv.symm_symm, PartialHomeomorph.toFun_eq_coe,
    invCoePartialHomeomorph_apply, PartialHomeomorph.coe_coe_symm, invCoePartialHomeomorph_symm_apply,
    inv_inf, toComplex_zero]
  have e : (fun z : â„‚ â†¦ chartAt â„‚ y (OnePoint.rec y f (â†‘z)â»Â¹)) = fun z : â„‚ â†¦
      extChartAt I y (if z = 0 then y else f zâ»Â¹) := by
    funext z; by_cases z0 : z = 0
    Â· simp only [if_pos z0, z0, coe_zero, inv_zero', rec_inf, extChartAt, PartialHomeomorph.extend,
        modelWithCornersSelf_partialEquiv, PartialEquiv.trans_refl, PartialHomeomorph.toFun_eq_coe,
        if_true]
    Â· simp only [inv_coe z0, rec_coe, extChartAt, PartialHomeomorph.extend,
        modelWithCornersSelf_partialEquiv, PartialEquiv.trans_refl, z0, ite_false,
        PartialHomeomorph.toFun_eq_coe]
  rw [e]; clear e
  apply Complex.analyticAt_of_differentiable_on_punctured_nhds_of_continuousAt
  Â· apply (inv_tendsto_atInf.eventually fa).mp
    apply (inv_tendsto_atInf.eventually (fi.eventually
      ((isOpen_extChartAt_source I y).eventually_mem (mem_extChartAt_source I y)))).mp
    apply eventually_nhdsWithin_of_forall; intro z z0 m fa
    simp only [Set.mem_compl_iff, Set.mem_singleton_iff] at z0
    have e : (fun z â†¦ extChartAt I y (if z = 0 then y else f zâ»Â¹)) =á¶ [ğ“ z]
        fun z â†¦ extChartAt I y (f zâ»Â¹) := by
      refine (continuousAt_id.eventually_ne z0).mp (eventually_of_forall fun w w0 â†¦ ?_)
      simp only [Ne.def, id.def] at w0; simp only [w0, if_false]
    refine' DifferentiableAt.congr_of_eventuallyEq _ e
    apply AnalyticAt.differentiableAt; apply HolomorphicAt.analyticAt I I
    refine' (HolomorphicAt.extChartAt _).comp _; exact m
    exact fa.comp (holomorphicAt_id.inv z0)
  Â· refine' (continuousAt_extChartAt' I _).comp _
    Â· simp only [eq_self_iff_true, if_pos, mem_extChartAt_source]
    Â· simp only [â† continuousWithinAt_compl_self, ContinuousWithinAt]
      apply tendsto_nhdsWithin_congr (f := fun z â†¦ f zâ»Â¹)
      intro z z0; simp only [Set.mem_compl_iff, Set.mem_singleton_iff] at z0
      simp only [z0, if_false]
      exact Filter.Tendsto.comp fi inv_tendsto_atInf

/-- `fill` is holomorphic -/
theorem holomorphic_fill {f : â„‚ â†’ T} {y : T} (fa : Holomorphic I I f) (fi : Tendsto f atInf (ğ“ y)) :
    Holomorphic I I (fill f y) := by
  intro z; induction z using OnePoint.rec
  Â· exact holomorphicAt_fill_inf (eventually_of_forall fa) fi
  Â· exact holomorphicAt_fill_coe (fa _)

/-- `lift'` is continuous at finite values -/
theorem continuousAt_lift_coe' (gc : ContinuousAt (uncurry g) (x, z)) :
    ContinuousAt (uncurry (lift' g y)) (x, â†‘z) := by
  simp only [lift', ContinuousAt, uncurry, rec_coe, OnePoint.nhds_coe_eq, prod_nhds_eq,
    Filter.tendsto_map'_iff, Function.comp]
  exact Filter.Tendsto.comp Filter.tendsto_map gc

/-- `lift'` is continuous at `âˆ` -/
theorem continuousAt_lift_inf' (gi : Tendsto (uncurry g) ((ğ“ x).prod atInf) atInf) :
    ContinuousAt (uncurry (lift' g âˆ)) (x, âˆ) := by
  simp only [ContinuousAt, Filter.Tendsto, Filter.le_def, Filter.mem_map]; intro s m
  simp only [OnePoint.nhds_infty_eq, Filter.coclosedCompact_eq_cocompact, Filter.mem_sup,
    Filter.mem_map, Filter.mem_pure, â† atInf_eq_cocompact, lift', rec_inf, uncurry] at m
  simp only [true_imp_iff, mem_setOf, uncurry, Tendsto] at gi; specialize gi m.1
  simp only [Filter.mem_map, preimage_preimage] at gi
  have e : uncurry (lift' g âˆ) â»Â¹' s =
      (fun x : X Ã— â„‚ â†¦ (x.1, (x.2 : ğ•Š))) ''
        ((fun x : X Ã— â„‚ â†¦ (g x.1 x.2 : ğ•Š)) â»Â¹' s) âˆª univ Ã—Ë¢ {âˆ} := by
    apply Set.ext; intro âŸ¨x, zâŸ©; induction z using OnePoint.rec
    Â· simp only [mem_preimage, mem_image, mem_union, mem_prod_eq, mem_univ, true_and_iff,
        mem_singleton_iff, eq_self_iff_true, or_true_iff, iff_true_iff, uncurry, lift', rec_inf,
        m.2]
    Â· simp only [uncurry, lift', mem_preimage, rec_coe, prod_singleton, image_univ, mem_union,
        mem_image, Prod.ext_iff, coe_eq_coe, Prod.exists, exists_eq_right_right, exists_eq_right,
        mem_range, OnePoint.infty_ne_coe, and_false, exists_false, or_false]
  rw [e]; exact prod_mem_inf_of_mem_atInf gi

/-- `lift'` is continuous -/
theorem continuous_lift' (gc : Continuous (uncurry g))
    (gi : âˆ€ x, Tendsto (uncurry g) ((ğ“ x).prod atInf) atInf) :
    Continuous (uncurry (lift' g âˆ)) := by
  rw [continuous_iff_continuousOn_univ]; intro âŸ¨x, zâŸ© _; apply ContinuousAt.continuousWithinAt
  induction z using OnePoint.rec
  Â· exact continuousAt_lift_inf' (gi x)
  Â· exact continuousAt_lift_coe' gc.continuousAt

/-- `lift` is continuous at finite values -/
theorem continuousAt_lift_coe (fc : ContinuousAt f z) : ContinuousAt (lift f y) z :=
  haveI gc : ContinuousAt (uncurry fun _ : Unit â†¦ f) ((), z) := by
    refine ContinuousAt.comp fc ?_; exact continuousAt_snd
  (continuousAt_lift_coe' gc).comp (ContinuousAt.prod continuousAt_const continuousAt_id)

/-- `lift` is continuous at `âˆ` -/
theorem continuousAt_lift_inf (fi : Tendsto f atInf atInf) : ContinuousAt (lift f âˆ) âˆ :=
  haveI gi : Tendsto (uncurry fun _ : Unit â†¦ f) ((ğ“ ()).prod atInf) atInf :=
    fi.comp Filter.tendsto_snd
  (continuousAt_lift_inf' gi).comp (ContinuousAt.prod continuousAt_const continuousAt_id)

/-- `lift` is continuous -/
theorem continuous_lift (fc : Continuous f) (fi : Tendsto f atInf atInf) :
    Continuous (lift f âˆ) := by
  rw [continuous_iff_continuousAt]; intro z; induction z using OnePoint.rec
  Â· exact continuousAt_lift_inf fi
  Â· exact continuousAt_lift_coe fc.continuousAt

/-- `lift` is holomorphic at finite values -/
theorem holomorphicAt_lift_coe (fa : AnalyticAt â„‚ f z) : HolomorphicAt I I (lift f y) z := by
  rw [lift_eq_fill]; exact holomorphicAt_fill_coe ((holomorphic_coe _).comp (fa.holomorphicAt I I))

/-- `lift` is holomorphic at `âˆ` -/
theorem holomorphicAt_lift_inf (fa : âˆ€á¶  z in atInf, AnalyticAt â„‚ f z) (fi : Tendsto f atInf atInf) :
    HolomorphicAt I I (lift f âˆ) âˆ := by
  rw [lift_eq_fill]; apply holomorphicAt_fill_inf
  exact fa.mp (eventually_of_forall fun z fa â†¦ (holomorphic_coe _).comp (fa.holomorphicAt I I))
  exact coe_tendsto_inf.comp fi

/-- `lift` is holomorphic -/
theorem holomorphic_lift (fa : AnalyticOn â„‚ f univ) (fi : Tendsto f atInf atInf) :
    Holomorphic I I (lift f âˆ) := by
  intro z; induction z using OnePoint.rec
  Â· exact holomorphicAt_lift_inf (eventually_of_forall fun z â†¦ fa z (mem_univ _)) fi
  Â· exact holomorphicAt_lift_coe (fa _ (mem_univ _))

/-- `lift'` is holomorphic (the parameterized version) -/
theorem holomorphicLift' {f : â„‚ â†’ â„‚ â†’ â„‚} (fa : AnalyticOn â„‚ (uncurry f) univ)
    (fi : âˆ€ x, Tendsto (uncurry f) ((ğ“ x).prod atInf) atInf) :
    Holomorphic II I (uncurry (lift' f âˆ)) := by
  apply osgoodManifold (continuous_lift' fa.continuous fi)
  Â· intro x z; induction z using OnePoint.rec
    Â· simp only [uncurry, lift_inf']; exact holomorphicAt_const
    Â· exact (holomorphic_coe _).comp ((fa _ (mem_univ âŸ¨_,_âŸ©)).along_fst.holomorphicAt _ _)
  Â· intro x z; refine holomorphic_lift (fun _ _ â†¦ (fa _ (mem_univ âŸ¨_,_âŸ©)).along_snd) ?_ z
    exact (fi x).comp (tendsto_const_nhds.prod_mk Filter.tendsto_id)

/-- `ğ•Š` is path connected -/
instance : PathConnectedSpace ğ•Š := by
  constructor; use âˆ
  have i1 : Joined âˆ ((1 : â„‚) : ğ•Š) := by
    generalize hp : (fun t : unitInterval â†¦ (((t : â„) : â„‚) : ğ•Š)â»Â¹) = p
    have pc : Continuous p := by
      rw [â† hp]
      exact continuous_inv.comp (continuous_coe.comp (Complex.continuous_ofReal.comp
        continuous_subtype_val))
    use âŸ¨p, pcâŸ©
    simp only [â† hp]; rw [Icc.coe_zero, Complex.ofReal_zero, coe_zero, inv_zero']
    simp only [â† hp]; rw [Icc.coe_one, Complex.ofReal_one, inv_coe one_ne_zero, inv_one]
  have cc : âˆ€ x y : â„‚, Joined (x : ğ•Š) (y : ğ•Š) := by
    intro x y
    have p := PathConnectedSpace.somePath x y
    use p.map continuous_coe
    repeat simp only [ContinuousMap.toFun_eq_coe, ContinuousMap.coe_coe, Path.source, Path.target]
  replace ic : âˆ€ x : â„‚, Joined âˆ (x : ğ•Š) := fun x â†¦ i1.trans (cc _ _)
  intro x y; induction x using OnePoint.rec
  Â· induction y using OnePoint.rec
    Â· exact Joined.refl _
    Â· apply ic
  Â· induction y using OnePoint.rec
    Â· exact (ic _).symm
    Â· apply cc

end RiemannSphere
