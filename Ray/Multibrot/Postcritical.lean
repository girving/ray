module
public import Ray.Multibrot.Defs
import Mathlib.Analysis.Calculus.Deriv.Inv
import Mathlib.Analysis.Calculus.Deriv.MeanValue
import Mathlib.Analysis.Convex.SpecificFunctions.Basic
import Mathlib.Analysis.SpecialFunctions.Log.Deriv
import Ray.Dynamics.Postcritical
import Ray.Dynamics.Potential
import Ray.Misc.Bound
import Ray.Misc.Bounds
import Ray.Multibrot.Basic
import Ray.Multibrot.Iterates
import Ray.Multibrot.Potential
import Ray.Multibrot.PotentialLower
import Ray.Multibrot.Rinv
import Ray.Multibrot.Specific

/-!
## Effective bounds on postcritical points for Multibrot sets

We show that any `(c,z)` with `4 â‰¤ abs c â‰¤ abs z` is postcritical.
-/

open Metric (closedBall)
open Real (exp log)
open RiemannSphere
open Set
open scoped OnePoint RiemannSphere Topology
noncomputable section

variable {c z : â„‚}

-- We fix `d â‰¥ 2`
variable {d : â„•} [Fact (2 â‰¤ d)]

/-- `log (log x)` is monotone for `1 < x` -/
lemma log_log_mono {x y : â„} (x0 : 1 < x) (xy : x â‰¤ y) : log (log x) â‰¤ log (log y) :=
  Real.log_le_log (Real.log_pos x0) (Real.log_le_log (by positivity) xy)

/-- `log (-log x)` is antitone for `0 < x < 1` -/
lemma log_neg_log_strict_anti {x y : â„} (x0 : 0 < x) (y0 : 0 < y) (x1 : x < 1) (y1 : y < 1) :
    log (-log y) < log (-log x) â†” x < y := by
  have lx := neg_pos.mpr (Real.log_neg x0 x1)
  have ly := neg_pos.mpr (Real.log_neg y0 y1)
  rw [Real.log_lt_log_iff ly lx, neg_lt_neg_iff, Real.log_lt_log_iff x0 y0]

/-- `log 2 * x â‰¤ log (1 + x)` for `0 â‰¤ x â‰¤ 1` -/
lemma le_log_one_add {x : â„} (x0 : 0 â‰¤ x) (x1 : x â‰¤ 1) : log 2 * x â‰¤ log (1 + x) := by
  rw [Real.le_log_iff_exp_le (by linarith)] --, Real.exp_mul, Real.exp_log (by norm_num)]
  have x0' : 0 â‰¤ 1 - x := by linarith
  have h := convexOn_exp.2 (mem_univ 0) (mem_univ (log 2)) x0' x0 (by abel)
  simp only [smul_eq_mul, mul_zero, zero_add, Real.exp_zero, mul_one, Real.exp_log zero_lt_two] at h
  ring_nf at h
  rwa [mul_comm]

/-- `log (x-1) / log x` is increasing for `2 â‰¤ x` -/
lemma log_ratio_mono : MonotoneOn (fun x â†¦ log (x-1) / log x) (Ici 2) := by
  have hd : âˆ€ x, 2 â‰¤ x â†’ HasDerivAt (fun x â†¦ log (x-1) / log x)
      ((1 / (x-1) * log x - log (x-1) * xâ»Â¹) / (log x)^2) x := by
    intro x x2
    have l0 : 0 < log x := Real.log_pos (by linarith)
    refine HasDerivAt.div ?_ (Real.hasDerivAt_log (by positivity)) l0.ne'
    exact HasDerivAt.log ((hasDerivAt_id _).sub_const _) (by linarith)
  have d : DifferentiableOn â„ (fun x â†¦ log (x-1) / log x) (Ici 2) :=
    fun x m â†¦ (hd x m).differentiableAt.differentiableWithinAt
  apply monotoneOn_of_deriv_nonneg (convex_Ici _)
  Â· exact d.continuousOn
  Â· exact d.mono interior_subset
  Â· intro x m
    simp only [nonempty_Iio, interior_Ici', mem_Ioi] at m
    have l0 : 0 < log x := Real.log_pos (by linarith)
    simp only [(hd x m.le).deriv, one_div]
    refine div_nonneg ?_ (by positivity)
    simp only [sub_nonneg, mul_comm]
    apply mul_le_mul
    Â· exact inv_antiâ‚€ (by linarith) (by linarith)
    Â· exact Real.log_le_log (by linarith) (by linarith)
    Â· exact Real.log_nonneg (by linarith)
    Â· exact inv_nonneg.mpr (by linarith)

/-- One iterate increases `log (log (abs z))` by `Î©(1)` for large `z` -/
lemma log_log_iter (z4 : 4 â‰¤ â€–zâ€–) (cz : â€–câ€– â‰¤ â€–zâ€–) :
    log (log â€–zâ€–) + 0.548 â‰¤ log (log â€–f' d c zâ€–) := by
  have zw : (â€–zâ€– - 1)^1 * â€–zâ€– â‰¤ â€–f' d c zâ€– := by
    refine iter_large (d := d) (n := 1) ?_ ?_ ?_ cz
    Â· linarith
    Â· exact le_refl _
  generalize â€–f' d c zâ€– = w at zw
  generalize â€–zâ€– = x at zw cz z4
  clear cz
  simp only [pow_one] at zw
  have lx1 : 1 < log (x-1) :=
    lt_trans (by norm_num) (lt_of_lt_of_le lt_log_3 (Real.log_le_log (by norm_num) (by linarith)))
  have lx : 1 < log x := lt_trans lx1 (Real.log_lt_log (by linarith) (by linarith))
  have ll : 0.791 â‰¤ log (x-1) / log x := by
    calc log (x-1) / log x
      _ â‰¥ log (4-1) / log 4 := by
          apply log_ratio_mono ?_ ?_ z4
          Â· simp only [mem_Ici]; norm_num
          Â· simp only [mem_Ici]; linarith
      _ = log 3 / log 4 := by norm_num
      _ â‰¥ 1.098 / 1.387 := div_le_divâ‚€ (by positivity) lt_log_3.le (by positivity) log_4_lt.le
      _ â‰¥ 0.791 := by norm_num
  have ll0 : 0 â‰¤ log (x-1) / log x := by positivity
  have ll1 : log (x-1) / log x â‰¤ 1 :=
    div_le_one_of_leâ‚€ (Real.log_le_log (by linarith) (by linarith)) (by positivity)
  calc log (log w)
    _ â‰¥ log (log ((x-1)*x)) := log_log_mono (by nlinarith) zw
    _ = log (log x + log (x-1)) := by rw [mul_comm, Real.log_mul (by positivity) (by linarith)]
    _ = log (log x) + log (1 + log (x-1) / log x) := by rw [log_add _ _ (by linarith) (by linarith)]
    _ â‰¥ log (log x) + log 2 * (log (x-1) / log x) := by bound [le_log_one_add ll0 ll1]
    _ â‰¥ log (log x) + 0.693 * 0.791 := by bound [lt_log_2]
    _ â‰¥ log (log x) + 0.548 := by bound

/-- Numerical bound we need below -/
lemma log_neg_log_le : log (-log 0.216) < -0.15 + log (log 12) := by
  trans 0.5
  Â· rw [Real.log_lt_iff_lt_exp (by bound)]
    trans 1.6
    Â· rw [neg_lt, Real.lt_log_iff_exp_lt (by norm_num)]
      norm_num
      exact exp_neg_div_lt
    Â· norm_num
      simpa using lt_exp_div (c := 8 / 5) (a := 1) (b := 2)
  Â· norm_num
    rw [Real.lt_log_iff_exp_lt (by bound)]
    trans 2
    Â· exact exp_div_lt
    Â· rw [Real.lt_log_iff_exp_lt (by bound)]
      exact exp_ofNat_lt

/-- For large `c`, large `z`'s are postcritical -/
public theorem postcritical_large (z4 : 4 â‰¤ â€–zâ€–) (cz : â€–câ€– â‰¤ â€–zâ€–) :
    Postcritical (superF d) c z := by
  -- Record a variety of inequalities
  have d0 : 0 < d := d_pos d
  have le_z : max 4 â€–câ€– â‰¤ â€–zâ€– := by bound
  have lcz : log (log (max 4 â€–câ€–)) â‰¤ log (log â€–zâ€–) := log_log_mono (by bound) le_z
  -- Reduce to s.potential c (f' d c z) < s.potential c â†‘c
  simp only [Postcritical, multibrot_p]
  set s := superF d
  rw [â† Real.pow_rpow_inv_natCast s.potential_nonneg d0.ne', â†
    Real.pow_rpow_inv_natCast (s.potential_nonneg : 0 â‰¤ s.potential c 0) d0.ne']
  simp only [â† s.potential_eqn]
  refine Real.rpow_lt_rpow s.potential_nonneg ?_ (by bound)
  generalize hw : f' d c z = w
  have e : f d c z = w := by rw [f, lift_coe', hw]
  simp only [f_0, e]; clear e
  have zw : â€–zâ€– â‰¤ â€–wâ€– := by rw [â†hw]; exact le_self_iter d (by linarith) cz 1
  have cw : â€–câ€– â‰¤ â€–wâ€– := le_trans cz zw
  -- Move to log (-log _) space
  have pw1 : s.potential c w < 1 := potential_lt_one_of_two_lt (by linarith) (by linarith)
  by_cases pc1 : s.potential c c = 1
  Â· linarith
  replace pc1 : s.potential c c < 1 := Ne.lt_of_le pc1 (s.potential_le_one)
  rw [â†log_neg_log_strict_anti potential_pos potential_pos pw1 pc1]
  -- Bounds about `iter_error`
  have lzw : log (log â€–zâ€–) + 0.548 â‰¤ log (log â€–wâ€–) := by
    rw [â†hw]; exact log_log_iter (by linarith) cz
  have ie : âˆ€ z : â„‚, 4 â‰¤ â€–zâ€– â†’ â€–câ€– â‰¤ â€–zâ€– â†’ iter_error d c z â‰¤ 0.15 := by
    intro z z4 cz
    refine le_trans (iter_error_le_of_z4 d z4 cz) ?_
    calc 0.8095 / (â€–zâ€– * log â€–zâ€–)
      _ â‰¤ 0.8095 / (4 * log 4) := by bound
      _ â‰¤ 0.8095 / (4 * 1.386) := by bound [lt_log_4]
      _ â‰¤ 0.15 := by norm_num
  have iew := ie w (by linarith) cw
  -- Use `log_neg_log_potential_approx` to replace `s.potential` with `log (log (abs _))`
  refine lt_of_lt_of_le ?_ (le_sub_iff_add_le.mp (abs_le.mp
    (log_neg_log_potential_approx d (by linarith) cw)).1)
  -- If `c` is small, we can use our potential lower bound
  by_cases c4 : â€–câ€– < 4
  Â· refine lt_of_le_of_lt (b := log (-log 0.216)) ?_ ?_
    Â· by_cases p1 : s.potential c c = 1
      Â· simp [p1]
        bound
      Â· replace p1 : s.potential c c < 1 := Ne.lt_of_le p1 s.potential_le_one
        bound
    Â· have w12 : 12 â‰¤ â€–wâ€– := by
        calc â€–wâ€–
          _ = â€–z ^ d + câ€– := by simp only [â† hw, f']
          _ â‰¥ â€–z ^ dâ€– - â€–câ€– := by bound
          _ = â€–zâ€– ^ d - â€–câ€– := by simp only [norm_pow]
          _ â‰¥ 4 ^ d - 4 := by bound
          _ â‰¥ 4 ^ 2 - 4 := by bound
          _ â‰¥ 12 := by norm_num
      grw [iew, â† w12]
      exact log_neg_log_le
  simp only [not_lt] at c4
  simp only [max_eq_right c4] at lcz
  -- Settle our inequality
  refine lt_of_le_of_lt (sub_le_iff_le_add.mp (abs_le.mp
    (log_neg_log_potential_approx d (by linarith) (le_refl _))).2) ?_
  have iec := ie c c4 (le_refl _)
  refine lt_of_lt_of_le (lt_of_le_of_lt (add_le_add iec lcz) ?_) (add_le_add (neg_le_neg iew) lzw)
  ring_nf; simp only [add_lt_add_iff_right]; norm_num

/-- For large `c` and small `z`, `(c,zâ»Â¹)` is postcritical -/
public lemma postcritical_small (le : â€–zâ€– â‰¤ rinv 4â»Â¹ c) :
    (c, (z : ð•Š)â»Â¹) âˆˆ (superF d).post := by
  set s := superF d
  by_cases z0 : z = 0
  Â· simp only [z0, coe_zero, inv_zero', s.post_a]
  Â· rw [inv_coe z0]
    obtain âŸ¨z4, zcâŸ© := le_rinv.mp le
    apply postcritical_large
    Â· simp only [norm_inv]
      rwa [le_inv_commâ‚€ (by bound) (norm_pos_iff.mpr z0)]
    Â· by_cases c0 : c = 0
      Â· simp only [c0, norm_zero, norm_inv, inv_nonneg, norm_nonneg]
      Â· simp only
        rwa [norm_inv, â† one_div, le_div_iffâ‚€ (by simpa)]
