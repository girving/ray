module
public import Mathlib.Analysis.Complex.Basic
public import Mathlib.Analysis.SpecialFunctions.Pow.Real
public import Ray.Multibrot.D
import Mathlib.Analysis.Complex.RealDeriv
import Mathlib.Tactic.Cases
import Ray.Misc.Bound
import Ray.Misc.Pow

/-!
## Real iteration bounds useful for `bottcher` bounds
-/

open Complex
open Metric (closedBall mem_closedBall mem_closedBall_self)
open Real (exp log)
open Set
open scoped Real Topology
noncomputable section

variable {c z : â„‚}
variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]

-- We fix `d â‰¥ 2`
variable {d : â„•} [Fact (2 â‰¤ d)]

/-!
### Noniteration lemmas
-/

/-- Prove `c * x â‰¤ y` if `c â‰¤ 0` -/
lemma cx_le {c x y : â„} (h : 0 < c â†’ c * x â‰¤ y) (x0 : 0 â‰¤ x := by bound)
    (y0 : 0 â‰¤ y := by bound) : c * x â‰¤ y := by
  by_cases c0 : c â‰¤ 0
  Â· exact le_trans (mul_nonpos_of_nonpos_of_nonneg c0 (by bound)) y0
  Â· exact h (by bound)

/-- Absorb a free `x` into `xâ»Â¹ ^ d` -/
lemma mul_inv_pow_d (d : â„•) [Fact (2 â‰¤ d)] (x : ğ•œ) : x * xâ»Â¹ ^ d = xâ»Â¹ ^ (d - 1) := by
  by_cases x0 : x = 0
  Â· simp only [x0, inv_zero, zero_mul]
    have d2 := two_le_d d
    rw [zero_pow (by omega)]
  Â· nth_rw 1 [â† Nat.sub_add_cancel (d_ge_one d), pow_succ', â† mul_assoc, mul_inv_cancelâ‚€ x0,
      one_mul]

/-- Loose bound on `c * x ^ d` -/
@[bound] lemma cxd_le (d : â„•) [d2 : Fact (2 â‰¤ d)] (c x : â„) (x0 : 0 â‰¤ x)
    (x3 : x â‰¤ 3â»Â¹) (cx : c * x â‰¤ 1) : c * x ^ d â‰¤ 3â»Â¹ := by
  refine cx_le fun c0 â†¦ ?_
  calc c * x ^ d
    _ â‰¤ c * x ^ 2 := by bound
    _ = c * x * x := by ring
    _ â‰¤ 1 * x := by bound
    _ â‰¤ 3â»Â¹ := by bound


/-- This one needs to be higher priority to be used by `bound`, which is a bit sketchy. -/
@[aesop safe apply (rule_sets := [Bound])] public lemma cxd_lt_1 (d : â„•) [d2 : Fact (2 â‰¤ d)]
    (c x : â„) (x0 : 0 â‰¤ x) (x3 : x â‰¤ 3â»Â¹) (cx : c * x â‰¤ 1) : c * x ^ d < 1 :=
  lt_of_le_of_lt (cxd_le d c x x0 x3 cx) (by norm_num)

/-- This one needs to be higher priority to be used by `bound`, which is a bit sketchy. -/
@[aesop safe apply (rule_sets := [Bound])] public lemma cxd_le_1 (d : â„•) [d2 : Fact (2 â‰¤ d)]
    (c x : â„) (x0 : 0 â‰¤ x) (x3 : x â‰¤ 3â»Â¹) (cx : c * x â‰¤ 1) : c * x ^ d â‰¤ 1 :=
  (cxd_lt_1 d c x x0 x3 cx).le

/-!
### Multibrot real iteration bounds
-/

/-- Function we'll iterate in tight bounds below -/
@[expose] public def fb (d : â„•) (b : â„) (x : â„) : â„ := x ^ d / (1 - b * x ^ d)

/-- Real iterates are positive and small -/
public lemma fb_nonneg_le (d : â„•) [d2 : Fact (2 â‰¤ d)] (c z : â„) (z3 : 3 â‰¤ z) (cz : c â‰¤ z) (n : â„•) :
    (fb d c)^[n] zâ»Â¹ âˆˆ Ioc 0 zâ»Â¹ := by
  have czi : c * zâ»Â¹ â‰¤ 1 := by bound
  have z3 : zâ»Â¹ â‰¤ 3â»Â¹ := by bound
  induction' n with n h
  Â· simp
    linarith
  Â· simp only [Function.iterate_succ_apply']
    generalize hx : (fb d c)^[n] zâ»Â¹ = x at h
    have cx : c * x â‰¤ 1 := cx_le fun c0 â†¦ le_trans (by bound) czi
    simp only [fb]
    refine âŸ¨by bound, ?_âŸ©
    calc x ^ d / (1 - c * x ^ d)
      _ â‰¤ x ^ 2 / (1 - 3â»Â¹) := by bound
      _ = x / (1 - 3â»Â¹) * x := by ring
      _ â‰¤ 3â»Â¹ / (1 - 3â»Â¹) * zâ»Â¹ := by bound
      _ â‰¤ zâ»Â¹ := by bound

@[bound] lemma fb_nonneg (d : â„•) [d2 : Fact (2 â‰¤ d)] (c z : â„) (z3 : 3 â‰¤ z) (cz : c â‰¤ z) (n : â„•) :
    0 â‰¤ (fb d c)^[n] zâ»Â¹ := (fb_nonneg_le d c z z3 cz n).1.le
@[bound] lemma fb_pos (d : â„•) [d2 : Fact (2 â‰¤ d)] (c z : â„) (z3 : 3 â‰¤ z) (cz : c â‰¤ z) (n : â„•) :
    0 < (fb d c)^[n] zâ»Â¹ := (fb_nonneg_le d c z z3 cz n).1
@[bound] lemma fb_le_z (d : â„•) [d2 : Fact (2 â‰¤ d)] (c z : â„) (z3 : 3 â‰¤ z) (cz : c â‰¤ z) (n : â„•) :
    (fb d c)^[n] zâ»Â¹ â‰¤ zâ»Â¹ := (fb_nonneg_le d c z z3 cz n).2
@[bound] lemma c_fb (d : â„•) [d2 : Fact (2 â‰¤ d)] (c z : â„) (z3 : 3 â‰¤ z) (cz : c â‰¤ z) (n : â„•) :
    c * (fb d c)^[n] zâ»Â¹ â‰¤ 1 := by
  refine cx_le fun c0 â†¦ ?_
  rw [mul_comm, â† le_div_iffâ‚€ c0, one_div]
  trans zâ»Â¹
  all_goals bound
@[bound] lemma fb_le_3i (d : â„•) [d2 : Fact (2 â‰¤ d)] (c z : â„) (z3 : 3 â‰¤ z) (cz : c â‰¤ z) (n : â„•) :
    (fb d c)^[n] zâ»Â¹ â‰¤ 3â»Â¹ := le_trans (fb_le_z d c z z3 cz n) (by bound)
@[bound] lemma fb_le_1 (d : â„•) [d2 : Fact (2 â‰¤ d)] (c z : â„) (z3 : 3 â‰¤ z) (cz : c â‰¤ z) (n : â„•) :
    (fb d c)^[n] zâ»Â¹ â‰¤ 1 := le_trans (fb_le_z d c z z3 cz n) (by bound)

@[bound] public lemma fb_mono_d (d : â„•) [Fact (2 â‰¤ d)] (b x : â„) (b0 : 0 â‰¤ b) (x3 : 3 â‰¤ x)
    (bx : b â‰¤ x) (n : â„•) : b * (fb d b)^[n] xâ»Â¹ ^ d â‰¤ b * (fb 2 b)^[n] xâ»Â¹ ^ 2 := by
  by_cases bz : b = 0
  Â· simp only [bz, zero_mul, le_refl]
  replace b0 : 0 < b := by positivity
  have xb : xâ»Â¹ â‰¤ bâ»Â¹ := by bound
  induction' n with n uv
  Â· simp only [Function.iterate_zero, id_eq]
    bound
  Â· simp only [Function.iterate_succ_apply']
    generalize hu : (fb d b)^[n] xâ»Â¹ = u at uv
    generalize hv : (fb 2 b)^[n] xâ»Â¹ = v at uv
    refine mul_le_mul_of_nonneg_left ?_ (by bound)
    have u1 : u ^ d / (1 - b * u ^ d) â‰¤ 1 := by
      calc u ^ d / (1 - b * u ^ d)
        _ â‰¤ u / (1 - 3â»Â¹) := by bound
        _ â‰¤ 3â»Â¹ / (1 - 3â»Â¹) := by bound
        _ â‰¤ 1 := by norm_num
    trans (fb d b u) ^ 2
    Â· simp only [fb]
      bound
    Â· simp only [fb]
      rw [mul_le_mul_iff_of_pos_left (by linarith)] at uv
      bound

@[bound] public lemma fb_mono_d_weak (d : â„•) [Fact (2 â‰¤ d)] (b x : â„) (b0 : 0 â‰¤ b) (x3 : 3 â‰¤ x)
    (bx : b â‰¤ x) (n : â„•) : (fb d b)^[n] xâ»Â¹ ^ d â‰¤ (fb 2 b)^[n] xâ»Â¹ ^ 2 := by
  by_cases bz : b â‰¤ 0
  Â· replace b0 : b = 0 := by linarith
    simp only [b0, ge_iff_le]
    induction' n with n h
    Â· simp only [Function.iterate_zero, id_eq, inv_pow]
      bound
    Â· simp only [Function.iterate_succ_apply', fb, zero_mul, sub_zero, div_one]
      trans ((fb 2 0)^[n] xâ»Â¹ ^ 2) ^ d
      Â· bound
      Â· bound
  Â· have h := fb_mono_d d b x b0 x3 bx n
    rwa [mul_le_mul_iff_of_pos_left (by bound)] at h

@[bound] public lemma f_le_fb (d : â„•) [Fact (2 â‰¤ d)] (c z : â„‚) (z3 : 3 â‰¤ â€–zâ€–) (cz : â€–câ€– â‰¤ â€–zâ€–)
    (n : â„•) : â€–(fun z â†¦ z ^ d / (1 + c * z ^ d))^[n] zâ»Â¹â€– â‰¤ (fb d â€–câ€–)^[n] â€–zâ€–â»Â¹ := by
  induction' n with n h
  Â· simp only [Function.iterate_zero, id_eq, norm_inv, le_refl]
  Â· simp only [Function.iterate_succ_apply']
    generalize hw : (fun z â†¦ z ^ d / (1 + c * z ^ d))^[n] zâ»Â¹ = w at h
    generalize hx : (fb d â€–câ€–)^[n] â€–zâ€–â»Â¹ = x at h
    simp only [norm_pow, norm_div, fb] at h âŠ¢
    apply div_le_divâ‚€ (by bound) (by bound) (by bound)
    calc â€–1 + c * w ^ dâ€–
      _ â‰¥ â€–(1 : â„‚)â€– - â€–c * w ^ dâ€– := by bound
      _ = 1 - â€–câ€– * â€–wâ€– ^ d := by simp only [norm_one, Complex.norm_mul, norm_pow]
      _ â‰¥ 1 - â€–câ€– * x ^ d := by bound

/-- `fb` is monotone in `z` for fixed `c` -/
@[bound] public lemma fb_mono_z (d : â„•) [Fact (2 â‰¤ d)] (c z : â„) (c3 : 3 â‰¤ c)
    (cz : c â‰¤ z) (n : â„•) : (fb d c)^[n] zâ»Â¹ â‰¤ (fb d c)^[n] câ»Â¹ := by
  induction' n with n h
  Â· simp
    bound
  Â· simp only [Function.iterate_succ_apply', fb]
    bound

/-- `fb` is monotone in `c` for fixed `z` -/
@[bound] public lemma fb_mono_cz (d : â„•) [Fact (2 â‰¤ d)] (c z : â„) (z3 : 3 â‰¤ z)
    (cz : c â‰¤ z) (n : â„•) : (fb d c)^[n] zâ»Â¹ â‰¤ (fb d z)^[n] zâ»Â¹ := by
  induction' n with n h
  Â· simp
  Â· simp only [Function.iterate_succ_apply', fb]
    bound

/-- Diagonal `fb` is monotone in `c`, in two different ways -/
lemma fb_mono_c (d : â„•) [Fact (2 â‰¤ d)] (c b : â„) (b3 : 3 â‰¤ b) (bc : b â‰¤ c) (n : â„•) :
    (fb d c)^[n] câ»Â¹ â‰¤ (fb d b)^[n] bâ»Â¹ âˆ§ c * (fb d c)^[n] câ»Â¹ ^ d â‰¤ b * (fb d b)^[n] bâ»Â¹ ^ d := by
  induction' n with n h
  Â· simp only [Function.iterate_zero, id_eq, mul_inv_pow_d]
    bound
  Â· have dd : d * d = d + d * (d - 1) := by rw [â† mul_one_add, Nat.add_sub_cancel' (d_ge_one d)]
    simp only [Function.iterate_succ_apply', fb, div_pow, â† mul_div_assoc, â† pow_mul, dd, pow_add,
      â† mul_assoc]
    bound

@[bound] public lemma fb_mono_c_weak (d : â„•) [Fact (2 â‰¤ d)] (c b : â„) (b3 : 3 â‰¤ b) (bc : b â‰¤ c)
    (n : â„•) : (fb d c)^[n] câ»Â¹ â‰¤ (fb d b)^[n] bâ»Â¹ := (fb_mono_c d c b b3 bc n).1
@[bound] public lemma fb_mono_c_strong (d : â„•) [Fact (2 â‰¤ d)] (c b : â„) (b3 : 3 â‰¤ b) (bc : b â‰¤ c)
    (n : â„•) : c * (fb d c)^[n] câ»Â¹ ^ d â‰¤ b * (fb d b)^[n] bâ»Â¹ ^ d := (fb_mono_c d c b b3 bc n).2

@[bound] public lemma fb_mono_cz_weak (d : â„•) [Fact (2 â‰¤ d)] {b c z : â„} (b3 : 3 â‰¤ b) (bc : b â‰¤ c)
    (cz : c â‰¤ z) (n : â„•) : (fb d c)^[n] zâ»Â¹ â‰¤ (fb d b)^[n] bâ»Â¹ :=
  le_trans (by bound) (fb_mono_c_weak d c b b3 bc n)
@[bound] public lemma fb_mono_cz_strong (d : â„•) [Fact (2 â‰¤ d)] {b c z : â„} (b3 : 3 â‰¤ b) (bz : b â‰¤ z)
    (cz : c â‰¤ z) (n : â„•) : c * (fb d c)^[n] zâ»Â¹ ^ d â‰¤ b * (fb d b)^[n] bâ»Â¹ ^ d :=
  le_trans (by bound) (fb_mono_c_strong d z b b3 bz n)

@[bound] public lemma term_mono_d (d : â„•) [Fact (2 â‰¤ d)] {c z : â„} (c0 : 0 â‰¤ c) (z3 : 3 â‰¤ z)
    (cz : c â‰¤ z) (n : â„•) :
    (1 - c * (fb d c)^[n] zâ»Â¹ ^ d) ^ (-1 / d ^ (n + 1) : â„) - 1 â‰¤
      (1 - c * (fb 2 c)^[n] zâ»Â¹ ^ 2) ^ (-1 / 2 ^ (n + 1) : â„) - 1 := by
  apply sub_le_sub_right
  trans (1 - c * (fb 2 c)^[n] zâ»Â¹ ^ 2) ^ (-1 / d ^ (n + 1) : â„)
  Â· apply Real.rpow_le_rpow_of_nonpos <;> bound
  Â· apply Real.rpow_le_rpow_of_exponent_ge
    Â· bound
    Â· bound
    Â· simp only [neg_div, one_div, neg_le_neg_iff]
      bound

/-!
### Factorised bounds
-/

/-- Iteration after we pull out the `bâ»Â¹ ^ 2 ^ d` factor -/
public def factor (d : â„•) (b : ğ•œ) (p : ğ•œ Ã— ğ•œ) : ğ•œ Ã— ğ•œ :=
  let a := (1 - b * p.1 ^ d)â»Â¹
  (p.1 ^ d * a, p.2 ^ d * a)

@[simp] lemma fst_factor (d : â„•) (b x : â„) (n : â„•) :
    ((factor d b)^[n] (x,1)).1 = (fb d b)^[n] x := by
  induction' n with n h
  Â· simp only [Function.iterate_zero, id_eq]
  Â· simp only [Function.iterate_succ_apply', factor, h, fb, div_eq_mul_inv]

/-- Factored version of `fb` iteration -/
lemma fb_eq_factor (d : â„•) (b x : â„) (n : â„•) :
    (fb d b)^[n] x = ((factor d b)^[n] (x,1)).2 * x ^ d ^ n := by
  induction' n with n h
  Â· simp only [Function.iterate_zero, id_eq, pow_zero, pow_one, one_mul]
  Â· simp only [Function.iterate_succ_apply', fb, factor, h, div_eq_mul_inv, mul_pow, â† pow_mul,
      â† pow_succ, mul_assoc, mul_comm (x ^ _), fst_factor]

/-- `factor.2` as a division -/
public lemma factor_eq_div {d : â„•} {b x : â„} (x0 : x â‰  0) {n : â„•} :
    ((factor d b)^[n] (x,1)).2 = (fb d b)^[n] x / x ^ d ^ n := by
  simp only [fb_eq_factor, mul_div_assoc, â† div_pow, div_self x0, one_pow, mul_one]

@[bound] lemma factor_pos (d : â„•) [Fact (2 â‰¤ d)] (b x : â„) (x3 : 3 â‰¤ x) (bx : b â‰¤ x)
    (n : â„•) : 0 < ((factor d b)^[n] (xâ»Â¹,1)).2 := by
  induction' n with n h
  Â· simp
  Â· simp only [Function.iterate_succ_apply', factor, fst_factor]
    bound

@[bound] lemma factor_nonneg (d : â„•) [Fact (2 â‰¤ d)] (b x : â„) (x3 : 3 â‰¤ x) (bx : b â‰¤ x) (n : â„•) :
    0 â‰¤ ((factor d b)^[n] (xâ»Â¹,1)).2 := (factor_pos d b x x3 bx n).le

@[bound] lemma factor_mono (d : â„•) [Fact (2 â‰¤ d)] {b c z : â„} (b3 : 3 â‰¤ b) (bz : b â‰¤ z) (cz : c â‰¤ z)
    (n : â„•) : ((factor d c)^[n] (zâ»Â¹, 1)).2 â‰¤ ((factor d b)^[n] (bâ»Â¹, 1)).2 := by
  induction' n with n h
  Â· simp
  Â· simp only [Function.iterate_succ_apply', factor, fst_factor]
    bound [fb_mono_cz_strong d b3 bz cz n]

@[bound] public lemma fb_le_factor (d : â„•) [Fact (2 â‰¤ d)] {b c z : â„} (b3 : 3 â‰¤ b) (c0 : 0 â‰¤ c)
    (bz : b â‰¤ z) (cz : c â‰¤ z) (n : â„•) :
    c * (fb d c)^[n] zâ»Â¹ ^ d â‰¤ ((factor d b)^[n] (bâ»Â¹, 1)).2 ^ d * zâ»Â¹ ^ (d ^ (n + 1) - 1) := by
  have z0 : 0 < z := by linarith
  simp only [fb_eq_factor, mul_pow, â† pow_mul, â† pow_succ, mul_comm c]
  rw [pow_subâ‚€ _ (by positivity) (by bound), â† mul_assoc, pow_one, inv_inv]
  bound

/-!
### Doubly exponential bounds, and related

These are used to bound the tail products of `term` bounds.
-/

lemma fb_le_pow_pow (d : â„•) [Fact (2 â‰¤ d)] {b : â„} (b3 : 3 â‰¤ b) (n : â„•) :
    (fb 2 b)^[n] bâ»Â¹ â‰¤ 2 / 3 * 2â»Â¹ ^ 2 ^ n := by
  induction' n with n h
  Â· norm_num
    simp only [one_div]
    bound
  Â· simp only [Function.iterate_succ_apply', fb]
    generalize hx : (fb 2 b)^[n] bâ»Â¹ = x at h
    calc x ^ 2 / (1 - b * x ^ 2)
      _ â‰¤ (2 / 3 * 2â»Â¹ ^ 2 ^ n) ^ 2 / (1 - 3â»Â¹) := by bound
    simp only [mul_pow, â† pow_mul, â† pow_succ, div_eq_inv_mul]
    linarith
