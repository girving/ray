module
public import Mathlib.Analysis.SpecialFunctions.Log.Basic
public import Ray.Dynamics.Bottcher
public import Ray.Manifold.RiemannSphere
public import Ray.Multibrot.D
import Mathlib.Analysis.Calculus.Deriv.Pow
import Ray.Analytic.Analytic
import Ray.Misc.Bound
import Ray.Misc.Cobounded

/-!
## Multibrot definitions, allowing minimal public imports
-/

open Bornology (cobounded)
open Filter (Tendsto atTop)
open Function (uncurry)
open OneDimension
open RiemannSphere
open Set
open scoped ContDiff OnePoint RiemannSphere Topology

-- We fix `d â‰¥ 2`
variable {d : â„•} [Fact (2 â‰¤ d)]
variable {c : â„‚}

/-!
## The defining iteration, the Multibrot set, and its complement
-/

/-- The Multibrot iteration, `â„‚ â†’ â„‚` version -/
@[expose] public def f' (d : â„•) (c z : â„‚) : â„‚ :=
  z ^ d + c

/-- The Multibrot iteration, `ğ•Š â†’ ğ•Š` version -/
@[expose] public def f (d : â„•) : â„‚ â†’ ğ•Š â†’ ğ•Š :=
  lift' (f' d) âˆ

/-- The Multibrot set is those points that do not escape to `âˆ` -/
@[expose] public def multibrot (d : â„•) : Set â„‚ :=
  {c | Â¬Tendsto (fun n â†¦ (f d c)^[n] â†‘c) atTop (ğ“ âˆ)}

/-- The complement of the Multibrot set, including `âˆ` -/
@[expose] public def multibrotExt (d : â„•) : Set ğ•Š :=
  ((fun z : â„‚ â†¦ (z : ğ•Š)) '' multibrot d)á¶œ âˆª {(âˆ : ğ•Š)}

/-!
## Basic properties of the iteration `f`

In particular, we show that `f d` has a superattracting fixpoint at `âˆ`.
-/

-- Basic properties of f
@[simp] public lemma f_0' (d : â„•) [Fact (2 â‰¤ d)] : f' d c 0 = c := by
  simp only [f', zero_pow (d_ne_zero _), zero_add]

@[simp] public lemma f_0 (d : â„•) [Fact (2 â‰¤ d)] : f d c 0 = c := by
  simp only [f, â† coe_zero, lift_coe', f', zero_pow (d_ne_zero _), zero_add]

public theorem analytic_f' {d : â„•} : AnalyticOnNhd â„‚ (uncurry (f' d)) univ := fun _ _ â†¦
  (analyticAt_snd.pow _).add analyticAt_fst

theorem tendsto_f'_cobounded (c : â„‚) :
    Tendsto (uncurry (f' d)) (ğ“ c Ã—Ë¢ cobounded â„‚) (cobounded â„‚) := by
  simp only [hasBasis_cobounded_norm_lt.tendsto_right_iff, Set.mem_setOf_eq,
    forall_true_left, uncurry, Metric.eventually_nhds_prod_iff]
  intro r; use 1, zero_lt_one, fun z â†¦ max r 0 + â€–câ€– + 1 < â€–zâ€–; constructor
  Â· refine (eventually_cobounded (max r 0 + â€–câ€– + 1)).mp (.of_forall fun w h â†¦ ?_)
    exact h
  Â· intro e ec z h
    simp only [Complex.dist_eq] at ec
    have zz : â€–zâ€– â‰¤ â€–z ^ dâ€– := by
      rw [norm_pow]
      refine le_self_powâ‚€ ?_ (d_ne_zero _)
      exact le_trans (le_add_of_nonneg_left (add_nonneg (le_max_right _ _) (norm_nonneg _))) h.le
    calc â€–f' d e zâ€–
      _ = â€–z ^ d + eâ€– := rfl
      _ = â€–z ^ d + (c + (e - c))â€– := by ring_nf
      _ â‰¥ â€–z ^ dâ€– - â€–c + (e - c)â€– := by bound
      _ â‰¥ â€–z ^ dâ€– - (â€–câ€– + â€–e - câ€–) := by bound
      _ â‰¥ â€–zâ€– - (â€–câ€– + 1) := by bound
      _ > max r 0 + â€–câ€– + 1 - (â€–câ€– + 1) := by bound
      _ = max r 0 := by ring_nf
      _ â‰¥ r := le_max_left _ _

public theorem mAnalyticAt_f : ContMDiff II I Ï‰ (uncurry (f d)) :=
  mAnalytic_lift' analytic_f' tendsto_f'_cobounded

public theorem writtenInExtChartAt_coe_f {d : â„•} {z : â„‚} :
    writtenInExtChartAt I I (z : ğ•Š) (f d c) = f' d c := by
  simp only [writtenInExtChartAt, f, Function.comp_def, lift_coe', RiemannSphere.extChartAt_coe,
    PartialEquiv.symm_symm, coePartialEquiv_apply, coePartialEquiv_symm_apply, toComplex_coe]

public lemma fl_f : fl (f d) âˆ = fun c z : â„‚ â†¦ z^d / (1 + c * z^d) := by
  funext c z
  simp only [fl, RiemannSphere.extChartAt_inf, Function.comp_def, invEquiv_apply,
    PartialEquiv.trans_apply, Equiv.toPartialEquiv_apply, PartialEquiv.coe_trans_symm,
    coePartialEquiv_symm_apply, PartialEquiv.symm_symm, coePartialEquiv_apply,
    Equiv.toPartialEquiv_symm_apply, invEquiv_symm, RiemannSphere.inv_inf, toComplex_zero,
    add_zero, sub_zero]
  by_cases z0 : z = 0
  Â· simp only [z0, coe_zero, inv_zero', f, lift_inf', RiemannSphere.inv_inf, toComplex_zero,
      zero_pow (d_ne_zero _), zero_div]
  simp only [f, f', inv_coe z0, lift_coe', inv_pow]
  have zd := pow_ne_zero d z0
  by_cases h : (z ^ d)â»Â¹ + c = 0
  Â· simp only [h, coe_zero, inv_zero', toComplex_inf]
    simp only [â† add_eq_zero_iff_neg_eq.mp h, neg_mul, inv_mul_cancelâ‚€ zd, â† sub_eq_add_neg,
      sub_self, div_zero]
  rw [inv_coe h, toComplex_coe, eq_div_iff, inv_mul_eq_iff_eq_mulâ‚€ h, right_distrib,
    inv_mul_cancelâ‚€ zd]
  contrapose h
  rw [add_comm, add_eq_zero_iff_eq_neg, â† eq_div_iff zd, neg_div, â†
    inv_eq_one_div, â† add_eq_zero_iff_eq_neg, add_comm] at h
  exact h

/-- `f` near `âˆ` with the `z^d` factor removed -/
@[expose] public noncomputable def gl (d : â„•) (c z : â„‚) :=
  (1 + c * z ^ d)â»Â¹

public theorem gl_f {z : â„‚} : g (fl (f d) âˆ c) d z = gl d c z := by
  simp only [fl_f, gl, g]
  by_cases z0 : z = 0
  simp only [if_pos, z0, zero_pow (d_ne_zero _), MulZeroClass.mul_zero, add_zero, inv_one]
  rw [if_neg z0, div_eq_mul_inv _ (_ + _), mul_comm, mul_div_assoc, div_self (pow_ne_zero _ z0),
    mul_one]

theorem analyticAt_gl : AnalyticAt â„‚ (gl d c) 0 := by
  apply (analyticAt_const.add (analyticAt_const.mul (analyticAt_id.pow _))).inv
  simp only [Pi.add_apply, Pi.mul_apply, Pi.pow_apply, id_eq, zero_pow (d_ne_zero _), mul_zero,
    add_zero, ne_eq, one_ne_zero, not_false_eq_true]

theorem fl_f' : fl (f d) âˆ = fun c z : â„‚ â†¦ (z - 0) ^ d â€¢ gl d c z := by
  funext c z; simp only [fl_f, gl, sub_zero, smul_eq_mul, div_eq_mul_inv]

theorem gl_zero : gl d c 0 = 1 := by
  simp only [gl, zero_pow (d_ne_zero _), MulZeroClass.mul_zero]; norm_num

theorem gl_frequently_ne_zero : âˆƒá¶  z in ğ“ 0, gl d c z â‰  0 := by
  refine (analyticAt_gl.continuousAt.eventually_ne ?_).frequently; simp only [gl_zero]
  exact one_ne_zero

public lemma fc_f : leadingCoeff (fl (f d) âˆ c) 0 = 1 := by
  rw [fl_f', analyticAt_gl.monomial_mul_leadingCoeff gl_frequently_ne_zero, leadingCoeff_of_ne_zero]
  exact gl_zero; rw [gl_zero]; exact one_ne_zero

public lemma fd_f : orderAt (fl (f d) âˆ c) 0 = d := by
  rw [fl_f', analyticAt_gl.monomial_mul_orderAt gl_frequently_ne_zero, orderAt_eq_zero, add_zero]
  rw [gl_zero]; exact one_ne_zero

theorem f_inf {d : â„•} : f d c âˆ = (âˆ : ğ•Š) := by
  simp only [f, lift_inf']

-- f has a superattracting fixpoint at âˆ
public theorem superF (d : â„•) [Fact (2 â‰¤ d)] : Super (f d) d âˆ :=
  { d2 := two_le_d d
    fa := mAnalyticAt_f
    fc := fun _ â†¦ fc_f
    fd := fun _ â†¦ fd_f
    f0 := fun _ â†¦ f_inf }

/-- `f` has one preimage of `âˆ` -/
public instance onePreimageF : OnePreimage (superF d) where
  eq_a := by
    intro c z; induction z using OnePoint.rec
    Â· simp only [imp_true_iff]
    Â· simp only [f, lift_coe', OnePoint.coe_ne_infty, IsEmpty.forall_iff]

/-!
## Bottcher coordinates!
-/

/-- The BÃ¶ttcher map for the Multibrot set is the diagonal of the dynamical map (`â„‚ â†’ â„‚` version) -/
@[expose] public noncomputable def bottcher' (d : â„•) [Fact (2 â‰¤ d)] (c : â„‚) : â„‚ :=
  (superF d).bottcher c c

/-- The BÃ¶ttcher map for the Multibrot set is the diagonal of the dynamical map (`ğ•Š â†’ â„‚` version) -/
@[expose] public noncomputable def bottcher (d : â„•) [Fact (2 â‰¤ d)] : ğ•Š â†’ â„‚ :=
  fill (bottcher' d) 0

/-- `bottcher` near `âˆ` as an analytic `â„‚ â†’ â„‚` function -/
public noncomputable def bottcher_inv (d : â„•) [Fact (2 â‰¤ d)] : â„‚ â†’ â„‚ :=
  fun z â†¦ bottcher d (â†‘z)â»Â¹

/-- `s.bottcher_inv` as an analytic `â„‚ â†’ â„‚ â†’ â„‚` function -/
public noncomputable def sbottcher_inv (d : â„•) [Fact (2 â‰¤ d)] : â„‚ â†’ â„‚ â†’ â„‚ :=
  fun c z â†¦ (superF d).bottcher c (z : ğ•Š)â»Â¹

public lemma bottcher_inv_def : bottcher_inv d = fun z : â„‚ â†¦ bottcher d (â†‘z)â»Â¹ := by rfl
public lemma sbottcher_inv_def :
    sbottcher_inv d = fun c z : â„‚ â†¦ (superF d).bottcher c (z : ğ•Š)â»Â¹ := by rfl

/-- `s.inv_ray` as an analytic `â„‚ â†’ â„‚` function -/
@[expose] public noncomputable def sinv_ray (d : â„•) [Fact (2 â‰¤ d)] : â„‚ â†’ â„‚ â†’ â„‚ :=
  fun c z â†¦ ((superF d).ray c z)â»Â¹.toComplex


/-!
## Error bound functions for iterates and potentials
-/

/-- Weird bound that we use below to be reasonably tight -/
@[expose] public noncomputable def f_error (d : â„•) (z : â„‚) :=
  -Real.log (1 - -Real.log (1 - 1/â€–zâ€–) / (d * Real.log (â€–zâ€–)))

/-- The infinite sum of `f_error` -/
@[expose] public noncomputable def iter_error (d : â„•) (c z : â„‚) :=
  âˆ‘' n, f_error d ((f' d c)^[n] z)

/-- We will use this function below to produce bounds on `s.potential` approximates -/
@[expose] public noncomputable def ene (x : â„) : â„ := Real.exp (-Real.exp x)

/-- The (negated) derivative of `ene` -/
@[expose] public noncomputable def dene (x : â„) : â„ := Real.exp (x - Real.exp x)

/-- Error term in the `potential` approximate -/
@[expose] public noncomputable def potential_error (d : â„•) (c z : â„‚) : â„ :=
  dene (Real.log (Real.log â€–zâ€–) - iter_error d c z) * iter_error d c z

/-!
## Balls whose size depends on an inverse

These work correctly if the inverse would be infinite.
-/

/-- `min r â€–câ€–â»Â¹`, but do the right thing if `c = 0` -/
@[expose] public noncomputable def rinv (r : â„) (c : â„‚) : â„ :=
  if c = 0 then r else min r â€–câ€–â»Â¹
