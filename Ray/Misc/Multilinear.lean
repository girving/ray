module
public import Mathlib.Analysis.Complex.Basic
public import Mathlib.Analysis.Normed.Module.Multilinear.Basic
public import Mathlib.Data.Fintype.Defs
public import Mathlib.Topology.Algebra.Module.Multilinear.Basic
import Mathlib.Analysis.CStarAlgebra.Classes
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Tactic.Cases

/-!
## Continuous multilinear map constructors

We define continuous multilinear maps for

1. `fstCmmap` for `fst`
2. `sndCmmap` for `snd`
3. `smulCmmap` for two continuous multilinear maps `smul`'ed together
4. Products of monomials: `termCmmap` for `x^a * y^b`
5. `conjCLM` for `conj` (this is one a continuous linear map)
6. `cmmapApplyCmap`, a continuous linear map that evaluates a continuous multilinear map at a point
-/

open scoped ComplexConjugate
noncomputable section

variable {n : â„•}
variable {ð•œ : Type} [NontriviallyNormedField ð•œ]
variable {Î¹ : Type}
variable {R A B E : Type} [Semiring R]

theorem ContinuousMultilinearMap.toFun_eq_coe {R A B : Type} [Semiring R] [AddCommMonoid A]
    [Module R A] [TopologicalSpace A] [AddCommMonoid B] [Module R B] [TopologicalSpace B]
    (f : ContinuousMultilinearMap R (fun _ : Fin n â†¦ A) B) : f.toFun = â‡‘f := by
  rw [MultilinearMap.toFun_eq_coe]; simp

/-- `fst` as a `ContinuousMultilinearMap` -/
def fstCmmap (R : Type) (A B : Type) [Semiring R] [AddCommMonoid A] [Module R A]
    [TopologicalSpace A] [AddCommMonoid B] [Module R B] [TopologicalSpace B] :
    ContinuousMultilinearMap R (fun _ : Fin 1 â†¦ A Ã— B) A :=
  ContinuousMultilinearMap.ofSubsingleton R (A Ã— B) A (0 : Fin 1) (ContinuousLinearMap.fst R A B)

/-- `snd` as a `ContinuousMultilinearMap` -/
def sndCmmap (R : Type) (A B : Type) [Semiring R] [AddCommMonoid A] [Module R A]
    [TopologicalSpace A] [AddCommMonoid B] [Module R B] [TopologicalSpace B] :
    ContinuousMultilinearMap R (fun _ : Fin 1 â†¦ A Ã— B) B :=
  ContinuousMultilinearMap.ofSubsingleton R (A Ã— B) B (0 : Fin 1) (ContinuousLinearMap.snd R A B)

theorem fstCmmap_apply [AddCommMonoid A] [Module R A] [TopologicalSpace A] [AddCommMonoid B]
    [Module R B] [TopologicalSpace B] (a : A) (b : B) : (fstCmmap R A B fun _ â†¦ (a, b)) = a := by
  simp only [fstCmmap, ContinuousMultilinearMap.ofSubsingleton_apply_apply,
    ContinuousLinearMap.coe_fst']

theorem sndCmmap_apply [AddCommMonoid A] [Module R A] [TopologicalSpace A] [AddCommMonoid B]
    [Module R B] [TopologicalSpace B] (a : A) (b : B) : (sndCmmap R A B fun _ â†¦ (a, b)) = b := by
  simp only [sndCmmap, ContinuousMultilinearMap.ofSubsingleton_apply_apply,
    ContinuousLinearMap.coe_snd']

theorem fstCmmap_norm [NormedRing A] [NormedAlgebra ð•œ A] [NormOneClass A] [NormedRing B]
    [NormedAlgebra ð•œ B] [NormOneClass B] : â€–fstCmmap ð•œ A Bâ€– = 1 := by
  apply le_antisymm
  Â· refine (fstCmmap ð•œ A B).opNorm_le_bound (M := 1) (by norm_num) ?_
    intro z; simp only [Finset.univ_unique, Fin.default_eq_zero, Finset.prod_singleton, one_mul]
    have e : z = (fun _ â†¦ ((z 0).1, (z 0).2)) := by apply funext; intro i; rw [Fin.eq_zero i]
    rw [e]
    rw [fstCmmap_apply]; simp; exact norm_fst_le (z 0)
  Â· have lo := (fstCmmap ð•œ A B).unit_le_opNorm (m := fun _ â†¦ (1, 1)) ?_
    rw [fstCmmap_apply, norm_one] at lo; assumption
    rw [pi_norm_le_iff_of_nonneg]; intro i; simp only [Prod.norm_def, norm_one]
    repeat norm_num

theorem sndCmmap_norm [NormedRing A] [NormedAlgebra ð•œ A] [NormOneClass A] [NormedRing B]
    [NormedAlgebra ð•œ B] [NormOneClass B] : â€–sndCmmap ð•œ A Bâ€– = 1 := by
  apply le_antisymm
  Â· apply (sndCmmap ð•œ A B).opNorm_le_bound (M := 1) (by norm_num)
    intro z; simp only [Finset.univ_unique, Fin.default_eq_zero, Finset.prod_singleton, one_mul]
    have e : z = (fun _ â†¦ ((z 0).1, (z 0).2)) := by apply funext; intro i; rw [Fin.eq_zero i]
    rw [e]
    rw [sndCmmap_apply]; simp; exact norm_snd_le (z 0)
  Â· have lo := (sndCmmap ð•œ A B).unit_le_opNorm (m := fun _ â†¦ (1, 1)) ?_
    rw [sndCmmap_apply, norm_one] at lo; assumption
    rw [pi_norm_le_iff_of_nonneg]; intro i; simp only [Prod.norm_def, norm_one]
    repeat norm_num

-- Lemmas for `smulCmmap`
theorem update_0_0 (z : Fin (n + 1) â†’ A) (x : A) :
    Function.update (fun _ : Fin 1 â†¦ z 0) 0 x = (fun _ : Fin 1 â†¦ x) := by
  apply funext; intro i
  have i0 : i = 0 := by simp only [eq_iff_true_of_subsingleton]
  rw [i0]; simp only [Function.update_self]

theorem update_0_succ (d : DecidableEq (Fin (n + 1))) (f : Fin (n + 1) â†’ A) (x : A) (i : Fin n) :
    @Function.update _ _ d f 0 x i.succ = f i.succ := by
  rw [Function.update_apply]; simp only [ite_eq_right_iff]
  have i0 := Fin.succ_ne_zero i
  intro h; exfalso; exact i0 h

theorem update_nz_0 (d : DecidableEq (Fin (n + 1))) (f : Fin (n + 1) â†’ A) {x : A} {i : Fin (n + 1)}
    (i0 : i â‰  0) : @Function.update _ _ d f i x 0 = f 0 := by rw [Function.update_of_ne i0.symm]

theorem update_nz_succ (d : DecidableEq (Fin (n + 1))) (f : Fin (n + 1) â†’ A) (x : A)
    {i : Fin (n + 1)} (i0 : i â‰  0) :
    (fun j : Fin n â†¦ @Function.update _ _ d f i x j.succ) =
      Function.update (fun j : Fin n â†¦ f j.succ) (i.pred i0) x := by
  apply funext; intro k
  by_cases ki : k.succ = i
  Â· have ki' : k = i.pred i0 := by simp_rw [â† ki, Fin.pred_succ]
    rw [ki, ki']; rw [Function.update_self]; rw [Function.update_self]
  Â· rw [Function.update_of_ne ki]
    rw [Function.update_of_ne _]
    by_contra h
    simp only [h, Fin.succ_pred, not_true_eq_false] at ki

/-- Raw cons of two continuous multilinear maps -/
def smulCmmapFn [AddCommMonoid A] [Module ð•œ A] [TopologicalSpace A] [NormedAddCommGroup B]
    [NormedSpace ð•œ B] (x : ContinuousMultilinearMap ð•œ (fun _ : Fin 1 â†¦ A) ð•œ)
    (xs : ContinuousMultilinearMap ð•œ (fun _ : Fin n â†¦ A) B) : (âˆ€ _ : Fin (n + 1), A) â†’ B :=
  fun z â†¦ (x.toFun (fun _ â†¦ z 0)) â€¢ xs.toFun (fun i â†¦ z i.succ)

/-- `smulCmmapFn` is multiadditive -/
theorem smul_cmmap_add [AddCommMonoid A] [Module ð•œ A] [TopologicalSpace A] [NormedAddCommGroup B]
    [NormedSpace ð•œ B] (x : ContinuousMultilinearMap ð•œ (fun _ : Fin 1 â†¦ A) ð•œ)
    (xs : ContinuousMultilinearMap ð•œ (fun _ : Fin n â†¦ A) B) :
    âˆ€ (d : DecidableEq (Fin (n + 1))) (z : âˆ€ _ : Fin (n + 1), A) (i : Fin (n + 1)) (u v : A),
      smulCmmapFn x xs (@Function.update _ _ d z i (u + v)) =
        smulCmmapFn x xs (@Function.update _ _ d z i u) +
          smulCmmapFn x xs (@Function.update _ _ d z i v) := by
  intro d z i u v
  by_cases i0 : i = 0
  Â· rw [i0]
    have uv := x.map_update_add (fun _ â†¦ z 0) 0 u v
    simp only [update_0_0 z _] at uv
    simp only [Function.update_self, MultilinearMap.toFun_eq_coe, ContinuousMultilinearMap.coe_coe,
      uv, add_smul, smulCmmapFn, update_0_succ]
  Â· simp only [smul_add, update_nz_0 d z i0, MultilinearMap.toFun_eq_coe,
    ContinuousMultilinearMap.coe_coe, update_nz_succ d z _ i0, MultilinearMap.map_update_add,
    smul_add, smulCmmapFn]

/-- `smulCmmapFn` commutes with scalars -/
theorem smul_cmmap_smul [AddCommMonoid A] [Module ð•œ A] [TopologicalSpace A] [NormedAddCommGroup B]
    [NormedSpace ð•œ B] (x : ContinuousMultilinearMap ð•œ (fun _ : Fin 1 â†¦ A) ð•œ)
    (xs : ContinuousMultilinearMap ð•œ (fun _ : Fin n â†¦ A) B) :
    âˆ€ (d : DecidableEq (Fin (n + 1))) (z : âˆ€ _ : Fin (n + 1), A) (i : Fin (n + 1)) (s : ð•œ) (u : A),
      smulCmmapFn x xs (@Function.update _ _ d z i (s â€¢ u)) =
        s â€¢ smulCmmapFn x xs (@Function.update _ _ d z i u) := by
  intro d z i s u
  rw [smulCmmapFn]
  by_cases i0 : i = 0
  Â· rw [i0]
    have su := x.map_update_smul (fun _ â†¦ z 0) 0 s u
    rw [update_0_0 z _, update_0_0 z _] at su
    simp only [Function.update_self, MultilinearMap.toFun_eq_coe, ContinuousMultilinearMap.coe_coe,
      su, smul_eq_mul, update_0_succ d z _ _, smulCmmapFn, â† smul_assoc]
  Â· have su := xs.map_update_smul (fun j â†¦ z j.succ) (i.pred i0) s u
    simp only [MultilinearMap.toFun_eq_coe, ContinuousMultilinearMap.coe_coe, update_nz_0 d z i0,
      update_nz_succ d z _ i0, su, smul_comm _ s, smulCmmapFn]

/-- `smulCmmapFn` is continuous -/
theorem smul_cmmap_cont [AddCommMonoid A] [Module ð•œ A] [TopologicalSpace A] [NormedAddCommGroup B]
    [NormedSpace ð•œ B] (x : ContinuousMultilinearMap ð•œ (fun _ : Fin 1 â†¦ A) ð•œ)
    (xs : ContinuousMultilinearMap ð•œ (fun _ : Fin n â†¦ A) B) : Continuous (smulCmmapFn x xs) := by
  apply Continuous.smul; repeat continuity

/-- Cons two `ContinuousMultilinearMap`s together, combining values via `â€¢` -/
def smulCmmap (ð•œ A B : Type) [NontriviallyNormedField ð•œ] [AddCommMonoid A] [Module ð•œ A]
    [TopologicalSpace A] [NormedAddCommGroup B] [NormedSpace ð•œ B]
    (x : ContinuousMultilinearMap ð•œ (fun _ : Fin 1 â†¦ A) ð•œ)
    (xs : ContinuousMultilinearMap ð•œ (fun _ : Fin n â†¦ A) B) :
    ContinuousMultilinearMap ð•œ (fun _ : Fin (n + 1) â†¦ A) B where
  toFun := smulCmmapFn x xs
  map_update_add' := smul_cmmap_add x xs _
  map_update_smul' := smul_cmmap_smul x xs _
  cont := smul_cmmap_cont x xs

theorem smulCmmap_apply [AddCommMonoid A] [Module ð•œ A] [TopologicalSpace A] [NormedAddCommGroup B]
    [NormedSpace ð•œ B] (x : ContinuousMultilinearMap ð•œ (fun _ : Fin 1 â†¦ A) ð•œ)
    (xs : ContinuousMultilinearMap ð•œ (fun _ : Fin n â†¦ A) B) (z : âˆ€ _ : Fin (n + 1), A) :
    smulCmmap _ _ _ x xs z = (x fun _ â†¦ z 0) â€¢ xs fun i â†¦ z i.succ := by
  rw [smulCmmap, â†ContinuousMultilinearMap.toFun_eq_coe]; simp only; rfl

theorem smulCmmap_norm [NormedAddCommGroup A] [NormedSpace ð•œ A] [NormedAddCommGroup B]
    [NormedSpace ð•œ B] (x : ContinuousMultilinearMap ð•œ (fun _ : Fin 1 â†¦ A) ð•œ)
    (xs : ContinuousMultilinearMap ð•œ (fun _ : Fin n â†¦ A) B) :
    â€–smulCmmap ð•œ A B x xsâ€– â‰¤ â€–xâ€– * â€–xsâ€– := by
  apply ContinuousMultilinearMap.opNorm_le_bound; bound
  intro z; rw [smulCmmap_apply]
  have xb := ContinuousMultilinearMap.le_opNorm x fun _ : Fin 1 â†¦ z 0
  have xsb := ContinuousMultilinearMap.le_opNorm xs fun i : Fin n â†¦ z i.succ
  simp only [Finset.univ_unique, Fin.default_eq_zero, Finset.prod_const, Finset.card_singleton,
    pow_one] at xb xsb
  have e0 := Fin.prod_cons â€–z 0â€– fun i : Fin n â†¦ â€–z i.succâ€–
  simp only at e0
  have e1 : â€–z 0â€– = (fun i : Fin (n + 1) â†¦ â€–z iâ€–) 0 := rfl
  have e2 : (fun i : Fin n â†¦ â€–z i.succâ€–) = Fin.tail fun i : Fin (n + 1) â†¦ â€–z iâ€– := rfl
  nth_rw 1 [e1] at e0; nth_rw 1 [e2] at e0; rw [Fin.cons_self_tail (fun i â†¦ â€–z iâ€–)] at e0
  calc â€–(x fun _ : Fin 1 â†¦ z 0) â€¢ xs fun i : Fin n â†¦ z i.succâ€–
    _ â‰¤ â€–xâ€– * â€–z 0â€– * (â€–xsâ€– * Finset.univ.prod fun i : Fin n â†¦ â€–z i.succâ€–) := by
      rw [norm_smul]; bound
    _ = â€–xâ€– * â€–xsâ€– * (â€–z 0â€– * Finset.univ.prod fun i : Fin n â†¦ â€–z i.succâ€–) := by ring
    _ = â€–xâ€– * â€–xsâ€– * Finset.univ.prod fun i : Fin (n + 1) â†¦ â€–z iâ€– := by rw [â†e0]

/-- A term of the general `n`-linear map on `ð•œ Ã— 1ð•œ,
    equal to `z0^k * z1^(n-k)` when applied to `fun _ â†¦ (z0,z1)` -/
public noncomputable def termCmmap (ð•œ : Type) [NontriviallyNormedField ð•œ] [NormedAddCommGroup E]
    [NormedSpace ð•œ E] : âˆ€ n : â„•, â„• â†’ E â†’ ContinuousMultilinearMap ð•œ (fun _ : Fin n â†¦ ð•œ Ã— ð•œ) E
  | 0 => fun _ x â†¦ ContinuousMultilinearMap.constOfIsEmpty _ _ x
  | n + 1 => fun k x â†¦
    smulCmmap _ _ _ (if n < k then fstCmmap ð•œ ð•œ ð•œ else sndCmmap ð•œ ð•œ ð•œ) (termCmmap ð•œ n k x)

public theorem termCmmap_apply [NormedAddCommGroup E] [NormedSpace ð•œ E] [SMulCommClass ð•œ ð•œ E]
    [IsScalarTower ð•œ ð•œ E] (n k : â„•) (a b : ð•œ) (x : E) :
    (termCmmap ð•œ n k x fun _ â†¦ (a, b)) = a ^ min k n â€¢ b ^ (n - k) â€¢ x := by
  induction' n with n h
  Â· simp only [termCmmap, ContinuousMultilinearMap.constOfIsEmpty_apply, min_zero, pow_zero,
    zero_tsub, one_smul]
  Â· rw [termCmmap, smulCmmap_apply, h]
    by_cases nk : n < k
    Â· simp [nk]
      rw [fstCmmap_apply]
      have nsk : n.succ â‰¤ k := Nat.succ_le_iff.mpr nk
      rw [min_eq_right nk.le, min_eq_right nsk, Nat.sub_eq_zero_of_le nk.le,
        Nat.sub_eq_zero_of_le nsk]
      simp only [pow_zero, one_smul, â† smul_assoc, smul_eq_mul, Nat.succ_eq_add_one, pow_succ']
    Â· simp [nk]; simp at nk
      rw [sndCmmap_apply]
      have nsk : k â‰¤ n.succ := Nat.le_succ_of_le nk
      rw [min_eq_left nk, min_eq_left nsk]
      rw [smul_comm b _, â† smul_assoc b _ _, smul_eq_mul, â† pow_succ', â† Nat.sub_add_comm nk]

public theorem termCmmap_norm (ð•œ : Type) [NontriviallyNormedField ð•œ] [NormedAddCommGroup E]
    [NormedSpace ð•œ E] (n k : â„•) (x : E) : â€–termCmmap ð•œ n k xâ€– â‰¤ â€–xâ€– := by
  induction' n with n nh
  Â· simp only [termCmmap, le_refl, ContinuousMultilinearMap.norm_constOfIsEmpty]
  Â· rw [termCmmap]; simp only
    generalize ht : termCmmap ð•œ n k x = t; rw [ht] at nh
    have tn := smulCmmap_norm (if n < k then fstCmmap ð•œ ð•œ ð•œ else sndCmmap ð•œ ð•œ ð•œ) t
    by_cases nk : n < k
    Â· simp [nk] at tn âŠ¢; rw [fstCmmap_norm] at tn; simp at tn; exact _root_.trans tn nh
    Â· simp [nk] at tn âŠ¢; rw [sndCmmap_norm] at tn; simp at tn; exact _root_.trans tn nh

/-- `conj` as a `ContinuousLinearMap`. This is `starâ‚—áµ¢ â„‚`, but with a simpler type. -/
public def conjCLM : â„‚ â†’L[â„] â„‚ where
  toFun z := conj z
  map_add' := by simp only [map_add, forall_const]
  map_smul' := by simp only [Complex.real_smul, map_mul, RingHom.id_apply, Complex.conj_ofReal,
    implies_true]

public theorem conjCLM_apply (z : â„‚) : conjCLM z = conj z := by rfl

/-- The continuous linear map that evaluates a continuous multilinear map at a point -/
public def cmmapApplyCmap (ð•œ : Type) {I : Type} (A : I â†’ Type) (B : Type) [Fintype I]
    [NontriviallyNormedField ð•œ] [âˆ€ i, NormedAddCommGroup (A i)] [âˆ€ i, NormedSpace ð•œ (A i)]
    [NormedAddCommGroup B] [NormedSpace ð•œ B] (x : âˆ€ i, A i) : ContinuousMultilinearMap ð•œ A B â†’L[ð•œ] B
    where
  toFun f := f x
  map_add' := by simp
  map_smul' := by simp
  cont := by simp [continuous_eval_const]

@[simp] public theorem cmmapApplyCmap_apply {I : Type} (A : I â†’ Type) (B : Type) [Fintype I]
    [âˆ€ i, NormedAddCommGroup (A i)] [âˆ€ i, NormedSpace ð•œ (A i)]
    [NormedAddCommGroup B] [NormedSpace ð•œ B] (x : âˆ€ i, A i) (f : ContinuousMultilinearMap ð•œ A B) :
    cmmapApplyCmap ð•œ A B x f = f x := by rfl

/-- Prove `A x = 0` by `x = 0` for a continuous linear map `A` -/
public lemma ContinuousLinearMap.apply_eq_zero_of_eq_zero {ð•œ X Y : Type} [NormedField ð•œ]
    [TopologicalSpace X] [NormedAddCommGroup X] [Module ð•œ X] [NormedAddCommGroup Y] [Module ð•œ Y]
    (f : X â†’L[ð•œ] Y) {x : X} (h : x = 0) : f x = 0 := by
  rw [h, ContinuousLinearMap.map_zero]

/-- `.smulRight` is nonzero if it's inputs are -/
public lemma ContinuousLinearMap.smulRight_ne_zero {R A B : Type} [Ring R] [TopologicalSpace A]
    [AddCommMonoid A] [TopologicalSpace R] [Module R A] [TopologicalSpace B] [AddCommMonoid B]
    [Module R B] [ContinuousSMul R B] [NoZeroSMulDivisors R B] {c : A â†’L[R] R} {f : B}
    (c0 : c â‰  0) (f0 : f â‰  0) :
    c.smulRight f â‰  0 := by
  rcases ContinuousLinearMap.exists_ne_zero c0 with âŸ¨x,cxâŸ©
  simp only [Ne, ContinuousLinearMap.ext_iff, not_forall, ContinuousLinearMap.zero_apply,
    ContinuousLinearMap.smulRight_apply, smul_eq_zero, not_or]
  use x

/-- `1 â‰  0`, `ContinuousLinearMap` case -/
public lemma ContinuousLinearMap.one_ne_zero {R A : Type} [Ring R] [TopologicalSpace A]
    [AddCommMonoid A] [Module R A] [Nontrivial A] : (1 : A â†’L[R] A) â‰  0 := by
  simp only [Ne, ContinuousLinearMap.ext_iff, not_forall, ContinuousLinearMap.zero_apply,
    ContinuousLinearMap.one_apply]
  apply exists_ne

/-- `mkPiRing` is continuous -/
public lemma ContinuousMultilinearMap.continuous_mkPiRing {ð•œ Î¹ E : Type} [NontriviallyNormedField ð•œ]
    [Fintype Î¹] [NormedAddCommGroup E] [NormedSpace ð•œ E] [CompleteSpace E] :
    Continuous (fun z : E â†¦ ContinuousMultilinearMap.mkPiRing ð•œ Î¹ z) := by
  rw [Metric.continuous_iff]
  intro x e e0
  refine âŸ¨e / 2, by bound, fun y xy â†¦ ?_âŸ©
  simp only [dist_eq_norm] at xy
  refine lt_of_le_of_lt (b := e / 2) ?_ (by bound)
  rw [dist_eq_norm, ContinuousMultilinearMap.opNorm_le_iff (by bound)]
  intro m
  simp only [ContinuousMultilinearMap.sub_apply, ContinuousMultilinearMap.mkPiRing_apply,
    â† smul_sub]
  refine le_trans (norm_smul_le _ _) ?_
  rw [mul_comm]
  bound

/-!
### Conjugate a `ContinuousMultilinearMap` with complex `conj`
-/

/-- Conjugate a `ContinuousMultilinearMap` with complex `conj` -/
public def ContinuousMultilinearMap.conj_conj [Fintype Î¹]
    (m : ContinuousMultilinearMap â„‚ (fun _ : Î¹ â†¦ â„‚) â„‚) :
    ContinuousMultilinearMap â„‚ (fun _ : Î¹ â†¦ â„‚) â„‚ where
  toFun := fun z â†¦ conj (m fun i â†¦ conj (z i))
  map_update_add' g a x y := by
    simp only [â† map_add, Function.apply_update (fun _ z â†¦ conj z) (g := g), â† m.map_update_add]
  map_update_smul' g a s y := by
    simp only [smul_eq_mul, Function.apply_update (fun _ z â†¦ conj z) (g := g), map_mul]
    simp only [â† smul_eq_mul, m.map_update_smul]
    simp only [smul_eq_mul, map_mul, RingHomCompTriple.comp_apply, RingHom.id_apply]
  cont := by continuity

@[simp] public lemma ContinuousMultilinearMap.conj_conj_apply [Fintype Î¹]
    (m : ContinuousMultilinearMap â„‚ (fun _ : Î¹ â†¦ â„‚) â„‚) (x : Î¹ â†’ â„‚) :
    m.conj_conj x = conj (m fun i â†¦ conj (x i)) := by rfl
