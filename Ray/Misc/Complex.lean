module
public import Mathlib.Analysis.Analytic.Basic
public import Mathlib.Analysis.Calculus.Deriv.Basic
public import Mathlib.Analysis.Complex.Basic
public import Mathlib.Analysis.SpecialFunctions.Complex.CircleMap
public import Mathlib.Analysis.SpecialFunctions.Trigonometric.Basic
public import Mathlib.RingTheory.Norm.Defs
public import Mathlib.Topology.Algebra.Module.Determinant
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Analysis.SpecialFunctions.Complex.Arg
import Mathlib.Analysis.SpecialFunctions.Complex.CircleMap
import Mathlib.Analysis.SpecialFunctions.Complex.LogDeriv
import Mathlib.MeasureTheory.Integral.CircleIntegral
import Mathlib.RingTheory.Complex
import Mathlib.RingTheory.Norm.Transitivity

/-!
## Complex facts
-/

open Classical
open Metric (sphere)
open Complex (arg log I imCLM slitPlane)
open ContinuousLinearMap (lsmul)
open Set
open scoped ContDiff Real ComplexConjugate
noncomputable section

variable {X : Type} [TopologicalSpace X]

lemma Complex.arg_lt_zero_iff {z : ‚ÑÇ} : arg z < 0 ‚Üî z.im < 0 := by
  rw [‚Üê not_iff_not, not_lt, not_lt]
  exact arg_nonneg_iff

/-- A clean version of `(z / w).im` -/
lemma div_im_eq_inner (z w : ‚ÑÇ) : (z / w).im = inner ‚Ñù z (w * I) / w.normSq := by
  simp [Complex.div_im, Complex.inner]
  ring

/-- Spheres are empty iff the radius is negative -/
@[simp]
theorem Metric.sphere_eq_empty {S : Type} [RCLike S] {c : S} {r : ‚Ñù} : sphere c r = ‚àÖ ‚Üî r < 0 := by
  constructor
  ¬∑ intro rp; contrapose rp; simp at rp
    refine Nonempty.ne_empty ‚ü®c + r, ?_‚ü©
    simpa only [mem_sphere_iff_norm, add_sub_cancel_left, RCLike.norm_ofReal, abs_eq_self]
  ¬∑ intro n; contrapose n
    rw [‚Üê not_nonempty_iff_eq_empty] at n
    simpa only [not_lt, NormedSpace.sphere_nonempty, not_le] using n

/-- `range (circleMap c r _) = sphere c r` even when restricted to `Ioc 0 (2œÄ)` -/
public theorem circleMap_Ioc {c z : ‚ÑÇ} {r : ‚Ñù} (zs : z ‚àà sphere c r) :
    ‚àÉ t, t ‚àà Ioc 0 (2 * œÄ) ‚àß z = circleMap c r t := by
  by_cases rp : r < 0
  ¬∑ simp only [Metric.sphere_eq_empty.mpr rp, mem_empty_iff_false] at zs
  simp only [not_lt] at rp
  rw [‚Üêabs_of_nonneg rp, ‚Üê range_circleMap, mem_range] at zs
  rcases zs with ‚ü®t, ht‚ü©
  generalize ha : 2 * œÄ = a
  have ap : a > 0 := by rw [‚Üêha]; bound
  generalize hs : t + a - a * ‚åàt / a‚åâ = s
  use s; constructor
  ¬∑ simp only [mem_Ioc, sub_pos, tsub_le_iff_right, ‚Üê hs]
    constructor
    ¬∑ calc a * ‚åàt / a‚åâ
        _ < a * (t / a + 1) := by bound
        _ = a / a * t + a := by ring
        _ = t + a := by field_simp [ap.ne']
    ¬∑ calc a + a * ‚åàt / a‚åâ
        _ ‚â• a + a * (t / a) := by bound
        _ = a / a * t + a := by ring
        _ = t + a := by field_simp [ap.ne']
  ¬∑ simp only [‚Üêht, circleMap, Complex.ofReal_sub, Complex.ofReal_add, Complex.ofReal_mul,
      Complex.ofReal_intCast, add_right_inj, mul_eq_mul_left_iff, Complex.ofReal_eq_zero, ‚Üê hs]
    rw [mul_sub_right_distrib, right_distrib, Complex.exp_sub, Complex.exp_add]
    rw [mul_comm _ (‚åà_‚åâ:‚ÑÇ), mul_assoc, Complex.exp_int_mul, ‚Üê ha]
    simp only [Complex.ofReal_mul, Complex.ofReal_ofNat, Complex.exp_two_pi_mul_I, mul_one,
      one_zpow, div_one, true_or]

@[fun_prop] public lemma ContinuousAt.complex_conj {f : X ‚Üí ‚ÑÇ} {x : X} (h : ContinuousAt f x) :
    ContinuousAt (fun x ‚Ü¶ conj (f x)) x :=
  Complex.continuous_conj.continuousAt.comp h

/-!
### Derivatives mixing `‚Ñù` and `‚ÑÇ`
-/

/-- `Complex.ofReal` is real analytic -/
public lemma Complex.analyticAt_ofReal {x : ‚Ñù} : AnalyticAt ‚Ñù Complex.ofReal x := by
  have e : Complex.ofReal = fun x ‚Ü¶ Complex.ofRealCLM x := by simp
  rw [e]
  exact Complex.ofRealCLM.analyticAt x

/-- `Complex.ofReal` is real analytic -/
public lemma AnalyticAt.ofReal {E : Type} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {f : E ‚Üí ‚Ñù}
    {x : E} (a : AnalyticAt ‚Ñù f x) : AnalyticAt ‚Ñù (fun x ‚Ü¶ (f x : ‚ÑÇ)) x :=
  Complex.analyticAt_ofReal.comp a

/-- `Complex.ofReal` is real analytic -/
lemma Complex.contDiffAt_ofReal {x : ‚Ñù} : ContDiffAt ‚Ñù œâ Complex.ofReal x :=
  Complex.analyticAt_ofReal.contDiffAt

/-- `Complex.ofReal` is real analytic -/
lemma Complex.contDiff_ofReal : ContDiff ‚Ñù œâ Complex.ofReal := by
  rw [contDiff_iff_contDiffAt]
  intro x
  apply Complex.contDiffAt_ofReal

/-- Complex `norm` is real analytic -/
public lemma Complex.analyticAt_norm {z : ‚ÑÇ} (z0 : z ‚â† 0) : AnalyticAt ‚Ñù (fun z : ‚ÑÇ ‚Ü¶ ‚Äñz‚Äñ) z :=
  (contDiffAt_norm (ùïú := ‚Ñù) z0).analyticAt

/-- Complex `norm` is real analytic -/
lemma AnalyticAt.norm {ùïú E : Type} [RCLike ùïú] [NormedAddCommGroup E] [NormedSpace ùïú E]
    [NormedSpace ùïú ‚ÑÇ] [NormedSpace ‚Ñù E] {f : E ‚Üí ‚ÑÇ} {x : E} (a : AnalyticAt ùïú f x) (f0 : f x ‚â† 0) :
    AnalyticAt ‚Ñù (fun x ‚Ü¶ ‚Äñf x‚Äñ) x :=
  (Complex.analyticAt_norm f0).comp a.restrictScalars

/-- A complex derivative, treated as `‚ÑÇ ‚ÜíL[‚Ñù] ‚Üí ‚ÑÇ` -/
lemma Complex.real_hasFDerivAt {f : ‚ÑÇ ‚Üí ‚ÑÇ} {z : ‚ÑÇ} {f' : ‚ÑÇ} (h : HasDerivAt f f' z) :
    HasFDerivAt f (lsmul ‚Ñù ‚ÑÇ f') z := by
  convert h.hasFDerivAt.restrictScalars ‚Ñù
  ext
  exact mul_comm _ _

/-- The derivative of `.im` -/
lemma hasFDerivAt_im {z : ‚ÑÇ} : HasFDerivAt Complex.im imCLM z := by
  have e : Complex.im = (fun z ‚Ü¶ imCLM z) := by ext z; simp only [Complex.imCLM_apply]
  rw [e]; apply ContinuousLinearMap.hasFDerivAt

/-- The derivative of `arg`, via `log` -/
lemma hasFDerivAt_arg {z : ‚ÑÇ} (m : z ‚àà slitPlane) :
    HasFDerivAt arg (imCLM ‚àòL lsmul ‚Ñù ‚ÑÇ z‚Åª¬π) z := by
  have e : arg = (fun z ‚Ü¶ (log z).im) := by ext z; rw [Complex.log_im]
  rw [e]
  exact HasFDerivAt.comp _ hasFDerivAt_im (Complex.real_hasFDerivAt (Complex.hasDerivAt_log m))

/-- The derivative of `arg` along a curve -/
public lemma HasDerivAt.arg {p : ‚Ñù ‚Üí ‚ÑÇ} {p' : ‚ÑÇ} {t : ‚Ñù} (h : HasDerivAt p p' t)
    (m : p t ‚àà slitPlane) : HasDerivAt (fun t ‚Ü¶ arg (p t)) ((p t)‚Åª¬π * p').im t := by
  convert ((hasFDerivAt_arg m).comp t h.hasFDerivAt).hasDerivAt
  simp

/-!
### Determinants of complex derivatives
-/

@[simp] public lemma Complex.algebra_norm (z : ‚ÑÇ) : Algebra.norm ‚Ñù (z : ‚ÑÇ) = ‚Äñz‚Äñ ^ 2 := by
  simp [Algebra.norm_complex_eq, Complex.normSq_eq_norm_sq]

/-- If `f` is complex differentiable at a point, it's `fderiv` determinant is clean -/
public lemma Complex.fderiv_det {f : ‚ÑÇ ‚Üí ‚ÑÇ} {z : ‚ÑÇ} (df : DifferentiableAt ‚ÑÇ f z) :
    (fderiv ‚Ñù f z).det = ‚Äñderiv f z‚Äñ ^ 2 := by
  have d1 := df.hasDerivAt.hasFDerivAt.restrictScalars ‚Ñù
  have d2 := (df.restrictScalars ‚Ñù).hasFDerivAt
  rw [d2.unique d1]
  simp [ContinuousLinearMap.det, ContinuousLinearMap.coe_restrictScalars, Complex.algebra_norm,
    LinearMap.det_restrictScalars, LinearMap.det_ring, smul_eq_mul, one_mul]
