import Mathlib.Analysis.Normed.Field.Basic
import Mathlib.Analysis.Normed.Group.Basic
import Mathlib.Order.Directed
import Mathlib.Order.Filter.Basic
import Mathlib.Topology.MetricSpace.Basic
import Ray.Misc.Topology

/-!
## Facts about `Bornology.cobounded`
-/

open Bornology (cobounded)
open Filter (Tendsto atTop)
open Metric (ball closedBall)
open Set
open scoped Topology

variable {Î± : Type}
variable {X : Type} [NormedAddCommGroup X]
variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]

/-- `Filter.hasBasis_cobounded_norm` but with `r < â€–xâ€–` instead of `r â‰¤ â€–xâ€–` -/
lemma hasBasis_cobounded_norm_lt :
    (cobounded X).HasBasis (fun _ â†¦ True) (fun r â†¦ {x | r < â€–xâ€–}) := by
  have b := Filter.hasBasis_cobounded_norm (E := X)
  simp only [Filter.hasBasis_iff, setOf_subset, true_and] at b âŠ¢
  intro s
  rw [b s]
  constructor
  all_goals exact fun âŸ¨r, hâŸ© â†¦ âŸ¨r + 1, fun x lt â†¦ h x (by linarith)âŸ©

/-- Characterization of `â†’ cobounded` convergence -/
theorem tendsto_cobounded {f : Î± â†’ X} {l : Filter Î±} :
    Tendsto f l (cobounded X) â†” âˆ€ r, âˆ€á¶  x in l, r < â€–f xâ€– := by
  rw [hasBasis_cobounded_norm_lt.tendsto_right_iff]
  simp only [true_imp_iff, mem_setOf]

/-- Characterization of `atTop â†’ cobounded` convergence -/
theorem tendsto_atTop_cobounded {f : â„• â†’ X} :
    Tendsto f atTop (cobounded X) â†” âˆ€ r, âˆƒ N, âˆ€ n, N â‰¤ n â†’ r < â€–f nâ€– := by
  simpa only [mem_Ici, mem_setOf_eq, exists_true_left, forall_true_left, true_and] using
    Filter.HasBasis.tendsto_iff (f := f) Filter.atTop_basis hasBasis_cobounded_norm_lt

/-- `cobounded` convergence in terms of norm convergence -/
theorem tendsto_cobounded_iff_norm_tendsto_atTop {f : Filter Î±} {g : Î± â†’ X} :
    Tendsto (fun x â†¦ g x) f (cobounded X) â†” Tendsto (fun x â†¦ â€–g xâ€–) f atTop := by
  rw [Filter.atTop_basis_Ioi.tendsto_right_iff]
  simp only [hasBasis_cobounded_norm_lt.tendsto_right_iff, true_imp_iff, mem_setOf, mem_Ioi]

/-- Characterization of `s âˆˆ cobounded` -/
theorem mem_cobounded_iff {s : Set X} : s âˆˆ cobounded X â†” âˆƒ r, {x | r < â€–xâ€–} âŠ† s := by
  simp only [Filter.hasBasis_iff.mp hasBasis_cobounded_norm_lt s, true_and]

/-- Eventually `cobounded` the norm is as large as desired -/
theorem eventually_cobounded (r : â„) : âˆ€á¶  x : X in cobounded X, r < â€–xâ€– := by
  rw [Filter.eventually_iff, mem_cobounded_iff]; use r

/-- Eventually `cobounded` is the same as eventually `ğ“[â‰ ] 0` for `xâ»Â¹` -/
theorem eventually_cobounded_iff_nhds_zero {p : ğ•œ â†’ Prop} :
    (âˆ€á¶  x in cobounded ğ•œ, p x) â†” âˆ€á¶  x in ğ“[â‰ ] 0, p xâ»Â¹ := by
  rw [hasBasis_cobounded_norm_lt.eventually_iff, Metric.nhdsWithin_basis_ball.eventually_iff]
  constructor
  Â· intro âŸ¨r,_,hâŸ©
    refine âŸ¨(max r 1)â»Â¹, by bound, fun x âŸ¨m,x0âŸ© â†¦ ?_âŸ©
    refine @h xâ»Â¹ ?_
    simp only [Metric.mem_ball, dist_zero_right, mem_compl_iff, mem_singleton_iff, mem_setOf_eq,
      norm_inv] at m x0 âŠ¢
    rw [â† lt_inv_commâ‚€ (by bound) (by simpa)] at m
    exact lt_of_le_of_lt (le_max_left _ _) m
  Â· intro âŸ¨i,i0,hâŸ©
    refine âŸ¨iâ»Â¹, trivial, fun x m â†¦ ?_âŸ©
    refine inv_inv x â–¸ @h xâ»Â¹ ?_
    simp only [mem_setOf_eq, mem_inter_iff, Metric.mem_ball, dist_zero_right, norm_inv,
      mem_compl_iff, mem_singleton_iff, inv_eq_zero] at m âŠ¢
    have x0 : x â‰  0 := by have : 0 < â€–xâ€– := lt_trans (by bound) m; simpa
    rw [â† inv_lt_commâ‚€ i0 (by simpa)]
    exact âŸ¨m, x0âŸ©

/-- Convergence `cobounded` is the same as convergence at `0` for the reciprocal function -/
theorem tendsto_cobounded_iff_tendsto_nhds_zero {l : Filter Î±}
    {f : ğ•œ â†’ Î±} : Tendsto f (cobounded ğ•œ) l â†” Tendsto (fun x â†¦ f xâ»Â¹) (ğ“[{0}á¶œ] 0) l := by
  rw [Filter.HasBasis.tendsto_left_iff hasBasis_cobounded_norm_lt,
    Metric.nhdsWithin_basis_ball.tendsto_left_iff]
  constructor
  Â· intro h t tl; rcases h t tl with âŸ¨r, _, mâŸ©
    by_cases rp : 0 < r
    Â· use râ»Â¹; simp only [rp, inv_pos, true_and]; intro x xs; refine m ?_
      simp only [mem_inter_iff, mem_ball_zero_iff, mem_compl_iff, mem_singleton_iff] at xs
      simp only [â† lt_inv_commâ‚€ (norm_pos_iff.mpr xs.2) rp, xs.1, mem_setOf_eq, norm_inv]
    Â· use 1; simp only [zero_lt_one, true_and]; intro x xs; refine m ?_
      simp only [mem_inter_iff, mem_ball_zero_iff, mem_compl_iff, mem_singleton_iff] at xs
      simp only [mem_setOf_eq, norm_inv]; simp only [not_lt] at rp
      exact lt_of_le_of_lt rp (inv_pos.mpr (norm_pos_iff.mpr xs.2))
  Â· intro h t tl; rcases h t tl with âŸ¨r, rp, mâŸ©; use râ»Â¹; simp only [true_and]
    intro x xs; simp only [mem_setOf_eq] at xs
    have m := @m xâ»Â¹ ?_; Â· simp only [inv_inv] at m; exact m
    simp only [mem_inter_iff, mem_ball_zero_iff, norm_inv, mem_compl_iff, mem_singleton_iff,
      inv_eq_zero]
    have np : 0 < â€–xâ€– := _root_.trans (inv_pos.mpr rp) xs
    simp [inv_lt_commâ‚€ np rp, xs, norm_pos_iff.mp np]

/-- `â»Â¹` tendsto `cobounded` near `0` -/
theorem inv_tendsto_cobounded : Tendsto (fun x : ğ•œ â†¦ xâ»Â¹) (ğ“[{(0 : ğ•œ)}á¶œ] 0) (cobounded ğ•œ) := by
  rw [â† tendsto_cobounded_iff_tendsto_nhds_zero (f := fun x : ğ•œ â†¦ x)]
  exact Filter.tendsto_id

/-- `â»Â¹` tendsto `0` near `cobounded` -/
theorem inv_tendsto_cobounded' : Tendsto (fun x : ğ•œ â†¦ xâ»Â¹) (cobounded ğ•œ) (ğ“ 0) := by
  simp only [tendsto_cobounded_iff_tendsto_nhds_zero, inv_inv]
  exact Filter.tendsto_id.mono_left nhdsWithin_le_nhds

/-- We either tend to infinity or have a cluster point -/
lemma tendsto_cobounded_or_mapClusterPt [ProperSpace X] (f : Î± â†’ X) (l : Filter Î±) :
    Tendsto f l (cobounded X) âˆ¨ âˆƒ z, MapClusterPt z l f := by
  by_cases t : Tendsto f l (cobounded X)
  Â· exact .inl t
  Â· simp only [t, false_or]
    simp only [tendsto_cobounded, not_forall, Filter.not_eventually, not_lt,
      â† add_mem_closedBall_iff_norm (a := (0 : X)), zero_add] at t
    obtain âŸ¨r,tâŸ© := t
    have t := IsCompact.exists_mapClusterPt_of_frequently (isCompact_closedBall _ _) t
    obtain âŸ¨z,m,câŸ© := t
    exact âŸ¨z,câŸ©
