import Mathlib.Data.Complex.Basic
import Mathlib.Data.Real.Basic
import Mathlib.Data.Real.NNReal
import Mathlib.Data.Real.Pi.Bounds
import Mathlib.Data.Set.Basic
import Mathlib.Topology.MetricSpace.Basic
import Mathlib.Topology.Semicontinuous
import Ray.Tactic.Bound

/-!
## Various topology lemmas
-/

open Metric (ball closedBall sphere mem_sphere mem_ball)
open Filter (atTop Tendsto eventually_of_forall)
open OrderDual (ofDual toDual)
open Set
open scoped Real NNReal Topology Filter
noncomputable section

/-- Turn `s âŠ† setOf p` back into a clean forall -/
theorem subset_setOf {X : Type} {p : X â†’ Prop} {s : Set X} : s âŠ† setOf p â†” âˆ€ x, x âˆˆ s â†’ p x :=
  Iff.rfl

/-- A proposition is true `âˆ€á¶  in ğ“Ë¢` if it is true on a larger open set -/
theorem eventually_nhdsSet_iff {X : Type} [TopologicalSpace X] {s : Set X} {p : X â†’ Prop} :
    (âˆ€á¶  x in ğ“Ë¢ s, p x) â†” âˆƒ t, IsOpen t âˆ§ s âŠ† t âˆ§ âˆ€ x, x âˆˆ t â†’ p x := by
  simp only [Filter.eventually_iff, mem_nhdsSet_iff_exists, subset_setOf]

/-- Continuous functions on compact sets are bounded above -/
theorem ContinuousOn.bounded {X : Type} [TopologicalSpace X] {f : X â†’ â„} {s : Set X}
    (fc : ContinuousOn f s) (sc : IsCompact s) : âˆƒ b : â„, b â‰¥ 0 âˆ§ âˆ€ x, x âˆˆ s â†’ f x â‰¤ b := by
  simpa using (sc.bddAbove_image fc).exists_ge 0

/-- Uniform cauchy sequences are cauchy sequences at points -/
theorem UniformCauchySeqOn.cauchySeq {X Y : Type} [MetricSpace Y]
    {f : â„• â†’ X â†’ Y} {s : Set X} (u : UniformCauchySeqOn f atTop s) :
    âˆ€ x, x âˆˆ s â†’ CauchySeq fun n â†¦ f n x := by
  intro x xs
  rw [Metric.cauchySeq_iff]
  rw [Metric.uniformCauchySeqOn_iff] at u
  intro e ep; rcases u e ep with âŸ¨N, HâŸ©
  exists N; intro a aN b bN
  exact H a aN b bN x xs

/-- Uniform cauchy sequences on compact sets are uniformly bounded -/
theorem UniformCauchySeqOn.bounded {X Y : Type} [TopologicalSpace X] [NormedAddCommGroup Y]
    {f : â„• â†’ X â†’ Y} {s : Set X} (u : UniformCauchySeqOn f atTop s) (fc : âˆ€ n, ContinuousOn (f n) s)
    (sc : IsCompact s) : âˆƒ b : â„, b â‰¥ 0 âˆ§ âˆ€ n x, x âˆˆ s â†’ â€–f n xâ€– â‰¤ b := by
  set c := fun n â†¦ Classical.choose ((fc n).norm.bounded sc)
  have cs : âˆ€ n, 0 â‰¤ c n âˆ§ âˆ€ x, x âˆˆ s â†’ â€–f n xâ€– â‰¤ c n := fun n â†¦
    Classical.choose_spec ((fc n).norm.bounded sc)
  rw [Metric.uniformCauchySeqOn_iff] at u
  rcases u 1 (by norm_num) with âŸ¨N, HâŸ©; clear u
  set bs := Finset.image c (Finset.range (N + 1))
  have c0 : c 0 âˆˆ bs := by simp; exists 0; simp
  set b := 1 + bs.max' âŸ¨_, c0âŸ©
  exists b; constructor
  Â· exact add_nonneg (by norm_num) (_root_.trans (cs 0).1 (Finset.le_max' _ _ c0))
  Â· intro n x xs
    by_cases nN : n â‰¤ N
    Â· have cn : c n âˆˆ bs := by simp; exists n; simp [Nat.lt_add_one_iff.mpr nN]
      exact _root_.trans ((cs n).2 x xs) (_root_.trans (Finset.le_max' _ _ cn) (by bound))
    Â· simp at nN
      specialize H N le_rfl n (by bound) x xs
      have cN : c N âˆˆ bs := by simp; exists N; simp
      have bN := _root_.trans ((cs N).2 x xs) (Finset.le_max' _ _ cN)
      rw [dist_eq_norm] at H
      calc â€–f n xâ€– = â€–f N x - (f N x - f n x)â€– := by rw [sub_sub_cancel]
        _ â‰¤ â€–f N xâ€– + â€–f N x - f n xâ€– := norm_sub_le _ _
        _ â‰¤ bs.max' _ + 1 := add_le_add bN H.le
        _ = 1 + bs.max' _ := by ring
        _ = b := rfl

/-- `{b | (a,b) âˆˆ s}` is open if `s` is open -/
theorem IsOpen.snd_preimage {A B : Type} [TopologicalSpace A] [TopologicalSpace B] {s : Set (A Ã— B)}
    (o : IsOpen s) (a : A) : IsOpen {b | (a, b) âˆˆ s} :=
  o.preimage (Continuous.Prod.mk a)

/-- `{b | (a,b) âˆˆ s}` is closed if `s` is closed -/
theorem IsClosed.snd_preimage {A B : Type} [TopologicalSpace A] [TopologicalSpace B]
    {s : Set (A Ã— B)} (c : IsClosed s) (a : A) : IsClosed {b | (a, b) âˆˆ s} :=
  c.preimage (Continuous.Prod.mk a)

/-- Tendsto commutes with â»Â¹ away from zero -/
theorem tendsto_iff_tendsto_inv {A B : Type} [NontriviallyNormedField B]
    {l : Filter A} {f : A â†’ B} {a : B} (a0 : a â‰  0) :
    Tendsto (fun x â†¦ (f x)â»Â¹) l (ğ“ aâ»Â¹) â†” Tendsto f l (ğ“ a) := by
  refine' âŸ¨fun h â†¦ _, fun h â†¦ h.invâ‚€ a0âŸ©
  have h := h.invâ‚€ (inv_ne_zero a0)
  field_simp [a0] at h; exact h

/-- `ContinuousAt` in terms of `ğ“[{x}á¶œ] x` (useful when `f x` is a special case) -/
theorem continuousAt_iff_tendsto_nhdsWithin {A B : Type} [TopologicalSpace A] [TopologicalSpace B]
    {f : A â†’ B} {x : A} : ContinuousAt f x â†” Tendsto f (ğ“[{x}á¶œ] x) (ğ“ (f x)) := by
  rw [ContinuousAt]; constructor
  exact fun t â†¦ t.mono_left nhdsWithin_le_nhds
  intro t; rw [â† nhdsWithin_compl_singleton_sup_pure]
  exact Filter.Tendsto.sup t (tendsto_pure_nhds _ _)

/-- If `f x âˆˆ s` for `s` open and `f` continuous at `z`, `âˆˆ` holds locally.
    This is `IsOpen.eventually_mem`, but assuming only `ContinuousAt`. -/
theorem ContinuousAt.eventually_mem {A B : Type} [TopologicalSpace A] [TopologicalSpace B]
    {f : A â†’ B} {x : A} (fc : ContinuousAt f x) {s : Set B} (o : IsOpen s) (m : f x âˆˆ s) :
    âˆ€á¶  y in ğ“ x, f y âˆˆ s := by
  exact fc (o.mem_nhds m)

/-- If `f x âˆˆ s` for `s âˆˆ ğ“ (f x)` and `f` continuous at `z`, `âˆˆ` holds locally -/
theorem ContinuousAt.eventually_mem_nhd {A B : Type} [TopologicalSpace A] [TopologicalSpace B]
    {f : A â†’ B} {x : A} (fc : ContinuousAt f x) {s : Set B} (m : s âˆˆ ğ“ (f x)) :
    âˆ€á¶  y in ğ“ x, f y âˆˆ s :=
  (eventually_mem_nhds.2 (fc m)).mono fun _x hx â†¦ mem_preimage.1 (mem_of_mem_nhds hx)

/-- `ContinuousAt.comp` for curried functions -/
theorem ContinuousAt.compâ‚‚ {A B C D : Type} [TopologicalSpace A] [TopologicalSpace B]
    [TopologicalSpace C] [TopologicalSpace D] {f : B Ã— C â†’ D} {g : A â†’ B} {h : A â†’ C} {x : A}
    (fc : ContinuousAt f (g x, h x)) (gc : ContinuousAt g x) (hc : ContinuousAt h x) :
    ContinuousAt (fun x â†¦ f (g x, h x)) x :=
  ContinuousAt.comp fc (gc.prod hc)

/-- `ContinuousAt.comp_of_eq` for curried functions -/
theorem ContinuousAt.compâ‚‚_of_eq {A B C D : Type} [TopologicalSpace A] [TopologicalSpace B]
    [TopologicalSpace C] [TopologicalSpace D] {f : B Ã— C â†’ D} {g : A â†’ B} {h : A â†’ C} {x : A}
    {y : B Ã— C} (fc : ContinuousAt f y) (gc : ContinuousAt g x) (hc : ContinuousAt h x)
    (e : (g x, h x) = y) : ContinuousAt (fun x â†¦ f (g x, h x)) x := by
  rw [â†e] at fc; exact fc.compâ‚‚ gc hc

/-- `ContinuousAt.comp` for curried functions and `ContinuousWithinAt` -/
theorem ContinuousAt.compâ‚‚_continuousWithinAt {A B C D : Type} [TopologicalSpace A]
    [TopologicalSpace B] [TopologicalSpace C] [TopologicalSpace D] {f : B Ã— C â†’ D} {g : A â†’ B}
    {h : A â†’ C} {x : A} {s : Set A} (fc : ContinuousAt f (g x, h x))
    (gc : ContinuousWithinAt g s x) (hc : ContinuousWithinAt h s x) :
    ContinuousWithinAt (fun x â†¦ f (g x, h x)) s x :=
  ContinuousAt.comp_continuousWithinAt fc (gc.prod hc)

/-- `ContinuousAt.comp_of_eq` for curried functions and `ContinuousWithinAt` -/
theorem ContinuousAt.compâ‚‚_continuousWithinAt_of_eq {A B C D : Type} [TopologicalSpace A]
    [TopologicalSpace B] [TopologicalSpace C] [TopologicalSpace D] {f : B Ã— C â†’ D} {g : A â†’ B}
    {h : A â†’ C} {x : A} {s : Set A} {y : B Ã— C} (fc : ContinuousAt f y)
    (gc : ContinuousWithinAt g s x) (hc : ContinuousWithinAt h s x) (e : (g x, h x) = y) :
    ContinuousWithinAt (fun x â†¦ f (g x, h x)) s x := by
  rw [â† e] at fc; exact fc.compâ‚‚_continuousWithinAt gc hc

/-- Curried continuous functions are continuous in the first argument -/
theorem Continuous.along_fst {A B C : Type} [TopologicalSpace A] [TopologicalSpace B] [TopologicalSpace C]
    {f : A Ã— B â†’ C} (fc : Continuous f) {b : B} : Continuous fun a â†¦ f (a, b) :=
  fc.comp (continuous_id.prod_mk continuous_const)

/-- Curried continuous functions are continuous in the second argument -/
theorem Continuous.along_snd {A B C : Type} [TopologicalSpace A] [TopologicalSpace B] [TopologicalSpace C]
    {f : A Ã— B â†’ C} (fc : Continuous f) {a : A} : Continuous fun b â†¦ f (a, b) :=
  fc.comp (continuous_const.prod_mk continuous_id)

/-- In a compact space, uniqueness of limit points implies convergence -/
theorem le_nhds_of_clusterPt_unique {A : Type} [TopologicalSpace A] [CompactSpace A] {l : Filter A}
    {y : A} (u : âˆ€ x, ClusterPt x l â†’ x = y) : l â‰¤ ğ“ y := by
  refine Filter.le_iff_ultrafilter.2 fun f hf â†¦ ?_
  obtain rfl : f.lim = y := u _ ((ClusterPt.of_le_nhds f.le_nhds_lim).mono hf)
  exact f.le_nhds_lim

/-- In a compact space, uniqueness of limit points implies convergence -/
theorem tendsto_of_cluster_pt_unique {A B : Type} [TopologicalSpace B]
    [CompactSpace B] {l : Filter A} {f : A â†’ B} {y : B}
    (u : âˆ€ x, MapClusterPt x l f â†’ x = y) : Tendsto f l (ğ“ y) :=
  le_nhds_of_clusterPt_unique u

/-- The reverse direction of `IsClosed.Icc_subset_of_forall_mem_nhdsWithin` -/
theorem IsClosed.Icc_subset_of_forall_mem_nhds_within' {X : Type}
    [ConditionallyCompleteLinearOrder X] [TopologicalSpace X] [OrderTopology X] [DenselyOrdered X]
    {a b : X} {s : Set X} (sc : IsClosed (s âˆ© Icc a b)) (sb : b âˆˆ s)
    (so : âˆ€ x, x âˆˆ s âˆ© Ioc a b â†’ s âˆˆ ğ“[Iio x] x) : Icc a b âŠ† s := by
  set s' := ofDual â»Â¹' s
  have rev : Icc (toDual b) (toDual a) âŠ† s' := by
    apply IsClosed.Icc_subset_of_forall_mem_nhdsWithin
    Â· have e : s' âˆ© Icc (toDual b) (toDual a) = ofDual â»Â¹' (s âˆ© Icc a b) := by
        apply Set.ext; intro x; simp only [Set.dual_Icc, Set.preimage_inter]
      rw [e]; exact IsClosed.preimage continuous_ofDual sc
    Â· simp only [Set.mem_preimage, OrderDual.ofDual_toDual, sb]
    Â· intro x m
      simp only [Set.mem_preimage, Set.mem_inter_iff, Set.mem_Ico, OrderDual.toDual_le,
        OrderDual.lt_toDual] at m
      simp only [mem_nhdsWithin_iff_eventually, eventually_nhds_iff, Set.mem_inter_iff,
        Set.mem_Ioc] at so âŠ¢
      rcases so (ofDual x) âŸ¨m.1, m.2.2, m.2.1âŸ© with âŸ¨n, h, o, nxâŸ©
      use ofDual â»Â¹' n
      refine' âŸ¨_, o.preimage continuous_ofDual, mem_preimage.mpr nxâŸ©
      intro y m xy; simp only [Set.mem_Ioi] at xy; simp only [Set.mem_preimage]
      simp only [Set.mem_Iio, Set.mem_preimage, OrderDual.ofDual_lt_ofDual] at h
      exact h _ m xy
  intro x m; simp only [Set.mem_Icc] at m; specialize @rev (toDual x)
  simp only [Set.dual_Icc, Set.mem_preimage, Set.mem_Icc, and_imp, OrderDual.ofDual_toDual] at rev
  exact rev m.1 m.2

/-- `fst` is a closed map if `B` is compact -/
theorem IsClosedMap.fst {A B : Type} [TopologicalSpace A] [TopologicalSpace B] [CompactSpace B] :
    IsClosedMap fun p : A Ã— B â†¦ p.1 :=
  -- The file where we prove `isClosedMap_snd_of_compactSpace` in `Mathlib`
  -- doesn't import `Homeomorph`
  -- probably, we should reorder imports to make `Homeomorph` available very early
  isClosedMap_snd_of_compactSpace.comp (Homeomorph.prodComm _ _).isClosedMap

lemma IsPreconnected.sUnion_of_pairwise_exists_isPreconnected {X : Type*} [TopologicalSpace X]
    {S : Set (Set X)} (hSc : âˆ€ s âˆˆ S, IsPreconnected s)
    (h : S.Pairwise fun s t â†¦ s.Nonempty â†’ t.Nonempty â†’
      âˆƒ u, u âŠ† â‹ƒâ‚€ S âˆ§ (s âˆ© u).Nonempty âˆ§ (u âˆ© t).Nonempty âˆ§ IsPreconnected u) :
    IsPreconnected (â‹ƒâ‚€ S) := by
  refine isPreconnected_of_forall_pair fun x hx y hy â†¦ ?_
  rcases mem_sUnion.1 hx with âŸ¨s, hs, hxsâŸ©
  rcases mem_sUnion.1 hy with âŸ¨t, ht, hytâŸ©
  rcases eq_or_ne s t with rfl | hst
  Â· exact âŸ¨s, subset_sUnion_of_mem hs, hxs, hyt, hSc s hsâŸ©
  Â· rcases h hs ht hst âŸ¨x, hxsâŸ© âŸ¨y, hytâŸ© with âŸ¨u, huS, hsu, hut, huâŸ©
    refine âŸ¨s âˆª u âˆª t, ?_, ?_, ?_, ?_âŸ©
    Â· simp [*, subset_sUnion_of_mem]
    Â· simp [*]
    Â· simp [*]
    Â· refine ((hSc s hs).union' hsu hu).union' (hut.mono ?_) (hSc t ht)
      exact inter_subset_inter_left _ (subset_union_right _ _)

lemma IsPreconnected.iUnion_of_pairwise_exists_isPreconnected {Î¹ X : Type*} [TopologicalSpace X]
    {s : Î¹ â†’ Set X} (hsc : âˆ€ i, IsPreconnected (s i))
    (h : Pairwise fun i j â†¦ (s i).Nonempty â†’ (s j).Nonempty â†’
      âˆƒ u, u âŠ† â‹ƒ i, s i âˆ§ (s i âˆ© u).Nonempty âˆ§ (u âˆ© s j).Nonempty âˆ§ IsPreconnected u) :
    IsPreconnected (â‹ƒ i, s i) := by
  apply IsPreconnected.sUnion_of_pairwise_exists_isPreconnected (forall_range_iff.2 hsc)
  rintro _ âŸ¨i, rflâŸ© _ âŸ¨j, rflâŸ© hij
  exact h (ne_of_apply_ne s hij)

/-- Open preconnected sets form a basis for `ğ“Ë¢ t` in any locally connected space,
    if `t` is preconnected -/
theorem local_preconnected_nhdsSet {X : Type} [TopologicalSpace X] [lc : LocallyConnectedSpace X]
    {s t : Set X} (tc : IsPreconnected t) (st : s âˆˆ ğ“Ë¢ t) :
    âˆƒ c, IsOpen c âˆ§ t âŠ† c âˆ§ c âŠ† s âˆ§ IsPreconnected c := by
  rw [â† subset_interior_iff_mem_nhdsSet] at st
  have hsub : t âŠ† â‹ƒ x : t, connectedComponentIn (interior s) x := fun x hx â†¦
    mem_iUnion.2 âŸ¨âŸ¨x, hxâŸ©, mem_connectedComponentIn (st hx)âŸ©
  refine âŸ¨_, isOpen_iUnion fun _ â†¦ isOpen_interior.connectedComponentIn, hsub,
    iUnion_subset fun x â†¦ ?_, ?_âŸ©
  Â· exact (connectedComponentIn_subset _ _).trans interior_subset
  Â· apply IsPreconnected.iUnion_of_pairwise_exists_isPreconnected
    Â· exact fun _ â†¦ isPreconnected_connectedComponentIn
    Â· exact fun x y _ _ _ â†¦ âŸ¨t, hsub, âŸ¨x, mem_connectedComponentIn (st x.2), x.2âŸ©,
        âŸ¨y, y.2, mem_connectedComponentIn (st y.2)âŸ©, tcâŸ©

/-- Open connected sets form a basis for `ğ“Ë¢ t` in any locally connected space,
    if `t` is connected -/
theorem local_connected_nhdsSet {X : Type} [TopologicalSpace X] [LocallyConnectedSpace X]
    {s t : Set X} (tc : IsConnected t) (st : s âˆˆ ğ“Ë¢ t) :
    âˆƒ c, IsOpen c âˆ§ t âŠ† c âˆ§ c âŠ† s âˆ§ IsConnected c :=
  let âŸ¨c, hco, htc, hcs, hcâŸ© := local_preconnected_nhdsSet tc.2 st
  âŸ¨c, hco, htc, hcs, tc.1.mono htc, hcâŸ©

/-- Lower semicontinuity composes with continuity -/
theorem LowerSemicontinuousAt.comp {X Y Z : Type} [TopologicalSpace X] [TopologicalSpace Y]
    [LinearOrder Z] {f : Y â†’ Z} {g : X â†’ Y} {x : X}
    (fc : LowerSemicontinuousAt f (g x)) (gc : ContinuousAt g x) :
    LowerSemicontinuousAt (fun x â†¦ f (g x)) x :=
  fun _ lt â†¦ gc.eventually (fc _ lt)

/-- Lower semicontinuity composes with continuity -/
theorem LowerSemicontinuous.comp {X Y Z : Type} [TopologicalSpace X] [TopologicalSpace Y]
    [LinearOrder Z] {f : Y â†’ Z} {g : X â†’ Y}
    (fc : LowerSemicontinuous f) (gc : Continuous g) : LowerSemicontinuous fun x â†¦ f (g x) :=
  fun x â†¦ (fc (g x)).comp gc.continuousAt

/-- Continuous functions locally injective near a compact set are injective on a neighborhood -/
theorem locally_injective_on_compact {X Y : Type} [TopologicalSpace X] [TopologicalSpace Y]
    [T2Space Y] {f : X â†’ Y} {s : Set X} (fc : âˆ€ x, x âˆˆ s â†’ ContinuousAt f x) (sc : IsCompact s)
    (inj : InjOn f s) (loc : âˆ€ x, x âˆˆ s â†’ âˆƒ u, u âˆˆ ğ“ x âˆ§ InjOn f u) :
    âˆƒ t, IsOpen t âˆ§ s âŠ† t âˆ§ InjOn f t := by
  -- We work by two-level compact induction on injectivity.  For the outer level, we ask that a
  -- neighborhood of a subset of s is distinct from a neighborhood of all of s.
  generalize hh : (fun u : Set X â†¦ âˆƒ t,
    IsOpen t âˆ§ u âŠ† t âˆ§ âˆ€á¶  x in ğ“Ë¢ s, âˆ€ y, y âˆˆ t â†’ f x = f y â†’ x = y) = h
  suffices hs : h s
  Â· rw [â† hh] at hs; rcases hs with âŸ¨t0, o0, st0, hâŸ©
    simp only [Filter.eventually_iff, mem_nhdsSet_iff_exists] at h
    rcases h with âŸ¨t1, o1, st1, hâŸ©
    use t0 âˆ© t1, o0.inter o1, subset_inter st0 st1
    intro x xm y ym
    exact h (inter_subset_right _ _ xm) y (inter_subset_left _ _ ym)
  apply @IsCompact.induction_on _ _ _ sc h
  Â· rw [â†hh]; use âˆ…
    simp only [empty_subset, and_true_iff, isOpen_empty, mem_empty_iff_false, IsEmpty.forall_iff,
      imp_true_iff, Filter.eventually_true, true_and_iff]
  Â· rw [â† hh]; intro u0 u1 u01 h; rcases h with âŸ¨t, o, ut, hâŸ©; use t, o, _root_.trans u01 ut, h
  Â· rw [â† hh]; intro u0 u1 h0 h1; rcases h0 with âŸ¨t0, o0, ut0, h0âŸ©; rcases h1 with âŸ¨t1, o1, ut1, h1âŸ©
    use t0 âˆª t1, o0.union o1, union_subset_union ut0 ut1
    refine' h0.mp (h1.mp (eventually_of_forall fun x h1 h0 y m â†¦ _))
    cases' m with m m; exact h0 _ m; exact h1 _ m
  -- For the inner level, we build up the set of points w.r.t. some neighborhood of x is injective
  rw [â† hh]
  clear hh; intro x m; simp only
  generalize hg : (fun u : Set X â†¦
    âˆƒ t : Set X, IsOpen t âˆ§ x âˆˆ t âˆ§ âˆ€á¶  x in ğ“Ë¢ u, âˆ€ y, y âˆˆ t â†’ f x = f y â†’ x = y) = g
  suffices gs : g s
  Â· rw [â† hg] at gs; rcases gs with âŸ¨t, o, m, gâŸ©
    use t, nhdsWithin_le_nhds (o.mem_nhds m), t, o, subset_refl _, g
  apply @IsCompact.induction_on _ _ _ sc g
  Â· rw [â† hg]; use univ
    simp only [isOpen_univ, mem_univ, nhdsSet_empty, Filter.eventually_bot, and_self_iff]
  Â· rw [â† hg]; clear hg; simp only; intro s0 s1 s01 g; rcases g with âŸ¨t, o, m, gâŸ©
    use t, o, m, Filter.Eventually.filter_mono (nhdsSet_mono s01) g
  Â· rw [â† hg]; clear hg; simp only; intro s0 s1 g0 g1
    rcases g0 with âŸ¨t0, o0, m0, g0âŸ©; rcases g1 with âŸ¨t1, o1, m1, g1âŸ©
    use t0 âˆ© t1, o0.inter o1, mem_inter m0 m1
    simp only [nhdsSet_union, Filter.eventually_sup]; constructor
    exact g0.mp (eventually_of_forall fun x i y m â†¦ i _ (inter_subset_left _ _ m))
    exact g1.mp (eventually_of_forall fun x i y m â†¦ i _ (inter_subset_right _ _ m))
  Â· rw [â† hg]; clear hg; simp only; intro y ym
    by_cases xy : x = y
    Â· -- We're injective near (x,x) by loc, which ensures an injective neighborhood of each x
      rw [â† xy]; rcases loc x m with âŸ¨u, un, uiâŸ©
      rcases mem_nhds_iff.mp un with âŸ¨v, vu, vo, xvâŸ©
      use v, nhdsWithin_le_nhds (vo.mem_nhds xv), v, vo, xv
      apply Filter.eventually_of_mem (vo.mem_nhdsSet.mpr (subset_refl _))
      exact ui.mono vu
    Â· -- We're injective near (x,y) for x â‰  y by continuity and injectivity on s
      rcases t2_separation (inj.ne m ym xy) with âŸ¨ux, uy, uxo, uyo, xu, yu, uxyâŸ©
      rcases mem_nhds_iff.mp (tendsto_nhds.mp (fc _ m) ux uxo xu) with âŸ¨tx, xf, xo, xtâŸ©
      rcases mem_nhds_iff.mp (tendsto_nhds.mp (fc _ ym) uy uyo yu) with âŸ¨ty, yf, yo, ytâŸ©
      use ty, nhdsWithin_le_nhds (yo.mem_nhds yt), tx, xo, xt
      apply Filter.eventually_of_mem (yo.mem_nhdsSet.mpr (subset_refl _))
      intro y ym x xm e; contrapose e
      replace xf := xf xm
      replace yf := yf ym
      simp only [mem_preimage] at xf yf
      exact (disjoint_iff_forall_ne.mp uxy xf yf).symm

open Filter in
/-- `p` and `q` occur frequently along two filters iff `p âˆ§ q` occurs frequently in the product
    filter -/
theorem Prod.frequently {A B : Type} {f : Filter A} {g : Filter B} {p : A â†’ Prop} {q : B â†’ Prop} :
    (âˆƒá¶  x : A Ã— B in f Ã—Ë¢ g, p x.1 âˆ§ q x.2) â†” (âˆƒá¶  a in f, p a) âˆ§ âˆƒá¶  b in g, q b := by
  simp only [frequently_iff_neBot, â† prod_neBot, â† prod_inf_prod, prod_principal_principal]
  rfl

/-- The product of `MapClusterPt` and `Tendsto` is `MapClusterPt` -/
theorem MapClusterPt.prod {A B C : Type} [TopologicalSpace B] [TopologicalSpace C]
    {f : A â†’ B} {g : A â†’ C} {a : Filter A} {b : B} {c : C}
    (fa : MapClusterPt b a f) (ga : Tendsto g a (ğ“ c)) :
    MapClusterPt (b, c) a fun x â†¦ (f x, g x) := by
  rw [mapClusterPt_iff] at fa âŠ¢; intro s n
  rcases mem_nhds_prod_iff.mp n with âŸ¨u, un, v, vn, subâŸ©
  apply (fa _ un).mp
  apply (Filter.tendsto_iff_forall_eventually_mem.mp ga v vn).mp
  exact eventually_of_forall fun x gv fu â†¦ sub (mk_mem_prod fu gv)

/-- If we converge to `g`, we're eventually greater than anything less than `g` -/
theorem Filter.Tendsto.exists_lt {X : Type} [LinearOrder X] [TopologicalSpace X]
    [OrderClosedTopology X] {f : â„• â†’ X} {g : X} (tend : Tendsto f atTop (ğ“ g)) :
    âˆ€ {x}, x < g â†’ âˆƒ n, x < f n := fun hx â†¦
  (tend.eventually (eventually_gt_nhds hx)).exists

/-- `â‰  â†’ eventual â‰ ` -/
theorem Ne.eventually_ne {X : Type} [TopologicalSpace X] [T2Space X] {x y : X} (h : x â‰  y) :
    âˆ€á¶  q : X Ã— X in ğ“ (x, y), q.1 â‰  q.2 :=
  (isOpen_ne_fun continuous_fst continuous_snd).mem_nhds h

/-- In a metric space, `sphere âŠ† ball` -/
theorem Metric.sphere_subset_ball {X : Type*} [PseudoMetricSpace X] {z : X} {a b : â„} (ab : a < b) :
    sphere z a âŠ† ball z b := fun _ _ â†¦ by simp_all

lemma frequently_lt_nhds {X : Type*} [Preorder X] [TopologicalSpace X] (x : X) [(ğ“[<] x).NeBot] :
    âˆƒá¶  y in ğ“ x, y < x :=
  Filter.frequently_iff_neBot.2 â€¹_â€º

lemma frequently_gt_nhds {X : Type*} [Preorder X] [TopologicalSpace X] (x : X) [(ğ“[>] x).NeBot] :
    âˆƒá¶  y in ğ“ x, x < y :=
  Filter.frequently_iff_neBot.2 â€¹_â€º

/-- A set is closed if the closure doesn't add new points -/
theorem isClosed_iff_closure_diff {X : Type} [TopologicalSpace X] {s : Set X} :
    IsClosed s â†” closure s \ s = âˆ… := by
  rw [diff_eq_empty, closure_subset_iff_isClosed]

/-- The `âŠ¥` filter has no cluster_pts -/
theorem ClusterPt.bot {X : Type} [TopologicalSpace X] {x : X} : Â¬ClusterPt x âŠ¥ := fun h â†¦
  (h.neBot.mono inf_le_right).ne rfl
