module
public import Mathlib.Analysis.Convex.Basic
public import Mathlib.Analysis.Normed.Module.Convex
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Analysis.Normed.Group.Basic
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.Analysis.Normed.Module.Convex
import Mathlib.Tactic.Bound
import Ray.Misc.Connected
import Ray.Misc.Topology

/-!
## Continuation of a function from a convex set to its closure

We give an abstract version of "analytic continuation" from a convex set to its compact closure,
assuming that local continuation is possible at each boundary point.  We do not refer to analytic
functions directly at all: instead we speak of functions which everywhere satisfy a predicate
`p : (E â†’ Î±) â†’ E â†’ Prop` where `E` is a normed space and `Î± : Type`.

Convexity is used only to guarantee a "good open cover" in the sense of
https://ncatlab.org/nlab/show/good+open+cover: a family of neighborhoods such that intersections
of neighborhoods are contractable.  Since our base set `s` is convex, we can use balls as good
neighborhoods, and all intersections are convex and thus contractable.

It would be better to define good neighborhoods directly and show that nice spaces have them,
but this may require a lot of machinery to cover manifolds in particular: the nLab page uses
the existence of Riemannian metrics.
-/

open Classical
open Filter (Tendsto atTop)
open Metric (ball closedBall isOpen_ball mem_ball mem_ball_self closedBall_zero)
open Set
open scoped Real Topology
noncomputable section

variable {E : Type} [NormedAddCommGroup E] [NormedSpace â„ E]
variable {Î± : Type} {p : (E â†’ Î±) â†’ E â†’ Prop} {s : Set E} {f : E â†’ Î±} {z : E}

-- Continuation of a functional equation from an open convex set to its closure
section Continuation

/-- Information we need to continue a function from a convex set `s` to `closure s`, while
    preserving local properties of the function.  Such properties are represented by an abstract
    `p : (E â†’ Î±) â†’ E â†’ Prop`, where `p f x` means `f` is a valid germ at `x`. -/
public structure Base (p : (E â†’ Î±) â†’ E â†’ Prop) (s : Set E) (f : E â†’ Î±) : Prop where
  /-- The base set is convex -/
  convex : Convex â„ s
  /-- Its closure is compact, so that we can stitch together finitely many local continuations -/
  compact : IsCompact (closure s)
  /-- `p f x` is a local property of `f` near `x` -/
  congr : âˆ€ {f g x}, p f x â†’ f =á¶ [ğ“ x] g â†’ p g x
  /-- `f` is valid near each `x âˆˆ s` -/
  start : âˆ€á¶  x in ğ“Ë¢ s, p f x
  /-- Given `x âˆˆ closure s`, we can continue `f` to a neighorhood of `x` -/
  point : âˆ€ {x}, x âˆˆ closure s â†’ âˆƒ g, (âˆ€á¶  z in ğ“ x, p g z) âˆ§ âˆƒá¶  z in ğ“ x, z âˆˆ s âˆ§ g z = f z
  /-- If `f0, f1` are valid on an open preconnected set, and match somewhere,
      they match everywhere -/
  unique : âˆ€ {f0 f1 : E â†’ Î±} {t : Set E}, IsOpen t â†’ IsPreconnected t â†’
    (âˆ€ x, x âˆˆ t â†’ p f0 x) â†’ (âˆ€ x, x âˆˆ t â†’ p f1 x) â†’ (âˆƒ x, x âˆˆ t âˆ§ f0 x = f1 x) â†’ EqOn f0 f1 t

/-- There is a ball around each `x âˆˆ closure s` with an associated defined `g` -/
lemma Base.ball (b : Base p s f) (x : closure s) :
    âˆƒ g r, 0 < r âˆ§ (âˆ€ z, z âˆˆ ball (x : E) r â†’ p g z) âˆ§ g =á¶ [ğ“Ë¢ (s âˆ© ball (x : E) r)] f := by
  rcases x with âŸ¨x, mâŸ©; simp only
  rcases b.point m with âŸ¨g, pg, eâŸ©
  rcases Metric.eventually_nhds_iff_ball.mp pg with âŸ¨r, rp, pgâŸ©
  rcases Filter.frequently_iff.mp e (Metric.ball_mem_nhds _ rp) with âŸ¨y, yb, ys, eâŸ©
  use g, r, rp, fun z zr â†¦ pg z zr
  simp only [Filter.EventuallyEq, Filter.eventually_iff, mem_nhdsSet_iff_forall]
  intro z âŸ¨zs, zrâŸ©; simp only [â† Filter.eventually_iff]
  have n : {z | p g z âˆ§ p f z} âˆˆ ğ“Ë¢ (s âˆ© Metric.ball x r) := by
    refine Filter.inter_mem ?_ ?_
    Â· exact nhdsSet_mono inter_subset_right (Filter.mem_of_superset isOpen_ball.mem_nhdsSet_self pg)
    Â· exact nhdsSet_mono inter_subset_left b.start
  rcases local_preconnected_nhdsSet (b.convex.inter (convex_ball _ _)).isPreconnected n with
    âŸ¨u, uo, iu, up, ucâŸ©
  have eq := b.unique uo uc (fun _ m â†¦ (up m).1) (fun _ m â†¦ (up m).2) âŸ¨y, iu âŸ¨ys, ybâŸ©, eâŸ©
  exact eq.eventuallyEq_of_mem (uo.mem_nhds (iu âŸ¨zs, zrâŸ©))

/-- A particular `g` that continues `f` near `x` -/
def Base.g (b : Base p s f) (x : closure s) : E â†’ Î± :=
  choose (b.ball x)

/-- The radius on which `g` is valid around `x` -/
def Base.r (b : Base p s f) (x : closure s) : â„ :=
  choose (choose_spec (b.ball x))

/-- The radius is positive -/
lemma Base.rp (b : Base p s f) (x : closure s) : 0 < b.r x :=
  (choose_spec (choose_spec (b.ball x))).1

/-- `g` is valid on `ball x r`-/
lemma Base.gp (b : Base p s f) (x : closure s) (m : z âˆˆ Metric.ball (x : E) (b.r x)) :
    p (b.g x) z :=
  (choose_spec (choose_spec (b.ball x))).2.1 _ m

/-- `g` matches `f` where they are both defined -/
lemma Base.gf (b : Base p s f) (x : closure s) :
    b.g x =á¶ [ğ“Ë¢ (s âˆ© Metric.ball (x : E) (b.r x))] f :=
  (choose_spec (choose_spec (b.ball x))).2.2

/-- There exists a finite subcover of the `g` balls -/
lemma Base.exists_cover (b : Base p s f) :
    âˆƒ c : Finset (closure s), closure s âŠ† â‹ƒ (x) (_ : x âˆˆ c), Metric.ball (x : E) (b.r x) := by
  refine b.compact.elim_finite_subcover (fun x : closure s â†¦ Metric.ball (x : E) (b.r x))
    (fun _ â†¦ isOpen_ball) ?_
  intro x m; exact mem_iUnion_of_mem âŸ¨x, mâŸ© (mem_ball_self (b.rp âŸ¨x, mâŸ©))

/-- Choose a finite subcover of the `g` balls -/
def Base.c (b : Base p s f) : Finset (closure s) :=
  choose b.exists_cover

/-- The union of our chosen finite set of `g` balls -/
def Base.t (b : Base p s f) : Set E :=
  â‹ƒ (x) (_ : x âˆˆ b.c), Metric.ball (x : E) (b.r x)

/-- Map a point in the union of our ball cover to one ball that contains it -/
def Base.y (b : Base p s f) (m : z âˆˆ b.t) : closure s :=
  choose (mem_iUnion.mp m)

lemma Base.yt (b : Base p s f) (m : z âˆˆ b.t) : z âˆˆ Metric.ball (b.y m : E) (b.r (b.y m)) := by
  simp only [Base.t, Base.y, mem_iUnion] at m âŠ¢; exact choose_spec (choose_spec m)

lemma Base.ot (b : Base p s f) : IsOpen b.t :=
  isOpen_iUnion fun _ â†¦ isOpen_iUnion fun _ â†¦ isOpen_ball

theorem Base.cover (b : Base p s f) : closure s âŠ† b.t :=
  choose_spec b.exists_cover

/-- Given two intersecting balls centered in `closure s`, their intersection touches `s` -/
theorem Convex.inter_ball (c : Convex â„ s) (x0 x1 : closure s) {r0 r1 : â„} (r0p : 0 < r0)
    (r1p : 0 < r1) (ne : âˆƒ z, z âˆˆ ball (x0 : E) r0 âˆ© ball (x1 : E) r1) :
    âˆƒ w, w âˆˆ s âˆ© ball (x0 : E) r0 âˆ© ball (x1 : E) r1 := by
  rcases x0 with âŸ¨x0, m0âŸ©; rcases x1 with âŸ¨x1, m1âŸ©; simp only
  have x01 : â€–x1 - x0â€– < r0 + r1 := by
    rcases ne with âŸ¨z, m0, m1âŸ©; simp only [mem_ball, dist_eq_norm] at m0 m1
    calc â€–x1 - x0â€–
      _ = â€–z - x0 - (z - x1)â€– := by abel_nf
      _ â‰¤ â€–z - x0â€– + â€–z - x1â€– := (norm_sub_le _ _)
      _ < r0 + r1 := add_lt_add m0 m1
  have sub : âˆ€ (x : E) {a b : â„}, 0 < a â†’ 0 < b â†’ (a / (a + b)) â€¢ x - x = -((b / (a + b)) â€¢ x) := by
    intro x a b ap bp; have rnz := (add_pos ap bp).ne'
    calc (a / (a + b)) â€¢ x - x
      _ = (a / (a + b) - (a + b) / (a + b)) â€¢ x := by simp only [one_smul, sub_smul, div_self rnz]
      _ = -((b / (a + b)) â€¢ x) := by rw [â† sub_div, sub_add_cancel_left, neg_div, neg_smul]
  have le : âˆ€ {a : â„}, 0 < a â†’ a / (r0 + r1) * â€–x1 - x0â€– < a := by
    intro a ap; apply lt_of_lt_of_le (mul_lt_mul_of_pos_left x01 (div_pos ap (add_pos r0p r1p)))
    rw [div_mul_cancelâ‚€ _ (add_pos r0p r1p).ne']
  have e : âˆ€á¶  p : E Ã— E in ğ“ (x0, x1),
      (r1 / (r0 + r1)) â€¢ p.1 + (r0 / (r0 + r1)) â€¢ p.2 âˆˆ ball x0 r0 âˆ© ball x1 r1 := by
    refine ContinuousAt.eventually_mem ?_ ((isOpen_ball.inter isOpen_ball).mem_nhds ?_)
    Â· exact ((continuous_fst.const_smul _).add (continuous_snd.const_smul _)).continuousAt
    Â· simp only [mem_inter_iff, mem_ball, dist_eq_norm, â† sub_add_eq_add_sub _ x0 _,
        add_sub_assoc _ _ x1]
      nth_rw 1 [add_comm r0 r1]; simp only [sub _ r0p r1p, sub _ r1p r0p]
      simp only [add_comm r1 r0, neg_add_eq_sub, â† sub_eq_add_neg, â† smul_sub, norm_smul,
        Real.norm_eq_abs, abs_div, abs_of_pos r0p, abs_of_pos r1p, abs_of_pos (add_pos r0p r1p),
        norm_sub_rev (x0 : E) x1]
      use le r0p, le r1p
  have f : âˆƒá¶  p : E Ã— E in ğ“ (x0, x1), p.1 âˆˆ s âˆ§ p.2 âˆˆ s := by
    simp only [nhds_prod_eq]; rw [Prod.frequently (p := fun x â†¦ x âˆˆ s) (q := fun x â†¦ x âˆˆ s)]
    use mem_closure_iff_frequently.mp m0, mem_closure_iff_frequently.mp m1
  rcases(f.and_eventually e).exists with âŸ¨âŸ¨z0, z1âŸ©, âŸ¨m0, m1âŸ©, mâŸ©
  refine âŸ¨_, âŸ¨?_, m.1âŸ©, m.2âŸ©
  apply c m0 m1; bound; bound
  simp only [â† add_div, add_comm r1 r0, div_self (add_pos r0p r1p).ne']

/-- Our full continuation `u` throughout `closure s` -/
public def Base.u (b : Base p s f) : E â†’ Î± := fun z â†¦
  if m : z âˆˆ b.t then b.g (b.y m) z else f z

/-- The continuation `u` is equal to each `g` -/
theorem Base.ug (b : Base p s f) (x : closure s) :
    EqOn b.u (b.g x) (b.t âˆ© Metric.ball (x : E) (b.r x)) := by
  intro z âŸ¨zt, mâŸ©; simp only [Base.u, zt, dif_pos]
  refine b.unique (isOpen_ball.inter isOpen_ball)
    ((convex_ball _ _).inter (convex_ball _ _)).isPreconnected
    (fun _ m â†¦ b.gp _ (inter_subset_left m)) (fun _ m â†¦ b.gp _ (inter_subset_right m))
    ?_ âŸ¨b.yt zt, mâŸ©
  rcases b.convex.inter_ball (b.y zt) x (b.rp _) (b.rp _) âŸ¨_, âŸ¨b.yt zt, mâŸ©âŸ© with âŸ¨w, mâŸ©
  exact âŸ¨w, âŸ¨m.1.2, m.2âŸ©, _root_.trans ((b.gf _).self_of_nhdsSet âŸ¨m.1.1, m.1.2âŸ©)
    ((b.gf x).self_of_nhdsSet âŸ¨m.1.1, m.2âŸ©).symmâŸ©

/-- `u` is equal to our original `f` -/
public theorem Base.uf (b : Base p s f) : b.u =á¶ [ğ“Ë¢ s] f := by
  simp only [Filter.EventuallyEq, Filter.eventually_iff, mem_nhdsSet_iff_forall]
  intro z m; simp only [â† Filter.eventually_iff]
  set x : closure s := âŸ¨z, subset_closure mâŸ©
  have zs : z âˆˆ Metric.ball (x : E) (b.r x) := mem_ball_self (b.rp x)
  have ug := (b.ug x).eventuallyEq_of_mem ((b.ot.inter isOpen_ball).mem_nhds
    âŸ¨b.cover (subset_closure m), zsâŸ©)
  exact ug.trans ((b.gf x).filter_mono (nhds_le_nhdsSet âŸ¨m, zsâŸ©))

/-- `u` is valid in `ğ“Ë¢ (closure s)` -/
public theorem Base.up (b : Base p s f) : âˆ€á¶  z in ğ“Ë¢ (closure s), p b.u z := by
  apply Filter.eventually_of_mem (b.ot.mem_nhdsSet.mpr b.cover)
  intro x m; refine b.congr (b.gp (b.y m) (b.yt m)) ?_
  exact ((b.ug _).eventuallyEq_of_mem ((b.ot.inter isOpen_ball).mem_nhds âŸ¨m, b.yt mâŸ©)).symm

/-!
### Continuation throughout a ball, starting from a point
-/

variable [ProperSpace E]
variable {c : E} {s' : Set E} {r t : â„}

/-- Information we need to continue a function throughout an open ball. -/
public structure Continuation [NormedSpace â„ E] [ProperSpace E] (p : (E â†’ Î±) â†’ E â†’ Prop)
    (c : E) (r : â„) (fs : E â†’ Î±) : Prop where
  /-- The radius is positive -/
  pos : 0 < r
  /-- `p f x` is a local property of `f` near `x` -/
  congr : âˆ€ {f g x}, p f x â†’ f =á¶ [ğ“ x] g â†’ p g x
  /-- The seed `fs` is valid near `x` -/
  start : âˆ€á¶  y in ğ“ c, p fs y
  /-- Given `f` valid on convex `s`, we can continue `f` to a neighorhood of any `x âˆˆ closure s` -/
  point : âˆ€ {f t x}, 0 < t â†’ t < r â†’ (âˆ€á¶  x in ğ“Ë¢ (ball c t), p f x) â†’ x âˆˆ closedBall c t â†’
    âˆƒ g, (âˆ€á¶  z in ğ“ x, p g z) âˆ§ âˆƒá¶  z in ğ“ x, z âˆˆ ball c t âˆ§ g z = f z
  /-- If `f0, f1` are valid on an open preconnected set, and match somewhere,
      they match everywhere -/
  unique : âˆ€ {f0 f1 : E â†’ Î±} {t : Set E}, IsOpen t â†’ IsPreconnected t â†’
    (âˆ€ x, x âˆˆ t â†’ p f0 x) â†’ (âˆ€ x, x âˆˆ t â†’ p f1 x) â†’ (âˆƒ x, x âˆˆ t âˆ§ f0 x = f1 x) â†’ EqOn f0 f1 t

namespace Continuation

variable {fs : E â†’ Î±}
variable {i : Continuation p c r fs}
attribute [bound_forward] Continuation.pos

/-- We can grow out through a set `t` -/
@[expose] public def Grow (_ : Continuation p c r fs) (s : Set E) : Prop :=
  âˆƒ f, f c = fs c âˆ§ âˆ€á¶  x in ğ“Ë¢ s, p f x

/-- Grow is monotonic -/
lemma Grow.mono (g : i.Grow s) (sub : s' âŠ† s) : i.Grow s' := by
  obtain âŸ¨f, e, hâŸ© := g
  exact âŸ¨f, e, h.filter_mono (nhdsSet_mono sub)âŸ©

/-- We can grow through a small open ball -/
lemma grow_small (i : Continuation p c r fs) : âˆƒ t > 0, t â‰¤ r âˆ§ i.Grow (ball c t) := by
  obtain âŸ¨t,t0,gâŸ© := Metric.eventually_nhds_iff_ball.mp i.start
  refine âŸ¨min t r, by bound, by bound, fs, ?_âŸ©
  simp only [isOpen_ball.nhdsSet_eq, Filter.eventually_principal]
  aesop

/-- If we can grow up to `ball c r`, we can grow through the closure -/
lemma Grow.closed (g : i.Grow (ball c t)) (tr : t < r) : i.Grow (closedBall c t) := by
  by_cases t0 : t â‰¤ 0
  Â· obtain âŸ¨u,u0,ur,gâŸ© := i.grow_small
    exact g.mono (Metric.closedBall_subset_ball (by linarith))
  simp only [not_le] at t0
  obtain âŸ¨f, e, pfâŸ© := g
  have b : Base p (ball c t) f := {
    convex := convex_ball _ _
    compact := by
      apply (isCompact_closedBall c r).of_isClosed_subset isClosed_closure
      simp only [closure_ball _ t0.ne', Metric.closedBall_subset_closedBall tr.le]
    congr := i.congr
    start := pf
    point := fun {x m} â†¦ i.point t0 tr pf (by simpa [closure_ball _ t0.ne'] using m)
    unique := i.unique }
  refine âŸ¨b.u, ?_, ?_âŸ©
  Â· exact (b.uf.self_of_nhdsSet (mem_ball_self t0)).trans e
  Â· refine b.up.filter_mono (nhdsSet_mono ?_)
    simp only [closure_ball _ t0.ne', subset_refl]

/-- If we can grow through a closed ball, we can grow through a larger open ball -/
lemma Grow.open (g : i.Grow (closedBall c t)) : âˆƒ u > t, i.Grow (ball c u) := by
  obtain âŸ¨f, e, hâŸ© := g
  obtain âŸ¨s',o,sub,hâŸ© := eventually_nhdsSet_iff_exists.mp h
  obtain âŸ¨u,lt,sub'âŸ© := exists_ball_superset sub o
  refine âŸ¨u, lt, f, e, ?_âŸ©
  simp only [isOpen_ball.nhdsSet_eq, Filter.eventually_principal]
  intro x m
  exact h x (sub' m)

/-- If we grow up until everything before `t`, we grow to `t` -/
lemma Grow.sup {u : â„• â†’ â„} (mono : Monotone u) (tend : Tendsto u atTop (ğ“ t)) (t0 : 0 < t)
    (grow : âˆ€ n, i.Grow (ball c (u n))) : i.Grow (ball c t) := by
  have ut : âˆ€ n, u n â‰¤ t := fun n â†¦ mono.ge_of_tendsto tend n
  have ex : âˆ€ t' < t, âˆƒ n, t' < u n := fun t' lt â†¦ tend.exists_lt lt
  set n : E â†’ â„• := fun x â†¦ if lt : â€–x - câ€– < t then Nat.find (ex _ lt) else Nat.find (ex 0 t0)
  have u0 : âˆ€ x, 0 < u (n x) := by
    intro x
    simp only [n]
    split_ifs with lt
    Â· exact lt_of_le_of_lt (norm_nonneg _) (Nat.find_spec (ex _ lt))
    Â· exact Nat.find_spec (ex 0 t0)
  have nlt : âˆ€ x, â€–x - câ€– < t â†’ â€–x - câ€– < u (n x) := by
    intro x lt
    simp only [lt, n]
    exact Nat.find_spec (ex _ lt)
  set fn : E â†’ E â†’ Î± := fun x â†¦ choose (grow (n x))
  have spec : âˆ€ x, fn x c = fs c âˆ§ âˆ€á¶  y in ğ“Ë¢ (ball c (u (n x))), p (fn x) y :=
    fun x â†¦ choose_spec (grow (n x))
  set f : E â†’ Î± := fun x â†¦ fn x x
  refine âŸ¨f, (spec _).1, ?_âŸ©
  simp only [isOpen_ball.nhdsSet_eq, Filter.eventually_principal, mem_ball, dist_eq_norm]
  intro x xlt
  apply i.congr (f := fn x) (g := f)
  Â· specialize spec x
    simp only [isOpen_ball.nhdsSet_eq, Filter.eventually_principal, mem_ball, dist_eq_norm] at spec
    exact spec.2 x (nlt x xlt)
  Â· have elt : âˆ€á¶  y in ğ“ x, â€–y - câ€– < u (n x) :=
      ContinuousAt.eventually_lt (f := fun x â†¦ â€–x - câ€–) (by fun_prop) continuousAt_const (nlt x xlt)
    filter_upwards [elt] with y ylt
    have sx := (spec x).2
    have sy := (spec y).2
    simp only [isOpen_ball.nhdsSet_eq, Filter.eventually_principal, mem_ball, dist_eq_norm] at sx sy
    refine i.unique (f0 := fn x) (f1 := fn y) (t := ball c (min (u (n x)) (u (n y)))) isOpen_ball
      (convex_ball _ _).isPreconnected ?_ ?_ âŸ¨c, ?_âŸ© ?_
    Â· intro z m
      apply sx
      simp only [mem_ball, dist_eq_norm, lt_inf_iff] at m
      exact m.1
    Â· intro z m
      apply sy
      simp only [mem_ball, dist_eq_norm, lt_inf_iff] at m
      exact m.2
    Â· simp [u0, (spec _).1]
    Â· have yt := lt_of_lt_of_le ylt (ut _)
      simp only [yt, â†“reduceDIte, mem_ball, dist_eq_norm, lt_inf_iff, ylt, true_and, gt_iff_lt, n]
      simpa using Nat.find_spec (ex _ yt)

/-- We can grow through the whole ball -/
public lemma grow : i.Grow (ball c r) := by
  set s : Set â„ := {t | 0 < t âˆ§ t â‰¤ r âˆ§ i.Grow (ball c t)}
  have above : BddAbove s := bddAbove_def.mpr âŸ¨r, by aesopâŸ©
  obtain âŸ¨t0, t0p, t0r, g0âŸ© := i.grow_small
  have start : t0 âˆˆ s := by aesop
  have ne : s.Nonempty := âŸ¨t0, startâŸ©
  have pos : 0 < sSup s := lt_csSup_of_lt above start t0p
  have sup_le : sSup s â‰¤ r := csSup_le ne (by aesop)
  have down : âˆ€ a b, 0 < a â†’ a â‰¤ b â†’ b âˆˆ s â†’ a âˆˆ s := by
    intro a b a0 ab bs
    exact âŸ¨a0, le_trans ab bs.2.1, bs.2.2.mono (Metric.ball_subset_ball ab)âŸ©
  have self : sSup s âˆˆ s := by
    obtain âŸ¨u,mono,tend,growâŸ© := exists_seq_tendsto_sSup ne above
    exact âŸ¨pos, sup_le, Grow.sup mono tend pos (fun n â†¦ (grow n).2.2)âŸ©
  by_cases sup_lt : sSup s < r
  Â· obtain âŸ¨t,sup_t,gâŸ© := (self.2.2.closed sup_lt).open
    have lt : sSup s < min t r := by bound
    obtain âŸ¨u,su,utrâŸ© := exists_between lt
    simp only [lt_inf_iff] at utr
    have us : u âˆˆ s := âŸ¨by linarith, by linarith, g.mono (Metric.ball_subset_ball utr.1.le)âŸ©
    linarith [le_csSup above us]
  Â· simp only [not_lt] at sup_lt
    exact (down r (sSup s) i.pos sup_lt self).2.2
