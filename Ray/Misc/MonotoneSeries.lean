module
public import Mathlib.Topology.Algebra.InfiniteSum.Defs
public import Mathlib.Topology.MetricSpace.Basic
import Mathlib.Algebra.Order.Group.CompleteLattice
import Mathlib.MeasureTheory.Integral.Lebesgue.Countable
import Ray.Misc.Bound
import Ray.Misc.Topology

/-!
## Monotone convergence theorem for series
-/

open Filter (Tendsto atTop)
open Set
open scoped ENNReal NNReal Topology

variable {Î¹ : Type} [Countable Î¹] [MeasurableSpace Î¹] [MeasurableSingletonClass Î¹]

/-- Monotone convergence theorem for series, `â„â‰¥0âˆ` `HasSum` version -/
theorem ENNReal.hasSum_iSup {f : â„• â†’ Î¹ â†’ â„â‰¥0âˆ} (mono : Monotone f) :
    HasSum (fun i â†¦ â¨† n, f n i) (â¨† n, âˆ‘' i, f n i) := by
  rw [ENNReal.summable.hasSum_iff]
  simp only [â† MeasureTheory.lintegral_count]
  exact MeasureTheory.lintegral_iSup (fun _ â†¦ measurable_of_countable _) mono

/-- Monotone convergence theorem for series, `â„â‰¥0âˆ` `âˆ‘'` version -/
theorem ENNReal.tsum_iSup {f : â„• â†’ Î¹ â†’ â„â‰¥0âˆ} (mono : Monotone f) :
    âˆ‘' i, â¨† n, f n i = â¨† n, âˆ‘' i, f n i := (hasSum_iSup mono).tsum_eq

/-- Monotone convergence theorem for series, `â„â‰¥0` `HasSum` version -/
theorem NNReal.hasSum_ciSup {f : â„• â†’ Î¹ â†’ â„â‰¥0} {a : â„• â†’ â„â‰¥0} (sum : âˆ€ n, HasSum (f n) (a n))
    (mono : Monotone f) (bound : BddAbove (range a)) : HasSum (fun i â†¦ â¨† n, f n i) (â¨† n, a n) := by
  set f' : â„• â†’ Î¹ â†’ â„â‰¥0âˆ := fun n i â†¦ f n i
  set a' : â„• â†’ â„â‰¥0âˆ := fun n â†¦ a n
  have mono' : Monotone f' := by
    intro n m nm i
    simp only [ENNReal.coe_le_coe, f']
    apply mono nm
  have sum' : âˆ€ n, HasSum (f' n) (a' n) := by
    intro n
    simpa [Function.comp_def] using ENNReal.continuous_coe.continuousAt.tendsto.comp (sum n)
  have bdd_f : âˆ€ {i}, BddAbove (range (fun n â†¦ f n i)) := by
    refine fun {i} â†¦ bound.range_mono _ fun n â†¦ ?_
    trans âˆ‘ i âˆˆ {i}, f n i
    Â· simp
    Â· exact sum_le_hasSum _ (by simp) (sum n)
  have h := (ENNReal.continuousAt_toNNReal ?_).tendsto.comp (ENNReal.hasSum_iSup mono')
  Â· simpa only [(sum' _).tsum_eq, HasSum, f', a', Function.comp_def, â† ENNReal.coe_iSup bdd_f,
      â† ENNReal.coe_finset_sum, ENNReal.toNNReal_coe, â† ENNReal.coe_iSup bound] using h
  Â· simp [f', ENNReal.tsum_coe_eq (sum _), ENNReal.iSup_coe_eq_top.not, bound]

/-- Monotone convergence theorem for series, `â„` `HasSum` version -/
theorem Real.hasSum_ciSup [Nonempty Î¹] {f : â„• â†’ Î¹ â†’ â„} {a : â„• â†’ â„} (sum : âˆ€ n, HasSum (f n) (a n))
    (mono : Monotone f) (bound_f : âˆ€ i, BddAbove (range (fun n â†¦ f n i)))
    (bound_a : BddAbove (range a)) : HasSum (fun i â†¦ â¨† n, f n i) (â¨† n, a n) := by
  have f_nonneg : âˆ€ {n i}, 0 â‰¤ f n i - f 0 i := by
    intro n i
    simp only [sub_nonneg]
    exact mono (Nat.zero_le _) _
  have a_nonneg : âˆ€ {n}, 0 â‰¤ a n - a 0 := by
    intro n
    simp only [sub_nonneg]
    exact hasSum_le (fun i â†¦ mono (Nat.zero_le _) _) (sum 0) (sum n)
  set f' : â„• â†’ Î¹ â†’ â„â‰¥0 := fun n i â†¦ (f n i - f 0 i).toNNReal
  set a' : â„• â†’ â„â‰¥0 := fun n â†¦ (a n - a 0).toNNReal
  have sum' : âˆ€ n, HasSum (f' n) (a' n) := fun n â†¦ ((sum n).sub (sum 0)).toNNReal (by bound)
  have mono' : Monotone f' := by intro n m nm; simp only [Pi.le_def]; bound
  have bound_a' : BddAbove (range a') :=
    bound_a.range_comp (g := fun x â†¦ (x - a 0).toNNReal) fun x y xy â†¦ by bound
  have s := NNReal.hasSum_ciSup sum' mono' bound_a'
  simp [â† NNReal.hasSum_coe, f', a', max_eq_left a_nonneg, max_eq_left f_nonneg,
    â† ciSup_sub bound_a, â† ciSup_sub (bound_f _)] at s
  simpa only [sub_add_cancel] using s.add (sum 0)

/- Variant where we know the limit of the sums -/
public theorem Real.hasSum_ciSup_of_tendsto [Nonempty Î¹] {f : â„• â†’ Î¹ â†’ â„} {a : â„• â†’ â„} {b : â„}
    (sum : âˆ€ n, HasSum (f n) (a n)) (mono : Monotone f)
    (bound_f : âˆ€ i, BddAbove (range (fun n â†¦ f n i))) (bound_a : BddAbove (range a))
    (tendsto : Tendsto a atTop (ğ“ b)) : HasSum (fun i â†¦ â¨† n, f n i) b := by
  have t : Tendsto a atTop (ğ“ (â¨† n, a n)) := by
    refine tendsto_atTop_ciSup ?_ bound_a
    intro n m nm
    refine hasSum_le ?_ (sum n) (sum m)
    bound
  exact tendsto_nhds_unique tendsto t â–¸ Real.hasSum_ciSup sum mono bound_f bound_a
