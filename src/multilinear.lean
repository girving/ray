-- Multilinear functions on pairs

import algebra.algebra.basic
import analysis.complex.basic
import analysis.normed.field.basic
import analysis.normed_space.basic
import analysis.normed_space.multilinear
import data.complex.basic
import topology.algebra.module.multilinear

import tactics

open_locale complex_conjugate
noncomputable theory
variables {n : â„•}
variables {ğ•œ : Type} [nontrivially_normed_field ğ•œ]
variables {R A B E : Type} [semiring R]

lemma continuous_multilinear_map.to_fun_eq_coe {R A B : Type} [semiring R]
    [add_comm_monoid A] [module R A] [topological_space A]
    [add_comm_monoid B] [module R B] [topological_space B]
    (f : continuous_multilinear_map R (Î» _ : fin n, A) B) : f.to_fun = â‡‘f := begin
  rw multilinear_map.to_fun_eq_coe, simp
end

-- A version of curry0 that doesn't assume commutativity
def curry0 (R A : Type) {B : Type} [semiring R]
    [add_comm_monoid A] [module R A] [topological_space A]
    [add_comm_monoid B] [module R B] [topological_space B]
    (b : B) : continuous_multilinear_map R (Î» _ : fin 0, A) B := {
  to_fun := Î» _, b,
  map_add' := by simp,
  map_smul' := by simp,
  cont := by continuity
}

lemma curry0_apply [add_comm_monoid A] [module R A] [topological_space A] [add_comm_monoid B] [module R B] [topological_space B]
    (b : B) (a : Î  _ : fin 0, A) : curry0 R A b a = b := by rw [curry0, â†continuous_multilinear_map.to_fun_eq_coe]

lemma curry0_norm [normed_add_comm_group A] [normed_space ğ•œ A] [normed_add_comm_group B] [normed_space ğ•œ B]
    (b : B) : âˆ¥curry0 ğ•œ A bâˆ¥ = âˆ¥bâˆ¥ := begin
  apply le_antisymm, {
    apply continuous_multilinear_map.op_norm_le_bound,
    simp, intro a, rw [curry0_apply b a, finset.univ_eq_empty, finset.prod_empty], simp
  }, {
    have lo := continuous_multilinear_map.unit_le_op_norm (curry0 ğ•œ A b) fin.elim0 _,
    rw curry0_apply at lo, assumption,
    rw pi.norm_def, simp
  }
end

-- fst as a continuous_multilinear_map
def fst_cmmap (R : Type) (A B : Type) [semiring R]
    [add_comm_monoid A] [module R A] [topological_space A]
    [add_comm_monoid B] [module R B] [topological_space B]
    : continuous_multilinear_map R (Î» _ : fin 1, A Ã— B) A := {
  -- Once we upgrade mathlib, this can be
  --   (continuous_linear_map.fst R A B).comp_continuous_multilinear_map
  --     (continuous_multilinear_map.of_subsingleton R (A Ã— B) (0 : fin 1))
  -- and similarly for snd_cmmap.
  to_fun := Î» z, (z 0).fst,
  map_add' := begin intros z i x y, have i0 : i = 0 := by simp, rw i0, simp end,
  map_smul' := begin intros z i s x, have i0 : i = 0 := by simp, rw i0, simp end,
  cont := by continuity
}

-- snd as a continuous_multilinear_map
def snd_cmmap (R : Type) (A B : Type) [semiring R]
    [add_comm_monoid A] [module R A] [topological_space A]
    [add_comm_monoid B] [module R B] [topological_space B]
    : continuous_multilinear_map R (Î» _ : fin 1, A Ã— B) B := {
  to_fun := Î» z, (z 0).snd,
  map_add' := begin intros z i x y, have i0 : i = 0 := by simp, rw i0, simp end,
  map_smul' := begin intros z i s x, have i0 : i = 0 := by simp, rw i0, simp end,
  cont := by continuity
}

lemma fst_cmmap_apply [add_comm_monoid A] [module R A] [topological_space A] [add_comm_monoid B] [module R B] [topological_space B]
    (a : A) (b : B) : fst_cmmap R A B (Î» _, (a,b)) = a := by rw [fst_cmmap, â†continuous_multilinear_map.to_fun_eq_coe]

lemma snd_cmmap_apply [add_comm_monoid A] [module R A] [topological_space A] [add_comm_monoid B] [module R B] [topological_space B]
    (a : A) (b : B) : snd_cmmap R A B (Î» _, (a,b)) = b := by rw [snd_cmmap, â†continuous_multilinear_map.to_fun_eq_coe]

lemma fst_cmmap_norm [normed_ring A] [normed_algebra ğ•œ A] [norm_one_class A] [normed_ring B] [normed_algebra ğ•œ B] [norm_one_class B]
    : âˆ¥fst_cmmap ğ•œ A Bâˆ¥ = 1 := begin
  apply le_antisymm, {
    apply @continuous_multilinear_map.op_norm_le_bound ğ•œ _ (Î» _ : fin 1, A Ã— B) _ _ _ _ _ _ _ _ (fst_cmmap ğ•œ A B) (1 : â„) (by norm_num),
    intro z, simp,
    have e : z = (Î» _, ((z 0).fst, (z 0).snd)), { apply funext, intro i, rw fin.eq_zero i, simp }, rw e,
    rw fst_cmmap_apply, simp, exact norm_fst_le (z 0)
  }, {
    have lo := @continuous_multilinear_map.unit_le_op_norm ğ•œ _ (Î» _ : fin 1, A Ã— B) A _ _ _ _ _ _ _ (fst_cmmap ğ•œ A B) (Î» _, (1,1)) _,
    rw [fst_cmmap_apply, norm_one] at lo, assumption,
    rw pi_norm_le_iff, intro i, rw prod.norm_def, simp, norm_num
  }
end

lemma snd_cmmap_norm [normed_ring A] [normed_algebra ğ•œ A] [norm_one_class A] [normed_ring B] [normed_algebra ğ•œ B] [norm_one_class B]
    : âˆ¥snd_cmmap ğ•œ A Bâˆ¥ = 1 := begin
  apply le_antisymm, {
    apply @continuous_multilinear_map.op_norm_le_bound ğ•œ _ (Î» _ : fin 1, A Ã— B) _ _ _ _ _ _ _ _ (snd_cmmap ğ•œ A B) (1 : â„) (by norm_num),
    intro z, simp,
    have e : z = (Î» _, ((z 0).fst, (z 0).snd)), { apply funext, intro i, rw fin.eq_zero i, simp }, rw e,
    rw snd_cmmap_apply, simp, exact norm_snd_le (z 0)
  }, {
    have lo := @continuous_multilinear_map.unit_le_op_norm ğ•œ _ (Î» _ : fin 1, A Ã— B) B _ _ _ _ _ _ _ (snd_cmmap ğ•œ A B) (Î» _, (1,1)) _,
    rw [snd_cmmap_apply, norm_one] at lo, assumption,
    rw pi_norm_le_iff, intro i, rw prod.norm_def, simp, norm_num
  }
end

-- Lemmas for smul_cmmap
lemma update_0_0 (z : fin (n+1) â†’ A) (x : A) : function.update (Î» _ : fin 1, z 0) 0 x = (Î» _ : fin 1, x) := begin
  apply funext, intro i,
  have i0 : i = 0 := by simp,
  rw i0, simp
end
lemma update_0_succ (f : fin (n+1) â†’ A) (x : A) (i : fin n) : function.update f 0 x i.succ = f i.succ := begin
  rw function.update_apply, simp,
  have i0 := fin.succ_ne_zero i,
  finish
end
lemma update_nz_0 (f : fin (n+1) â†’ A) {x : A} {i : fin (n+1)} (i0 : i â‰  0) : function.update f i x 0 = f 0 :=
  by rw function.update_noteq i0.symm
lemma update_nz_succ (f : fin (n+1) â†’ A) (x : A) {i : fin (n+1)} (i0 : i â‰  0)
    : (Î» j : fin n, function.update f i x j.succ) = function.update (Î» j : fin n, f j.succ) (i.pred i0) x := begin
  apply funext, intro k,
  by_cases ki : k.succ = i, {
    have ki' : k = i.pred i0 := by simp_rw [â†ki, fin.pred_succ],
    rw [ki,ki'], rw function.update_same, rw function.update_same
  }, {
    rw function.update_noteq ki,
    rw function.update_noteq _,
    by_contradiction,
    rw [h,fin.succ_pred _] at ki,
    finish
  }
end

-- Raw cons of two continuous multilinear maps
def smul_cmmap_fn [add_comm_monoid A] [module ğ•œ A] [topological_space A] [normed_add_comm_group B] [normed_space ğ•œ B]
    (x : continuous_multilinear_map ğ•œ (Î» _ : fin 1, A) ğ•œ)
    (xs : continuous_multilinear_map ğ•œ (Î» _ : fin n, A) B)
    : (Î  _ : fin (n+1), A) â†’ B := 
  Î» z, (x.to_fun (Î» _, z 0)) â€¢ (xs.to_fun (Î» i, z i.succ))

-- smul_cmmap_fn is multiadditive
lemma smul_cmmap_add [add_comm_monoid A] [module ğ•œ A] [topological_space A] [normed_add_comm_group B] [normed_space ğ•œ B]
    (x : continuous_multilinear_map ğ•œ (Î» _ : fin 1, A) ğ•œ)
    (xs : continuous_multilinear_map ğ•œ (Î» _ : fin n, A) B)
    : âˆ€ (z : Î  _ : fin (n+1), A) (i : fin (n+1)) (u v : A),
      smul_cmmap_fn x xs (function.update z i (u + v)) =
      smul_cmmap_fn x xs (function.update z i u) + smul_cmmap_fn x xs (function.update z i v) := begin
  intros z i u v,
  rw smul_cmmap_fn,
  by_cases i0 : i = 0, {
    rw i0, simp, simp_rw update_0_succ z _ _,
    have uv := x.map_add (Î» _, z 0) 0 u v,
    rw update_0_0 z _ at uv,
    rw update_0_0 z _ at uv,
    rw update_0_0 z _ at uv,
    rw [uv, add_smul]
  }, {
    simp,
    simp_rw update_nz_0 z i0,
    rw update_nz_succ z _ i0,
    rw update_nz_succ z _ i0,
    rw update_nz_succ z _ i0,
    have uv := xs.map_add (Î» j, z j.succ) (i.pred i0) u v,
    rw [uv, smul_add]
  }
end

-- smul_cmmap_fn commutes with scalars
lemma smul_cmmap_smul [add_comm_monoid A] [module ğ•œ A] [topological_space A] [normed_add_comm_group B] [normed_space ğ•œ B]
    (x : continuous_multilinear_map ğ•œ (Î» _ : fin 1, A) ğ•œ)
    (xs : continuous_multilinear_map ğ•œ (Î» _ : fin n, A) B)
    : âˆ€ (z : Î  _ : fin (n+1), A) (i : fin (n+1)) (s : ğ•œ) (u : A),
      smul_cmmap_fn x xs (function.update z i (s â€¢ u)) = s â€¢ smul_cmmap_fn x xs (function.update z i u) := begin
  intros z i s u,
  rw smul_cmmap_fn,
  by_cases i0 : i = 0, {
    rw i0, simp, simp_rw update_0_succ z _ _,
    have su := x.map_smul (Î» _, z 0) 0 s u,
    rw update_0_0 z _ at su,
    rw update_0_0 z _ at su,
    rw [su, smul_assoc]
  }, {
    simp,
    simp_rw update_nz_0 z i0,
    rw update_nz_succ z _ i0,
    rw update_nz_succ z _ i0,
    have su := xs.map_smul (Î» j, z j.succ) (i.pred i0) s u,
    rw [su, smul_comm]
  }
end

-- smul_cmmap_fn is continuous
lemma smul_cmmap_cont [add_comm_monoid A] [module ğ•œ A] [topological_space A] [normed_add_comm_group B] [normed_space ğ•œ B]
    (x : continuous_multilinear_map ğ•œ (Î» _ : fin 1, A) ğ•œ)
    (xs : continuous_multilinear_map ğ•œ (Î» _ : fin n, A) B)
    : continuous (smul_cmmap_fn x xs) := begin
  rw smul_cmmap_fn, continuity,
  exact x.cont, exact xs.cont
end

-- Cons two continuous_multilinear_maps together
def smul_cmmap (ğ•œ A B : Type) [nontrivially_normed_field ğ•œ]
    [add_comm_monoid A] [module ğ•œ A] [topological_space A] [normed_add_comm_group B] [normed_space ğ•œ B]
    (x : continuous_multilinear_map ğ•œ (Î» _ : fin 1, A) ğ•œ)
    (xs : continuous_multilinear_map ğ•œ (Î» _ : fin n, A) B)
    : continuous_multilinear_map ğ•œ (Î» _ : fin (n+1), A) B := {
  to_fun := smul_cmmap_fn x xs,
  map_add' := smul_cmmap_add x xs,
  map_smul' := smul_cmmap_smul x xs,
  cont := smul_cmmap_cont x xs,
}

lemma smul_cmmap_apply [add_comm_monoid A] [module ğ•œ A] [topological_space A] [normed_add_comm_group B] [normed_space ğ•œ B]
    (x : continuous_multilinear_map ğ•œ (Î» _ : fin 1, A) ğ•œ)
    (xs : continuous_multilinear_map ğ•œ (Î» _ : fin n, A) B)
    (z : Î  _ : fin (n+1), A)
    : smul_cmmap _ _ _ x xs z = x (Î» _, z 0) â€¢ xs (Î» i, z i.succ) := begin
  rw [smul_cmmap, â†continuous_multilinear_map.to_fun_eq_coe],
  simp, rw smul_cmmap_fn, simp
end

lemma smul_cmmap_norm [normed_add_comm_group A] [normed_space ğ•œ A] [normed_add_comm_group B] [normed_space ğ•œ B]
    (x : continuous_multilinear_map ğ•œ (Î» _ : fin 1, A) ğ•œ)
    (xs : continuous_multilinear_map ğ•œ (Î» _ : fin n, A) B)
    : âˆ¥smul_cmmap ğ•œ A B x xsâˆ¥ â‰¤ âˆ¥xâˆ¥ * âˆ¥xsâˆ¥ := begin
  apply continuous_multilinear_map.op_norm_le_bound, bound,
  intro z, rw smul_cmmap_apply,
  have xb := continuous_multilinear_map.le_op_norm x (Î» _ : fin 1, z 0),
  have xsb := continuous_multilinear_map.le_op_norm xs (Î» i : fin n, z i.succ),
  simp at xb xsb,
  have e0 := fin.prod_cons (âˆ¥z 0âˆ¥) (Î» i : fin n, âˆ¥z i.succâˆ¥),
  simp at e0,
  have e1 : âˆ¥z 0âˆ¥ = (Î» i : fin (n+1), âˆ¥z iâˆ¥) 0 := rfl,
  have e2 : (Î» (i : fin n), âˆ¥z i.succâˆ¥) = fin.tail (Î» i : fin (n+1), âˆ¥z iâˆ¥) := rfl,
  nth_rewrite 0 e1 at e0, nth_rewrite 0 e2 at e0, rw fin.cons_self_tail at e0,
  calc âˆ¥x (Î» _ : fin 1, z 0) â€¢ xs (Î» i : fin n, z i.succ)âˆ¥
      â‰¤ âˆ¥xâˆ¥ * âˆ¥z 0âˆ¥ * (âˆ¥xsâˆ¥ * finset.univ.prod (Î» (i : fin n), âˆ¥z i.succâˆ¥)) : by { rw norm_smul, bound }
  ... = âˆ¥xâˆ¥ * âˆ¥xsâˆ¥ * (âˆ¥z 0âˆ¥ * finset.univ.prod (Î» (i : fin n), âˆ¥z i.succâˆ¥)) : by ring
  ... = âˆ¥xâˆ¥ * âˆ¥xsâˆ¥ * finset.univ.prod (Î» (i : fin (n + 1)), âˆ¥z iâˆ¥) : by rw â†e0
end

-- A term of the general n-linear map on â„‚ Ã— â„‚, equal to z0^k * z1^(n-k) when applied to (Î» _, (z0,z1))
noncomputable def term_cmmap (ğ•œ : Type) [nontrivially_normed_field ğ•œ] [normed_add_comm_group E] [normed_space ğ•œ E]
    : Î  (n : â„•), â„• â†’ E â†’ continuous_multilinear_map ğ•œ (Î» _ : fin n, ğ•œ Ã— ğ•œ) E
| 0 := Î» _ x, curry0 _ _ x
| (n+1) := Î» k x, smul_cmmap _ _ _ (if n < k then fst_cmmap ğ•œ ğ•œ ğ•œ else snd_cmmap ğ•œ ğ•œ ğ•œ) (term_cmmap n k x)

lemma term_cmmap_apply [normed_add_comm_group E] [normed_space ğ•œ E] [smul_comm_class ğ•œ ğ•œ E] [is_scalar_tower ğ•œ ğ•œ E]
    (n k : â„•) (a b : ğ•œ) (x : E) : term_cmmap ğ•œ n k x (Î» _, (a,b)) = a^(min k n) â€¢ b^(n-k) â€¢ x := begin
  induction n with n h, {
    rw term_cmmap, rw curry0_apply, simp
  }, {
    rw [term_cmmap, smul_cmmap_apply, h],
    by_cases nk : n < k, {
      simp [nk],
      rw fst_cmmap_apply,
      have nsk : n.succ â‰¤ k := nat.succ_le_iff.mpr nk,
      rw [min_eq_right (le_of_lt nk), min_eq_right nsk, nat.sub_eq_zero_of_le (le_of_lt nk), nat.sub_eq_zero_of_le nsk],
      simp, rw [â†smul_assoc, smul_eq_mul, â†pow_succ]
    }, {
      simp [nk], simp at nk,
      rw snd_cmmap_apply,
      have nsk : k â‰¤ n.succ := nat.le_succ_of_le nk,
      rw [min_eq_left nk, min_eq_left nsk],
      rw [smul_comm b _, â†smul_assoc b _ _, smul_eq_mul, â†pow_succ, â†nat.sub_add_comm nk],
      assumption, assumption
    }
  }
end

lemma term_cmmap_norm (ğ•œ : Type) [nontrivially_normed_field ğ•œ] [normed_add_comm_group E] [normed_space ğ•œ E]
    (n k : â„•) (x : E) : âˆ¥term_cmmap ğ•œ n k xâˆ¥ â‰¤ âˆ¥xâˆ¥ := begin
  induction n with n nh, {
    rw [term_cmmap, curry0_norm]
  }, {
    rw term_cmmap, simp,
    generalize ht : term_cmmap ğ•œ n k x = t, rw ht at nh,
    have tn := smul_cmmap_norm (ite (n < k) (fst_cmmap ğ•œ ğ•œ ğ•œ) (snd_cmmap ğ•œ ğ•œ ğ•œ)) t,
    by_cases nk : n < k, {
      simp [nk] at âŠ¢ tn, rw fst_cmmap_norm at tn, simp at tn, exact trans tn nh
    }, {
      simp [nk] at âŠ¢ tn, rw snd_cmmap_norm at tn, simp at tn, exact trans tn nh
    }
  }
end

-- conj as a continuous_linear_map
def conj_clm : â„‚ â†’L[â„] â„‚ := {
  to_fun := Î» z, conj z,
  map_add' := by simp,
  map_smul' := by simp,
}
lemma conj_clm_apply (z : â„‚) : conj_clm z = conj z := rfl

-- The continuous linear map that evaluates a continuous multilinear map at a point
def cmmap_apply_cmap (ğ•œ : Type) {I : Type} (A : I â†’ Type) (B : Type)
    [fintype I] [decidable_eq I] [nontrivially_normed_field ğ•œ]
    [Î  i, normed_add_comm_group (A i)] [Î  i, normed_space ğ•œ (A i)] [normed_add_comm_group B] [normed_space ğ•œ B]
    (x : Î  i, A i) : continuous_multilinear_map ğ•œ A B â†’L[ğ•œ] B := {
  to_fun := Î» f, f x,
  map_add' := by simp,
  map_smul' := by simp,
  cont := by simp [continuous_multilinear_map.continuous_eval_left],
}