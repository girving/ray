-- Useful bounds

import analysis.special_functions.log.deriv
import data.complex.exponential_bounds
import data.real.basic
import data.real.ennreal
import data.real.nnreal
import data.real.pi.bounds
import data.set.basic
import tactic.field_simp
import topology.metric_space.basic

import finset
import tactics

open complex (abs exp log I)
open filter (at_top)
open_locale real nnreal topology

-- A finset ‚Ñï with only large elements
def late (N : finset ‚Ñï) (m : ‚Ñï) := ‚àÄ n, n ‚àà N ‚Üí n ‚â• m

def sdiff_late {m : ‚Ñï} {B : finset ‚Ñï} (A : finset ‚Ñï) : B ‚â• finset.range m ‚Üí late (A \ B) m := begin
  intros Bm n nAB,
  rw finset.mem_sdiff at nAB,
  by_contradiction, simp at h,
  have nr := finset.mem_range.mpr h,
  have nB := finset.mem_of_subset Bm nr,
  finish
end

-- Summing a subset of a geometric series is ‚â§ the series sum
theorem partial_geometric_bound {a : ‚Ñù} (N : finset ‚Ñï) (a0 : 0 ‚â§ a) (a1 : a < 1)
    : N.sum (Œª n, a^n) ‚â§ (1 - a)‚Åª¬π := begin
  have pos : ‚àÄ n, n ‚àâ N ‚Üí 0 ‚â§ a^n, { intros n nN, exact pow_nonneg a0 n },
  exact sum_le_has_sum N pos (has_sum_geometric_of_lt_1 a0 a1),
end

-- Summing a subset of a scaled_geometric series is ‚â§ the series sum
theorem partial_scaled_geometric_bound {a : ‚Ñù} (c : ‚Ñù‚â•0) (N : finset ‚Ñï) (a0 : 0 ‚â§ a) (a1 : a < 1)
    : N.sum (Œª n, (c : ‚Ñù) * a^n) ‚â§ c * (1 - a)‚Åª¬π := begin
  rw ‚Üêfinset.mul_sum, bound [partial_geometric_bound N a0 a1],
end

-- Summing a late part of a series is equivalent to summing a shifted series
lemma late_series_sum {m : ‚Ñï} {N : finset ‚Ñï} (h : late N m) (f : ‚Ñï ‚Üí ‚Ñù)
    : N.sum f = (finset.image (Œª n, n - m) N).sum (Œª n, f (n + m)) := begin
  set Ns := finset.image (Œª n, n - m) N,
  have NNs : N = finset.image (Œª n, n + m) Ns, {
    apply finset.ext, intro k,
    rw [finset.image_image, finset.mem_image],
    simp,
    apply iff.intro, {
      intro kN, existsi k, apply and.intro,
      assumption, exact nat.sub_add_cancel (h k kN)
    }, {
      intro ha, rcases ha with ‚ü®a,aN,ak‚ü©,
      rw nat.sub_add_cancel (h a aN) at ak,
      rw ‚Üêak, assumption
    }
  },
  rw NNs, apply finset.sum_image,
  intros a aN b bN, exact nat.add_right_cancel
end

-- late_series_sum, but forget the image set
lemma late_series_sum' {m : ‚Ñï} {N : finset ‚Ñï} (h : late N m) (f : ‚Ñï ‚Üí ‚Ñù)
    : ‚àÉ M : finset ‚Ñï, N.sum f = M.sum (Œª n, f (n + m)) := begin
  existsi finset.image (Œª n, n - m) N,
  exact late_series_sum h f
end

-- Summing a late subset of a geometric series is even smaller
theorem late_geometric_bound {m : ‚Ñï} {a : ‚Ñù} {N : finset ‚Ñï}
    (h : late N m) (a0 : a ‚â• 0) (a1 : a < 1)
    : N.sum (Œª n, a^n) ‚â§ a^m * (1 - a)‚Åª¬π := begin
  rcases late_series_sum' h (Œª n, a^n) with ‚ü®M, L‚ü©,
  rw L, clear L, simp,
  have pa : (Œª n, a^(n + m)) = (Œª n, a^n * a^m), { apply funext, intro n, rw pow_add },
  calc M.sum (Œª n, a^(n + m)) = M.sum (Œª n, a^n * a^m) : by rw pa
  ... = M.sum (Œª n, (Œª n, a^n) n * a^m) : by simp
  ... = M.sum (Œª n, a^n) * a^m : (@finset.sum_mul _ _ M (a^m) (Œª n, a^n) _).symm
  ... ‚â§ (1 - a)‚Åª¬π * a^m : by bound [partial_geometric_bound M a0 a1]
  ... = a^m * (1 - a)‚Åª¬π : by ring
end

lemma finset_partition (A B : finset ‚Ñï) : A = (A \ B) ‚à™ (A ‚à© B) := begin
  apply finset.ext, intro a,
  apply iff.intro, {
    finish
  }, {
    rw [finset.mem_union, finset.mem_inter, finset.mem_sdiff],
    finish
  }
end

lemma finset_sum_partition (A B : finset ‚Ñï) (f : ‚Ñï ‚Üí ‚ÑÇ)
    : A.sum (Œª n, f n) = (A \ B).sum (Œª n, f n) + (A ‚à© B).sum (Œª n, f n) := begin
  have ha : A = (A \ B) ‚à™ (A ‚à© B) := finset_partition A B,
  nth_rewrite 0 ha,
  exact finset.sum_union (finset.disjoint_sdiff_inter A B)
end

lemma sdiff_sdiff_disjoint (A B : finset ‚Ñï) : disjoint (A \ B) (B \ A) :=
  finset.disjoint_of_subset_right (finset.sdiff_subset B A) finset.sdiff_disjoint

lemma symm_diff_union (A B : finset ‚Ñï) : A ‚àÜ B = A \ B ‚à™ B \ A :=
  by rw [symm_diff_def, finset.sup_eq_union]

-- Bound the difference of two finset sums based on their symmetric difference
theorem symm_diff_bound (A B : finset ‚Ñï) (f : ‚Ñï ‚Üí ‚ÑÇ)
    : dist (A.sum (Œª n, f n)) (B.sum (Œª n, f n)) ‚â§ (A ‚àÜ B).sum (Œª n, abs (f n)) := begin
  rw finset_sum_partition A B f,
  rw finset_sum_partition B A f,
  rw finset.inter_comm B A,
  rw dist_add_right ((A\B).sum (Œªn,f n)) ((B\A).sum (Œªn,f n)) ((A‚à©B).sum (Œªn,f n)),
  rw complex.dist_eq,
  transitivity (A \ B).sum (Œª n, abs (f n)) + (B \ A).sum (Œª n, abs (f n)), {
    have ha := finset_complex_abs_sum_le (A \ B) f,
    have hb := finset_complex_abs_sum_le (B \ A) f,
    calc abs ((A \ B).sum (Œª n, f n) - (B \ A).sum (Œª n, f n))
        ‚â§ abs ((A \ B).sum (Œª n, f n)) + abs ((B \ A).sum (Œª n, f n)) : complex.abs.sub_le' _ _
    ... ‚â§ (A \ B).sum (Œª n, abs (f n)) + (B \ A).sum (Œª n, abs (f n)) : by bound [ha, hb],
  }, {
    apply le_of_eq,
    rw [‚Üêfinset.sum_union (sdiff_sdiff_disjoint A B), symm_diff_union]
  }
end

-- Symmetric differences of sets containing ranges are late
theorem symm_diff_late {A B : finset ‚Ñï} {m : ‚Ñï} (ha : A ‚â• finset.range m) (hb : B ‚â• finset.range m)
    : late (A ‚àÜ B) m := begin
  intros n ab, 
  rw [symm_diff_def, finset.sup_eq_union, finset.mem_union] at ab,
  by_contradiction, simp at h,
  cases ab, {
    rw finset.mem_sdiff at ab,
    have h := finset.mem_of_subset hb (finset.mem_range.mpr h),
    finish
  }, {
    rw finset.mem_sdiff at ab,
    have h := finset.mem_of_subset ha (finset.mem_range.mpr h),
    finish
  }
end

lemma sub_near (a z : ‚ÑÇ) : |abs (a - z) - abs a| ‚â§ abs z := begin
  rw abs_le, constructor, {
    simp,
    calc abs (a - z) + abs z ‚â• |abs a - abs z| + abs z : by bound [complex.abs.abs_abv_sub_le_abv_sub  a z]
    ... ‚â• abs a - abs z + abs z : by bound [le_abs_self (abs a - abs z)]
    ... = abs a : by simp,
  }, {
    calc abs (a - z) - abs a ‚â§ abs a + abs z - abs a : by bound
    ... = abs z : by simp,
  }
end

lemma add_near (a z : ‚ÑÇ) : |abs (a + z) - abs a| ‚â§ abs z := begin
  have h := sub_near a (-z),
  simp at h,
  assumption
end

lemma near_one_avoids_negative_reals {z : ‚ÑÇ} : abs (z - 1) < 1 ‚Üí z.re > 0 ‚à® z.im ‚â† 0 := begin
  intro h, apply or.inl,
  have hr : (1 - z).re < 1, {
    calc (1 - z).re ‚â§ |(1 - z).re| : le_abs_self (1 - z).re
    ... ‚â§ abs (1 - z) : complex.abs_re_le_abs _
    ... = abs (z - 1) : by { rw ‚Üêcomplex.abs.map_neg (1 - z), simp }
    ... < 1 : h
  },
  simp at hr, assumption
end

lemma near_one_avoids_zero {z : ‚ÑÇ} : abs (z - 1) < 1 ‚Üí z ‚â† 0 := begin
  intro h,
  have g := near_one_avoids_negative_reals h,
  by_contradiction, rw h at g, simp at g, assumption
end

theorem deriv_within.cid {z : ‚ÑÇ} {s : set ‚ÑÇ}
    (o : is_open s) (zs : z ‚àà s) : deriv_within (Œª z, z) s z = 1 :=
  deriv_within_id z s (is_open.unique_diff_within_at o zs)

theorem deriv_within.clog {f : ‚ÑÇ ‚Üí ‚ÑÇ} {z : ‚ÑÇ} {s : set ‚ÑÇ}
    (o : is_open s) (zs : z ‚àà s)
    (hf : differentiable_within_at ‚ÑÇ f s z) (hx : (f z).re > 0 ‚à® (f z).im ‚â† 0)
    : deriv_within (Œª z, log (f z)) s z = deriv_within f s z / f z := begin
  have hz := differentiable_within_at.has_deriv_within_at hf,
  have h := has_deriv_within_at.clog hz _,
  repeat { rw has_deriv_within_at.deriv_within },
  repeat { assumption <|> exact is_open.unique_diff_within_at o zs }
end

theorem differentiable_on.cpow {f : ‚ÑÇ ‚Üí ‚ÑÇ} {g : ‚ÑÇ ‚Üí ‚ÑÇ} {s : set ‚ÑÇ}
    (df : differentiable_on ‚ÑÇ f s)
    (dg : differentiable_on ‚ÑÇ g s)
    (h : ‚àÄ z, z ‚àà s ‚Üí 0 < (f z).re ‚à® (f z).im ‚â† 0)
    : differentiable_on ‚ÑÇ (Œª z, f z ^ g z) s := begin
  intros z zs,
  exact differentiable_within_at.cpow (df z zs) (dg z zs) (h z zs)
end

-- log (1+z) is small for small z (complex case, assuming strict inequality)
theorem weak_log1p_small {z : ‚ÑÇ} {r : ‚Ñù} (r1 : r < 1) (h : abs z < r)
    : abs (log (1+z)) ‚â§ 1 / (1 - r) * abs z := begin
  by_cases rp : r ‚â§ 0, {
    have a0 : abs z < 0 := lt_of_lt_of_le h rp,
    have a0' : abs z ‚â• 0 := by bound,
    linarith
  }, {
    simp at rp,
    have L : ‚Äñlog (1+z) - log 1‚Äñ ‚â§ 1 / (1-r) * ‚Äñ1+z-1‚Äñ, {
      set s := metric.ball (1 : ‚ÑÇ) r,
      have o : is_open s := metric.is_open_ball,
      have s1z : (1+z) ‚àà s, { simp, assumption },
      have s1 : (1 : ‚ÑÇ) ‚àà s := by { simp, assumption },
      have sp : ‚àÄ w : ‚ÑÇ, w ‚àà s ‚Üí w.re > 0 ‚à® w.im ‚â† 0, {
        intros w ws,
        apply near_one_avoids_negative_reals,
        simp at ws, rw complex.dist_eq at ws,
        calc abs (w - 1) < r : by assumption
        ... < 1 : r1
      },
      have sa : ‚àÄ w : ‚ÑÇ, w ‚àà s ‚Üí abs w ‚â• 1 - r, {
        intros w ws, simp at ws, rw complex.dist_eq at ws,
        calc abs w = abs (1 + (w - 1)) : by abel
        ... ‚â• abs (1 : ‚ÑÇ) - abs (w - 1) : by bound
        ... ‚â• abs (1 : ‚ÑÇ) - r : by bound
        ... = 1 - r : by rw complex.abs.map_one
      },
      refine convex.norm_image_sub_le_of_norm_deriv_within_le _ _ _ s1 s1z, {
        exact differentiable_on.clog differentiable_on_id sp
      }, {
        intros w ws,
        rw [deriv_within.clog o ws, deriv_within.cid o ws],
        simp, rw inv_le, have aw := sa w ws, simp at aw, field_simp, assumption,
        have aw := sa w ws, linarith, norm_num,
        assumption,
        exact differentiable_within_at_id,
        exact sp w ws
      }, {
        exact convex_ball _ _
      }
    },
    simp at L, simp, assumption
  }
end

-- To show a ‚â§ b, it suffices to show a ‚â§ b + e for small positive e
lemma le_of_forall_small_le_add {a b t : ‚Ñù}
    (tp : t > 0) (h : ‚àÄ e, 0 < e ‚Üí e < t ‚Üí a ‚â§ b + e) : a ‚â§ b := begin
  apply le_of_forall_pos_lt_add,
  intros e ep,
  by_cases et : e ‚â• t, {
    specialize h (t/2) (by bound) (by bound),
    calc a ‚â§ b + t/2 : h
    ... ‚â§ b + e/2 : by bound
    ... < b + e : by bound
  }, {
    simp at et,
    calc a ‚â§ b + e/2 : h (e/2) (by bound) (by linarith)
    ... < b + e : by bound
  }
end

-- 1 / (1 - x) ‚â§ 1 + 2*x for small positive x
theorem one_over_one_sub_le {x : ‚Ñù} : 0 ‚â§ x ‚Üí x ‚â§ 1/2 ‚Üí 1 / (1 - x) ‚â§ 1 + 2*x := begin
  intros xp xh,
  have x1 : 1 - x > 0 := by linarith,
  rw div_le_iff x1,
  calc (1 + 2*x) * (1 - x) = 1 + x*(1 - 2*x) : by ring
  ... ‚â• 1 + x*(1 - 2*(1/2)) : by bound
  ... = 1 : by ring,
end

-- Version of metric.continuous_at_iff.mp which provides an upper bound on the radius.
-- This makes downstream usage more convenient in some cases.
lemma metric.continuous_near {f : ‚ÑÇ ‚Üí ‚ÑÇ} {z : ‚ÑÇ} {r : ‚Ñù}
    (fc : continuous_at f z) (rp : r > 0)
    : ‚àÄ e, e > 0 ‚Üí ‚àÉ s, 0 < s ‚àß s ‚â§ r ‚àß ‚àÄ {w}, abs (w - z) < s ‚Üí abs (f w - f z) < e := begin
  intros e ep,
  rcases metric.continuous_at_iff.mp fc e ep with ‚ü®s,sp,sc‚ü©,
  simp_rw complex.dist_eq at sc,
  by_cases sr : s ‚â§ r, { existsi s, exact ‚ü®sp,sr,Œª w wr, sc wr‚ü© },
  simp at sr, existsi r, refine ‚ü®rp, by bound, _‚ü©,
  intros w wr, refine @sc w _,
  transitivity r, assumption, assumption
end

-- Given z ‚â† 0, we can find a slightly smaller nearby w
lemma slightly_smaller {z : ‚ÑÇ} (nz : z ‚â† 0) {r : ‚Ñù} (rp : 0 < r)
    : ‚àÉ w, abs (w - z) < r ‚àß abs w < abs z := begin
  by_cases rz : abs z < r, {
    use 0, simp [rz, nz],
  },
  simp only [not_lt] at rz,
  have azp : abs z > 0 := complex.abs.pos nz,
  generalize ha : 1 - r / 2 / abs z = a,
  have a0 : a ‚â• 0, {
    rw ‚Üêha, refine sub_nonneg.mpr ((div_le_one azp).mpr _),
    refine trans _ rz,
    bound,
  },
  have a1 : a < 1, { rw ‚Üêha, simp only [sub_lt_self_iff], positivity },
  generalize hw : ‚Üëa * z = w,
  use w, constructor, {
    rw [‚Üêhw,‚Üêha],
    simp only [complex.of_real_sub, complex.of_real_one, complex.of_real_div, complex.of_real_bit0],
    rw mul_sub_right_distrib,
    simp only [one_mul, sub_sub_cancel_left, absolute_value.map_neg, absolute_value.map_mul, map_div‚ÇÄ,
      complex.abs_of_real, complex.abs_two, complex.abs_abs, abs_of_pos rp, div_mul (r / 2),
      div_self (ne_of_gt azp), div_one],
    bound,
  }, {
    simp only [‚Üêhw, absolute_value.map_mul, complex.abs_of_real, abs_of_nonneg a0],
    calc a * abs z < 1 * abs z : mul_lt_mul_of_pos_right a1 azp
    ... = abs z : by ring
  },
end

-- There are smaller values nearby any z ‚â† 0
lemma frequently_smaller {z : ‚ÑÇ} (z0 : z ‚â† 0) : ‚àÉ·∂† w : ‚ÑÇ in ùìù z, abs w < abs z := begin
  simp only [filter.frequently, metric.eventually_nhds_iff, not_exists, not_forall, not_not, complex.dist_eq],
  intros r rp, rcases slightly_smaller z0 rp with ‚ü®w,b,lt‚ü©, use [w,b,lt],
end

-- Turn a weak small theorem into a strong small theorem
lemma weak_to_strong_small {f : ‚ÑÇ ‚Üí ‚ÑÇ} {z : ‚ÑÇ} {r c : ‚Ñù}
    (rp : r > 0) (cp : c > 0) (zr : abs z ‚â§ r) (fc : continuous_at f z)
    (h : ‚àÄ z : ‚ÑÇ, abs z < r ‚Üí abs (f z) ‚â§ c * abs z)
    : abs (f z) ‚â§ c * abs z := begin
  by_cases nz : z = 0, { refine h z _, rw nz, simpa },
  apply le_of_forall_small_le_add zero_lt_one,
  intros e ep e1,
  rcases metric.continuous_near fc rp e ep with ‚ü®s,sp,sr,sc‚ü©,
  rcases slightly_smaller nz sp with ‚ü®w,wz,awz‚ü©,
  have wr : abs w < r := lt_of_lt_of_le awz zr,
  calc abs (f z) = abs (f w - (f w - f z)) : by abel
  ... ‚â§ abs (f w) + abs (f w - f z) : complex.abs.sub_le' _ _
  ... ‚â§ c * abs w + e : by bound [h w wr, sc wz]
  ... ‚â§ c * abs z + e : by bound,
end

-- log (1+z) is small for small z
theorem log1p_small {z : ‚ÑÇ} (zs : abs z ‚â§ 1/2) : abs (log (1+z)) ‚â§ 2 * abs z := begin
  have rp : (1/2 : ‚Ñù) > 0 := by norm_num,
  have cp : (2 : ‚Ñù) > 0 := by norm_num,
  have fc : continuous_at (Œª z, log (1 + z)) z, {
    apply continuous_at.clog, apply continuous_at.add,
    exact continuous_at_const, exact continuous_at_id,
    refine near_one_avoids_negative_reals _,
    simp, exact lt_of_le_of_lt zs (by bound)
  },
  apply weak_to_strong_small rp cp zs fc,
  intros w wr,
  have ws := @weak_log1p_small w (1/2) (by bound) wr,
  have t : 1 / (1 - 1 / 2) = (2 : ‚Ñù) := by norm_num,
  rw t at ws, simpa
end

-- log (1+x) is small for small x
theorem real.log1p_small {x : ‚Ñù} (xs : |x| ‚â§ 1/2) : |real.log (1+x)| ‚â§ 2 * |x| := begin
  set z := (x : ‚ÑÇ),
  have zx : abs z = |x| := complex.abs_of_real _,
  simp only [‚Üêcomplex.log_of_real_re, ‚Üêzx] at xs ‚ä¢,
  refine trans (trans (complex.abs_re_le_abs _) _) (log1p_small xs),
  simp only [complex.of_real_add, complex.of_real_one],
end

-- log z is small for z ‚âà 1
theorem log_small {z : ‚ÑÇ} (zs : abs (z - 1) ‚â§ 1/2) : abs (log z) ‚â§ 2 * abs (z - 1) := begin
  generalize zw : z - 1 = z1, have wz : z = 1 + z1, { rw ‚Üêzw, ring },
  rw wz, refine log1p_small _, rw ‚Üêzw, assumption
end

-- exp z ‚âà 1 for z ‚âà 0, strict inequality version
theorem weak_exp_small {z : ‚ÑÇ} (h : abs z < 1) : abs (exp z - 1) ‚â§ 2 * abs z := begin
  have hr : 0 ‚â§ (1 : ‚Ñù) := by norm_num,
  have L := complex.locally_lipschitz_exp hr (by bound) 0 z (by { simp, assumption }),
  simp at L,
  have t : 1 + 1 = (2 : ‚Ñù) := by norm_num,
  rw t at L, assumption
end

-- exp z ‚âà 1 for z ‚âà 0
theorem exp_small {z : ‚ÑÇ} (zs : abs z ‚â§ 1) : abs (exp z - 1) ‚â§ 2 * abs z := begin
  have rp : (1 : ‚Ñù) > 0 := by norm_num,
  have cp : (2 : ‚Ñù) > 0 := by norm_num,
  have fc : continuous_at (Œª z, exp z - 1) z, {
    apply continuous_at.sub, apply continuous_at.cexp,
    exact continuous_at_id, exact continuous_at_const
  },
  apply weak_to_strong_small rp cp zs fc,
  intros w wr, simp, 
  exact weak_exp_small wr,
end

-- abs ((1+z)^w - 1) ‚â§ 2 * abs (zw) for small z,w
theorem pow1p_small {z w : ‚ÑÇ} (zs : abs z ‚â§ 1/2) (ws : abs w ‚â§ 1)
    : abs ((1+z)^w - 1) ‚â§ 4 * abs z * abs w := begin
  have z1 : 1 + z ‚â† 0, {
    rw ‚Üêcomplex.abs.ne_zero_iff, apply ne_of_gt,
    calc abs (1 + z) ‚â• abs (1 : ‚ÑÇ) - abs z : by bound
    ... ‚â• abs (1 : ‚ÑÇ) - 1/2 : by bound
    ... > 0 : by norm_num
  },
  rw complex.cpow_def_of_ne_zero z1,
  have ls := log1p_small zs,
  have eas : abs (log (1 + z) * w) ‚â§ 1, {
    rw complex.abs.map_mul,
    calc abs (log (1 + z)) * abs w ‚â§ 2 * abs z * abs w : by bound
    ... ‚â§ 2 * (1/2) * 1 : by bound
    ... = 1 : by norm_num
  },
  have es := exp_small eas,
  rw [complex.abs.map_mul, ‚Üêmul_assoc] at es,
  transitivity 2 * abs (log (1 + z)) * abs w,
  assumption,
  calc 2 * abs (log (1 + z)) * abs w ‚â§ 2 * (2 * abs z) * abs w : by bound
  ... = 4 * abs z * abs w : by ring,
end

-- abs (z^w - 1) ‚â§ 2 * abs ((z-1)w) for z ‚âà 1, w small
theorem pow_small {z w : ‚ÑÇ} (zs : abs (z - 1) ‚â§ 1/2) (ws : abs w ‚â§ 1)
    : abs (z^w - 1) ‚â§ 4 * abs (z - 1) * abs w := begin
  generalize zw : z - 1 = z1, have wz : z = 1 + z1, { rw ‚Üêzw, ring },
  rw wz, refine pow1p_small _ ws, rw ‚Üêzw, assumption,
end

-- a + b ‚â† 0 from abs b < abs a
lemma add_ne_zero_of_abs_lt {a b : ‚ÑÇ} (h : abs b < abs a) : a + b ‚â† 0 := begin
  have e : a + b = a - (-b) := by abel,
  rw [e, sub_ne_zero], contrapose h, simp only [not_not] at h,
  simp only [h, not_lt, absolute_value.map_neg],
end 

-- e < 3
lemma real.exp_one_lt_3 : real.exp 1 < 3 := trans real.exp_one_lt_d9 (by norm_num)

-- log (a + b) = log a + log (1 + b/a)
lemma log_add (a b : ‚Ñù) (a0 : 0 < a) (ab0 : 0 < a + b) : real.log (a + b) = real.log a + real.log (1 + b/a) := begin
  have d0 : 0 < 1 + b/a, { field_simp [ne_of_gt a0], bound },
  rw [‚Üêreal.log_mul (ne_of_gt a0) (ne_of_gt d0), left_distrib, mul_one, mul_div_cancel' _ (ne_of_gt a0)],
end

-- log (abs (a + b)) = log (abs a) + log (abs (1 + b/a))
lemma log_abs_add (a b : ‚ÑÇ) (a0 : a ‚â† 0) (ab0 : a + b ‚â† 0)
    : real.log (abs (a + b)) = real.log (abs a) + real.log (abs (1 + b/a)) := begin
  have d0 : 1 + b/a ‚â† 0 := by field_simp [a0, ab0],
  have a0' : abs a ‚â† 0 := complex.abs.ne_zero a0,
  have d0' : abs (1 + b/a) ‚â† 0 := complex.abs.ne_zero d0,
  rw [‚Üêreal.log_mul a0' d0', ‚Üêcomplex.abs.map_mul, left_distrib, mul_one, mul_div_cancel' _ a0],
end

-- e^(1/4) ‚â§ 4/3
lemma real.exp_forth_lt_four_thirds : real.exp (1/4) < 4/3 := begin
  rw [‚Üêreal.exp_one_rpow, one_div, ‚Üê@real.pow_nat_rpow_nat_inv (4/3) (by norm_num) 4 (by norm_num),
    nat.cast_bit0, nat.cast_bit0, nat.cast_one],
  refine real.rpow_lt_rpow (le_of_lt (real.exp_pos _)) _ (by norm_num),
  exact trans real.exp_one_lt_d9 (by norm_num),
end

-- Bound abs (product - 1) in terms of abs (sum)
lemma dist_prod_one_le_abs_sum {f : ‚Ñï ‚Üí ‚ÑÇ} {s : finset ‚Ñï} {c : ‚Ñù} (le : s.sum (Œª n, abs (f n - 1)) ‚â§ c) (c1 : c ‚â§ 1/2)
    : abs (s.prod f - 1) ‚â§ 4*c := begin
  set g := Œª n, complex.log (f n),
  have b : ‚àÄ n, n ‚àà s ‚Üí abs (f n - 1) ‚â§ c, {
    intros n m, refine trans _ le,
    exact @finset.single_le_sum _ _ _ (Œª n, abs (f n - 1)) _ (Œª _ _, complex.abs.nonneg _) _ m,
  },
  have f0 : ‚àÄ n, n ‚àà s ‚Üí f n ‚â† 0, {
    intros n m, specialize b n m, contrapose b, simp only [not_not] at b,
    simp only [b, not_le], norm_num, linarith,
  },
  have a0 : ‚àÄ n, n ‚àà s ‚Üí abs (f n) ‚â† 0 := Œª n m, complex.abs.ne_zero_iff.mpr (f0 n m),
  have sg : abs (s.sum g) ‚â§ 2*c, {
    refine trans (complex.abs.sum_le _ _) _,
    refine trans (finset.sum_le_sum (Œª n m, log_small (trans (b n m) c1))) _,
    rw ‚Üêfinset.mul_sum, linarith,
  },
  have e : s.prod f = complex.exp (s.sum g), {
    rw complex.exp_sum, apply finset.prod_congr rfl, intros n m, rw complex.exp_log (f0 n m),
  },
  rw e, exact trans (exp_small (by linarith)) (by linarith),
end

