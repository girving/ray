-- Basics about analytic functions (general field case)

import analysis.analytic.basic
import analysis.analytic.composition
import analysis.analytic.linear
import analysis.analytic.isolated_zeros
import analysis.calculus.fderiv_analytic
import analysis.calculus.formal_multilinear_series
import analysis.complex.cauchy_integral
import analysis.normed_space.operator_norm
import data.complex.basic
import data.finset.basic
import data.real.basic
import data.real.ennreal
import data.real.nnreal
import data.real.pi.bounds
import data.set.basic
import data.stream.defs
import topology.basic

import bounds
import multilinear
import tactics
import topology

open complex (abs exp I log)
open filter (at_top eventually_of_forall)
open function (curry uncurry)
open metric (ball closed_ball sphere is_open_ball)
open linear_order (min)
open set (univ)
open_locale real nnreal ennreal topology
noncomputable theory

variables {ğ•œ : Type} [nontrivially_normed_field ğ•œ]
variables {E : Type} [normed_add_comm_group E] [normed_space ğ•œ E] [complete_space E]
variables {F : Type} [normed_add_comm_group F] [normed_space ğ•œ F] [complete_space F]
variables {H : Type} [normed_add_comm_group H] [normed_space ğ•œ H] [complete_space H]
variables {G : Type} [normed_add_comm_group G] [normed_space ğ•œ G] [complete_space G]

-- Infinite radius of convergence implies entire
lemma radius_inf_to_entire {f : E â†’ F} (p : formal_multilinear_series ğ•œ E F) (z : E)
    : has_fpower_series_on_ball f p z âˆ â†’ analytic_on ğ•œ f univ := begin
  intros h w wu,
  refine has_fpower_series_on_ball.analytic_at_of_mem h _,
  rw emetric.mem_ball, exact edist_lt_top w z,
end

-- Analytic functions have derivatives
lemma analytic_at.has_deriv_at {f : ğ•œ â†’ E} {z : ğ•œ}
    : analytic_at ğ•œ f z â†’ has_deriv_at f (deriv f z) z := begin
  intro a,
  have dwa : differentiable_within_at ğ•œ f univ z := analytic_at.differentiable_within_at a,
  refine (dwa.differentiable_at _).has_deriv_at,
  exact is_open.mem_nhds is_open_univ (set.mem_univ z),
end

-- id is entire
theorem analytic_at_id {x : E} : analytic_at ğ•œ (Î» x : E, x) x := (continuous_linear_map.id ğ•œ E).analytic_at x
theorem analytic_on_id {s : set E} : analytic_on ğ•œ (Î» x : E, x) s := Î» _ _, analytic_at_id

-- Finite sums of analytic functions are analytic
theorem analytic_at.sum {f : â„• â†’ E â†’ F} {c : E}
    (h : âˆ€ n, analytic_at ğ•œ (f n) c) (N : finset â„•) : analytic_at ğ•œ (Î» z, N.sum (Î» n, f n z)) c := begin
  induction N using finset.induction with a B aB hB, {
    simp only [finset.sum_empty], exact analytic_at_const,
  }, {
    simp_rw finset.sum_insert aB,
    apply analytic_at.add,
    exact h a,
    exact hB
  },
end
theorem analytic.sum {f : â„• â†’ E â†’ F} {s : set E}
    (h : âˆ€ n, analytic_on ğ•œ (f n) s) (N : finset â„•) : analytic_on ğ•œ (Î» z, N.sum (Î» n, f n z)) s :=
  Î» z zs, analytic_at.sum (Î» n, h n z zs) N

-- Power series terms are analytic
theorem change_origin.analytic_at (p : formal_multilinear_series ğ•œ E F) (rp : p.radius > 0) (n : â„•)
    : analytic_at ğ•œ (Î» x, p.change_origin x n) 0 :=
  (formal_multilinear_series.has_fpower_series_on_ball_change_origin p n rp).analytic_at

-- Analytic at a point means analytic locally
theorem analytic_at.eventually {f : E â†’ F} {z : E} (fa : analytic_at ğ•œ f z)
    : âˆ€á¶  w in ğ“ z, analytic_at ğ•œ f w :=
  (is_open_analytic_at ğ•œ f).eventually_mem fa

-- Analytic at a point means analytic in a small ball
theorem analytic_at.ball {f : E â†’ F} {z : E}
    : analytic_at ğ•œ f z â†’ âˆƒ (r : â„), r > 0 âˆ§ analytic_on ğ•œ f (ball z r) := begin
  intro a,
  rcases a with âŸ¨p,r,hâŸ©,
  by_cases ri : r = âˆ, {
    existsi (1 : â„),
    exact âŸ¨by norm_num, Î» z zs, has_fpower_series_on_ball.analytic_on h z (by {rw ri, simp})âŸ©
  }, {
    existsi r.to_real,
    constructor, {
      exact ennreal.to_real_pos (ne_of_gt h.r_pos) ri
    }, {
      intros z zs,
      refine has_fpower_series_on_ball.analytic_on h z _,
      simp at zs âŠ¢,
      have rr := ennreal.of_real_to_real ri,
      rw [â†rr, edist_lt_of_real], assumption
    }
  }
end

-- Analytic at a point means analytic in a small closed ball
theorem analytic_at.cball {f : E â†’ F} {z : E}
    : analytic_at ğ•œ f z â†’ âˆƒ (r : â„), r > 0 âˆ§ analytic_on ğ•œ f (closed_ball z r) := begin
  intro a,
  rcases analytic_at.ball a with âŸ¨r, rp, aoâŸ©,
  existsi r/2,
  constructor, {
    bound
  }, {
    intros z zs,
    refine ao z _,
    simp at âŠ¢ zs,
    exact lt_of_le_of_lt zs (by bound)
  }
end

-- analytic_on depends only on values on s (â†’ version)
lemma analytic_on.congr {f g : E â†’ F} {s : set E}
    (fa : analytic_on ğ•œ f s) (o : is_open s) (fg : âˆ€ z, z âˆˆ s â†’ f z = g z)
    : analytic_on ğ•œ g s := begin
  intros z zs,
  rcases metric.is_open_iff.mp o z zs with âŸ¨r0,r0p,r0sâŸ©,
  rcases fa z zs with âŸ¨p,r1,fpâŸ©,
  existsi p, existsi (min (ennreal.of_real r0) r1),
  refine { r_le := trans (min_le_right _ _) fp.r_le,
           r_pos := lt_min_iff.mpr âŸ¨ennreal.of_real_pos.mpr r0p,fp.r_posâŸ©,
           has_sum := _ },
  intros w wr, simp at wr,
  specialize fg (z + w) _, {
    apply r0s,
    simp, exact wr.left
  }, {
    rw â†fg, refine fp.has_sum _,
    simp, exact wr.right
  },
end

-- analytic_on depends only on values on s (â†” version)
lemma analytic_on_congr {f g : E â†’ F} {s : set E}
    (o : is_open s) (fg : âˆ€ z, z âˆˆ s â†’ f z = g z)
    : analytic_on ğ•œ f s â†” analytic_on ğ•œ g s :=
  âŸ¨Î» fa, fa.congr o fg, Î» ga, ga.congr o (Î» z zs, (fg z zs).symm)âŸ©

-- analytic_at depends only on values near c (â†’ version)
lemma analytic_at.congr {f g : E â†’ F} {x : E}
    (fa : analytic_at ğ•œ f x) (h : f =á¶ [nhds x] g) : analytic_at ğ•œ g x := begin
  rcases metric.eventually_nhds_iff_ball.mp h with âŸ¨e,ep,esâŸ©,
  rcases fa.ball with âŸ¨r,rp,frâŸ©,
  have fer : analytic_on ğ•œ f (ball x (min e r)) := fr.mono (metric.ball_subset_ball (by bound)),
  have es' : âˆ€ y, y âˆˆ ball x (min e r) â†’ f y = g y :=
    Î» y yer, es y (metric.ball_subset_ball (by bound) yer),
  exact fer.congr is_open_ball es' x (metric.mem_ball_self (by bound)),
end

-- analytic_at depends only on values near c (â†” version)
lemma analytic_at_congr {f g : E â†’ F} {x : E} (h : f =á¶ [nhds x] g) : analytic_at ğ•œ f x â†” analytic_at ğ•œ g x :=
  âŸ¨Î» fa, fa.congr h, Î» ga, ga.congr h.symmâŸ©

-- fst and snd are analytic
lemma analytic_at_fst {p : E Ã— F} : analytic_at ğ•œ (Î» p : E Ã— F, p.fst) p := (continuous_linear_map.fst ğ•œ E F).analytic_at p
lemma analytic_at_snd {p : E Ã— F} : analytic_at ğ•œ (Î» p : E Ã— F, p.snd) p := (continuous_linear_map.snd ğ•œ E F).analytic_at p
lemma analytic_on_fst {s : set (E Ã— F)} : analytic_on ğ•œ (Î» p : E Ã— F, p.fst) s := Î» p _, analytic_at_fst
lemma analytic_on_snd {s : set (E Ã— F)} : analytic_on ğ•œ (Î» p : E Ã— F, p.snd) s := Î» p _, analytic_at_snd

-- Products of analytic functions are analytic
lemma analytic_at.prod {f : E â†’ F} {g : E â†’ G} {x : E} (fa : analytic_at ğ•œ f x) (ga : analytic_at ğ•œ g x)
    : analytic_at ğ•œ (Î» x, (f x, g x)) x := begin
  rcases fa with âŸ¨p,pr,fpâŸ©,
  rcases ga with âŸ¨q,qr,gqâŸ©,
  set pq : formal_multilinear_series ğ•œ E (F Ã— G) := Î» n, (p n).prod (q n),
  have pqr : min pr qr â‰¤ pq.radius, {
    apply ennreal.le_of_forall_nnreal_lt, intros r rr,
    rcases p.norm_mul_pow_le_of_lt_radius (lt_of_lt_of_le rr (trans (min_le_left pr qr) fp.r_le)) with âŸ¨pc,pcp,phâŸ©,
    rcases q.norm_mul_pow_le_of_lt_radius (lt_of_lt_of_le rr (trans (min_le_right pr qr) gq.r_le)) with âŸ¨qc,qcp,qhâŸ©,
    apply pq.le_radius_of_bound (max pc qc), intro n,
    calc â€–pq nâ€– * â†‘r^n = (max â€–p nâ€– â€–q nâ€–) * â†‘r^n : by simp only [continuous_multilinear_map.op_norm_prod]
    ... = max (â€–p nâ€– * â†‘r^n) (â€–q nâ€– * â†‘r^n) : max_mul_of_nonneg _ _ (by bound)
    ... â‰¤ max pc qc : by bound [ph n, qh n, max_le_max],
  },
  use [pq, min pr qr],
  exact {
    r_le := pqr,
    r_pos := by bound [fp.r_pos, gq.r_pos],
    has_sum := begin
      intros y yr, apply has_sum.prod_mk,
      exact fp.has_sum (emetric.ball_subset_ball (by bound) yr),
      exact gq.has_sum (emetric.ball_subset_ball (by bound) yr),
    end,
  },
end

-- Products of analytic functions are analytic
theorem analytic_on.prod {f : E â†’ F} {g : E â†’ G} {s : set E} (fa : analytic_on ğ•œ f s) (ga : analytic_on ğ•œ g s)
    : analytic_on ğ•œ (Î» z, (f z,g z)) s := Î» _ m, (fa _ m).prod (ga _ m)

lemma analytic_on.comp {f : F â†’ G} {g : E â†’ F} {s : set F} {t : set E} (fa : analytic_on ğ•œ f s) (ga : analytic_on ğ•œ g t)
    (m : set.maps_to g t s) : analytic_on ğ•œ (Î» x, f (g x)) t := Î» x xs, (fa _ (m xs)).comp (ga _ xs)

-- analytic_at.comp for a curried function
lemma analytic_at.curry_comp {h : F â†’ G â†’ H} {f : E â†’ F} {g : E â†’ G} {x : E}
    (ha : analytic_at ğ•œ (uncurry h) (f x, g x)) (fa : analytic_at ğ•œ f x) (ga : analytic_at ğ•œ g x)
    : analytic_at ğ•œ (Î» x, h (f x) (g x)) x := begin
  have e : (Î» x, h (f x) (g x)) = uncurry h âˆ˜ (Î» x, (f x, g x)) := rfl,
  rw e, exact analytic_at.comp ha (fa.prod ga),
end

-- analytic_on.comp for a curried function
lemma analytic_on.curry_comp {h : F â†’ G â†’ H} {f : E â†’ F} {g : E â†’ G} {s : set (F Ã— G)} {t : set E}
    (ha : analytic_on ğ•œ (uncurry h) s) (fa : analytic_on ğ•œ f t) (ga : analytic_on ğ•œ g t) (m : âˆ€ x, x âˆˆ t â†’ (f x, g x) âˆˆ s)
    : analytic_on ğ•œ (Î» x, h (f x) (g x)) t :=
  Î» x xt, (ha _ (m _ xt)).curry_comp (fa _ xt) (ga _ xt)

-- Curried analytic functions are analytic in each component
lemma analytic_at.in1 {f : E â†’ F â†’ G} {x : E} {y : F} (fa : analytic_at ğ•œ (uncurry f) (x,y)) : analytic_at ğ•œ (Î» x, f x y) x :=
  analytic_at.curry_comp fa analytic_at_id analytic_at_const
lemma analytic_at.in2 {f : E â†’ F â†’ G} {x : E} {y : F} (fa : analytic_at ğ•œ (uncurry f) (x,y)) : analytic_at ğ•œ (Î» y, f x y) y :=
  analytic_at.curry_comp fa analytic_at_const analytic_at_id
lemma analytic_on.in1 {f : E â†’ F â†’ G} {s : set (E Ã— F)} {y : F} (fa : analytic_on ğ•œ (uncurry f) s)
    : analytic_on ğ•œ (Î» x, f x y) {x | (x,y) âˆˆ s} := Î» x m, (fa (x,y) m).in1
lemma analytic_on.in2 {f : E â†’ F â†’ G} {x : E} {s : set (E Ã— F)} (fa : analytic_on ğ•œ (uncurry f) s)
    : analytic_on ğ•œ (Î» y, f x y) {y | (x,y) âˆˆ s} := Î» y m, (fa (x,y) m).in2 

-- Analytic everywhere means continuous
lemma analytic_on.continuous {f : E â†’ F} (fa : analytic_on ğ•œ f univ) : continuous f := begin
  rw continuous_iff_continuous_on_univ, exact fa.continuous_on,
end

-- Order of a zero at a point.
-- We define this in terms of the function alone so that expressions involving order can depend only on f.
def order_at (f : ğ•œ â†’ E) (c : ğ•œ) : â„• :=
  @dite _ (analytic_at ğ•œ f c) (classical.dec _) (Î» p, (classical.some p).order) (Î» _, 0)

-- Order is unique, since power series are
lemma has_fpower_series_at.order_at_unique {f : ğ•œ â†’ E} {p : formal_multilinear_series ğ•œ ğ•œ E} {c : ğ•œ}
    (fp : has_fpower_series_at f p c) : order_at f c = p.order := begin
  have fa : analytic_at ğ•œ f c := âŸ¨p,fpâŸ©,
  have pr := exists.intro p fp,
  simp only [order_at, fa, dif_pos],
  have s := classical.some_spec pr,
  generalize hq : classical.some pr = q,
  simp_rw hq at s,
  rw fp.eq_formal_multilinear_series s,
end

-- order_at is zero for nonzeros
lemma order_at_eq_zero {f : ğ•œ â†’ E} {c : ğ•œ} (f0 : f c â‰  0) : order_at f c = 0 := begin
  by_cases fp : analytic_at ğ•œ f c, {
    rcases fp with âŸ¨p,fpâŸ©, rw fp.order_at_unique, rw â†fp.coeff_zero 1 at f0,
    rw formal_multilinear_series.order_eq_zero_iff', right,
    contrapose f0, simp only [not_not] at f0,
    simp only [f0, continuous_multilinear_map.zero_apply, ne.def, eq_self_iff_true, not_true, not_false_iff],
  }, {
    simp [order_at, fp],
  },
end

-- order_at = 0 means either f = 0 or f c â‰  0
lemma order_at_eq_zero_iff {f : ğ•œ â†’ E} {c : ğ•œ} (fa : analytic_at ğ•œ f c)
    : order_at f c = 0 â†” f =á¶ [ğ“ c] 0 âˆ¨ f c â‰  0 := begin
  rcases fa with âŸ¨p,fpâŸ©,
  simp only [fp.order_at_unique, â†fp.coeff_zero (Î» _, 0), formal_multilinear_series.order_eq_zero_iff'],
  nth_rewrite 1 â†norm_ne_zero_iff, rw continuous_multilinear_map.fin0_apply_norm, rw norm_ne_zero_iff,
  apply or_congr_left, intro h, exact fp.locally_zero_iff.symm,
end

-- order_at = 1 â†’ deriv â‰  0
lemma deriv_ne_zero_of_order_at_eq_one {f : ğ•œ â†’ E} {c : ğ•œ} (o : order_at f c = 1) : deriv f c â‰  0 := begin
  by_cases fa : analytic_at ğ•œ f c, {
    rcases fa with âŸ¨p,fpâŸ©,
    rw fp.order_at_unique at o,
    have o0 : p.order â‰  0, { rw o, exact one_ne_zero },
    have p0 := formal_multilinear_series.apply_order_ne_zero' o0,
    rw o at p0,
    simpa only [fp.deriv, formal_multilinear_series.apply_eq_pow_smul_coeff, one_pow, one_smul,
      formal_multilinear_series.coeff_eq_zero, ne.def],
  }, {
    simp only [order_at, fa] at o, rw dif_neg at o, norm_num at o, exact not_false,
  },
end

-- The leading nonzero coefficient of f's power series
def leading_coeff (f : ğ•œ â†’ E) (c : ğ•œ) : E := function.swap dslope c^[order_at f c] f c

-- leading_coeff for nonzeros
lemma leading_coeff_of_ne_zero {f : ğ•œ â†’ E} {c : ğ•œ} (f0 : f c â‰  0) : leading_coeff f c = f c :=
  by simp only [leading_coeff, order_at_eq_zero f0, function.iterate_zero_apply]

-- f is approximated by its leading monomial
lemma analytic_at.leading_approx {f : ğ•œ â†’ E} {c : ğ•œ} (fa : analytic_at ğ•œ f c)
    : (Î» z, f z - (z - c)^(order_at f c) â€¢ leading_coeff f c) =o[ğ“ c] (Î» z, (z-c)^(order_at f c)) := begin
  rcases fa with âŸ¨p,fpâŸ©,
  generalize ha : leading_coeff f c = a,
  generalize hd : order_at f c = d,
  have ha' : (function.swap dslope c^[d] f c = a) := by rw [â†ha, â†hd, leading_coeff],
  have e := fp.eq_pow_order_mul_iterate_dslope,
  simp_rw [â†fp.order_at_unique, hd] at e,
  apply asymptotics.is_o.of_is_O_with, intros k kp,
  rw asymptotics.is_O_with_iff,
  apply e.mp,
  have dc : continuous_at (function.swap dslope c^[d] f) c :=
    (fp.has_fpower_series_iterate_dslope_fslope d).analytic_at.continuous_at,
  rcases metric.continuous_at_iff.mp dc k kp with âŸ¨r,rp,rhâŸ©,
  rw ha' at rh,
  generalize hg : function.swap dslope c^[d] f = g, rw hg at rh,
  rw metric.eventually_nhds_iff, use [r, rp], intros y yr fe, rw fe,
  specialize rh yr, rw dist_eq_norm at rh,
  calc â€–(y-c)^d â€¢ g y - (y-c)^d â€¢ aâ€– = â€–(y-c)^dâ€– * â€–(g y - a)â€– : by rw [â†smul_sub, norm_smul]
  ... â‰¤ â€–(y-c)^dâ€– * k : by bound
  ... = k * â€–(y-c) ^ dâ€– : by rw mul_comm,
end

-- order > 0 means f has a zero
lemma analytic_at.zero_of_order_pos {f : ğ•œ â†’ E} {c : ğ•œ} (fa : analytic_at ğ•œ f c) (p : order_at f c > 0)
    : f c = 0 := begin
  have a := (asymptotics.is_O_with_iff.mp (fa.leading_approx.forall_is_O_with zero_lt_one)).self,
  simp only [(pow_eq_zero_iff p).mpr, sub_self, zero_smul, sub_zero, norm_zero, mul_zero, norm_le_zero_iff] at a,
  exact a,
end

-- The power series of (z - c) â€¢ f z
def formal_multilinear_series.unshift' (p : formal_multilinear_series ğ•œ ğ•œ E) (c : E) : formal_multilinear_series ğ•œ ğ•œ E :=
  ((continuous_linear_map.smul_rightL ğ•œ ğ•œ E (continuous_linear_map.id ğ•œ ğ•œ)).comp_formal_multilinear_series p).unshift c
@[simp] def formal_multilinear_series.unshift_coeff_zero (p : formal_multilinear_series ğ•œ ğ•œ E) (c : E)
    : (p.unshift' c).coeff 0 = c :=
  by simp only [formal_multilinear_series.coeff, formal_multilinear_series.unshift', formal_multilinear_series.unshift,
    continuous_multilinear_curry_fin0_symm_apply]
@[simp] def formal_multilinear_series.unshift_coeff_succ (p : formal_multilinear_series ğ•œ ğ•œ E) (c : E) (n : â„•)
    : (p.unshift' c).coeff (n+1) = p.coeff n := begin
  simp only [formal_multilinear_series.coeff, formal_multilinear_series.unshift', formal_multilinear_series.unshift,
    continuous_linear_map.comp_formal_multilinear_series_apply, linear_isometry_equiv.norm_map],
  simp [continuous_linear_map.smul_rightL, finset.univ, fintype.elems, fin.init],
end

-- The power series of (z - c)^n â€¢ f z
def formal_multilinear_series.unshift_iter (p : formal_multilinear_series ğ•œ ğ•œ E) (n : â„•) :=
  (Î» p, formal_multilinear_series.unshift' p (0 : E))^[n] p
def formal_multilinear_series.unshift_iter_coeff (p : formal_multilinear_series ğ•œ ğ•œ E) (n : â„•) (i : â„•) :
    (p.unshift_iter n).coeff i = if i < n then 0 else p.coeff (i - n) := begin
  revert i, induction n with n h, {
    simp only [formal_multilinear_series.unshift_iter, function.iterate_zero, id.def, not_lt_zero', tsub_zero, if_false,
      eq_self_iff_true, forall_const],
  }, {
    simp_rw formal_multilinear_series.unshift_iter at h,
    simp only [formal_multilinear_series.unshift_iter, function.iterate_succ', function.comp],
    generalize hq : (Î» (p : formal_multilinear_series ğ•œ ğ•œ E), p.unshift' 0)^[n] p = q, rw hq at h, clear hq,
    intro i, induction i with i hi, {
      simp only [formal_multilinear_series.unshift_coeff_zero, nat.succ_pos', if_true],
    }, {
      simp only [nat.succ_lt_succ_iff, h i, formal_multilinear_series.unshift_coeff_succ, nat.succ_sub_succ_eq_sub],
    },
  },
end

-- unshift' respects norm and radius norm
def formal_multilinear_series.unshift_norm' (p : formal_multilinear_series ğ•œ ğ•œ E) (c : E) (n : â„•)
    : â€–p.unshift' c (n+1)â€– = â€–p nâ€– :=
  by simp only [formal_multilinear_series.norm_apply_eq_norm_coef, formal_multilinear_series.unshift_coeff_succ]
def formal_multilinear_series.unshift_radius' (p : formal_multilinear_series ğ•œ ğ•œ E) {c : E} : (p.unshift' c).radius = p.radius := begin
  simp_rw formal_multilinear_series.radius,
  apply le_antisymm, {
    refine suprâ‚‚_le _, intros r k, refine supr_le _, intro h, refine trans _ (le_suprâ‚‚ r (k*â†‘râ»Â¹)),
    have h := Î» n, mul_le_mul_of_nonneg_right (h (n+1)) (nnreal.coe_nonneg râ»Â¹),
    by_cases r0 : r = 0, { simp only [r0, ennreal.coe_zero, ennreal.supr_zero_eq_zero, le_zero_iff] },
    simp only [pow_succ', â†mul_assoc _ _ â†‘r, mul_assoc _ â†‘r _, mul_inv_cancel (nnreal.coe_ne_zero.mpr r0),
      nonneg.coe_inv, mul_one, p.unshift_norm'] at h,
    exact le_supr _ h,
  }, {
    refine suprâ‚‚_le _, intros r k, refine supr_le _, intro h, refine trans _ (le_suprâ‚‚ r (max â€–câ€– (k*â†‘r))),
    have h' : âˆ€ n, â€–p.unshift' c nâ€– * â†‘r^n â‰¤ (max â€–câ€– (k*â†‘r)), {
      intro n, induction n with n i,
      simp only [formal_multilinear_series.unshift_coeff_zero, formal_multilinear_series.norm_apply_eq_norm_coef, pow_zero,
        mul_one, le_max_iff, le_refl, true_or],
      simp only [formal_multilinear_series.norm_apply_eq_norm_coef] at h,
      simp only [formal_multilinear_series.unshift_coeff_succ, pow_succ', â†mul_assoc,
        formal_multilinear_series.norm_apply_eq_norm_coef, le_max_iff],
      right, exact mul_le_mul_of_nonneg_right (h n) (nnreal.coe_nonneg _),
    },
    exact le_supr _ h',
  },
end

-- The power series of (z - c) â€¢ f z is the unshifted power series
lemma has_fpower_series_on_ball.unshift {f : ğ•œ â†’ E} {p : formal_multilinear_series ğ•œ ğ•œ E} {c : ğ•œ} {r : ennreal}
    (fp : has_fpower_series_on_ball f p c r) : has_fpower_series_on_ball (Î» z, (z - c) â€¢ f z) (p.unshift' 0) c r := {
  r_le := trans fp.r_le (ge_of_eq p.unshift_radius'),
  r_pos := fp.r_pos,
  has_sum := begin
    intros y yr, simp only [formal_multilinear_series.apply_eq_pow_smul_coeff, add_sub_cancel'],
    generalize hs : (Î» n, y^n â€¢ (p.unshift' 0).coeff n) = s,
    have s0 : y â€¢ f (c + y) = y â€¢ f (c + y) + (finset.range 1).sum s :=
      by simp only [â†hs, p.unshift_coeff_zero, finset.range_one, finset.sum_singleton, smul_zero, add_zero],
    rw [s0, â†has_sum_nat_add_iff, â†hs],
    simp only [p.unshift_coeff_succ, pow_succ, â†smul_smul], apply has_sum.const_smul,
    have h := fp.has_sum yr, simp only [formal_multilinear_series.apply_eq_pow_smul_coeff] at h, exact h,
  end,
}
lemma has_fpower_series_at.unshift {f : ğ•œ â†’ E} {p : formal_multilinear_series ğ•œ ğ•œ E} {c : ğ•œ}
    (fp : has_fpower_series_at f p c) : has_fpower_series_at (Î» z, (z - c) â€¢ f z) (p.unshift' 0) c := begin
  rcases fp with âŸ¨r,faâŸ©, use [r, fa.unshift],
end
lemma has_fpower_series_at.unshift_iter {f : ğ•œ â†’ E} {p : formal_multilinear_series ğ•œ ğ•œ E} {c : ğ•œ} {n : â„•}
    (fp : has_fpower_series_at f p c) : has_fpower_series_at (Î» z, (z - c)^n â€¢ f z) (p.unshift_iter n) c := begin
  induction n with n h, {
    simp only [nat.nat_zero_eq_zero, pow_zero, one_smul], exact fp,
  }, {
    simp only [pow_succ, â†smul_smul, formal_multilinear_series.unshift_iter, function.iterate_succ', function.comp],
    exact h.unshift,
  },
end

-- Power series terms are zero iff their coeffs are zero
lemma formal_multilinear_series.zero_iff_coeff_zero (p : formal_multilinear_series ğ•œ ğ•œ E) {n : â„•}
    : p n = 0 â†” p.coeff n = 0 := begin
  constructor, {
    intro h, rw [formal_multilinear_series.coeff, h], simp only [continuous_multilinear_map.zero_apply],
  }, {
    intro h, rw [â†p.mk_pi_field_coeff_eq n, h], simp only [continuous_multilinear_map.mk_pi_field_zero],
  },
end
lemma formal_multilinear_series.ne_zero_iff_coeff_ne_zero (p : formal_multilinear_series ğ•œ ğ•œ E) {n : â„•}
    : p n â‰  0 â†” p.coeff n â‰  0 := begin
  constructor, {
    intro h, contrapose h, simp only [not_not] at âŠ¢ h, exact p.zero_iff_coeff_zero.mpr h,
  }, {
    intro h, contrapose h, simp only [not_not] at âŠ¢ h, exact p.zero_iff_coeff_zero.mp h,
  },
end

-- Power series coefficients of (z - n)^n â€¢ f z are what you expect
lemma analytic_at.monomial_mul_order_at {f : ğ•œ â†’ E} {c : ğ•œ} (fa : analytic_at ğ•œ f c) (fnz : âˆƒá¶  z in ğ“ c, f z â‰  0) (n : â„•)
    : order_at (Î» z, (z - c)^n â€¢ f z) c = n + order_at f c := begin
  rcases fa with âŸ¨p,fpâŸ©,
  have pnz : p â‰  0, {
    contrapose fnz, simp at fnz, simpa only [has_fpower_series_at.locally_zero_iff fp, filter.not_frequently, not_not],
  },
  have pe : âˆƒ i, p i â‰  0, { rw function.ne_iff at pnz, exact pnz },
  have pne : âˆƒ i, (p.unshift_iter n) i â‰  0, {
    rcases pe with âŸ¨i,piâŸ©, use n+i,
    simp only [formal_multilinear_series.ne_zero_iff_coeff_ne_zero] at âŠ¢ pi,
    simpa only [p.unshift_iter_coeff, add_lt_iff_neg_left, add_tsub_cancel_left],
  },
  have fq : has_fpower_series_at (Î» z, (z - c)^n â€¢ f z) (p.unshift_iter n) c := fp.unshift_iter,
  rw [fp.order_at_unique, fq.order_at_unique],
  rw @formal_multilinear_series.order_eq_find _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ (classical.dec_pred _) pe,
  rw @formal_multilinear_series.order_eq_find _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ (classical.dec_pred _) pne,
  rw nat.find_eq_iff, constructor, {
    have s := @nat.find_spec _ (classical.dec_pred _) pe,
    simp only [p.zero_iff_coeff_zero, ne.def] at s,
    simp only [p.unshift_iter_coeff, formal_multilinear_series.zero_iff_coeff_zero, s, ne.def, add_lt_iff_neg_left, not_lt_zero',
      add_tsub_cancel_left, if_false, not_false_iff],
  }, {
    intros m mp, simp [formal_multilinear_series.zero_iff_coeff_zero, p.unshift_iter_coeff], intro mn,
    generalize ha : m - n = a, have hm : m = n + a := by rw [â†ha, add_comm, nat.sub_add_cancel mn],
    simp only [hm, add_lt_add_iff_left, nat.lt_find_iff, not_not] at mp,
    specialize mp a (le_refl _), rwa â†formal_multilinear_series.zero_iff_coeff_zero,
  },
end
lemma analytic_at.monomial_mul_leading_coeff {f : ğ•œ â†’ E} {c : ğ•œ} (fa : analytic_at ğ•œ f c) (fnz : âˆƒá¶  z in ğ“ c, f z â‰  0) (n : â„•)
    : leading_coeff (Î» z, (z - c)^n â€¢ f z) c = leading_coeff f c := begin
  simp [leading_coeff, fa.monomial_mul_order_at fnz n], generalize ha : order_at f c = a,
  induction n with n h, simp only [zero_add, pow_zero, one_smul],
  simp [pow_succ, â†smul_smul, nat.succ_add],
  generalize hg : (Î» z, (z - c)^n â€¢ f z) = g,
  have hg' : âˆ€ z, (z - c)^n â€¢ f z = g z, { rw â†hg, simp only [eq_self_iff_true, forall_const] },
  simp_rw hg' at âŠ¢ h,
  have e : function.swap dslope c (Î» z, (z - c) â€¢ g z) = g, {
    simp only [function.swap, (@dslope_sub_smul) _ _ _ _ _ (classical.dec_eq _),
      function.update_eq_self_iff, sub_self],
    rw deriv_smul, simp only [sub_self, zero_smul, deriv_sub, differentiable_at_id', differentiable_at_const, deriv_id'',
      deriv_const', sub_zero, one_smul, zero_add],
    exact differentiable_at_id.sub (differentiable_at_const _),
    rw â†hg, exact ((differentiable_at_id.sub (differentiable_at_const _)).pow _).smul fa.differentiable_at,
  },
  rw [e, h],
end

-- analytic_at version of analytic_on.cont_diff_on
lemma analytic_at.cont_diff_at {f : E â†’ F} {c : E} (fa : analytic_at ğ•œ f c) : cont_diff_at ğ•œ âŠ¤ f c := begin
  rcases fa.ball with âŸ¨r,rp,faâŸ©,
  have m : c âˆˆ ball c r := metric.mem_ball_self rp, 
  exact (fa.cont_diff_on _ m).cont_diff_at (is_open_ball.mem_nhds m),
end

-- fderiv and deriv are analytic_at
lemma analytic_at.fderiv {f : E â†’ F} {c : E} (fa : analytic_at ğ•œ f c) : analytic_at ğ•œ (fderiv ğ•œ f) c := begin
  rcases fa.ball with âŸ¨r,rp,faâŸ©, exact fa.fderiv _ (metric.mem_ball_self rp),
end
lemma analytic_at.deriv {f : ğ•œ â†’ ğ•œ} {c : ğ•œ} (fa : analytic_at ğ•œ f c) [complete_space ğ•œ]
    : analytic_at ğ•œ (Î» x, deriv f x) c := begin
  simp only [â†fderiv_deriv],
  have a1 : âˆ€ g, analytic_at ğ•œ (Î» g : ğ•œ â†’L[ğ•œ] ğ•œ, continuous_linear_map.apply ğ•œ ğ•œ 1 g) g :=
    Î» g, continuous_linear_map.analytic_at _ _,
  refine (a1 _).comp fa.fderiv,
end

-- deriv in the second variable is analytic
lemma analytic_at.deriv2 [complete_space ğ•œ] {f : E â†’ ğ•œ â†’ ğ•œ} {c : E Ã— ğ•œ} (fa : analytic_at ğ•œ (uncurry f) c)
    : analytic_at ğ•œ (Î» x : E Ã— ğ•œ, deriv (f x.1) x.2) c := begin
  set p : (E Ã— ğ•œ â†’L[ğ•œ] ğ•œ) â†’L[ğ•œ] ğ•œ := continuous_linear_map.apply ğ•œ ğ•œ (0,1),
  have e : âˆ€á¶  x : E Ã— ğ•œ in ğ“ c, deriv (f x.1) x.2 = p (fderiv ğ•œ (uncurry f) x), {
    refine fa.eventually.mp (eventually_of_forall _),
    rintros âŸ¨x,yâŸ© fa, simp only [â†fderiv_deriv],
    have e : f x = uncurry f âˆ˜ (Î» y, (x,y)) := rfl, 
    rw e, rw fderiv.comp,
    have pd : fderiv ğ•œ (Î» y : ğ•œ, (x, y)) y = continuous_linear_map.inr ğ•œ E ğ•œ, {
      apply has_fderiv_at.fderiv, apply has_fderiv_at_prod_mk_right,
    },
    simp only [pd, continuous_linear_map.comp_apply, continuous_linear_map.inr_apply,
      continuous_linear_map.apply_apply],
    exact fa.differentiable_at, exact (differentiable_at_const _).prod differentiable_at_id,
  },
  rw analytic_at_congr e,
  exact (p.analytic_at _).comp fa.fderiv,
end

-- Scaling does the expected thing to power series
lemma has_fpower_series_at.const_smul {f : ğ•œ â†’ E} {c a : ğ•œ} {p : formal_multilinear_series ğ•œ ğ•œ E}
    (fp : has_fpower_series_at f p c) : has_fpower_series_at (Î» z, a â€¢ f z) (Î» n, a â€¢ p n) c := begin
  rw has_fpower_series_at_iff at fp âŠ¢, refine fp.mp (eventually_of_forall (Î» z h, _)),
  simp only [formal_multilinear_series.coeff, continuous_multilinear_map.smul_apply, smul_comm _ a],
  exact h.const_smul a,
end

-- Nonzero scaling does not change analyticitiy
lemma analytic_at_iff_const_smul {f : ğ•œ â†’ E} {c a : ğ•œ} (a0 : a â‰  0)
    : analytic_at ğ•œ (Î» z, a â€¢ f z) c â†” analytic_at ğ•œ f c := begin
  constructor, {
    rintros âŸ¨p,fpâŸ©,
    have e : f = (Î» z, aâ»Â¹ â€¢ (a â€¢ f z)), {
      funext, simp only [â†smul_assoc, smul_eq_mul, inv_mul_cancel a0, one_smul],
    },
    rw e, exact âŸ¨_, fp.const_smulâŸ©,
  }, {
    rintros âŸ¨p,fpâŸ©, exact âŸ¨_, fp.const_smulâŸ©,
  },
end

-- Nonzero scaling does not change order_at
lemma order_at_const_smul {f : ğ•œ â†’ E} {c a : ğ•œ} (a0 : a â‰  0) : order_at (Î» z, a â€¢ f z) c = order_at f c := begin
  by_cases fa : analytic_at ğ•œ f c, {
    rcases fa with âŸ¨p,fpâŸ©,
    have e : âˆ€ n, a â€¢ p n â‰  0 â†” p n â‰  0 := Î» n, by simp only [a0, ne.def, smul_eq_zero, false_or],
    simp only [fp.order_at_unique, fp.const_smul.order_at_unique, formal_multilinear_series.order, e],
  }, {
    have ga := fa, rw â†analytic_at_iff_const_smul a0 at ga,
    simp only [order_at, fa, ga], rw [dif_neg, dif_neg],
    exact not_false, exact not_false, apply_instance,
  },
end

-- The leading coefficient of zero is zero
lemma leading_coeff.zero {c : ğ•œ} : leading_coeff (Î» z : ğ•œ, (0 : E)) c = 0 := begin
  simp only [leading_coeff],
  generalize hn : order_at (Î» z : ğ•œ, (0 : E)) c = n, clear hn,
  induction n with n h, simp only [function.iterate_zero_apply],
  simp only [function.iterate_succ_apply], convert h,
  simp only [function.swap, dslope, deriv_const],
  funext, simp only [slope_fun_def, vsub_eq_sub, sub_zero, smul_zero, function.update_apply],
  by_cases ac : a = c, simp only [ac, if_pos], simp only [ac], rw [if_neg], exact not_false,
end

-- deriv scales linearly without assuming differentiability
lemma deriv_const_smul' {f : ğ•œ â†’ E} {c : ğ•œ} (a : ğ•œ) : deriv (Î» x, a â€¢ f x) c = a â€¢ deriv f c := begin
  by_cases a0 : a = 0, simp only [a0, zero_smul, deriv_const],
  by_cases d : differentiable_at ğ•œ f c, exact deriv_const_smul _ d,
  have ad : Â¬differentiable_at ğ•œ (Î» x, a â€¢ f x) c, {
    contrapose d, simp only [not_not] at d âŠ¢,
    have e : f = (Î» z, aâ»Â¹ â€¢ (a â€¢ f z)), {
      funext, simp only [â†smul_assoc, smul_eq_mul, inv_mul_cancel a0, one_smul],
    },
    rw e, exact d.const_smul _,
  },
  simp only [deriv_zero_of_not_differentiable_at d, deriv_zero_of_not_differentiable_at ad, smul_zero],
end

-- leading_coeff has linear scaling
lemma leading_coeff_const_smul {f : ğ•œ â†’ E} {c a : ğ•œ} : leading_coeff (Î» z, a â€¢ f z) c = a â€¢ leading_coeff f c := begin
  by_cases a0 : a = 0, simp only [a0, zero_smul, leading_coeff.zero],
  simp only [leading_coeff, order_at_const_smul a0],
  generalize hn : order_at f c = n, clear hn,
  have e : (function.swap dslope c^[n] (Î» z : ğ•œ, a â€¢ f z)) = a â€¢ (function.swap dslope c^[n] f), {
    induction n with n h, funext, simp only [function.iterate_zero_apply, pi.smul_apply],
    generalize hg : function.swap dslope c^[n] f = g,
    simp only [function.iterate_succ_apply', h, hg],
    funext x, simp only [function.swap],
    by_cases cx : x = c,
    simp only [cx, dslope_same, pi.smul_apply, pi.smul_def, deriv_const_smul'],
    simp only [dslope_of_ne _ cx, pi.smul_apply, slope, vsub_eq_sub, â†smul_sub, smul_comm _ a],
  },
  simp only [e, pi.smul_apply],
end

-- leading_coeff is nonzero for nonzero order
lemma leading_coeff_ne_zero {f : ğ•œ â†’ E} {c : ğ•œ} (fa : analytic_at ğ•œ f c) (o0 : order_at f c â‰  0)
    : leading_coeff f c â‰  0 := begin
  rcases fa with âŸ¨p,fpâŸ©,
  simp only [fp.order_at_unique, leading_coeff] at o0 âŠ¢,
  exact fp.iterate_dslope_fslope_ne_zero (formal_multilinear_series.ne_zero_of_order_ne_zero o0),
end